.******************************************************************************
.*  Include   :  DGADTLVE.PBL                                                 *
.*                                                                            *
.*  Function  :  To PUT A PATIENT ON LEAVE from a Datagate request            *
.*                                                                            *
.*  Author    :  ROD   (19/10/95)                                             *
.*                                                                            *
.*  Mods      :                                                               *
.*        V10.04.01 28/04/2014  Steve Armstrong   CAR 261630                  *
.*                  Removed all reference to PATAULFD                         *
.******************************************************************************
.*        V12.00.01 27/05/2025  PJ Le Febour      TSK 0955096 AlphaNum Vistnum*
.*                  replace  NADMNO FORM 8        to NADMNO DIM 8             *
.*                           COMPARE NADMNO,TADMN to MATCH NADMNO,TADMN       *
.*                           MOVE    ZERO,WSTBY   to MOVE  ZEROVISN,WSTBY     *
.*                           COMPARE ZERO,WADMNO  to MATCH ZEROVISN,WADMNO    *
.*                           COMPARE AADMNO,MHLEADMN to MATCH ZEROVISN,WADMNO *
.******************************************************************************

.*        V10.01.02 02/02/2011  Steve Armstrong   CAR 237520                  *
.*                  Mods to remove Detente interface (PATDETNT)               *
.*        V10.01.01 01/12/2010  Peter Vela        CAR 233148                  *
.*                  Initialised pattranf variables to spaces before write     *
.******************************************************************************
.*        V9.12.01  15/06/2009 Ebon Clements  CAR 159088                      *
.*                  Added OERTIME - Expected return from leave time           *
.*        V9.08.01  06/10/2006 J.Tan  CAR 119972                              *
.*                  Mods CFEETYP=5 for NZ Private                             *
.*        V9.04.01  05/10/2004 J.Tan  CAR 53798                               *
.*                  Removed PATCHBF                                           *
.*        V9.02.02  29.11.2001  B.G.Ackland                                   *
.*                  Remove pi's from Program                                  *
.*        V9.02.01  24.10.2001  B.G.Ackland                                   *
.*                  Remove pi from CFRDTE & CMMHDT Control File Update        *
.*        V5.08.01  21/08/2000  Jill Habasque                                 *
.*                  Added variables for patdetnt                              *
.*        V5.08.B02 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADAIO MATADBFD & MATADBIO             *
.*        V5.08.B01 25/02/2000  Greg Horvat                                   *
.*                  Removed PATADJFD PATADJIO PATMMRFD PATMMRIO & PATPRADJ    *
.*                  25/02/2000  Greg Horvat      SRF 637565                   *
.*                  Recompiled for PATDYCAS - Changes for 4 character DRGs    *
.******************************************************************************
.*            V5.07.02 22/12/1998  Davin                                  *
.*                     Y2K mods                                           *
.*            V5.07.01 17/11/1998  Jim Stathopoulos                       *
.*                     Removed the HCS Admission and Discharge            *
.*                     Data Audit Files                                   *
.*            V5.07.00 22/10/1998  Davin                                  *
.*                     Mods for 507                                       *
.*            V5.04.01 17/04/1997  Steve Armstrong                        *
.*                     Mods to use inbound & outbound comms client        *
.*                     (DGATEIN & DGATEOUT).                              *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTLVE
 IFGE     $$VER,7
.
          INC       IBAPASFD/INC
          INC       MEHDL1FD/INC            * Discharge to leave file
          INC       MEHLEGFD/INC
          INC       MEHLERFD/INC
          INC       MEHREVFD/INC
          INC       OUTCLIFD/INC
          INC       PATALWFD/INC
          INC       PATHSPFD/INC
          INC       PATMRGFD/INC
          INC       PATNOBFD/INC
          INC       PATNUMFD/INC
          INC       PATONLFD/INC
          INC       PATRCAUD/INC
.
ADDESC    DIM       17
ADIAG     DIM       50
AFTDECP   FORM      2  * After Decimal Place
AFTDECPF  FORM      2  * After Decimal Place Format String
ATT       DIM       80        * attribute string
AUDIFLAG  FORM      1                       * audits
BEFDECP   FORM      2  * Before Decimal Place
BEFDECPF  FORM      2  * Before Decimal Place Format String
BFAMT     FORM      4.2
BFCOL     FORM      1
BFEND1    FORM      3
BFEND2    FORM      3
BFEND3    FORM      3
BFEND4    FORM      3
BFEND5    FORM      3
BFEND6    FORM      3
BJDAY     FORM      3
CALDAYS   FORM      3
CALLPOSN  FORM      1
CDYSDAYS  FORM      6                       * number of days calculated
CDYSFDTE  DIM       8                       * From Date
CDYSTDTE  DIM       8                       * To Date
CDYSYEAR  FORM      2                       * Work Year
CHG       FORM      1
CHG1      FORM      1
CJDAY     FORM      3
CMDLINE   DIM       35
CODE      DIM       2
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODED     INIT      "D "
CODELE    INIT      "LE"                    * Leave end code category (	LE)
CODELTYP  DIM       3                       * Type of Leave code
CODEU2    INIT      "U2"
COL1      FORM      2
COL2      FORM      2
COL3      FORM      2
COUNTER   FORM      2
CUDATE    DIM       6                       * current date yymmdd
CURRDATE  DIM       8                       * current date ccyymmdd
DANS1     DIM       20
DANS2     DIM       20
DANS3     DIM       20
DANS4     DIM       20
DANS5     DIM       4
DANS6     DIM       11
DECIMALP  INIT      "."
DEFLAG    FORM      1
DEPCODE   DIM       3
DETCADES  DIM       20
DETCODES  DIM       20
DETNTREC  FORM      2
DIM14     DIM       14
DIM14A    DIM       14
DIM15     DIM       15
DIM19A    DIM       19
DIM4      DIM       4
DIM5      DIM       5                       * HR:MM 
DIM8      DIM       8
DIM80     DIM       80
DISPPEND  FORM      1
DOCCODE   DIM       6
DOCDISP   DIM       8
DOCNAME   DIM       12
DPSTAT    DIM       31
DSEX      DIM       6
DSMOK3    DIM       3
EOF       FORM      "-3"
FIELD     FORM      2
FNAMEM    DIM       8
FORM21    FORM      21
FORM3     FORM      3
FORM5     FORM      5
FORM62    FORM      6.2
FORMAT21  FORM      21
FROMDATE  DIM       8
INWARD    DIM       3
JYEAR     FORM      2
KEYCOL    DIM       1
LALLW     FORM      6.2
LATYPE    DIM       3
LBED      DIM       3
LCHSTAT   DIM       3
LDATE     DIM       8                       * Last Transfer Date
LDISC     FORM      6.2
LEAVTYPE  FORM      1                       * indicator for leave type as a form
LEFTBRAK  INIT      "("
LLCNT     FORM      5
LMOVE     DIM       1                       * Last Transfer Move
LOOPTRN   FORM      1                       * 0 = Enquiry
LRBEND    FORM      3
LRBTYP    DIM       3
LTAMTF    FORM      6.2
LTAMTH    FORM      6.2
LTIME     DIM       8                       * Last Transfer Time
LWARD     DIM       3
MAXNUM    FORM      1
MODE      DIM       1
MTINDC    FORM      1                       * MTINDC=1  ... Empty Beds Only
NADMNO    DIM       8
NALLOW    DIM       2
NBED      DIM       3
NBRDAYS   FORM      4
NDATE     DIM       8
NDISC     FORM      3.2
NETTOT    FORM      6.2
NEWBED    DIM       3
NEWWARD   DIM       3
NLWAMT    FORM      6.2
NMPNUMB   DIM       20
NOFEE     FORM      "0"
NTAMTF    FORM      6.2
NTAMTG    FORM      6.2
NTAMTH    FORM      6.2
NTIME     DIM       8
NUMBER    FORM      3
NWARD     DIM       3
OLDBED    DIM       3
OLDWARD   DIM       3
ON        INIT      " on "
ONAME     DIM       30
OPERCODE  DIM       4
PIPE      INIT      "|"
PTPSEDAT  DIM       8
PNAME     DIM       20
PRINTVAR  DIM       80
PRTITCOD  DIM       9
RANGE     FORM      1
RECTYPEA  INIT      "A"
RETDATE8  DIM       8                       * return date formatted
SAVADMNO  DIM       8                       * save Admission Key before scan
SAVBTYP   DIM       3
SAVCOL    FORM      1
SAVEND    FORM      3
SAVHF     FORM      4.2
SAVKEY13  DIM       13
SAVKEY28  DIM       28
SAVPAT    FORM      4.2
SAVREVW   DIM       3
SAVTOT    FORM      4.2
SAVURNO   DIM       8                       * save Master Key before scan
SEC1      FORM      "00001"
SKEY30    DIM       30
SKEY6     DIM       6
SORL      DIM       1
SP70      INIT      "                                               ":
                    "                       "
SVDATE    DIM       8
SVTIME    DIM       5
THCFLG    FORM      2
TIMEIS    DIM       8
TMPDTCAT  DIM       2
TMPDTCOD  DIM       3
TMPDTACT  DIM       1                    * detente temporary action
TMPDVIST  DIM       1                    * detente temporary visit type
TMPDVISN  DIM       8                    * detente temporary visit number
TMPDDATE  DIM       8                    * detente temporary visit date
TMPKEY    DIM       8[16]
TODATE    DIM       8
TOTAL     FORM      4.2
TOTDISC   FORM      6.2
TTIME1    DIM       8                       * Transfer In Time
TTIME2    DIM       8                       *          Out Time
VAR       DIM       80
VERT1     FORM      2
VERT2     FORM      2
VERT3     FORM      2
WARDESC   DIM       30                      * Ward description
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WGLINE    DIM       35
WRDESC    DIM       30
WREXTN    DIM       4
WSOPT     FORM      1
WSWARD    DIM       3
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
+
.************************************************************************
.*  DGALV000  :  process a datagate On Leave message                    *
.************************************************************************
DGALV000  CALL      LVINI000                      * initialisation/open files
.
          READ      DGATEOUT;NADMNO,SVDATE,TTIME1,CODELTYP,OERDATE
.
          CALL      VALID000                      * validate and get extra info
          BRANCH    EXIT,DGALV900                 * error
.
          CALL      LVHMS000                      * subtract data for MHMS adjus
          CALL      LVMIS000                      * update admission file
          CALL      LVTRN000                      * write transfer file record
          CALL      LVDCS000                      * calc new charge for dy/case
          CALL      LVNUM000                      * update ward usage file
          CALL      LVOLV000                      * write on leave record
          CALL      LVWRD000                      * update ward/bed file
          CALL      LVCON000                      * update control file
          CALL      LVMEH000                      * update Mental Health dets
.
          MOVE      NADMNO,KEY8
          CALL      UUPTMIS1                      * unlock admission number
.
          CALL      LVSBY000                      * update standby patient dets
.
. send reply message as successful
.     NB.  SP1 at end of reply has been added because the interpreter does
.     not place a "|" after the last field, and Datagate requires this.  By
.     putting an extra field on the end (which is blank), a "|" will then
.     be added after the last field.
.
          MOVE      "00000",RPLYCODE
          MOVE      SP50,RPLYDESC
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
          ADD       ONE,SUCCPROC                  * increment success message
          GOTO      DGALV999
.
. send reply message as Error
.
DGALV900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
          ADD       ONE,ERRRPROC                  * increment # error messages
          GOTO      DGALV999
.
DGALV999  GOTO      ENDPRLVE
+
.*********************************************************************
.*  LVINI000  :  initialisation                                      *
.*********************************************************************
LVINI000  MOVE      "LVE",RPLYHEAD
.
          COMPARE   ONE,MHCNUSE                   * using Mental Health?
          IF        @EQUAL
            OPEN      MEHDL1A1,"mehdl1af"
            OPEN      MEHVI1A1,"mehvi1af"
            OPEN       MEHLEGA1,"mehlegaf";
            OPEN       MEHREVA1,"mehrevaf";
            IF         MHCNKLER = 1
              OPEN       MEHLERA1,"mehleraf"
            ENDIF
          ENDIF
.
          OPEN      PATONLV1,"patonlvf"
          OPEN      PATNOBE1,"patnobef"
.
LVINI999  RETURN
+
.*************************************************************************
.*  VALID000  :  validate patient info & get some info                   *
.*************************************************************************
VALID000  PACK      KEY8,NADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,VALID800
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,VALID810
          CALL      RDPMPX21
.
          PACK      KEY30,NADMNO,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VALID820
.
          MATCH     NADMNO,TADMN
          GOTO      VALID820 IF NOT EQUAL
.
          MATCH     "L",TMOVE                       * already on leave?
          GOTO      VALID830 IF EQUAL
.
          MATCH     "D",TMOVE                       * discharged?
          GOTO      VALID840 IF EQUAL
.
. make sure the date/time of the new transfer record will be after the last one
.
          MATCH     TDATE,SVDATE
          GOTO      VALID850 IF LESS           * new date before last date
          IF        @EQUAL
            MATCH     TTIME,TTIME1
            GOTO      VALID850 IF EQUAL        * new date/time = last date/time
            GOTO      VALID850 IF LESS         * new time < last time (dates =)
          ENDIF
.
.         save transfer file vars
.
VALID200  MOVE      TRATEF,LTAMTF
          MOVE      TRATEH,LTAMTH
          MOVE      TDISC,LDISC
          MOVE      TALLW,LALLW
          MOVE      TRBTYP,LRBTYP
          MOVE      TRBEND,LRBEND
          MOVE      TDATE,LDATE
          MOVE      TTIME,LTIME
          MOVE      TMOVE,LMOVE
.
          MOVE      TWARD,LWARD
          MOVE      TBED,LBED
          MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      TCHSTAT,STCHSTAT
          MOVE      TEXTRA,STEXTRA
          MOVE      TDEPT,STDEPT            * save department
          MOVE      PTTREPNO,STEPNO         * save episode number
.
          MOVE      ZERO,EXIT
          GOTO      VALID999
.
. set up error reply messages
.
VALID800  MOVE      "Missing admission record",RPLYDESC
          MOVE      "02100",RPLYCODE
          GOTO      VALID900
.
VALID810  MOVE      "U/R Number does not exist",RPLYDESC
          MOVE      "01100",RPLYCODE
          GOTO      VALID900
.
VALID820  MOVE      "Missing transfer record",RPLYDESC
          MOVE      "02102",RPLYCODE
          GOTO      VALID900
.
VALID830  MOVE      "Patient already on leave",RPLYDESC
          MOVE      "02110",RPLYCODE
          GOTO      VALID900
.
VALID840  MOVE      "Patient is discharged",RPLYDESC
          MOVE      "02112",RPLYCODE
          GOTO      VALID900
.
VALID850  MOVE      "Transfer date/time must be after last transfer",RPLYDESC
          MOVE      "02180",RPLYCODE
          GOTO      VALID900
.
VALID900  MOVE      ONE,EXIT                     * set error flag
.
VALID999  RETURN
+
.*************************************************************************
.*  LVHMS000  :  update MHMS adjustment file                             *
.*************************************************************************
LVHMS000  MOVE      SVDATE,TDATE            * restore transfer date
          MOVE      TTIME1,TTIME
          MOVE      NADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
.
LVHMS999  RETURN
+
.************************************************************************
.*  LVMIS000  :  update admission data                                  *
.************************************************************************
LVMIS000  MOVE      FOUR,ASTAT
          MOVE      NADMNO,KEY8
          CALL      UPLMISS1
.
LVMIS999  RETURN
+
.***********************************************************************
.*  LVTRN000  :  set transfer file vars                                *
.***********************************************************************
LVTRN000  MOVE      SVDATE,TDATE
          MOVE      TTIME1,TTIME
          MOVE      NADMNO,TADMN
          MOVE      AURNO,TURNO
          MOVE      LWARD,TWARD
          MOVE      LBED,TBED
          MOVE      LTAMTF,TRATEF
          MOVE      LTAMTH,TRATEH
          MOVE      ZERO,TRATEG
          MOVE      LDISC,TDISC
          MOVE      LALLW,TALLW
          MOVE      LRBTYP,TRBTYP
          MOVE      LRBEND,TRBEND
.
          MOVE      STATYPE,TATYPE
          MOVE      STADOCT,TADOCT
          MOVE      STCHSTAT,TCHSTAT
          MOVE      STEXTRA,TEXTRA
          MOVE      STDEPT,TDEPT            * department
          MOVE      STEPNO,PTTREPNO         * episode number
          MOVE      CODELTYP,PTTRLTYP       * type of leave
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          MOVE      "L",TMOVE
          MOVE      NADMNO,TADMN
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
LVTRN999  RETURN
+
.********************************************************************
.*  LVDCS000  :  calculate new charge if admit/leave same day       *
.********************************************************************
LVDCS000  MOVE      KEY30,SKEY30            * save the key
          MATCH     TDATE,ADATE             * see if admitted/leave same day
          IF        @EQUAL & (CFEETYP = 0 | CFEETYP = 4 )
            MOVE      ADATE,DDATE             * set as day case
            OPEN      PATDTRA1,"patdtraf"
            OPEN      PATITEM1,"patitemn"
            CALL      PATDYCAS                * calculate new charge
            CLOSE     PATDTRA1
            CLOSE     PATITEM1
            MOVE      SP8,DDATE               * clear discharge date
            MOVE      SKEY30,KEY30
            CALL      RDTRAN2               * get back the details
          ENDIF
.
LVDCS999  RETURN
+
.********************************************************************
.*  LVNUM000  :  Update the ward usage file                         *
.********************************************************************
LVNUM000  BRANCH    PTCNNUMM,LVNUM999     * Skip if doing the monthly update
.
          OPEN      PATNUMW1,"patnumwr"
.                                         of the PATNUM file
          PACK      CPTDATE,TDATE
          REP       " 0",CPTDATE
          CALL      GPERD                 * Get the financial period for date
          BRANCH    EXIT,LVNUM900         * Date not in period file
.
          PACK      PDATE,DRGYR,DRGNUM
          MOVE      TWARD,PWARD
.
.         update Number of Patient Transfer Out
.
LVNUM100  PACK      KEY9,PWARD,PDATE
          CALL      RLNUMW1
          BRANCH    OVRCD,LVNUM300,LVNUM190
          ADD       ONE,PLEAV
          CALL      UPNUMW1
          CALL      UUNUMW1
          GOTO      LVNUM900
LVNUM190  DISPLAY   *W,"Waiting on Record Lock PATNUMFD"
          GOTO      LVNUM100
.
LVNUM300  MOVE      ZERO,PTRIN
          MOVE      ZERO,PTROUT
          MOVE      ZERO,PDSCH
          MOVE      ZERO,PADMN
          MOVE      ONE,PLEAV
          MOVE      ZERO,PRETN
          CALL      WRNUMW1
.
LVNUM900  CLOSE     PATNUMW1
.
LVNUM999  RETURN
+
.********************************************************************
.*  LVOLV000  :  write on leave record                              *
.********************************************************************
LVOLV000  MOVE      TWARD,OWARD
          MOVE      TBED,OBED
          MOVE      NADMNO,OADMNO
          MOVE      SP3,OROOM
          MOVE      SP8,OERTIME
          MOVE      SP30,OSPARE
.
          MATCH     SP3,OBED
          GOTO      LVOLV300 IF NOT EQUAL
.
          MOVE      NBROOM,OROOM
.
LVOLV300  PACK      KEY14,OWARD,OBED,OADMNO
          CALL      WRONLV1
.
LVOLV999  RETURN
+
.********************************************************************
.*  LVWRD000  :  empty out ward/bed                                 *
.********************************************************************
LVWRD000  MATCH     SP3,OBED
          GOTO      LVWRD500 IF EQUAL
.
          MOVE      OWARD,TWARD
          MOVE      OBED,TBED
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1 
          BRANCH    OVRCD,LVWRD999
.
          MOVE      WSTBY,WADMNO         * Drop any standby patient into the bed
          MOVE      ZERO,WPLUR
          MOVE      ZEROVISN,WSTBY       * Clear standby patient if necessary
          CALL      UPWARD1
          GOTO      LVWRD999
.
LVWRD500  MOVE      ZERO,NBPLUR
          PACK      KEY13,OWARD,NADMNO,NBPLUR
          CALL      DENOBE1
.
LVWRD999  RETURN
+
.**************************************************************************
.*  LVCON000  : Update  control file with the Transfer date if neccessary *
.**************************************************************************
LVCON000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY8;*148,CFRDTE
          MATCH     CFRDTE,TDATE
          GOTO      LVCON999 IF NOT LESS
          MOVE      TDATE,CFRDTE
          WRITAB    CONTROLF,EIGHTY8;*148,CFRDTE
.
LVCON999  RETURN
+
.**************************************************************************
.*  LVMEH000  : Update MEH discharge to leave dets. and upd MH visit file *
.**************************************************************************
LVMEH000  MOVE      FOUR,LEAVTYPE           * no type of leave, set as ShortTerm
          MATCH     SP3,CODELTYP
          GOTO      LVMEH100 IF EQUAL       * no type of leave
.
          MOVE      SP1,TCINDC1
          PACK      KEY5,ANST,ANSL,CODELTYP
          CALL      RDCODE1
          REP       "T1H2A3 4",TCINDC1      * set as valid number
          MOVE      TCINDC1,LEAVTYPE        * set the leave type
.
LVMEH100  IF        LEAVTYPE = 1 | LEAVTYPE = 2
            MOVE      AADMNO,MHDLADMN       * admission number
            PACK      MHDLDATE,SVDATE
            REP       " 0",MHDLDATE
            MOVE      TTIME1,MHDLTIME
            PACK      KEY24,MHDLADMN,MHDLDATE,MHDLTIME
            CALL      WRMHDLV1
            MOVE      ONE,AUDIFLAG            * set for add audit
            CALL      MHAUDDLV                * write audits
.
            MOVE      LEAVTYPE,MHVISTAT       * copy to visit file value
            ADD       TWO,MHVISTAT            * convert to correct status
            CALL      UPMHVIS1                * update visit file
            MOVE      THREE,AUDIFLAG          * after change
            CALL      MHAUDVIS                * write audit
.
            CALL      UPMAST1                 * update master file
            CALL      ULSD0000                * Update other legal status dets
          ENDIF
.
LVMEH999  RETURN
+
.**********************************************************************
.*  LVSBY000  :  update standby patient details                       *
.**********************************************************************
LVSBY000  MATCH     ZEROVISN,WADMNO
          GOTO      LVSBY999 IF EQUAL
.
          MOVE      WADMNO,AADMNO
.
. update the files with new patient
.
          MOVE      AADMNO,KEY8
          CALL      RLPTMIS1
          COMPARE   ZERO,OVRCD
          GOTO      LVSBY999 IF NOT EQUAL
.
          MOVE      WWARD,AWARD
          MOVE      WBED,ABED
          CALL      UPMISS1
          CALL      UUPTMIS1
.
LVSBY999  RETURN
+
.*****************************************************************************
.*                              ULSD0000                                     *
.*                  Update the legal status and ls review file               *
.*****************************************************************************
.
ULSD0000  PACK      KEY21,AADMNO,Z30
          CALL      RSMHLEG1
          CALL      RPMHLEG1
          BRANCH    OVRCD,ULSD9999
.
          MATCH     AADMNO,MHLEADMN
          GOTO      ULSD9999 IF NOT EQUAL
.
          MATCH     MHLESTAT,PTMXLS
          GOTO      ULSD9999 IF EQUAL        * didn't change
.
          MOVE      MHLEREVW,SAVREVW
.
          CALL      IBACLOCK
          PACK      DIM8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIM8
          MOVE      CTIMEIS,DIM5
.
          MOVE      AADMNO,MHLEADMN
          MOVE      DIM8,MHLEDATE
          MOVE      DIM5,MHLETIME
          MOVE      PTMXLS,MHLESTAT
          MOVE      ANSN,MHLESENT
          MOVE      SAVREVW,MHLEREVW
          MOVE      SP1,MHLEFLAG
          PACK      MHLESPAR,SP10,SP5
          CALL      WRMHLEG1
.
          MOVE      AADMNO,MHRVADMN
          MOVE      DIM8,MHRVDATE
          MOVE      DIM5,MHRVTIME
          MOVE      ONE,MHRVDECI
          MOVE      PTMXLSDT,MHRVLDAT
          MOVE      DIM5,MHRVLTIM
          MOVE      MHVILSRV,MHRVNDAT
          MOVE      SP3,MHRVREAS
          MOVE      SP20,MHRVSPAR
          CALL      WRMHREV1
.
ULSD9999  RETURN
+
OVERCOND  MOVE      ONE,OVRCD
          RETURN
OVERLOCK  MOVE      TWO,OVRCD
          RETURN
CNOCHG    MOVE      ONE,CDCHG
          RETURN
CNOQST    MOVE      ONE,CQUEST
          GOTO      OVERCOND
STOPDATE  
STOPDATC  DISPLAY   *PCCOL:CVERT,SP10
          RETURN
.
.
.
          INC       CALCDAYS                * calculate #days b/w dates
          INC       DISPITEM                * set up display string
          INC       DSPBGRND                * display background
          INC       DSPFIELD                * display fields
          INC       DXPATCOD                * display for patcodfd 
          INC       IBAPASIO/INC
          INC       MEHCLDSC                * clear discharge variables
          INC       MEHDL1IO/INC            * Discharge to leave file
          INC       MEHDS1IO/INC            * Discharge file
          INC       MEHDXDSC                * Sets up the DATA & ATTRIBUTES
          INC       MEHLEGIO/INC            * legal status file
          INC       MEHLERIO/INC
          INC       MEHREVIO/INC            * LS reveiw file
          INC       MHAUDDLV                * audits for dlv file
          INC       MHAUDVIS                * audits for vis file
          INC       NHOSPDAY    
          INC       NZCOMPIO/INC
          INC       OUTCLIIO/INC
          INC       PATALWDS    
          INC       PATCODKY                * keyin codes file entry
          INC       PATDRGIO/INC
          INC       PATHEAD
          INC       PATHSPIO/INC
          INC       PATALWIO/INC
          INC       PATNOBIO/INC
          INC       PATNUMIO/INC
          INC       PATONLIO/INC
          INC       PATRCAIO/INC
          INC       PATMRGIO/INC
......    INC       PATSWARD
.......   INC       PATWRDDS
          INC       SCRSBGIO/INC            * backgroun file
+
.
 XIF
ENDPRLVE  ENDPROC
