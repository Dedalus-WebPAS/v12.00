.******************************************************************************
.*  File          :  PAT10XMN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.10 Coding Maintenance Routines for PATWEB88     *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.*       V11.02.01  19/07/2022  Bella Turco    TSK 0921957                    *
.*                  Added NOMORE00 - Display "No more records" at end of list *
.*                  Added PTCNNRTD - Displays number of rows set by parameter *
.*                  value                                                     *
.*       V10.10.00  08/03/2017  Davin           TSK 0833093   HDP 2017/2018   *
.*                  Created - cloned from PAT109MN.PBL                        *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 10 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICDXDI00  RESET     PTICD001
          MOVE      "10",ICD10EDN
.                                           * make sure files are open
          OPEN      PATI1XD1,"pat10dxf"     * Open ICD10 Ed.10 Diseases
          OPEN      PATKXDA1,"patkcdxf"     * ICD10 Ed.10 Key Word Diag.
          OPEN      PATKXDA2,"patkcdxf"     * ICD10 Ed.10 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICDXDI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICDXDI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICDXDI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICDXDI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VALXCD00
            GOTO      ICDXDI99
          ENDIF
.
          GOTO      ICDXDI93
.
ICDXDI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICDXDI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT1XD1
          COMPARE   ZERO,OVRCD
          GOTO      ICDXDI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT1XD1      * Writes away the data
          PROC      PATKXDUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDXDI99
.
ICDXDI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT1XD1
          BRANCH    OVRCD,ICDXDI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICDXDI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT1XD1      * Writes away the data
          PROC      PATKXDUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDXDI99
.
ICDXDI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT1XD1
          BRANCH    OVRCD,ICDXDI91
          CALL      DEPT1XD1      * Deletes the data
          PROC      PATKXDUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDXDI99
.
ICDXDI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT1XD1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 10 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICDXDI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICDXDI99
.
ICDXDI91  MOVE      "ICD10 Edition 10 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXDI99
.
ICDXDI92  MOVE      "ICD10 Edition 10 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXDI99
.
ICDXDI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXDI99
.
ICDXDI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 10 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICDXOP00  RESET     PTICO001
          MOVE      "10",ICD10EDN
.
          OPEN      PATI1XO1,"pat10oxf"     * Open ICD10 Ed.10 Operations
          OPEN      PATKXOA1,"patkcoxf"     * ICD10 Ed.10 Key Word Proc.
          OPEN      PATKXOA2,"patkcoxf"     * ICD10 Ed.10 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICDXOP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICDXOP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICDXOP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICDXOP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VALXCD00
            GOTO      ICDXOP99
          ENDIF
.
          GOTO      ICDXOP93
.
ICDXOP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICDXOP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT1XO1
          COMPARE   ZERO,OVRCD
          GOTO      ICDXOP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT1XO1      * Writes away the data
          PROC      PATKXOUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICDXOP99
.
ICDXOP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT1XO1
          BRANCH    OVRCD,ICDXOP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICDXOP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT1XO1      * Writes away the data
          PROC      PATKXOUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICDXOP99
.
ICDXOP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT1XO1
          BRANCH    OVRCD,ICDXOP91
          CALL      DEPT1XO1      * Deletes the data
          PROC      PATKXOUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICDXOP99
.
ICDXOP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT1XO1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 10 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICDXOP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICDXOP99
.
ICDXOP91  MOVE      "ICD10 Edition 10 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXOP99
.
ICDXOP92  MOVE      "ICD10 Edition 10 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXOP99
.
ICDXOP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDXOP99
.
ICDXOP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 10 Code Validation
.------------------------------------------------------------------------------
VALXCD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VALXCD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VALXCD10,VALXCD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VALXCD90
.
VALXCD10  MOVE      VALDCODE,KEY9
          CALL      RDPT1XO1
          IF        OVRCD=0
            MOVE      PT0ODSC2,VALDNAME
          ENDIF
          GOTO      VALXCD90
.
VALXCD20  MOVE      VALDCODE,KEY9
          CALL      RDPT1XD1
          IF        OVRCD=0
            MOVE      PT0DDSC2,VALDNAME
          ENDIF
          GOTO      VALXCD90
.
VALXCD90  CALL      OPENHTML
          BRANCH    EXIT,VALXCD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VALXCD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 10 Procedure Code: Search Table
.-----------------------------------------------------------
SPTXOA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD     * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDVX,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.10 in use yet?
....      GOTO      SPTXOA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPTXO2
SPTXOA10  CALL      RKPTXO2
          BRANCH    OVRCD,SPTXOA90
.
          CALL      SPTXOB00
          BRANCH    OVRCD,SPTXOA10,SPTXOA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT1XO1
          BRANCH    OVRCD,SPTXOA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPTXOA10
          ENDIF
.
          BRANCH    LNK2PRT,SPTXOA20,SPTXOA30
          GOTO      SPTXOA99
.
SPTXOA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
          GOTO      SPTXOA80
.
SPTXOA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODSC2:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
SPTXOA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPTXOA10 IF NOT EQUAL
          GOTO      SPTXOA99
.
SPTXOA90  CALL      NOMORE00        * Display No More Records Message
.
SPTXOA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 10 Procedure Record
.------------------------------------------------------------
SPTXOB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPTXOB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPTXOC00
            BRANCH    EXIT,SPTXOB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPTXO2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPTXOB91 IF EQUAL
.
          GOTO      SPTXOB90
.
SPTXOB90  MOVE      ZERO,OVRCD
          GOTO      SPTXOB99
SPTXOB91  MOVE      ONE,OVRCD
          GOTO      SPTXOB99
SPTXOB92  MOVE      TWO,OVRCD
SPTXOB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 10 Procedure Words
.------------------------------------------------------------
SPTXOC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPTXOC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPTXOC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPTXO1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPTXOC10 IF EQUAL
.
          CALL      RKPTXO1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPTXOC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPTXOC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPTXOC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPTXOC10              * Check Next Search Word
.
SPTXOC90  MOVE      ONE,EXIT
SPTXOC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 10 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPTXDA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD     * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDVX,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.10 in use yet?
....      GOTO      SPTXDA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPTXD2
SPTXDA10  CALL      RKPTXD2
          BRANCH    OVRCD,SPTXDA90
.
          CALL      SPTXDB00
          BRANCH    OVRCD,SPTXDA10,SPTXDA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT1XD1
          BRANCH    OVRCD,SPTXDA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPTXDA10
          ENDIF
.
          BRANCH    LNK2PRT,SPTXDA20,SPTXDA30
          GOTO      SPTXDA99
.
SPTXDA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
          GOTO      SPTXDA80
.
SPTXDA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDSC2:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
SPTXDA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPTXDA10 IF NOT EQUAL
          GOTO      SPTXDA99
.
SPTXDA90  CALL      NOMORE00   * Display No More Records Message
.
SPTXDA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 10 Diagnosis Record
.------------------------------------------------------------
SPTXDB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPTXDB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPTXDC00
            BRANCH    EXIT,SPTXDB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPTXD2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPTXDB91 IF EQUAL
.
          GOTO      SPTXDB90
.
SPTXDB90  MOVE      ZERO,OVRCD
          GOTO      SPTXDB99
SPTXDB91  MOVE      ONE,OVRCD
          GOTO      SPTXDB99
SPTXDB92  MOVE      TWO,OVRCD
SPTXDB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 10 Diagnosis Words
.------------------------------------------------------------
SPTXDC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPTXDC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPTXDC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPTXD1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPTXDC10 IF EQUAL
.
          CALL      RKPTXD1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPTXDC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPTXDC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPTXDC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPTXDC10              * Check Next Search Word
.
SPTXDC90  MOVE      ONE,EXIT
SPTXDC99  RETURN
