.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : DXOUTREF.PBL                                              *
.*                                                                            *
.* Function       : To Display all the variables for Outpatient Referrals     *
.*                                                                            *
.* Author         : Justin Coates           03/11/1995                        *
.*                                                                            *
.* Parameters     : SCRPSTFD record                                           *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V11.04.01 27/11/2023  Peter Vela     TSK 0935874                           *
.*           Added validation for PTCNUNET = 3 at DOTRF055                    *
.* V10.11.01 23/08/2017  Thanh T.       TSK 0821710                           *
.*           Audit Amission/outpatient visit comments                         *
.******************************************************************************
.*       V10.07.01  29/10/2015  Ebon Clements     CAR 268970                  *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*       V10.04.01  25/02/2014  Don Nguyen        CAR 291879                  *
.*                  Added field 55 - IHI Number                               *
.*        V9.12.01  16/10/2009  Ebon Clements     CAR 190680                  *
.*                  Fixed pack of VAR after calls to CMPH0000                 *
.*        V9.10.01  09/09/2008  Ebon Clements     CAR 175728                  *
.*                  Added item 53 - Compensable/Heath Fund Name               *
.*                  Added item 54 - Compensable/Heath Fund Number             *
.*        V9.08.02  18/09/2007  Steve Armstrong    CAR 124435                 *
.*                  Mods for items:                                           *
.*                     50 - Claim Code                                        *
.*                     51 - Claim Code Description                            *
.*                     52 - ACC Number                                        *
.*        V9.08.01  02/03/2007  Ebon Clements      CAR 132645                 *
.*                  Save current open site prefix file in CFILEPRE            *
.******************************************************************************
.*        V9.04.01  07/04/2005  Steve Armstrong    CAR 59639                  *
.*                  Mods for OUTRF1FD _ new variable - Item 49, Outpatient    *
.*                  number for labels                                         *
.******************************************************************************
.*        V9.03.01  11/12/2003  Ebon Clements         CAR 45549               *
.*                  Mods for OUTRF1FD - change from doctors file to hcp file  *
.*                  14/07/2001  Sandra Barcham                                *
.*                  Replace GP with HCP                                       *
.*                  13/03/2000  Steve Armstrong                               *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.*                  17/7/96 Whirlpool  sr 641765                              *
.*                  Fixed display of clinic id Description if blank           *
.******************************************************************************
DXOUTREF  MOVE      SCPSITM,FIELDNO
          BRANCH    FIELDNO,DOTRF001,DOTRF002,DOTRF003,DOTRF004,DOTRF005:
                            DOTRF006,DOTRF007,DOTRF008,DOTRF009,DOTRF010: 10
                            DOTRF011,DOTRF012,DOTRF013,DOTRF014,DOTRF015:
                            DOTRF016,DOTRF017,DOTRF018,DOTRF019,DOTRF020: 20
                            DOTRF021,DOTRF022,DOTRF023,DOTRF024,DOTRF025:
                            DOTRF026,DOTRF027,DOTRF028,DOTRF029,DOTRF030: 30
                            DOTRF031,DOTRF032,DOTRF033,DOTRF034,DOTRF035:
                            DOTRF036,DOTRF037,DOTRF038,DOTRF039,DOTRF040: 40
                            DOTRF041,DOTRF042,DOTRF043,DOTRF044,DOTRF045:
                            DOTRF046,DOTRF047,DOTRF048,DOTRF049,DOTRF050: 50
                            DOTRF051,DOTRF052,DOTRF053,DOTRF054,DOTRF055
          GOTO      DOTRF999
.
DOTRF001  UNPACK    OTRLDATE,CCENT,CYEAR,CMON,CDAY    * Letter Date
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          GOTO      DOTRF995
.
DOTRF002  PACK      VAR,OTRLRDOC,SP70,SP20
          GOTO      DOTRF995
.
DOTRF003  PACK      VAR,SP20,SP30,SP20
          MATCH     SP8,OTRLRDOC
          GOTO      DOTRF995 IF EQUAL
          PACK      KEY10,OTRLRDOC,SP70
          MOVE      "Invalid Code",PMHCSNAM
          MOVE      SP35,PMHCGNAM
          CALL      RDPMHCP1
          PACK      VAR,PMHCSNAM,SP70,SP10
          STRIP     VAR
          ENDSET    VAR
          APPEND    SP1,VAR
          APPEND    PMHCGNAM,VAR
          APPEND    SP70,VAR
          RESET     VAR
          GOTO      DOTRF999
.
DOTRF004  PACK      VAR,OTRLCONS,SP70,SP20
          GOTO      DOTRF995
.
DOTRF005  PACK      VAR,SP70,SP20
          CALL      DSIT0000            * get the site code 
          BRANCH    EXIT,DOTRF995
.
          PACK      KEY20,OTRLSITE,OTRLCONS,OTRLDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTRLSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTRLCONS,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,DOTRF995      * Clinic record not found
.
          PACK      VAR,OCADESC,SP70,SP20
          MATCH     SP6,OCADOCT
          GOTO      DOTRF995 IF EQUAL   * no doctor code
.
          MOVE      OCADOCT,KEY6        * use doctor code
          CALL      RDDOCT1
          BRANCH    OVRCD,DOTRF995
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          PACK      VAR,PACFNAME,SP70
          GOTO      DOTRF995
.
DOTRF006  PACK      VAR,OTRLDEPT,SP70,SP10
          GOTO      DOTRF995
.
DOTRF007  MOVE      "A ",TCODE
          MOVE      OTRLDEPT,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF008  PACK      VAR,OTRLDIAG,SP70,SP10
          GOTO      DOTRF995
.
DOTRF009  PACK      VAR,OTRLPRIO,SP70,SP10
          GOTO      DOTRF995
.
DOTRF010  MOVE      "PR",TCODE
          MOVE      OTRLPRIO,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF011  PACK      VAR,OTRLUSD1,SP70,SP10
          GOTO      DOTRF995
.
DOTRF012  MOVE      "L1",TCODE
          MOVE      OTRLUSD1,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF013  PACK      VAR,OTRLUSD2,SP70,SP10
          GOTO      DOTRF995
.
DOTRF014  MOVE      "L2",TCODE
          MOVE      OTRLUSD2,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF015  PACK      VAR,OTRLUSD3,SP70,SP10
          GOTO      DOTRF995
.
DOTRF016  MOVE      "L3",TCODE
          MOVE      OTRLUSD3,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF017  PACK      VAR,OTRLUSD4,SP70,SP10
          GOTO      DOTRF995
.
DOTRF018  MOVE      "L4",TCODE
          MOVE      OTRLUSD4,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF019  PACK      VAR,VSMTCMNT,SP70,SP10    * Comments
          GOTO      DOTRF995
.
DOTRF020  PACK      VAR,OTRLRFGP,SP70,SP10
          GOTO      DOTRF995
.
DOTRF021  PACK      VAR,SP20,SP30,SP20
          MATCH     SP8,OTRLRFGP
          GOTO      DOTRF995 IF EQUAL
          PACK      KEY10,OTRLRFGP,SP70
          MOVE      "Invalid Code",PMHCSNAM
          MOVE      SP35,PMHCGNAM
          CALL      RDPMHCP1
          PACK      VAR,PMHCSNAM,SP70,SP10
          STRIP     VAR
          ENDSET    VAR
          APPEND    SP1,VAR
          APPEND    PMHCGNAM,VAR
          APPEND    SP70,VAR
          RESET     VAR
          GOTO      DOTRF995
.
DOTRF022  MOVE      "-",ANS
          MATCH     SP2,OTRLGPPT
          IF        @EQUAL
            MOVE      SP1,ANS
          ENDIF
          PACK      VAR,OTRLGPPC,ANS,OTRLGPPT,SP70,SP10
          GOTO      DOTRF995
.
DOTRF023  PACK      VAR,SP20,SP30,SP20
          MATCH     SP8,OTRLGPPC
          GOTO      DOTRF995 IF EQUAL
          PACK      KEY10,OTRLGPPC,SP70
          PACK      KEY12,KEY10,OTRLGPPT,SP70
          MOVE      "Invalid Code",PMHGPNAM
          CALL      RDPMHCG1
          PACK      VAR,PMHGPNAM,SP70,SP10
          GOTO      DOTRF995
.
DOTRF024  PACK      VAR,OTRLRQST,SP70,SP10
          GOTO      DOTRF995
.
DOTRF025  UNPACK    OTRLCDTE,CCENT,CYEAR,CMON,CDAY    * Letter Date
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          GOTO      DOTRF995
.
DOTRF026  PACK      VAR,OTRLREAS,SP70,SP10
          GOTO      DOTRF995
.
DOTRF027  MOVE      "RQ",TCODE
          MOVE      OTRLREAS,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF028  PACK      VAR,OTRLSPFC,SP70,SP10
          GOTO      DOTRF995
.
DOTRF029  MOVE      "OF",TCODE
          MOVE      OTRLSPFC,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF030  PACK      VAR,OTRLCAD1,SP70,SP10
          GOTO      DOTRF995
.
DOTRF031  PACK      VAR,OTRLCAD2,SP70,SP10
          GOTO      DOTRF995
.
DOTRF032  PACK      VAR,OTRLCAD3,SP70,SP10
          GOTO      DOTRF995
.
DOTRF033  PACK      VAR,OTRLCAD4,SP70,SP10
          GOTO      DOTRF995
.
DOTRF034  PACK      VAR,OTRLCPC,SP70,SP10
          GOTO      DOTRF995
.
DOTRF035  PACK      VAR,OTRLECRN,SP70,SP10
          GOTO      DOTRF995
.
DOTRF036  PACK      VAR,OTRLGPFH,SP70,SP10
          GOTO      DOTRF995
.
DOTRF037  PACK      VAR,OTRLRFGP,SP70,SP10  * GP code - no practice
          GOTO      DOTRF995
.
DOTRF038  PACK      VAR,OTRLCTYP,SP70,SP10  * Clinic Type            
          GOTO      DOTRF995
.
DOTRF039  MOVE      SP70,OCTDESC 
          MOVE      SP70,OCTGRP
          PACK      VAR,SP20,SP20,SP20,SP20
          CALL      DSIT0000            * get the site code 
          BRANCH    EXIT,DOTRF995
.
          PACK      KEY12,OTRLSITE,OTRLCTYP,SP10
          CALL      RDCTYA1             * Read the clinic type  
          PACK      VAR,OCTGRP,SP1,OCTDESC,SP70,SP20
          GOTO      DOTRF995
.
DOTRF040  PACK      VAR,OTRLSRCE,SP70,SP10  
          GOTO      DOTRF995
.
DOTRF041  MOVE      "S ",TCODE
          MOVE      OTRLSRCE,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF042  PACK      VAR,OTRLRHCP,SP70,SP10  
          GOTO      DOTRF995
.
DOTRF043  MOVE      OTRLRHCP,KEY6
          CALL      DXPATDOC
          GOTO      DOTRF999
.
DOTRF044  UNPACK    OTRLDREC,CCENT,CYEAR,CMON,CDAY  
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          GOTO      DOTRF995
.
DOTRF045  UNPACK    OTRLDPRI,CCENT,CYEAR,CMON,CDAY  
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          GOTO      DOTRF995
.
DOTRF046  PACK      VAR,OTRLPURC,SP70,SP10  
          GOTO      DOTRF995
.
DOTRF047  MOVE      "HP",TCODE
          MOVE      OTRLPURC,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF048  PACK      VAR,OTRLRANK,SP70,SP10  
          GOTO      DOTRF995
.
DOTRF049  PACK      VAR,OTRLOUTN,SP70,SP10  
          GOTO      DOTRF995
.
DOTRF050  PACK      VAR,OTRLCLAM,SP70,SP10       * claim code
          GOTO      DOTRF995
.
DOTRF051  MOVE      "CL",TCODE                   * claim code description
          MOVE      OTRLCLAM,ACODE
          CALL      DXPATCOD
          GOTO      DOTRF999
.
DOTRF052  PACK      VAR,SP70,SP10
          OPEN      PATWC1A1,"patwc1af"          * ACC Number
          MOVE      OTRLOUTN,KEY8
          CALL      RDWCOM1
          IF        OVRCD = 0
            MATCH     SP20,WCCLMNO
            IF        !@EQUAL
              PACK      VAR,WCCLMNO,SP70,SP10  
            ENDIF
          ENDIF
          GOTO      DOTRF995
.
DOTRF053  MOVE      OTRLCLAM,CLAIMCOD
          CALL      CMPH0000
          PACK      VAR,CLMDET1,SP70,SP10
          GOTO      DOTRF995
.
DOTRF054  MOVE      OTRLCLAM,CLAIMCOD
          CALL      CMPH0000
          PACK      VAR,CLMDET2,SP70,SP10
          GOTO      DOTRF995
.
DOTRF055  PACK      VAR,SP70,SP70
          MATCH     "1",PTCNUNET
          IF        @EQUAL
            CALL      GTIHINUM                   * IHI Number
            MOVE      IHINUMBR,VAR
          ELSE
            MATCH     "3",PTCNUNET
            IF        @EQUAL
              CALL      GTIHINUM                   * IHI Number
              LJUSTIFY  IHINUMBR
              UNPACK    IHINUMBR,DIM4,DIM4A,DIM4B,DIM4C
              PACK      VAR,DIM4,SP1,DIM4A,SP1,DIM4B,SP1,DIM4C
            ENDIF
          ENDIF
          GOTO      DOTRF995
.
DOTRF995  CALL      DISPITEM
.
DOTRF999  RETURN
.
.*********************************************************************
.  DSIT0000 - Get the Site Code
.*********************************************************************
DSIT0000  COMPARE ZERO,OTRLTREF
          GOTO    DSIT9500 IF NOT EQUAL
.
          OPEN      OUTSITA1,"outsitaf" * Open the site file
. 
          MATCH   SP6,OTRLSITE
          GOTO    DSIT9500 IF EQUAL
.
          MOVE    OTRLSITE,KEY6
          CALL    RDSITA1
          BRANCH  OVRCD,DSIT9500
.
          MOVE      OSTFILE,CFILEPRE   * Save Current File Prefix
.
          PACK      CFNAME,OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCTYA2
          OPEN      OUTCTYA2,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCTYA3
          OPEN      OUTCTYA3,CFNAME
.
          MOVE    ZERO,EXIT
          GOTO    DSIT9999
.
DSIT9500  MOVE    ONE,EXIT
.
DSIT9999  RETURN
