.----------------------------------------------------------------
. Standard Screen Painter Code Routines
.----------------------------------------------------------------
. GLOAD000 - Used to Initialize Graphics Characters Array 
. KMOVE000 - Arrow Key Movements in Area 
.            MINCOL,MAXC0L MINLINE,MAXLINE
. PUTCH000 - Put Character on screen with video display
. SCAN0000 - Scan Surrounding Positions for Joins
. VGRAP000 - Vertical Graphics Display
. HGRAP000 - Horizontal Graphics Display
. GMOVE000 - Arrow Key Movements Graphic in area 
.            MINCOL, MAXCOL, MINLINE,MAXLINE
.----------------------------------------------------------------
.----------------------------------------------------------------
. GLOAD000 - used to load the graphics characters into an array 
.----------------------------------------------------------------
GLOAD000  
          DISPLAY   *P1:24,*G1,*G2,*G3,*G4,*G5,*G6,*G7,*G8,*G9,*G10:
                           *G11,*G12,*G13,*G14,*G15,*G16,*G17,*G18,*G19,*G20:
                           *G21,*G22,*G23,*G24,*G25,*G26,*G27,*G28,*G29,*G30:
                           *G31,*G32,*G33,*G34,*G35,*G36,*G37,*G38,*G39,*G40:
                           *G41,*G42,*G43,*G44,*G45
          GETVAR    GRAPHIC,GRAPATTB,1,24
          UNPACK    GRAPHIC,G[1],G[2],G[3],G[4],G[5]:
                            G[6],G[7],G[8],G[9],G[10]:
                            G[11],G[12],G[13],G[14],G[15]:
                            G[16],G[17],G[18],G[19],G[20]:
                            G[21],G[22],G[23],G[24],G[25]:
                            G[26],G[27],G[28],G[29],G[30]:
                            G[31],G[32],G[33],G[34],G[35]:
                            G[36],G[37],G[38],G[39],G[40]:
                            G[41],G[42],G[43],G[44],G[45]
          DISPLAY   *P1:24,*EL
          MOVE      "0011",AGRAPHIC[1]
          MOVE      "0110",AGRAPHIC[2]
          MOVE      "1001",AGRAPHIC[3]
          MOVE      "1100",AGRAPHIC[4]
          MOVE      "0022",AGRAPHIC[5]
          MOVE      "0220",AGRAPHIC[6]
          MOVE      "2002",AGRAPHIC[7]
          MOVE      "2200",AGRAPHIC[8]
          MOVE      "0021",AGRAPHIC[9]
          MOVE      "0120",AGRAPHIC[10]
          MOVE      "2001",AGRAPHIC[11]
          MOVE      "2100",AGRAPHIC[12]
          MOVE      "0012",AGRAPHIC[13]
          MOVE      "0210",AGRAPHIC[14]
          MOVE      "1002",AGRAPHIC[15]
          MOVE      "1200",AGRAPHIC[16]
          MOVE      "1111",AGRAPHIC[17]
          MOVE      "0111",AGRAPHIC[18]
          MOVE      "1101",AGRAPHIC[19]
          MOVE      "1110",AGRAPHIC[20]
          MOVE      "1011",AGRAPHIC[21]
          MOVE      "2222",AGRAPHIC[22]
          MOVE      "0222",AGRAPHIC[23]
          MOVE      "2202",AGRAPHIC[24]
          MOVE      "2220",AGRAPHIC[25]
          MOVE      "2022",AGRAPHIC[26]
          MOVE      "2121",AGRAPHIC[27]
          MOVE      "0121",AGRAPHIC[28]
          MOVE      "2101",AGRAPHIC[29]
          MOVE      "2120",AGRAPHIC[30]
          MOVE      "2021",AGRAPHIC[31]
          MOVE      "1212",AGRAPHIC[32]
          MOVE      "0212",AGRAPHIC[33]
          MOVE      "1202",AGRAPHIC[34]
          MOVE      "1210",AGRAPHIC[35]
          MOVE      "1012",AGRAPHIC[36]
          MOVE      "1010",AGRAPHIC[37]
          MOVE      "2020",AGRAPHIC[38]
          MOVE      "0101",AGRAPHIC[39]
          MOVE      "0202",AGRAPHIC[40]
          RETURN
.----------------------------------------------------------------------
. PUTCH000 - Put Character on screen with video display
.----------------------------------------------------------------------
PUTCH000  
          MOVE      CATTR,NEWATTR
          AND       ATTGRAPH,NEWATTR
          IF        FV2LON
            OR        ATTV2LON,NEWATTR
          ENDIF
          IF        FHON  
            OR        ATTHON,NEWATTR
          ENDIF
          IF        FULON  
            OR        ATTULON,NEWATTR
          ENDIF
          IF        FBLINK
            OR        ATTBLINK,NEWATTR
          ENDIF
          PUTVAR    CDATA,NEWATTR,CCOL,CVERT
          RETURN
.
.-----------------------------------------------
. GMOVE000 - Arrow Key Movements Graphic in area 
.            MINCOL, MAXCOL, MINLINE,MAXLINE
.-----------------------------------------------
GMOVE000  MOVE      ONE,EXIT
          MOVE      SP1,CDATA
          CMOVE     ATTNORM,CATTR
          IF        LINETYPE
            CMOVE     ATTGRAPH,CATTR
          ENDIF
          GOTO      GMOVE100 IF LEFT
          GOTO      GMOVE200 IF RIGHT
          GOTO      GMOVE300 IF UP
          GOTO      GMOVE400 IF DOWN
          MOVE      ZERO,EXIT
          GOTO      GMOVE999
.
GMOVE100  COMPARE   MINCOL,CCOL
          GOTO      GMOVE999 IF EQUAL
.
          CALL      SCAN0000
          MOVE      LINETYPE,RLEF
          CALL      HGRAP000
. 
          SUB       ONE,CCOL
          GOTO      GMOVE999
.
GMOVE200  COMPARE   MAXCOL,CCOL
          GOTO      GMOVE999 IF EQUAL
.
          CALL      SCAN0000
          MOVE      LINETYPE,RRIG
          CALL      HGRAP000
.
          ADD       ONE,CCOL
          GOTO      GMOVE999
.
GMOVE300  COMPARE   MINLINE,CVERT
          GOTO      GMOVE999 IF EQUAL
.
          CALL      SCAN0000
          MOVE      LINETYPE,RTOP
          CALL      VGRAP000
.
          SUB       ONE,CVERT
          GOTO      GMOVE999
GMOVE400  COMPARE   MAXLINE,CVERT
          GOTO      GMOVE999 IF EQUAL
.
          CALL      SCAN0000
          MOVE      LINETYPE,RBOT
          CALL      VGRAP000
.         
          ADD       ONE,CVERT
          GOTO      GMOVE999
GMOVE999  RETURN
.----------------------------------------------------------------------
. HGRAP000 - Horizontal Graphics Display
.----------------------------------------------------------------------
HGRAP000  IF        LINETYPE
            CMOVE     ATTGRAPH,CATTR
            PACK      DIM4,RLEF,RTOP,RRIG,RBOT
            SEARCH    DIM4,AGRAPHIC[1],FI40,SUBSTR
            IF        @EQUAL
              PUTVAR    G[SUBSTR],CATTR,CCOL,CVERT
            ELSE
              PACK      REPSTR,ONE,LINETYPE,TWO,LINETYPE
              REP       REPSTR,DIM4
              SEARCH    DIM4,AGRAPHIC[1],FI40,SUBSTR
              IF        @EQUAL
                PUTVAR    G[SUBSTR],CATTR,CCOL,CVERT
              ELSE
                LOAD      CDATA,LINETYPE,G[37],G[38],G[GCHARNUM]
                PUTVAR    CDATA,CATTR,CCOL,CVERT
              ENDIF
            ENDIF
          ELSE
            CMOVE     ATTNORM,CATTR
            PUTVAR    SP1,CATTR,CCOL,CVERT
          ENDIF
          RETURN
.----------------------------------------------------------------------
. VGRAP000 - Vertical Graphics Display
.----------------------------------------------------------------------
VGRAP000  IF        LINETYPE
            CMOVE     ATTGRAPH,CATTR
            PACK      DIM4,RLEF,RTOP,RRIG,RBOT
            MOVE      ZERO,SUBSTR
            SEARCH    DIM4,AGRAPHIC[1],FI40,SUBSTR
            IF        @EQUAL
              PUTVAR    G[SUBSTR],CATTR,CCOL,CVERT
            ELSE
              PACK      REPSTR,ONE,LINETYPE,TWO,LINETYPE
              REP       REPSTR,DIM4
              SEARCH    DIM4,AGRAPHIC[1],FI40,SUBSTR
              IF        @EQUAL
                PUTVAR    G[SUBSTR],CATTR,CCOL,CVERT
              ELSE
                LOAD      CDATA,LINETYPE,G[39],G[40],G[GCHARNUM]
                PUTVAR    CDATA,CATTR,CCOL,CVERT
              ENDIF
            ENDIF
          ELSE
            CMOVE     ATTNORM,CATTR
            PUTVAR    SP1,CATTR,CCOL,CVERT
          ENDIF
          RETURN
          
.----------------------------------------------------------------------
. SCAN000 - Scan Surrounding Positions for Joins
.----------------------------------------------------------------------
SCAN0000
.
. Left of Position
.         
          MOVE      ZERO,RTOP
          MOVE      ZERO,RBOT
          MOVE      ZERO,RLEF
          MOVE      ZERO,RRIG
          RESET     GRAPHIC
          IF        CCOL>MINCOL
            SUB       ONE,CCOL
            GETVAR    CDATA,CATTR,CCOL,CVERT
            ADD       ONE,CCOL
            CMATCH    CATTR,ATTGRAPH
            IF        @EQUAL|@LESS
              SCAN      CDATA,GRAPHIC
              IF        @EQUAL
                MOVEFPTR  GRAPHIC,SUBSTR
                UNPACK    AGRAPHIC[SUBSTR],DIM2,RLEF
              ENDIF
            ENDIF
          ENDIF
.
. Right of Position
.         
          RESET     GRAPHIC
          IF        CCOL<MAXCOL
            ADD       ONE,CCOL
            GETVAR    CDATA,CATTR,CCOL,CVERT
            SUB       ONE,CCOL
            CMATCH    CATTR,ATTGRAPH
            IF        @EQUAL|@LESS
              SCAN      CDATA,GRAPHIC
              IF        @EQUAL
                MOVEFPTR  GRAPHIC,SUBSTR
                UNPACK    AGRAPHIC[SUBSTR],RRIG
              ENDIF
            ENDIF
          ENDIF
.
. Top of Position
.         
          RESET     GRAPHIC
          IF        CVERT>MINLINE
            SUB       ONE,CVERT
            GETVAR    CDATA,CATTR,CCOL,CVERT
            ADD       ONE,CVERT
            CMATCH    CATTR,ATTGRAPH
            IF        @EQUAL|@LESS
              SCAN      CDATA,GRAPHIC
              IF        @EQUAL
                MOVEFPTR  GRAPHIC,SUBSTR
                UNPACK    AGRAPHIC[SUBSTR],DIM3,RTOP
              ENDIF
            ENDIF
          ENDIF
.
. Bottom of Position
.         
          RESET     GRAPHIC
          IF        CVERT<MAXLINE
            ADD       ONE,CVERT
            GETVAR    CDATA,CATTR,CCOL,CVERT
            SUB       ONE,CVERT
            CMATCH    CATTR,ATTGRAPH
            IF        @EQUAL|@LESS
              SCAN      CDATA,GRAPHIC
              IF        @EQUAL
                MOVEFPTR  GRAPHIC,SUBSTR
                UNPACK    AGRAPHIC[SUBSTR],DIM1,RBOT
              ENDIF
            ENDIF
          ENDIF
          RESET     GRAPHIC
.
SCAN9999  RETURN
.----------------------------------------
. Arrow Key Movements in area MINCOL,MAXCOL MINLINE,MAXLINE
.----------------------------------------
KMOVE000  MOVE      ONE,EXIT
          GOTO      KMOVE100 IF LEFT
          GOTO      KMOVE200 IF RIGHT
          GOTO      KMOVE300 IF UP
          GOTO      KMOVE400 IF DOWN
          MOVE      ZERO,EXIT
          GOTO      KMOVE999
KMOVE100  COMPARE   MINCOL,CCOL
          GOTO      KMOVE999 IF EQUAL
          SUB       ONE,CCOL
          GOTO      KMOVE999
KMOVE200  COMPARE   MAXCOL,CCOL
          GOTO      KMOVE999 IF EQUAL
          ADD       ONE,CCOL
          GOTO      KMOVE999
KMOVE300  COMPARE   MINLINE,CVERT
          GOTO      KMOVE999 IF EQUAL
          SUB       ONE,CVERT
          GOTO      KMOVE999
KMOVE400  COMPARE   MAXLINE,CVERT
          GOTO      KMOVE999 IF EQUAL
          ADD       ONE,CVERT
          GOTO      KMOVE999
KMOVE999  RETURN
.
