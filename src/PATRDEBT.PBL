.PATRDEBT.PBL
.-------------------------------------------------------------------------
.         Write a record to the Reassign Debt file
.         after raising Patient Only invoice for Inpat/Outpat/EMR
.         (IPATFLAG=1 or 2)
.-------------------------------------------------------------------------
.
          DEFPROC   PATRDEBT
.
          INC       PATRASFD/INC
          INC       PATUREFD/INC
.
          OPEN      PATRASA1,"patrasaf"
          OPEN      PATUREA1,"patureaf"
.
.-------------------------------------------------------------------------
.         Write a record to the Reassign Debt file
.         after raising Patient Only invoice (IPATFLAG=1)
.-------------------------------------------------------------------------
RDEB0000  MOVE      "dm",KEY2
.
.         Get the first code for category dm with ind1=P
.
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
RDEB1000  CALL      RDKCODE1
          BRANCH    OVRCD,RDEB9999
.
          MATCH     KEY2,TCODE
          GOTO      RDEB9999 IF NOT EQUAL
.
          MATCH     "P",TCINDC1
          GOTO      RDEB1000 IF NOT EQUAL
.
          CALL      RECN0000         * Update Unreconciled invoice file
.
          MOVE      ONE,FORM2
.
.         Get the last counter
.
          PACK      KEY10,PINVNO,Z70
          CALL      RSPTRAS1
          CALL      RPPTRAS1
          IF        OVRCD=0
            MATCH     PINVNO,PTRAINVN      * Same invoice ?
            IF        @EQUAL
              MOVE      PTRACNTN,FORM2
            ENDIF
          ENDIF
.
RDEB2000  MOVE      FORM2,PTRACNTN
          PACK      KEY10,PINVNO,PTRACNTN,SP70
          CALL      RDPTRAS1
          IF        OVRCD=0
            ADD       ONE,FORM2
            GOTO      RDEB2000
          ENDIF
          PACK      CURRDATE,CCC,CYY,CMM,CDD  * write a new "patrasaf" record
          REP       " 0",CURRDATE
          MOVE      FORM2,PTRACNTN
          MOVE      PINVNO,PTRAINVN
          MOVE      "3",PTRASYST            * Inpatient
          MOVE      PTUROSTD,PTRAOUST       * Outstanding amount
          MOVE      ACODE,PTRARMOV          * Reason of Movement
          MOVE      SP70,PTRADBAG           * Debt Collection Agency (code)
          MOVE      PTUROSTD,PTRAPPOR       * Patient portion=Outstanding amt
          MOVE      PINVBLCD,PTRAIBAL       * Date invoice balanced
          MOVE      "0",PTRADELE            * set Deleted flag off
.
          CALL      IBACLOCK
          MOVE      CURRDATE,PTRACDAT
          MOVE      CTIMEIS,PTRACTIM
          MOVE      WBSEUID,PTRACUID        * WEB user id
.
          UNPACK    SP70,PTRAUDAT,PTRAUTIM,PTRAUUID
          CALL      WRPTRAS1
          GOTO      RDEB9999
+
.------------------------------------------------------------
.         Write a record to the Unreconciled invoice file
.         after raising Patient Only invoice (IPATFLAG=1)
.------------------------------------------------------------
RECN0000  MOVE      CNEXTINV,KEY8
          CALL      RDPTURE1             * shouldn't have record in unreconciled
          COMPARE   ZERO,OVRCD           * file as the invoice had just been
          GOTO      RECN9999 IF EQUAL    * raised.
.
          CALL      RDINV1
          BRANCH    OVRCD,RECN9999
.
          MOVE      PINVAMT,PTUROSTD     * Invoice amount +
          ADD       PINVPATA,PTUROSTD    * Patient payments +
          ADD       PINVHFDA,PTUROSTD    * H.F payments +
          ADD       PINVOTHA,PTUROSTD    * Other payments +
          ADD       PTINCRTT,PTUROSTD    * Credit Notes +
          ADD       PINVJOUR,PTUROSTD    * Journals = amount outstanding
          ADD       PTINGSTJ,PTUROSTD    * Journal GST = amount outstanding
.
          MOVE      PINVNO,PTURINVN
          MOVE      PINVADM,PTURVISN
          MOVE      "0",PTURSTAT                * New status
          MOVE      PINVSYS,PTURSYST
          MOVE      ACLAIM,PTURCLMN
          MOVE      AFUNDH,PTURHFND
          MOVE      AFNDTB,PTURHTAB
.
          MOVE      PINVAMT,PTURINVT     * Invoice total
          MOVE      PINVPATA,PTURPAYM    * Patient payments +
          ADD       PINVHFDA,PTURPAYM    * H.F payments +
          ADD       PINVOTHA,PTURPAYM    * Other payments +
.
          MOVE      PTINCRTT,PTURJRNL    * Credit Notes +
          ADD       PINVJOUR,PTURJRNL    * Journals = amount outstanding
          ADD       PTINGSTJ,PTURJRNL    * Journal GST = amount outstanding
.
          CALL      IBACLOCK
          PACK      PTURCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURCDAT
          MOVE      CTIMEIS,PTURCTIM
          MOVE      WBSEUID,PTURCUID        * WEB user id
.
          UNPACK    SP70,PTURUDAT,PTURUTIM,PTURUUID
          CALL      WRPTURE1
.
RECN9999  RETURN
+
          INC       IBAOVRIO/INC
          INC       PATRASIO/INC
          INC       PATUREIO/INC
.
RDEB9999  ENDPROC
+

