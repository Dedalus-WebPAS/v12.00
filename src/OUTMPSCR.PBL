.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : OUTMPSCR.PBL                                              *
.*                                                                            *
.* Function       : This is the code to PRINT the screens for the Clinic      *
.*                  Master Master Listing and Enquiry programs.               *
.*                                                                            *
.* Author         : Justin Coates      23/10/1995                             *
.*                                                                            *
.* Modifications  :                                                           *
.*                  V11.00.01 23/03/2020  Peter Vela     TSK 0889143          *
.*                  Fixed OUTLET IO calls                                     *
.*                  V10.07.01 29/10/2015  Ebon Clements  CAR 268970           *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*                  16/03/2000  Steve Armstrong   5.08 Mods                   *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.*                  07/04/1997  Steve Armstrong   SRF 642553                  *
.*                  Fixed printing of 2 lines at bottom of page               *
.******************************************************************************
+
.*********************************************************************
.         print screen A
.*********************************************************************
PRNA0000  PACK      KEY5 WITH CODECI,OMACIND
          CALL      RDCODE1
          MOVE      TDESC,DIM11
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OMASITE,OMACLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OMASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OMACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            GOTO      PRNA0100
          ENDIF
.
          MATCH     SP30 TO OCADESC
          GOTO      PRNA0100 IF NOT EQUAL
.
          PACK      KEY6 WITH OCADOCT
          CALL      RDDOCT1
          MOVE      DGNAME TO ANS
          PACK      OCADESC WITH ANS,DOT,DSNAME
.
PRNA0100  MOVE      OCADESC TO DIM16
          MOVE      OCADESC TO DIM24
.
          LOAD      DKYDAY BY OMADAY FROM DMON2,DTUE2,DWED2,DTHU2:
                    DFRI2,DSAT2,DSUN2
.
          LOAD      DDESC BY OMADAY FROM DMON,DTUE,DWED,DTHU:
                    DFRI,DSAT,DSUN
.
          MOVE      DMONTH,DESCFRQ
          LOAD      DESCFRQ BY OMAFREQF FROM DYEAR
.
          CMATCH    ANSY TO OMAREG
          GOTO      PRNA0300 IF NOT EQUAL
          MOVE      "Yes" TO DESCREG
          GOTO      PRNA0500
.
PRNA0300  MOVE      "No " TO DESCREG
.
PRNA0500  CMATCH    ANSY TO OMAKDOC
          GOTO      PRNA0700 IF NOT EQUAL
.
          MOVE      "Yes" TO DESCDOC
          GOTO      PRNA0900
.
PRNA0700  MOVE      "No " TO DESCDOC
.
PRNA0900  MOVE      SP8 TO DSTAT
          COMPARE   ZERO TO OMASTAT
          GOTO      PRNA1100 IF NOT EQUAL
          MOVE      "Active  " TO DSTAT
          GOTO      PRNA1300
.
PRNA1100  MOVE      "Inactive" TO DSTAT
.
PRNA1300  CALL      UND132
          MOVE      EIGHT,CLNO
          PRINT     *1,"|",*3,"Clinic Id",*18,": ",OMACLIN,*28,OCADESC,*132,"|":
                    *N,"|",*3,"Day",*18,": ",DKYDAY,*28,DDESC,*132,"|":
                    *N,"|",*3,"Session Starts : ",OMASTART,*132,"|":
                    *N,"|",*132,"|":
                    *N,"|",*3,"Bookings Finish",*22,": ",OMAFIN:
                    *68,"No. of Slots for NEW Patient":
                    *101,": ",OMANEWSL,*132,"|":
                    *N,"|",*3,"End of Session",*22,": ",OMAEND:
                    *68,"No. of Slots for SPECIAL Patient":
                    *101,": ",OMASPCSL,*132,"|":
                    *N,"|",*3,"Clinic Type",*22,": ",OMATYP,*32,OCTDESC:
                    *68,"Slot Time Allocated",*101,": ",OMADURN:
                    *107,"Minutes",*132,"|":
                    *N,"|",*3,"Clinic Indicator",*22,": ",OMACIND,*32,TDESC:
                    *68,"Report Sequence",*101,": ",OMASEQ,*132,"|":
                    *N,"|",*3,"Comments Line 1",*22,": ",OMACOM1:
                    *68,"Registrar's Clinic",*101,": ",DESCREG,*132,"|":
                    *N,"| Comments Line 2",*22,": ",OMACOM2:
                    *68,"Status",*101,": ",DSTAT,*132,"|":
                    *N,"| Session Frequency/",DESCFRQ;
.
          BRANCH    OMAFREQF,PRNA1500
          PRINT     *31,": ",OMAFREQM,*132,"|"
          GOTO      PRNA1700
.
PRNA1500  PACK      KEY53,OMAFREQY,OMAFREQM
          PRINT     *31,": ",KEY53,*132,"|"
.
PRNA1700   
          CALL      UND132
.
PRNA9999  RETURN
+
.*********************************************************************
.*        Print the second input screen of data
.*********************************************************************
PRNB0000  MOVE      "Yes",DESCDOC           * default to yes
          MATCH     "Y",OMAKDOC
          GOTO      PRNB0050 IF EQUAL       * keep as yes
          MOVE      "No ",DESCDOC
.
PRNB0050  PRINT     *1,"| Keyin Doctor Code (Y/N)                : ",DESCDOC:
                    *132,"|"
.
.         See if using default doctor code
.
PRNB0100  COMPARE   ONE,OTCNUDDC            * using default doctor code ?
          GOTO      PRNB0150 IF NOT EQUAL   * no
.
          MOVE      SP30,DOCNAME
          MATCH     SP6,OTMADEFD            * doctor code on file ?
          GOTO      PRNB0125 IF EQUAL       * no
.
          MOVE      "Unknown doctor",DOCNAME
          PACK      KEY6,OTMADEFD
          CALL      RDDOCT1
          BRANCH    OVRCD,PRNB0125
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
.
PRNB0125  PRINT     *1,"| Default Doctor Code                    : ":
                    *46,OTMADEFD,*52,DOCNAME,*132,"|"
.
. ------- extra screen when attending -------
.
PRNB0150  ADD       ONE,FORM2
          MOVE      "Yes",KEY3           * default to yes
          MATCH     ANSY,OTMAIXTA
          GOTO      PRNB0200 IF EQUAL       * keep as yes
          MOVE      "No ",KEY3
.
PRNB0200  PRINT     *1,"| Input Extra screen when Attending (Y/N): ":
                    *46,KEY3,*132,"|"
.
. ------- using 'allocate visit type when booking' -------
.
PRNB0300  COMPARE   ONE,OTCNAVIS
          GOTO      PRNB1000 IF NOT EQUAL   * not using field
.
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "Y",OTMAAVIS
          GOTO      PRNB0500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB0500  PRINT     *1,"| Allocate visit type when booking ?     : ":
                    *46,ACODE,*132,"|"
.
. ------- using 'slot times visit type controlled' -------
.
PRNB1000  COMPARE   ONE,OTCNSTVT
          GOTO      PRNB2000 IF NOT EQUAL   * not using field
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "Y",OTMASTVT
          GOTO      PRNB1500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB1500  PRINT     *1,"| Slot times visit type controlled ?     : ":
                    *46,ACODE,*132,"|"
.
. ------- using 'suppress extended slots on displays' -------
.
PRNB2000  COMPARE   ONE,OTCNSUPX
          GOTO      PRNB3000 IF NOT EQUAL   * not using field
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "Y",OTMASUPX
          GOTO      PRNB2500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB2500  PRINT     *1,"| Suppress extended slots on displays ?  : ":
                    *46,ACODE,*132,"|"
.
. ------- see if using RUS Automated Extraction -------
.
PRNB3000  COMPARE   ONE,OTCNRUSE
          GOTO      PRNB4000 IF NOT EQUAL   * not using field
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "Y",OTMARUSE
          GOTO      PRNB3500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB3500  PRINT     *1,"| Automated Outpatient Proc Extraction?  : ":
                    *46,ACODE,*132,"|"
.
. ------- see if displaying comments when booking ------
.
PRNB4000  COMPARE   ONE,OTCNDCMB
          GOTO      PRNB5000 IF NOT EQUAL   * not using field
.
.         using 'display comments at booking time'
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "1",OTMADCMB
          GOTO      PRNB4500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB4500  PRINT     *1,"| Display Comments at Booking Time (Y/N) : ":
                    *46,ACODE,*132,"|"
.
. ------- see if using the attendance register ------
.
PRNB5000  COMPARE   ONE,OTCNUARF
          GOTO      PRNB9999 IF NOT EQUAL   * not using field
.
          MOVE      "No ",ACODE             * set to no
          MATCH     "1",OTMAUATR
          GOTO      PRNB5500 IF NOT EQUAL   * keep as no
          MOVE      "Yes",ACODE             * set to yes
.
PRNB5500  PRINT     *1,"| Using Attendance Register (OUT73)(Y/N) : ":
                    *46,ACODE,*132,"|"
PRNB9999  RETURN
+
.**************************************************************************
.         Print Screen 3 if using new file
.**************************************************************************
PRNC0000  PRINT     *1,"| Discharge pat. after X number of DNA's : ":
                    *46,OTMADDNA,*132,"|"
.
PRNC0200  PRINT     *1,"| Number of days for re-appointment      : ":
                    *46,OTMANDRP,*132,"|"
.
PRNC0300  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OMASITE,OTMACLSE,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OMASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTMACLSE,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,PRNC0350
.
          MATCH     SP6,OCADOCT
          GOTO      PRNC0350 IF EQUAL       * valid code
.
.         have a doctor code for the clinic so use this for description
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PRNC0350          * invalid doctor code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
.
PRNC0350  PRINT     *1,"| Clinic Sessions                        : ":
                    *46,OTMACLSE,*55,OCADESC,*132,"|"
.
PRNC0400  MOVE      "<<Missing>>",PTHSNAME
          MOVE      OTMAHOSP,KEY3
          CALL      RDPTHSP1
          PRINT     *1,"| Hospital code                          : ":
                    *46,OTMAHOSP,*55,PTHSNAME,*132,"|"
.
PRNC0500  PRINT     *1,"| Clinic Location                        : ":
                    *46,OTMACLOC,*132,"|"
.
PRNC0600  MOVE      SP20,TDESC
          MATCH     SP3,OTMALTYP
          GOTO      PRNC0650 IF EQUAL       * nothing to display
.
          MOVE      "<< Missing Code >>",TDESC
          PACK      KEY5,CODECT,OTMALTYP
          CALL      RDCODE1
.
PRNC0650  PRINT     *1,"| Clinic Location Type                   : ":
                    *46,OTMALTYP,*55,TDESC,*132,"|"
.
PRNC0700  PRINT     *1,"| Default Send Letter value for OUT66    :    ";
.
          MATCH     ANSY,OTMADFSL
          IF        @EQUAL
            PRINT     *46,"Yes",*132,"|"
          ELSE  
            PRINT     *46,"No ",*132,"|"
          ENDIF
.
PRNC0800  PRINT     *1,"| Generate Case notes on attendance      : ";
.
          MATCH     ANSY,OTMADFGN
          IF        @EQUAL
            PRINT     *46,"Yes",*132,"|"
          ELSE
            PRINT     *46,"No ",*132,"|"
          ENDIF
.
PRNC0900  COMPARE   ONE,OTCNUWDA
          GOTO      PRNC1100 IF NOT EQUAL

          PRINT     *1,"| Is this a Ward Clinic (Y/N)            : ";
          MATCH     ANSY,OTMAWDCL
          IF        @EQUAL
            PRINT     *46,"Yes   ",*132,"|"
          ELSE
            PRINT     *46,"No    ",*132,"|"
          ENDIF
.
PRNC1000  MOVE      SP30,WBDESC
          PACK      KEY6,OTMAWARD,SP10
          CALL      RDWARD1
          PRINT     *1,"| Ward Code for Clinic                   : ":
                    *46,OTMAWARD,SP3,WBDESC,*132,"|"
.
PRNC1100  COMPARE   ONE,OTCNURVS
          GOTO      PRNC1200 IF NOT EQUAL

          PRINT     *1,"| Security level to use Reserved slots   : ":
                    *46,OTMASLRV,*132,"|"
.
PRNC1200  COMPARE   ONE,OTCNOVBK
          GOTO      PRNC1500 IF NOT EQUAL

          PRINT     *1,"| Security level to Overbook             : ":
                    *46,OTMASOVB,*132,"|"
.
          PRINT     *1,"| Number of Overbookings allowed         : ":
                    *46,OTMANOOB,*132,"|"
.
          PRINT     *1,"| Security level to Forcebook            : ":
                    *46,OTMASLFB,*132,"|"
.
PRNC1500  COMPARE   ONE,OTCNNCOC
          GOTO      PRNC1600 IF NOT EQUAL

          MOVE      DNO,KEY3
          LOAD      KEY3,OTMANCOC,DYES
          PRINT     *1,"| Exclude from Contracting/Korner (Y/N)  : ":
                    *46,KEY3,*132,"|"
.
PRNC1600
          CALL      UND132
.
PRNC9999  RETURN
+
.**************************************************************************
.         Print Screen 4 - Letters and Labels                    *
.**************************************************************************
PRND0000  PRINT     *1,"| Booking Label Number                       : ":
                    *50,OTMASBKL,*132,"|":
                    *N,"| Appointment Label Number                   : ":
                    *50,OTMASAPL,*132,"|":
                    *N,"| PMI Mailing Label Number                   : ":
                    *50,OTMASPMM,*132,"|":
                    *N,"| G.P. Mailing Label Number                  : ":
                    *50,OTMASGPM,*132,"|"
                    
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMALPTR,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| DNA Letter to Patient on Re-appointment    : ":
                    *50,OTMALPTR,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMALPTD,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| DNA Letter to Patient on Discharge         : ":
                    *50,OTMALPTD,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMALGPD,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| DNA Letter to G.P. on Discharge            : ":
                    *50,OTMALGPD,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMALPPC,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| Letter to Patient on Cancellation          : ":
                    *50,OTMALPPC,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMABKLT,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| Standard Booking Letter                    : ":
                    *50,OTMABKLT,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMARALT,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| Standard Re-Appointment Letter             : ":
                    *50,OTMARALT,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMARSLT,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| Standard Re-Schedule Letter (Patient)      : ":
                    *50,OTMARSLT,*55,KEY25,*132,"|"
.
          MOVE      SP30,OTLTTEXT
          PACK      KEY6,OTMASRLH,SP1,SP1,ZERO
          CALL      RDOTLET1
          MOVE      OTLTTEXT,KEY25
          PRINT     *1,"| Standard Re-Schedule Letter (Hospital)     : ":
                    *50,OTMASRLH,*55,KEY25,*132,"|"
.
          CALL      UND132
.
PRND9999  RETURN
+
.*********************************************************************
.         Routine to print OUTMASB1
.         Para's  : KEY18         key to master A
.*********************************************************************
          DEFPROC   OUTPMASB
.
BCOUNT    FORM      2
MASAKEY   DIM       18
SAVEREC   FORM      1
SP21      INIT      "                     "
SAVEKEY1  DIM       21
SAVEKEY2  DIM       21
SAVEKEY3  DIM       21
SAVEKEY4  DIM       21
SAVEKEYX  DIM       21
POS       FORM      1
LOADED    FORM      1
.
PMSB0000  MOVE      KEY18,MASAKEY
          MOVE      ONE,CNOUNDLN          srf 642553
          CALL      HEAD132
          CALL      UND132
          PRINT     *1,"|",*132,"|":
                    *N,"|",*3,"T/Slot  Time  Visit Type":
                    *35,"T/Slot  Time  Visit Type":
                    *67,"T/Slot  Time  Visit Type":
                    *99,"T/Slot  Time  Visit Type",*132,"|":
                    *N,"|",*3,"------  ----  ----------":
                    *35,"------  ----  ----------":
                    *67,"------  ----  ----------":
                    *99,"------  ----  ----------",*132,"|":
                    *N,"|",*132,"|"
.
          PACK      KEY21,MASAKEY,SP3
          MOVE      SP30,SAVEKEYX
          MOVE      ZERO,LOADED
.
PMSB0200  MOVE      ZERO,SAVEREC
          MOVE      TWENTY2,BCOUNT
          MOVE      SP21,SAVEKEY1
          MOVE      SP21,SAVEKEY2
          MOVE      SP21,SAVEKEY3
          MOVE      SP21,SAVEKEY4
          CALL      RDSMASB1
.
PMSB1000  CALL      RDKMASB1
          BRANCH    OVRCD,PMSB4000
.
          MATCH     OMASTART,OMBSTRT
          GOTO      PMSB4000 IF NOT EQUAL
          COMPARE   OMADAY,OMBDAY
          GOTO      PMSB4000 IF NOT EQUAL
          MATCH     OMACLIN,OMBCLIN
          GOTO      PMSB4000 IF NOT EQUAL
          MATCH     OMASITE,OMBSITE
          GOTO      PMSB4000 IF NOT EQUAL
.
          ADD       ONE,BCOUNT
          COMPARE   TWENTY3 TO BCOUNT
          GOTO      PMSB1000 IF NOT EQUAL
.
          MOVE      ZERO TO BCOUNT
          ADD       ONE TO SAVEREC
          PACK      KEY21 WITH OMBSITE,OMBCLIN,OMBDAY,OMBSTRT,OMBSLOT
.
          MATCH     SAVEKEYX TO KEY21
          GOTO      PMSB9000 IF EQUAL
.
          STORE     KEY21 BY SAVEREC INTO SAVEKEY1,SAVEKEY2,SAVEKEY3,SAVEKEY4
.
          BRANCH    LOADED TO PMSB3000
          MATCH     SP21 TO SAVEKEY2
          GOTO      PMSB3000 IF EQUAL
          MOVE      SAVEKEY2 TO SAVEKEYX
          MOVE      ONE TO LOADED
.
PMSB3000  COMPARE   FOUR TO SAVEREC
          GOTO      PMSB4000 IF EQUAL
          GOTO      PMSB1000
.
PMSB4000  MOVE      ZERO TO POS
          PRINT     "|";
.
PMSB6000  ADD       ONE TO POS
          LOAD      KEY21 BY POS FROM SAVEKEY1,SAVEKEY2,SAVEKEY3,SAVEKEY4
.
          MATCH     SP21 TO KEY21
          GOTO      PMSB7000 IF EQUAL
.
          CALL      RDMASB1
.
          IF        OTMBRESV = ZERO
            PRINT     SP2,OMBSLOT,SP4,OMBTIME,SP3,OMBVTYP,SP10,SP1;
          ELSE
            PRINT     SP2,OMBSLOT,SP4,OMBTIME,SP3,OMBVTYP,SP2,"(",OTMBRESV:
                      ")",SP5;
          ENDIF
.
          COMPARE   POS TO SAVEREC
          GOTO      PMSB6000 IF NOT EQUAL
.
PMSB7000  PRINT     *132,"|"
          MATCH     SP21 TO SAVEKEY1
          GOTO      PMSB9000 IF EQUAL
.
          MOVE      SAVEKEY1 TO KEY21
          GOTO      PMSB0200
.
PMSB9000  PRINT     "|",*132,"|"
          CALL      UND132
          GOTO      PMSB9999
.
          INC       IBAOVRIO/INC
.
PMSB9999  ENDPROC
+
.******************************************************************************
.* Procedure Name : OUTPRXSF.PBL                                              *
.*                                                                            *
.* Function       : To PRINT the extra staff for a clinic master              *
.*                                                                            *
.* Author         : Justin Coates      23/10/1995                             *
.*                                                                            *
.* Parameters     : KEY18              key to outma1af                        *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
. 
          DEFPROC   OUTPRXSF
.
          INC       OUTMXSFD/INC            * extra staff
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctor file
.
MASAKEY   DIM       18
CATDTDSC  DIM       20
DESCDOCT  DIM       30
CATDT     INIT      "DT"
.
.*********************************************************************
.                   ML0000
.         MAINLINE CODE / CONTROLLING LOGIC
.*********************************************************************
MAIN0000  CALL      INIT0000
          CALL      PROC0000                * enter details
          GOTO      OUTXSTF9
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.         initilization
.*********************************************************************
INIT0000  MOVE      KEY18,MASAKEY
          PACK      CFNAME,UID,FILMXSA1
          OPEN      OUTMXSA1,CFNAME
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          PACK      KEY5,CATDT,SP3
          CALL      RDCODE1
          MOVE      TDESC,CATDTDSC
.
INIT9999  RETURN
+
.*********************************************************************
.*                  PROC0000                    Called by : ML3000   *
.*        Enter all the data for the patient                         *
.*********************************************************************
PROC0000  PRINT     "|                 Extra staff details",*132,"|":
                    *N,"|",*7,"Health Care Professional",SP20,SP3:
                    *57,CATDTDSC,SP4,*132,"|":
                    *N,"|",*7,"----------------------------------------------":
                    *57,"------------------------",*132,"|"
          MOVE      " 1",KEY2
          PACK      KEY20,MASAKEY,KEY2
          CALL      RDOTMXS1
          BRANCH    OVRCD,PROC9000          * nothing in file
.
.         have defaults so print
.
PROC1000  CALL      FDNAM000
          PRINT     *1,"| ",OTXSCNT,". ",OTXSDOCT,SP1,DESCDOCT:
                    *57,OTXSSPEC,SP1,TDESC,*132,"|"
.
.         see if any more details
.
          CALL      RKOTMXS1
          BRANCH    OVRCD,PROC3000          * end of file
          PACK      KEY18,OTXSSITE,OTXSCLIN,OTXSDAY,OTXSTIME
          MATCH     MASAKEY,KEY18
          GOTO      PROC1000 IF EQUAL       * more data for session
.
PROC3000  MOVE      ONE,EXIT
          GOTO      PROC9999
.
PROC9000  MOVE      ZERO,EXIT               * nothing on file
.
PROC9999  RETURN
+
.*********************************************************************
.         format the doctor name
.*********************************************************************
FDNAM000  MOVE      OTXSDOCT,KEY6
          CALL      RDDOCT1
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOCT
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,CATDT,OTXSSPEC
          CALL      RDCODE1
.
FDNAM999  RETURN
+
          INC       IBAOVRIO/INC            * extra meals
          INC       OUTMXSIO/INC            * item file
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctor file
.
OUTXSTF9  ENDPROC
..............................................................................
