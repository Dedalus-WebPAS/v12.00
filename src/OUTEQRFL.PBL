.
.  Include          :    OUTEQRFL.PBL
.
.  Function         :    Enquiry on Referral Letters for a patient
.  
.  Author           :    Whirlpool 4/4/96 ( and IBAOUT33!)
.
.  Requires         :    VURNO
.
.  Returns          :    EXIT = 1  - Leave enquiry completely
.                        EXIT = 0  - Get Next U/R
.
.  Modifications    :                                                
.         V10.07.01 30/10/2015  Ebon Clements  CAR 268970
.                   Change to use OUTCLIFD index 1 instead of index 2
.         V5.09.B01 05/01/2001  Greg Horvat
.                   Replaced certain variables with KEY?? variables cause
.                   OUT66 couldnt compile
.         V5.08.01    16/03/2000  Steve Armstrong   5.08 Mods
.                     Mods for changes to OUTCLIFD (effective date)
.         V5.07.01    J.Tan
.                     Mods to display Clinic Id, date, status
.         V5.05.01    Nick   SRF 643848
.                     Mods to get source of referral from ref. letter file
.                     on Referral letter screen. If source of ref. is missing
.                     or invalid, the source of ref. is taken from OUTBOKB    
.                   :    11/06/1996  Steve Armstrong    SRF 641617
.                        Mods to display clinic type if clinic id blank
.                   :    30/01/1997  Howellsy                       
.                        Check if Outpatient Referral OTRLTREF = 0      
.         V5.04.05  11/03/1997  Howellsy          5.05
.                   added clinic id to enquiry
.
OUTEQRFL  OPEN      OUTRF1A2,"outrf1af"
          DISPLAY   *P1:7,*EF:
                    *P1:7,*V2LON,*ULON,*P1:7,"Ref.Date  ":
                    *P12:7,"Ref. to Clinic      ":
                    *P33:7,"Clinic Id":
                    *P43:7,"Clinic Date":
                    *P55:7,"Status":
                    *P63:7,"Source of Ref.   "
.
ERFL0500  MOVE      EIGHT,CVERT                     * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCREEN                    * init screen no.
          MOVE      ONE,ITEM                      * init item
.
          PACK      KEY24,VURNO,Z70               * position at start
          CALL      RSOTRFL2
ERFL1000  CALL      RPOTRFL2                      * read next record
          BRANCH    OVRCD,ERFL5000                * no more records
.
. check if same U/R
.  
          MATCH     VURNO,OTRLURNO
          GOTO      ERFL5000 IF NOT EQUAL
.
. check if Outpatient Referral
.  
          COMPARE   ZERO,OTRLTREF 
          GOTO      ERFL1000 IF NOT EQUAL
.
.
. check if enough room for display
.
ERFL1500  COMPARE   TEN6,ITEM
          GOTO      ERFL3000 IF LESS
.
. we need a new screen
.
          CALL      ST1K0000                      * ask question with Next scrn
          BRANCH    EXIT,ERFL9999:                * Abort selected
                         ERFL8000:                * First screen selected
                         ERFL2500:                * Search More (Next screen)
                         ERFL9000                 * get another UR (Next)
.
. Next screen was selected
.
ERFL2500  ADD       ONE,SCREEN                    * increment screen no.
.
. save the current key
.
          DISPLAY   *P1:8,*EF                     * erase screen
.
          MOVE      EIGHT,CVERT                     * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,ITEM                      * init screen disp counter
.
. display record
.
ERFL3000  UNPACK    OTRLDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:CVERT,CPCDATE 
.
. ----- Consultant/Clinic -----
.
          MATCH     SP6,OTRLCONS        * consultant blank ?
          IF        @EQUAL
            MATCH     SP6,OTRLCTYP      * yes - clinic type blank ?
            IF        !@EQUAL
              PACK      CFNAME,UID,FILCTYA1     * get the name of the CTYA1 file
              OPEN      OUTCTYA1,CFNAME         * open the file
.
              PACK      KEY12,OTRLSITE,OTRLCTYP
              CALL      RDCTYA1         * clinic type on file ?
              IF        OVRCD = 0
                PACK      DIM20,OCTCTYP,SP1,HYPHEN,SP1,OCTDESC
                DISPLAY   *P12:CVERT,DIM20
              ENDIF
              CLOSE     OUTCTYA1
            ENDIF
            GOTO      ERFL3200
          ENDIF
.
          PACK      KEY20,OTRLSITE,OTRLCONS,OTRLDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTRLSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTRLCONS,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            PACK      DIM20,OCACLIN,SP1,HYPHEN,SP1,OCADESC
            DISPLAY   *P12:CVERT,DIM20
          ENDIF
          BRANCH    OVRCD,ERFL3200      * Clinic record not found
.
          MATCH     SP6,OCADOCT
          IF        @EQUAL
            PACK      DIM20,OCACLIN,SP1,HYPHEN,SP1,OCADESC
            DISPLAY   *P12:CVERT,DIM20
          ELSE
            MOVE      OCADOCT,KEY6        * use doctor code
            CALL      RDDOCT1
            BRANCH    OVRCD,ERFL3200
.
            CALL      CONCATDR            * Routine found in OUTDSCLN
            PACK      DIM20,OCACLIN,SP1,HYPHEN,SP1,DIM40
            DISPLAY   *P12:CVERT,DIM20
          ENDIF
.
ERFL3200  UNPACK    OTRLBDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P33:CVERT,OTRLBCLI:      * clinic ID
                    *P43:CVERT,CPCDATE;       * date
.
          PACK      KEY28,OTRLBSIT,OTRLBCLI,OTRLBDTE:
                          OTRLBSTR,OTRLBSLT
          CALL      RDBOKA1
          BRANCH    OVRCD,ERFL3700
.
          IF        OBASTAT=1
            DISPLAY   *P55:CVERT,"Book";
          ELSE
            IF        OBASTAT=4
              DISPLAY   *P55:CVERT,"Att.";
            ENDIF
          ENDIF
.
.     Get Source of Referral if possible         
.
ERFL3700  MATCH     OTRLSRCE,SP3   
          GOTO      ERFL3800 IF EQUAL
.
          PACK      TDESC,SP30
          PACK      KEY5,ANSS,SP1,OTRLSRCE
          CALL      RDCODE1
          BRANCH    OVRCD,ERFL3800
.
          PACK      KEY17,TDESC
          DISPLAY   *P63:CVERT,KEY17
          GOTO      ERFL4000
.          
ERFL3800  PACK      KEY8,OTRLOUTN
          CALL      RDBOKB1
          BRANCH    OVRCD,ERFL4000
          
          MATCH     SP3,OBASRCR              * Do we have a source of referal ?
          GOTO      ERFL4000 IF EQUAL        * No. Invalid
.
          MOVE      SP30,KEY17
          PACK      KEY5,OSTCATG,OBASRCR     * Key for codes record
          CALL      RDCODE1                  * Validate the code
          IF        OVRCD=1
            MOVE      "Invalid Code",KEY17
          ELSE
            PACK      KEY17,TDESC,SP30
          ENDIF
          DISPLAY   *P63:CVERT,KEY17           
.
ERFL4000  ADD       ONE,CVERT                     * update row pos
          ADD       ONE,ITEM                      * increment counter
          GOTO      ERFL1000                      * get next record
.
. we have no more records to display
.
ERFL5000  CALL     ST2K0000
          BRANCH   EXIT,ERFL9999:                 * Exit selected
                        ERFL8000:                 * First screen selected
                        ERFL9000                  * Next Ur selected         
          GOTO     ERFL5000
.
.         First screen Selected 
.
ERFL8000  DISPLAY  *P1:8,*EF
          GOTO      ERFL0500
.
ERFL9000  MOVE      ZERO,EXIT                      * Get the next U/R  
.
ERFL9999  RETURN
+
+
.**************************************************************************
.*                                STIK0000            Called by: OUTEQRFL *
.*                 ask line 24 question with (N)ext                       *
.**************************************************************************
ST1K0000  MOVE      ZERO,EXIT
          BRANCH    SCREEN,ST1K0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSS,*HOFF,")earch more, (":
                    *V2LON,ANSN,*HOFF,")ext U/R, (":
                    *V2LON,ANSF,*HOFF,")irst screen ? "
.
          MOVE      "53",CCOL                     * keyin position
          GOTO      ST1K1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
ST1K0500  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSS,*HOFF,")earch more, (":
                    *V2LON,ANSN,*HOFF,")ext U/R ? " 
.
          MOVE      "37",CCOL                     * keyin position
.
. now get keyin
.
ST1K1000  KEYIN     *PCCOL:24,*V2LON,ANS
.
          REP       UPPLOW,ANS                 * Change to lower case
.
          MATCH     ANSE,ANS
          IF        @EQUAL
           MOVE       ONE,EXIT
           GOTO       ST1K9999
          ENDIF
.
          MATCH     ANSF,ANS
          IF        @EQUAL
           BRANCH     SCREEN,ST1K7200            * don't allow if first screen
           MOVE       TWO,EXIT
           GOTO       ST1K9999
          ENDIF
.
          MATCH     ANSS,ANS
          IF        @EQUAL
           MOVE       THREE,EXIT
           GOTO       ST1K9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
           MOVE       FOUR,EXIT
           GOTO       ST1K9999
          ENDIF

ST1K7200  BEEP
          GOTO      ST1K0000
.
ST1K9999  RETURN
+
.**************************************************************************
.*                                ST2K0000            Called by: OUTEQRFL *
.*                 ask line 24 question without (N)ext                    *
.**************************************************************************
ST2K0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          MOVE      ZERO,EXIT
          BRANCH    SCREEN,ST2K0500
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSN,*HOFF,")ext U/R, (":
                    *V2LON,ANSF,*HOFF,")irst screen ? "
.
          MOVE      "38",CCOL                     * keyin position
          GOTO      ST2K1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
ST2K0500  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSN,*HOFF,")ext U/R ? " 
.
          MOVE      "22",CCOL                     * keyin position
.
. now get keyin
.
ST2K1000  KEYIN     *PCCOL:24,*V2LON,ANS
.
          REP       UPPLOW,ANS                 * Change to lower case
.
          MATCH     ANSE,ANS
          IF        @EQUAL
           MOVE       ONE,EXIT
           GOTO       ST2K9999
          ENDIF
.
          MATCH     ANSF,ANS
          IF        @EQUAL
           BRANCH     SCREEN,ST2K1500            * don't allow if first screen
           MOVE       TWO,EXIT
           GOTO       ST2K9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
           MOVE       THREE,EXIT
           GOTO       ST2K9999
          ENDIF
.
ST2K1500  BEEP  
          GOTO      ST2K0000
.
.
ST2K9999  RETURN
+
