.*******************************************************************************
.    System          : Theatre System
.    Include Function: OPRSES01.PBL
.    Description     : Collection of functions concerning the oprsesaf
.*******************************************************************************
.    Date            : 11/10/1990 Almost the end of a good year's programming.
.    Author          : Bill Dew
.    Modifications   :
.*******************************************************************************
.  
.    FUNCTION ROSTER  DESCRIPTION
.    ---------------  ------------------------------------------------------
.    OPSUSPND         Suspend a session by setting OPSESUSP to one
.    OPUNSPND         Unsuspend a session by setting OPSESUSP to two
.    OPCHKFUL         Determine if a session is full
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, OPRSESFD
.                                1      
.
.    I/O's : OPRSESIO
.
.    PBL's : STDOPR01 
.
.*******************************************************************************
.                   OPSUSPND
.    The function Sets the suspension flag by setting OPSESUSP=1.
.  
.    PARAMETERS.
.    KEY19    : Must be set to session date, time and clinic/doctor
.
.    RETURN PARAMETERS.
.    EXIT = 1 : Session KEY19 not on file    
.    EXIT = 0 : No Problems
.*******************************************************************************
OPSUSPND  
          CALL      RDOPSES1           * get session details
          BRANCH    OVRCD,SUSPND95
.
          MOVE      "2",AUDIFLAG       * before change audit
          CALL      AUSES000           * write audit record
          MOVE      ONE,OPSESUSP       * suspend session
          CALL      UPOPSES1           * update record
          MOVE      "3",AUDIFLAG       * after change audit
          CALL      AUSES000           * write audit record
.
SUSPND80  CALL      DSPSUSPD           * display message
          MOVE      ZERO,EXIT
          GOTO      SUSPND99
.
SUSPND95  MOVE      TRUE,EXIT          * invalid session
.
SUSPND99  RETURN
+
.*******************************************************************************
.                   DSPSUSPD
.    The function displays the word SUSPENDED in the session heading
.*******************************************************************************
.  
DSPSUSPD  DISPLAY   *P37:6,*V2LON,*BLINKON,"SUSPENDED"
          RETURN
+
.*******************************************************************************
.                   OPUNSPND
.    The function Resets the suspension flag by setting OPSESUSP=2.
.  
.    PARAMETERS.
.    KEY19    : Must be set to session date, time and clinic/doctor
.
.    RETURN PARAMETERS.
.    EXIT = 1 : Session KEY19 not on file    
.    EXIT = 0 : No Problems
.*******************************************************************************
OPUNSPND  
          CALL      RDOPSES1           * get session details
          BRANCH    OVRCD,UNSPND95
.
          MOVE      "2",AUDIFLAG       * before change audit
          CALL      AUSES000           * write audit record
          MOVE      TWO,OPSESUSP       * Unsuspend session
          CALL      UPOPSES1           * update record
          MOVE      "3",AUDIFLAG       * after change audit
          CALL      AUSES000           * write audit record
.
UNSPND80  DISPLAY   *P37:6,SP9
          MOVE      ZERO,EXIT
          GOTO      UNSPND99
.
UNSPND95  MOVE      TRUE,EXIT          * invalid session
.
UNSPND99  RETURN
+
.*******************************************************************************
.                   OPCHKFUL
.    The function checks if a session is full by calculating the percentage
.    utilization and comparing it to the system parameter OPCNFULL to
.    determine whether to set the session suspension status
.  
.    PARAMETERS.
.    KEY19    : Must be set to session date, time and clinic/doctor
.
.    RETURN PARAMETERS.
.    EXIT = 1 : Session is not full or session does not exist.
.    EXIT = 0 : session requires its suspension flag to be set.
.*******************************************************************************
OPCHKFUL  
          CALL      RDOPSES1                * get session details
.
          COMPARE   ZERO,OPCNFULL
          GOTO      CHKFUL44 IF NOT EQUAL   * using the system parameter
.
          SUB       OPSEBRKT,OPSEDURA       * Duration -= breaktime
          COMPARE   OPSETIMB,OPSEDURA       * test duration is < than book time
          GOTO      CHKFUL80 IF LESS        * duration is less -> full
          GOTO      CHKFUL90                * session is not full
.
.         check against the system parameter
.
CHKFUL44  MOVE      OPSETIMB,FORM6P2   * Set up time booked in session
          SUB       OPSEBRKT,OPSEDURA  * Duration -= breaktime
          DIV       OPSEDURA,FORM6P2   * Time book = time book/duration
          MULT      "100",FORM6P2      * convert to a percentage
.
          MOVE      FORM6P2,FORM3      * Get rid of fractions
          COMPARE   OPCNFULL,FORM3     * test percentage full against sys param
          GOTO      CHKFUL90 IF LESS
.
.         session is full
.
CHKFUL80  MOVE      ZERO,EXIT          * Signal session is full
          GOTO      CHKFUL99
.
.         session is not full
.
CHKFUL90  MOVE      ONE,EXIT           * signal session is not full
.
CHKFUL99  RETURN
+
