.******************************************************************************
.*  Include Name  : KEYINDOC.PBL                                              *
.*  Description   : Keyin of a Doctor file entry                              *
.*  Author        : Barney Rubble                                             *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : DOCCODE  - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V9.11.01  06/10/2008  Peter Vela CAR 179286                         *
.*                  Ported to 910                                             *
.*                                                                            *
.******************************************************************************
KEYINDOC  
KDOCT010  MATCH     SP6,CKYIDEF6                  * using a default ?
          GOTO      KDOCT050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:      * no default used
                    *PECOL:EVERT,*V2LON,DOCCODE:
                    *PECOL:EVERT,*DV,DOCCODE
          GOTO      KDOCT080
.
KDOCT050  MOVE      CKYIDEF6,DOCCODE
          KEYIN     *PECOL:EVERT,*DV,DOCCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,DOCCODE:
                    *PECOL:EVERT,*DV,DOCCODE
.
.         check what was entered
.
KDOCT080  CALL      CODDR000
          BRANCH    EXIT,KDOCT100,KDOCT705,KDOCT805,KDOCT905
.                         "?"      valid    nothing exitchar
          GOTO      KDOCT010
.
.         a "?" was input
.
KDOCT100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KDOCT105  MOVE      ONE,CNEWDS
          MOVE      DOCCODE,KEY6
          CALL      PATDOCDS
.
.         now ask for code while leaving displayed codes on screen
.
KDOCT150  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      KDOCT200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN6:      * no default used
                    *P11:24,*V2LON,DOCCODE:
                    *P11:24,*DV,DOCCODE
          GOTO      KDOCT300
.
KDOCT200  MOVE      CKYIDEF6,DOCCODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,CKYIDEF6:     * default used
                    *P11:24,*V2LON,*RV,DOCCODE:
                    *P11:24,*DV,DOCCODE
.
.         repeat the validation of the code
.         check what was entered
.
KDOCT300  CALL      CODDR000
          BRANCH    EXIT,KDOCT105,KDOCT700,KDOCT800,KDOCT900
.                         "?"      valid    nothing exitchar
.
          GOTO      KDOCT150
.
.         a valid code was entered 
. 
KDOCT700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DOCCODE
.
KDOCT705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KDOCT999 
.
.         nothing was input
.
KDOCT800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DOCCODE
.
KDOCT805  MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      KDOCT999 
.
.         EXITCHAR input
.
KDOCT900  CALL      FRESCR00                      * free the screen saved
.
KDOCT905  MOVE      TWO,EXIT                      * EXITCHAR
.
KDOCT999  RETURN
+
.***************************************************************************
.*                              CODDR000             Called by: KEYINDOC   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
CODDR000  ENDSET    DOCCODE
          APPEND    SP6,DOCCODE
          RESET     DOCCODE
.
          MATCH     EXITCHAR,DOCCODE
          GOTO      CODDR600 IF EQUAL
.
          MATCH     SP6,DOCCODE
          GOTO      CODDR700 IF EQUAL
.
.         check for a "?"
.
          MATCH     QUEST,DOCCODE
          GOTO      CODDR800 IF EQUAL
.
. a code was entered so validate
. 
CODDR500  PACK      KEY6,DOCCODE,SP10
          CALL      RDDOCT1
          BRANCH    OVRCD,CODDR900
.
          MOVE      TWO,EXIT
          GOTO      CODDR999
.
. exitchar entered
.
CODDR600  MOVE      FOUR,EXIT
          MOVE      SP6,DOCCODE
          GOTO      CODDR999
.
. nothing entered
.
CODDR700  BRANCH    CKYIMAND,CODDR950             * mandatory
          MOVE      THREE,EXIT
          GOTO      CODDR999
.
. "?" entered
.
CODDR800  MOVE      ONE,EXIT
          GOTO      CODDR999
.
. invalid code entered
.
CODDR900  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CODDR999
.
CODDR950  BEEP
          MOVE      ZERO,EXIT
.
CODDR999  RETURN
.
