.******************************************************************************
.* System   : THEATRE                                                         *
.* Program  : IBAOPR51                                                        *
.* Desc     : Re-print Instrument Orders                                      *
.******************************************************************************
.* Date     : 23/02/1990                                                      *
.* Author   : Bill Dew                                                        *
.*  Mod's    :                                                                *
.******************************************************************************
.*        V12.00.01 16/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*        V11.03.03 21/04/2023  Thanh T          TSK 0909393                  *
.*                  Changed TEAM0000, SURG0000 to use RSOPDEA6/RPOPDEA6       *
.*                  instead of RDOPDEA1                                       *
.*        V11.03.02 20/03/2023  PJ Le Febour     TSK 0909393                  *
.*                  Amended KEY19 to KEY22 for theatre index changes          *
.*        V11.03.01 31/01/2023  Thanh T.         TSK 0919770                  *
.*                  Recompiled for OPRTRAFD/IO changes                        *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.15.02 09/12/2019 Peter Vela Task 0871644                        *
.*                  Recompiled for OPRREQTM                                   *
.*        V10.15.01 28/11/2019  Peter Vela     Task 0871644                   *
.*                  Recompiled for OPRREQFD                                   *
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.04.01  17/02/2005  Lina Vo     CAR 56404                         *
.*                  Changed program to read Operating Room item file with new *
.*                  key size. Also changed all the item variable size used    *
.*                  in the program.                                           *
.*                  Removed Doctor Preference FD. Not used.                   *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  31/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.02  18/01/1999  Nicole Harrington 507 rebound 090             *
.*                  Mods to print century correctly                           *
.*        V5.07.00  28/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*            V5.03.01  08/05/1996 Howellsy        5.04                      *
.*           :           Added 2 more nurse codes & types.                    *
.*            20/04/90  Bill Dew                                              *
.*                      Convert prog r5.01 to r5                              *
.*            V5.01.10  19/10/90 Justin Coates                                *
.*                      Modified to suit new FD's                             *
.*            V5.01.11  21/01/91 Rod Knowles                                  *
.*                      Converted to Release 5.01                             *
.*            V5.01.12  12/02/91 Graeme Williams                              *
.*                      Fixed up display of bookings                          *
.*            V5.01.13  03/05/91 i chung           SRF 108113                 *
.*                      Modify to enable re-print for non-inpatients as well  *
.*            V5.01.14  22/05/91  Steve O'Gorman                              *
.*                      Fixed Errors found by the New Compiler                *
.*            V5.01.15  25/07/91  ROD                                         *
.*                      Recompiled for OPRSESFD                               *
.*            V5.01.16  25/05/1994  Matt Surridge                             *
.*                      Fixed bugs for global recompile.                      *
.*            V5.01.17  14/09/1994  Max White      Hawkes Bay Mods            *
.*                      Amended index on OPRREQFD requisitions file           *
.*                                                                            *
.******************************************************************************
.
.$$$$$$
.
.   Arrange Daily List IBAOPR51
.   ---------------------------
.
.   Program description : This program enables operator to re-order equipment
.   List.  The report lay out was designed by the Bill Dew.  If you dont like
.   it, you can SOD off.
.
.   Program flow :
.   Get clinic key details which are code date and time.
.   Display patient details.
.   Get the unique number from the operation details file
.   Use the unique number to index the records in the oprreqaf.
.
.   END
.
.$$$$$$
.
          INC       STD001FD
.
          INC       BOKRC1FD/INC       * Booking Master file
          INC       OPRCLIFD/INC       * Clinic file
          INC       OPRCONFD/INC       * System Parameters
          INC       OPRDEAFD/INC       * Session Booking Details file
          INC       OPRDEBFD/INC       * Operating Teams file
          INC       OPRITEFD/INC       * Items file
          INC       OPRREQFD/INC       * Instruments and Tray Items Requisition
          INC       OPRSESFD/INC       * Sessions file
          INC       OPRTRAFD/INC       * Tray Header
          INC       OPRTRBFD/INC       * Tray Details
.
          INC       PATCODFD/INC       * Codes file 
          INC       PATCONFD/INC       * Patient System System Parameters 
          INC       PATDO1FD/INC       * Doctors file
          INC       PATMA1FD/INC       * Patient Master file
          INC       PATMI1FD/INC       * Patient Admission file
          INC       PMSVX1FD/INC       * Patient Admission file
          INC       PATNOBFD/INC       * No-Bed file
          INC       PATPR1FD/INC       * Patient Pre-Admissions file
          INC       PATTRNFD/INC       * Patient Tranfers file
          INC       PATWR1FD/INC       * Ward Bed file
.
. ------- program variables -------
.
OPRTMPF1  IFILE     SQL, FIXED=40, KEY="U1-15,36-36,40-42,43-48"
OPRTMPF2  IFILE     SQL, FIXED=40, KEY="U16-35,1-15,36-36,40-42,43-48"
.   1-15 itemcode  16-35 item desc  36-36 item type  
.  37-39 quantity  40-42 class      43-48 doctor code
.
ITEMCODE  DIM       15                  * instrument or tray item code
ITEMDESC  DIM       20                  * decription to ITEMCDOE
ITEMTYPE  DIM       1                   * I for instrument and T for tray item
ITEMAMNT  FORM      3                   * amount
ITEMCLSS  DIM       3                   * class
ITEMDCOD  DIM       6                   * doctor code
.
ADMINNUM  DIM       8                  * ADMIssioN NUMber
ADMNDATE  DIM       10                 * ADMissioN DATE
AMOUNT    FORM      3                  * number of items quantity
ANESCODE  DIM       6                  * anaesthetic code
.
CASENUM   DIM       3                  * CASE NUMber
CLINIC    DIM       6                  * CLINIC code
CLINDESC  DIM       25                 * CLINic DESCription
COUNTER   FORM      2
DELCOMM   DIM       20                 * delete command for removing tempfiles
DIM3      DIM       3                  * patdsdoc
DIM36     DIM       36
DIM42     DIM       42
DOSCMND1  DIM       80                 * dos command 1
DOCCODE   DIM       6
.
FORM6     FORM      6
DIM8      DIM       8
.
INDEX     FORM      2
ITEMFLAG  FORM      1                   * 0 = tray,  1 = instrument
.
.
LASTCASE  DIM       3                  * last case number displayed
LASTHOSP  DIM       3                  * last hospital id displayed
LINE80    DIM       80                 * multi purpose 80 char line buffer
.
MASK      DIM       20                 * Used in GOPT funtion
MAXRANGE  FORM      2
.
NEXTODET  DIM       26                 * next operation detail record key
NORMDATE  DIM       10                 * session date display format for users
.
OPERDES1  DIM       30                 * operation description 1
OPERDES2  DIM       30                 * operation description 2
OPERDES3  DIM       30                 * operation description 3
.
PATAGE    DIM       3                  * PATient AGE
PATNAME   DIM       40                 * Patient's Name
.
REPLY     DIM       2                  * reply from GET OPTION
REQNUM    DIM       10                 * unique number
.
SCRNFLAG  FORM      1                  * signals redisplays vars
SESNDATE  DIM       8                  * use as key file date cc yy mm dd
SESNTIME  DIM       5                  * start time of operation
SEXDESC   DIM       8                  * male, female, indeterminate & unknown
SP36      INIT      "                                    "
SP42      INIT      "                                          "
STARTDES  DIM       33                 * start description for paging
STARTIME  DIM       5                  * expected startime of operation
SURGCODE  DIM       6                  * SURGeon CODE
SURGDESC  DIM       30                 * SURGeon DESCription
.
TEAMNUM   DIM       1                  * team number
TEMPNAM1  DIM       8                  * temporary data filename
THEATRE   DIM       6                  * operating room code
TODAY     DIM       8                  * todays file date
.
URNUMBER  DIM       8                  * UR NUMBER patient code
WARDBED   DIM       7                  * WARD and BED number
.
. ------- program constants -------
.
NUM40     FORM      "40"
NUM60     FORM      "60"
ITEMHEAD  INIT      "Type Item    Description           Qty"
.
PRGID     INIT      "IBAOPR51"
PRGNAM    INIT      "Re-Print Instrument Orders"
VERSION   INIT      "V12.00.01"
+
.*****************************************************************************
.                         MAIN LINE Rock and Roll
.*****************************************************************************
ML0000    CALL      INIT0000           * initialization routine
.
ML1000    CALL      CLRV0000           * clear variables
          CALL      SCRN0000           * display the screen
.
          CALL      CLIN0000           * get clinic code
          BRANCH    EXIT,ML9000        * nothing entered
.
          CALL      GDAT0000           * Get session DATe 
ML1010    CALL      GTIM0000           * Get session TIMe
          BRANCH    EXIT,ML1000
.
          CALL      VSES0000           * validate session key information
          BRANCH    EXIT,ML1010        * not a valid session
.
          CALL      CONT0000           * Confirm correct session
          BRANCH    EXIT,ML1000        * exit=0 yes and exit=1 no
.
ML1111    CALL      CLRV5555           * clear extra variables
.
          CALL      DADM0000           * display list of valid admissions
          BRANCH    EXIT,ML1000        * no admissions to display
.
          CALL      SADM0000           * select a admission from item numbers
          BRANCH    EXIT,ML1000        * nothing entered
.
          CALL      SDET0000           * Show operation DETails
.
          CALL      TEAM0000           * get team num 
          BRANCH    EXIT,ML1111        * no operating team details
.
          CALL      SURG0000           * get chief surgeon & requisition number
.
          CALL      CONT1111           * right patient continue
          BRANCH    EXIT,ML1111
.
          CALL      TRAN0000           * transfer data to sort tempfile
          BRANCH    EXIT,ML1111
.
          CALL      RPIO0000           * RePrint Instrument Order
          GOTO      ML1111
.
ML9000    CALL      DELTEMP1           * clean up tempfile 1
.
ML9999    CHAIN     PGM                * chain back to calling menu
+
.*****************************************************************************
.                   INIT0000
.         Initialization and open files
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,*EL,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetbf";
          OPEN      OPRDETB1,"oprdetbf"
.
          DISPLAY   *P64:24,"opriteaf";
          OPEN      OPRITEA1,"opriteaf"
.
          DISPLAY   *P64:24,"oprreqaf";
          OPEN      OPRREQA1,"oprreqaf"
.
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P64:24,"oprtryaf";
          OPEN      OPRTRYA1,"oprtryaf"
          OPEN      OPRTRYA2,"oprtryaf"
.
          DISPLAY   *P64:24,"oprtrybf";
          OPEN      OPRTRYB1,"oprtrybf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,NUM40;*96,OPCNCDSC,OPCNCLSU
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REPLACE   " 0",TODAY                     * set todays date
.
          MOVE      ONE,CNEWDS                     * set the new ? routines
.
. ------  Setup temp file usage  ------
.
          MOVE      PORT,CPORT                     * set the port number
          REPLACE   " 0",CPORT
.
. ------  Setup DOSCMND1 : set up tempfile command line  ------
.
          CLEAR     TEMPNAM1                       * setup physical filename
          APPEND    "oprtf1",TEMPNAM1
          APPEND    CPORT,TEMPNAM1
          RESET     TEMPNAM1
.
          PACK      DOSCMND1,SP30,SP20
          CLEAR     DOSCMND1
          APPEND    "isbuild ",DOSCMND1
          APPEND    TEMPNAM1,DOSCMND1
          APPEND    " 40 u1-6,27-27,31-33,34-39 ",DOSCMND1
          APPEND    "u7-26,1-6,27-27,31-33,34-39",DOSCMND1
          RESET     DOSCMND1
.
INIT9999  RETURN
.*****************************************************************************
.                   CLRV0000
.         Clear all main variables with spaces or zeros
.*****************************************************************************
CLRV0000  PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,CLINIC,CLINDESC,SESNDATE,SESNTIME,NORMDATE
.
.~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.
CLRV5555  PACK      LINE80,SP30,SP30,SP20
          UNPACK    LINE80,OPERDES1,OPERDES2,CASENUM
          UNPACK    LINE80,OPERDES3,STARTIME,PATNAME,TEAMNUM
          UNPACK    LINE80,WARDBED,PATAGE,SEXDESC,URNUMBER
          RETURN
+
.*********************************************************************
.*                  SCRN0000                                         *
.*        Display the screen                                         *
.*********************************************************************
SCRN0000  DISPLAY   *P1:4,*EF,*P5:4,OPCNCDSC,":":
                    *P5:5,"Session Date   :":
                    *P35:5,"Session Time :":
                    *V2LON,*P22:4,CLINIC,*P22:5,NORMDATE,*P50:5,SESNTIME:
                    *HOFF,*P35:4,CLINDESC;
SCRN9999  RETURN
+
.*****************************************************************************
.                   CLIN0000
.         get clinic code
.         Returns : CLINIC        the code
.                   CLINDESC      the description
.*****************************************************************************
CLIN0000  MOVE      ONE,SCRNFLAG       * set the redisplay flag
          DISPLAY   *P22:4,*EL,UNDLN6,*P22:4;
.
CLIN1111  KEYIN     *V2LON,CLINIC      * get doctor code
          ENDSET    CLINIC
          APPEND    SP6,CLINIC
          RESET     CLINIC
.
          MATCH     SP6,CLINIC         * test for return hit
          GOTO      CLIN7777 IF EQUAL
.
          CMATCH    "?",CLINIC         * test for ? routine
          GOTO      CLIN6666 IF NOT EQUAL
.
          CALL      QRTN0000           * do ? routine for clinic or surgeon
          IF        OPCNCLSU = 1
            BRANCH    EXIT,CLIN7777
            GOTO      CLIN6666
          ENDIF
CLIN4444
          DISPLAY   *P1:24,*EL,OPCNCDSC," : ",UNDLN6,*P19:24;
          GOTO      CLIN1111
CLIN6666
          CALL      VCLN0000           * validate clinic or surgeon
          BRANCH    EXIT,CLIN0000,CLIN4444
          CALL      REDP0000           * do redisplay if needed
          DISPLAY   *P22:4,*V2LON,CLINIC,*HOFF,*P35:4,CLINDESC;
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      CLIN9999
CLIN7777  
          MOVE      ONE,EXIT           * exit=1 if return hit
          CALL      REDP0000           * do redisplay if needed
          DISPLAY   *P22:4,*EL;        * clear this region
          GOTO      CLIN9999
.
CLIN9999  RETURN
+
.*****************************************************************************
.                   QRTN0000
.    Question mark routine display codes from doctors or clinic depending
.    on sys params
.*****************************************************************************
QRTN0000  BRANCH    OPCNCLSU,QRTN5555  * test if using surgeons or clinics
          CALL      OPRCLIDS           * codes ? routine
          MOVE      ZERO,SCRNFLAG      * set flag requiring redisplay
          MOVE      ZERO,EXIT
          GOTO      QRTN9999
.
QRTN5555  CALL      GETSCR00
          MOVE      ONE,SCRNFLAG      * set flag requiring redisplay
          MOVE      SP6,CLINIC
          CALL      PATDOCDS
          CALL      PUTSCR00                      * restore screen
          BRANCH    EXIT,QRTN9999
          MOVE      FALSE,EXIT                    * valid input
          MOVE      DOCCODE,CLINIC
.
QRTN9999  RETURN
+
.*****************************************************************************
.                   VCLN0000
.         Validate clinic or surgeon code
.*****************************************************************************
VCLN0000  BRANCH    OPCNCLSU,VCLN5555  * test if using surgeons or clinics
          PACK      KEY6,CLINIC,SP6
          CALL      RDOPCLI1           * check for valid clinic
          BRANCH    OVRCD,VCLN8000     * tell user his code not on file
          MOVE      OPCLDESC,CLINDESC  * copy clinic description to work var
          GOTO      VCLN7777
VCLN5555
          PACK      KEY6,CLINIC,SP6
          CALL      DOCN0000           * read doctors information
          BRANCH    EXIT,VCLN8800
          MOVE      SURGDESC,CLINDESC
VCLN7777
          MOVE      ZERO,EXIT
          GOTO      VCLN9999
VCLN8000  
          DISPLAY   *P1:24,*B,*EL,*V2LON,"Clinic code not on file.   ";
          GOTO      VCLN8888
VCLN8800 
          DISPLAY   *P1:24,*B,*EL,*V2LON,"Doctor code not on file.   ";
VCLN8888
          CALL      HITENTER
          MOVE      TWO,EXIT           * scrnflag=1 exit=1 or scrnflag=0 exit=2
          LOAD      EXIT,SCRNFLAG,ONE
.
VCLN9999  RETURN
+
.*****************************************************************************
.                                 REDP0000
.    Redisplay routine : this routine handles all redisplay formats by 
.    assigning a unique value to REDPFMAT 
.*****************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
          MOVE      ONE,SCRNFLAG
          CALL      SCRN0000
......    CALL      SDET0000
.
REDP9999  RETURN
+
.*****************************************************************************
.                   GDAT0000
.         Get the session date
.         Returns : SESNDATE      the date
.*****************************************************************************
GDAT0000  MOVE      "22",CCOL
          MOVE      "5",CVERT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
.
GDAT3333  CALL      KEYDATE            * start date default todays date
          BRANCH    OVRCD,GDAT3333
.
          PACK      SESNDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",SESNDATE
.
          MATCH     TODAY,SESNDATE     * test start date is >= today
          GOTO      GDAT8888 IF LESS
.
          PACK      NORMDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",SESNDATE
          GOTO      GDAT9999
GDAT8888
          DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "Session date must be greater than today's date.   ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT9999  RETURN
+
.*****************************************************************************
.                   GTIM0000
.         Get start time for booking the session
.         Returns : SESNTIME      the session time
.*****************************************************************************
GTIM0000  MOVE      FIVE,CVERT         * set up time keyin screen position
          MOVE      "50",CCOL
.
GTIM4444  PACK      SESNTIME,SP5       * clear the startime 
          UNPACK    SP4,CHOUR,CMIN     * blank the hour and mins for KEYTIME
          CALL      KEYTIME            * get start time
          MATCH     SP2,CHOUR          * return hit?
          GOTO      GTIM9990 IF EQUAL
          BRANCH    OVRCD,GTIM0000
          PACK      SESNTIME,CHOUR,COLON,CMIN
          MOVE      ZERO,EXIT          * exit=0 valid time
          GOTO      GTIM9999
.
GTIM9990  MOVE      ONE,EXIT           * exit=1 for return hit
.
GTIM9999  RETURN
+
.*****************************************************************************
.                   VSES0000
.        validate the Clinic date and time exist on the session file.
.        EXIT=0 session on file  EXIT=1 session not on file
.******************************************************************************
VSES0000  PACK      KEY22,SESNDATE,SESNTIME,CLINIC,SP20
.         CALL      RDOPSES7           * read session masters file
.         BRANCH    OVRCD,VSES8888
.
          CALL      RSOPSES7
          CALL      RKOPSES7
          BRANCH    OVRCD,VSES8888
.
          MATCH     SESNDATE,OPSEDATE    * same date
          GOTO      VSES8888 IF NOT EQUAL
.
          MATCH     SESNTIME,OPSETIME    * same time
          GOTO      VSES8888 IF NOT EQUAL
.
          MATCH     CLINIC,OPSECLIN      * same clinic
          GOTO      VSES8888 IF NOT EQUAL
.
          MOVE      OPSETHET,THEATRE   * get the operating room
          MOVE      ZERO,EXIT          * Exit equals 0 if code entered
          GOTO      VSES9999
VSES8888
          DISPLAY   *P1:24,*EL,*B,*V2LON,"Session not on file.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VSES9999  RETURN
+
.*****************************************************************************
.                                 CONT0000
.    Continue with information entered
.    EXIT=1 No and EXIT=0 for yes this is known as negative logic
.*****************************************************************************
CONT0000  DISPLAY   *P1:24,*EL,"Correct Session";
          MOVE      ONE,EXIT
          GOTO      CONT2222
.
.~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.
CONT1111  DISPLAY   *P1:24,*EL,"Re-print Order for this patient";
          MOVE      TWO,EXIT
.
.~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.
CONT2222  DISPLAY   " (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
          KEYIN     *V2LON,ANS;
          REPLACE   UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      CONT3333 IF NOT EQUAL
          MOVE      ZERO,EXIT                    * exit=0 if Yes
          GOTO      CONT8888
CONT3333
          MATCH     "N",ANS
          GOTO      CONT6666 IF NOT EQUAL
          MOVE      ONE,EXIT                     * exit=1 if No 
          GOTO      CONT8888
CONT6666
          BEEP
          BRANCH    EXIT,CONT0000,CONT1111
CONT8888
          DISPLAY   *P1:24,*EL;
CONT9999  RETURN
+
.*****************************************************************************
.                   DADM0000
.    Display session admission numbers of patients in the oprdetaf also
.    calculate the number of records in session MAXRANGE
.*****************************************************************************
DADM0000  DISPLAY   *P1:6,*EF,*P1:7,*V2LON,*ULON:
                     "    Adm. No.   Patient Name                 ":
                     "            Case No.  Start Time";
.
          MOVE      SEVEN,CVERT
          MOVE      ZERO,MAXRANGE
.         PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,SP3
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,SP3,ZERO,SP70
          CALL      RSOPDEA6
.
DADM2222  CALL      RKOPDEA6                * next read on details file
          BRANCH    OVRCD,DADM7000
.
          MATCH     SESNDATE,OPDADATE
          GOTO      DADM7000 IF NOT EQUAL
          MATCH     SESNTIME,OPDATIME
          GOTO      DADM7000 IF NOT EQUAL
          MATCH     CLINIC,OPDACLIN
          GOTO      DADM7000 IF NOT EQUAL
          COMPARE   ZERO,OPDASTAT
.         GOTO      DADM7000 IF NOT EQUAL
          GOTO      DADM2222 IF NOT EQUAL
.
.         have a valid detail record
.
          ADD       ONE,CVERT               * increment the row counter
          COMPARE   TWENTY3,CVERT           * test if reached a page
          GOTO      DADM3000 IF LESS
.
          MOVE      ZERO,EXIT               * have a full page
          GOTO      DADM9999
.
.         get the patient's name for the operation
.
DADM3000  MOVE      OPDAADMN,KEY8
          CALL      RDMISS1                  * get patients UR number
          BRANCH    OVRCD,DADM3100
          MOVE      AURNO,URNUMBER
          GOTO      DADM3200
.
.           --- no patmi1af record so get U/R from bokrc1af ---
.
DADM3100  MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,DADM5000
          MOVE      BKREURNO,URNUMBER
.
.           --- check for zero U/R ---
.
DADM3200  MATCH     ZEROUR,URNUMBER
          GOTO      DADM3300 IF NOT EQUAL
.
.               zero U/R, get U/R from patpramf
.
          MOVE      OPDAADMN,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,DADM4000           * not on Pre-Admissions file
          GOTO      DADM3400

DADM3300  MOVE      URNUMBER,KEY8
          CALL      RDMAST1                  * get surname given name and title
          BRANCH    OVRCD,DADM4000
.
DADM3400  CALL      PATN0000                 * set patient name
          GOTO      DADM6000
.
DADM4000  MOVE      "< Missing Name >",PATNAME
          GOTO      DADM6000
.
DADM5000  SUB       ONE,CVERT                * update row counter
          GOTO      DADM2222                 * get next patient
.
DADM6000  ADD       ONE,MAXRANGE             * display another patient case
.
          DISPLAY   *P1:CVERT,*V2LON,MAXRANGE,*HOFF,". ",OPDAADMN,SP3:
                    PATNAME,*P58:CVERT,OPDACASE,*P70:CVERT,OPDAEXPS
          PACK      NEXTODET,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                             OPDACASE,SP70
          MOVE      OPDACASE,LASTCASE        * save the case number displayed
          MOVE      OPDAHOSP,LASTHOSP        * save the last hospital id
          GOTO      DADM2222                 * lets go and get another one
.
.         end of session has been reached
.
DADM7000  MOVE      ZERO,EXIT
.
          COMPARE   ZERO,MAXRANGE            * test if any records found
          GOTO      DADM9999 IF NOT EQUAL    * record(s) found
.
          DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "No patients booked in this session.     ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
DADM9999  RETURN
+
.*****************************************************************************
.                   SADM0000
.    Select an admission number in the session chosen
.    This routine is closely linked with DADM0000 because it displays
.    the patients in the selected session with a index counter.
.    and Passes MAXRANGE ( maximum number of selectable patients )
.    When a patient is selected the routine reads backwards to the
.    desired record.  This is more efficient than reading forward because
.    of multiple pages.  Furthermore this routine operates the paging
.    capabilities.
.*****************************************************************************
SADM0000  CALL      GOPT0111
          BRANCH    EXIT,SADM9999,SADM3333,SADM5555
.
          COMPARE   INDEX,MAXRANGE
          GOTO      SADM1111 IF LESS
.
          MOVE      MAXRANGE,COUNTER       * set counter to count to index
.
.         PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,LASTCASE
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,LASTCASE,ZERO:
                          LASTHOSP,SP70
          CALL      RDOPDEA6
          GOTO      SADM2000
.
SADM1111  BEEP
          GOTO      SADM0000
.
.         loop backwards to get the desired record
.
SADM2000  COMPARE   INDEX,COUNTER          * test if read backwards enough
          GOTO      SADM3000 IF EQUAL
.
          CALL      RPOPDEA6               * Read backwards to desired item
.
. check for patmi1af or bokrc1af record.  If neither found don't update COUNTER
.
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1                * is there a patmi1af record ?
          BRANCH    OVRCD,SADM2100         * no
          GOTO      SADM2200
.
SADM2100  MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1               * is there a bokrc1af record ?
          BRANCH    OVRCD,RDPD2000         * no
.
SADM2200  SUB       ONE,COUNTER            * decrease COUNTER
          GOTO      SADM2000
.
.         have located the desired record
.
SADM3000  MOVE      OPDACASE,CASENUM       * record found lets getsome
          MOVE      OPDAADMN,ADMINNUM
          CALL      RDPD0000               * read patient details
          MOVE      OPDADES1,OPERDES1      * copy the operation descriptions
          MOVE      OPDADES2,OPERDES2
          MOVE      OPDADES3,OPERDES3
          MOVE      ZERO,EXIT              * valid number selected
          GOTO      SADM9999
.
.         redisplay page and select again
.
SADM3333  CALL      DADM0000               * redisplay all valid admissions
          GOTO      SADM0000
.
.         display next page
.
SADM5555  COMPARE   TEN5,MAXRANGE          * must be a screen full for next page
          GOTO      SADM1111 IF NOT EQUAL
.
          DISPLAY   *P1:8,*EF;
          MOVE      SEVEN,CVERT
          MOVE      ZERO,MAXRANGE
          MOVE      NEXTODET,KEY26
          CALL      RDOPDEA1
          CALL      DADM2222
          GOTO      SADM0000
.
SADM9999  RETURN
+
.*****************************************************************************
.                   SDET0000
.         Show operation DETails and Prompts
.*****************************************************************************
SDET0000  DISPLAY   *P1:6,*EF:
                    *P5:6,"Admission No   :",*P35:6,"Ward/Bed     :":
                    *P61:6,"Age :",*P5:7,"U/R Number     :":
                    *P5:8,"Case Number    :",*P35:8,"Team Number  :":
                    *P5:10,"Chief Surgeon  :":
                    *P5:11,"Operation Desc :";
.
          DISPLAY   *V2LON,*P22:6,ADMINNUM,*P50:6,WARDBED,*P66:6,PATAGE:
                    *P71:6,SEXDESC,*P22:7,URNUMBER,*HOFF,*P35:7,PATNAME,*V2LON:
                    *P22:8,CASENUM,*P50:8,TEAMNUM,*HOFF:
                    *P22:10,*EL,*P22:11,*EL,*P22:12,*EL,*P22:13,*EL;
.
SDET9999  RETURN
+
.*****************************************************************************
.                   PATN0000
.         Obtain the patients name
.         Returns : PATNAME
.*****************************************************************************
PATN0000  PACK      PATNAME,PTITL,SP30,SP10
          SCAN      SP10,PATNAME       * this scans for the end of title
          MOVEFPTR  PATNAME,FORM2      * save the current form ptr
          APPEND    PGNAME,PATNAME     * append the given name
          RESET     PATNAME,FORM2      * reset formptr for scan
          SCAN      SP4,PATNAME        * set the next location to load givenname
          APPEND    PSNAME,PATNAME   
          RESET     PATNAME
PATN9999  RETURN
+
.*****************************************************************************
.                   RDPD0000
.    This routine gets patient's UR number, ward bed, age, sex, and name
.*****************************************************************************
RDPD0000  MOVE      "< Missing Name >",PATNAME
          MOVE      SP8,SEXDESC
          MOVE      SP3,PATAGE
.
          MOVE      ADMINNUM,KEY8
          CALL      RDMISS1                * get patients UR Number
          BRANCH    OVRCD,RDPD2000
          MOVE      AURNO,URNUMBER
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * set up admission date
          CALL      PACDATE
          MOVE      CPCDATE,ADMNDATE
          REP       " 0",ADMNDATE
.
          PACK      KEY30,ADMINNUM,Z70     * pack key to read at end of admin
          CALL      RDSTRAN2               * position the fptr
          CALL      RDPTRAN2               * and read back one
          BRANCH    OVRCD,RDPD1000
.
          MOVE      ADMINNUM,DIM8          * copy admission number to form field
          MATCH     DIM8,TADMN             * check whether the right admission
          GOTO      RDPD1000 IF NOT EQUAL
.
          PACK      WARDBED,TWARD,SLASH,TBED
          MATCH     SP3,TBED               * test there is a bed number
          GOTO      RDPD3000 IF NOT EQUAL
.
          MOVE      APLUR,NBPLUR
          PACK      KEY13,TWARD,ADMINNUM,NBPLUR
          CALL      RDNOBE1
          BRANCH    OVRCD,RDPD3000
          PACK      WARDBED,TWARD,SLASH,NBROOM
          GOTO      RDPD3000
.
RDPD1000  MOVE      SP10,WARDBED
          GOTO      RDPD3000
.
.           --- no patmi1af record so get U/R from bokrc1af ---
.
RDPD2000  MOVE      SP10,ADMNDATE
          MOVE      SP7,WARDBED
          MOVE      ADMINNUM,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,RDPD9999
          MOVE      BKREURNO,URNUMBER
          MATCH     SP3,BKREWARD
          GOTO      RDPD3000 IF EQUAL
          PACK      WARDBED,BKREWARD,SLASH,BKREEBED
.
.           --- check for zero U/R ---
.
RDPD3000  MATCH     ZEROUR,URNUMBER
          GOTO      RDPD3100 IF NOT EQUAL
.
.               zero U/R, get U/R from patpramf
.
          MOVE      ADMINNUM,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,RDPD9999         * not on Pre-Admissions file
          GOTO      RDPD3200
.
RDPD3100  MOVE      URNUMBER,KEY8
          CALL      RDMAST1                * get patient master record
          BRANCH    OVRCD,RDPD9999
.
RDPD3200  CALL      PATN0000               * set patient's name
.
          MOVE      PSAGE,PATAGE           * get patient age
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEXDESC
.* ******************************************   End of Changes         *C-49016
.
RDPD9999  RETURN
+
.*****************************************************************************
.                   TEAM0000
.    Get team number from operating team file, if only one team then display
.    else get user to select which operating team
.*****************************************************************************
TEAM0000  DISPLAY   *P1:24,*EL;
.
.          PACK      KEY23,SESNDATE,SESNTIME,CLINIC,ZERO,CASENUM
.          CALL      RDOPDEA1                 * read details file
.          BRANCH    OVRCD,TEAM3000           * invalid details
.
          PACK      KEY26,SESNDATE,SESNTIME,CLINIC,CASENUM,ZERO,SP70
          CALL      RSOPDEA6
          CALL      RKOPDEA6
          BRANCH    OVRCD,TEAM3000
.
          MATCH     SESNDATE,OPDADATE    * same date
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     SESNTIME,OPDATIME    * same time
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     CLINIC,OPDACLIN      * same clinic
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     "0",DOPDASTA         * same status
          GOTO      TEAM3000 IF NOT EQUAL
.
          MATCH     CASENUM,OPDACASE     * same case number
          GOTO      TEAM3000 IF NOT EQUAL
.
          MOVE      OPDAUNIQ,REQNUM          * save unique number
.
.         Check if we have mnultiple teams
.
          PACK      KEY11,OPDAUNIQ,TWO       * Just check for team 2
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM1000           * No team two.
          GOTO      TEAM4000                 * Yes. Ask for which team
.
.         We have at most one team. Now check if we have any teams
.
TEAM1000  PACK      KEY11,OPDAUNIQ,ONE       * Just check for team 1
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM2000           * No team one. Check for a def team
          MOVE      OPDBTEAM,TEAMNUM        
          GOTO      TEAM6666
.
.         Check for a default team
.
TEAM2000  CALL      DEFT0000
          BRANCH    EXIT,TEAM3100
          GOTO      TEAM6666
.
.         Operating details have disappeared
.
TEAM3000  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "Operating Details have disappeared.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      TEAM9999
.
.         No operating team details available.
.
TEAM3100  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "Operating Team information not on file.   ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      TEAM9999
.
.         Ask the user to select the team to be used.
.
TEAM4000  CALL      DTEM0000                 * display team details
.
TEAM5555  KEYIN     *P50:8,"_",*P50:8,*V2LON,FORM1;   * select a team
.
          COMPARE   ZERO,FORM1
          GOTO      TEAM9000 IF EQUAL
.
          PACK      KEY11,REQNUM,FORM1
          CALL      RDOPDEB1
          BRANCH    OVRCD,TEAM5800           * invalid team number
          GOTO      TEAM6666
.
.         Check for a default team
.
TEAM5800  COMPARE   ONE,FORM1
          GOTO      TEAM5555 IF NOT EQUAL
.
          CALL      DEFT0000
          BRANCH    EXIT,TEAM5555
.
TEAM6666  MOVE      OPDBTEAM,TEAMNUM         * Save team number
          DISPLAY   *P50:8,*V2LON,TEAMNUM,*P1:13,*EF;
          PACK      KEY11,REQNUM,TEAMNUM     * get team selected
          CALL      RDOPDEB1
          MOVE      ZERO,EXIT                * display the team number
          GOTO      TEAM9999
.
TEAM9000  MOVE      ONE,EXIT
.
TEAM9999  RETURN
+
.*****************************************************************************
.                   DEFT0000
.         Set up default team              
.*****************************************************************************
DEFT0000  PACK      DIM42,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4,OPSETYD5:
                          OPSETYD6,OPSETYD7,OPSETYD8,OPSETYN1,OPSETYN2:
                          OPSETYN3,OPSETYN4,OPSETYN5,OPSETYN6
.
          MATCH     SP42,DIM42                    * are details blank?
          GOTO      DEFT9000 IF EQUAL
.
.         We have a default team
.
          UNPACK    DIM42,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4,OPDBTYD5:
                          OPDBTYD6,OPDBTYD7,OPDBTYD8,OPDBTYN1,OPDBTYN2:
                          OPDBTYN3,OPDBTYN4,OPDBTYN5,OPDBTYN6
          PACK      DIM36,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4,OPSEDCC5:
                          OPSEDCC6,OPSEDCC7,OPSEDCC8
          UNPACK    DIM36,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4,OPDBDCC5:
                          OPDBDCC6,OPDBDCC7,OPDBDCC8
          PACK      DIM36,OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4,OPSENUC5,OPSENUC6
          UNPACK    DIM36,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5,OPDBNUC6
          MOVE      ONE,OPDBTEAM
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
.         No default team
.
DEFT9000  MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.                   DTEM0000
.         Display possible Team Numbers
.*****************************************************************************
DTEM0000  DISPLAY   *P1:12,*EL,*V2LON,*ULON,*P1:13,*EF,"Team Number":
                    *P15:13,"Chief Surgeon",*P50:13,"Anaesthetist";
.
.         read operation details file to get OPDAUNIQ so can access team file
.
          MOVE      OPDAUNIQ,REQNUM         * save the unique number
          MOVE      TEN4,CVERT
          MOVE      ZERO,FORM1
.
DTEM2222  ADD       ONE,FORM1
          COMPARE   ZERO,FORM1
          GOTO      DTEM9999 IF EQUAL
.
          PACK      KEY11,OPDAUNIQ,FORM1    * key of unique number
          CALL      RDOPDEB1                * Read team details
          BRANCH    OVRCD,DTEM2500
          GOTO      DTEM3000
.
.         We may need to check for a default team as the 1st team
.
DTEM2500  COMPARE   ONE,FORM1
          GOTO      DTEM9999 IF NOT EQUAL   * Not team 1 -> we have finished
.
          CALL      DEFT0000
          BRANCH    EXIT,DTEM2222           * no default team
.
.         Get the details of this team
.
DTEM3000  UNPACK    SP20,SURGCODE,ANESCODE  * clear surgeon and anaes. code
          MOVE      ZERO,INDEX              * zero doctor type counter
.
.         get the doctor type
.
DTEM3333  ADD       ONE,INDEX
          LOAD      ACODE,INDEX,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          MATCH     SP3,ACODE
          GOTO      DTEM6666 IF EQUAL       * no code on file
.
          PACK      KEY5,ANSO,ANSP,ACODE
          CALL      RDCODE1                 * get indicator
          BRANCH    OVRCD,DTEM6666
.
          CMATCH    ANSS,TCINDC1
          GOTO      DTEM4444 IF NOT EQUAL   * not a surgeon
.
.         get the doctors code
.
          LOAD      SURGCODE,INDEX,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      DTEM6666                * exit loop and get doctors detail
.
.         test if code read is an anaesthetist
.
DTEM4444  CMATCH    ANSN,TCINDC1
          GOTO      DTEM6666 IF NOT EQUAL   * not an anaesthetist
.
          LOAD      ANESCODE,INDEX,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
DTEM6666  COMPARE   EIGHT,INDEX
          GOTO      DTEM3333 IF LESS        * more codes to check
.
          PACK      KEY6,SURGCODE,SP6       * get the chief surgeon's name
          CALL      DOCN0000
          DISPLAY   *P6:CVERT,*V2LON,OPDBTEAM,*HOFF,*P15:CVERT,SURGDESC;
          PACK      KEY6,ANESCODE,SP6       * get the anesthetist name
          CALL      DOCN0000
          DISPLAY   *P50:CVERT,SURGDESC;
.
          ADD       ONE,CVERT               * increment the display position
          GOTO      DTEM2222
.
DTEM9999  RETURN
+
.*****************************************************************************
.                  TRAN0000
.         Transfer data from requisition file to tempfile to sort in alpha betic
.*****************************************************************************
TRAN0000  CALL      OPENTMP1           * create tempfile to sort by description
          MOVE      ZERO,COUNTER
          PACK      KEY27,REQNUM,SP20
          CALL      RSOPREQ1                * positional read requisition file
.
TRAN2222  CALL      RKOPREQ1                * next read
          BRANCH    OVRCD,TRAN8888 
.
          MATCH     OPRQUNIQ,REQNUM         * test end of requisition record
          GOTO      TRAN8888 IF NOT EQUAL   * different requisition
.
          MOVE      OPRQITEM,ITEMCODE       * copy itemcode for LIDS
          MOVE      OPRQTYPE,ITEMFLAG       * copy type to form feild
          MOVE      OPRQCLSS,ITEMCLSS       * get class
.
          CALL      LIDS0000                * get instrument/tray item desc
          BRANCH    EXIT,TRAN2222           * invalid
.
          MOVE      ITEMFLAG,ITEMTYPE
          REP        "0T1I",ITEMTYPE
          PACK      KEY35,ITEMCODE,ITEMTYPE,ITEMCLSS,OPRQDCOD
          WRITE     OPRTMPF1,KEY35;ITEMCODE,ITEMDESC,ITEMTYPE:
                                   OPRQAMNT,ITEMCLSS,OPRQDCOD
          ADD       ONE,COUNTER
          GOTO      TRAN2222
.
TRAN8888  MOVE      ZERO,EXIT
.
          COMPARE   ZERO,COUNTER
          GOTO      TRAN9999 IF NOT EQUAL   * have requisitions on file
.
          DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "Theatre Requisitions have not been entered.   ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TRAN9999  RETURN
+
.*****************************************************************************
.                   RPIO0000
.    RePrint Instrument Order
.    Print clinic and patient details also operating room cheif surgeon and 
.    operation description
.*****************************************************************************
RPIO0000  DISPLAY   *P1:24,*EL,"Itemcode : ",*V2LON,*P60:24,"*** Printing ***";
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.
          CALL      TABH0000           * print table header
.
          MOVE      ZERO,SCRNFLAG
          PACK      KEY45,SP70         * read tempfile using the 2nd index
          READ      OPRTMPF2,KEY45;;
RPIO2222
          READKS    OPRTMPF2;ITEMCODE,ITEMDESC,ITEMTYPE,AMOUNT,ITEMCLSS,ITEMDCOD
          GOTO      RPIO9000 IF OVER
.
          DISPLAY   *P12:24,ITEMCODE   * show operator the program is working
          COMPARE   NUM60,CLNO
          GOTO      RPIO3333 IF LESS
          CALL      HEAD80
          CALL      TABH0000           * print table header
RPIO3333
          REPLACE   "0T1I",ITEMTYPE
.
          BRANCH    SCRNFLAG,RPIO4444
          PRINT     *3,ITEMTYPE,SP2,ITEMCODE,SP2,ITEMDESC,SP2,AMOUNT;
          MOVE      ONE,SCRNFLAG
          GOTO      RPIO2222
RPIO4444
          PRINT     *45,ITEMTYPE,SP2,ITEMCODE,SP2,ITEMDESC,SP2,AMOUNT
          MOVE      ZERO,SCRNFLAG
          ADD       ONE,CLNO
          GOTO      RPIO2222
RPIO9000  IF        SCRNFLAG=1
            PRINT     *N
          ENDIF
          COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL
          CALL      DRAWLINE
          PRINT     *N,*N,"*** End of Report ***",*N,*N:
                    *N,*N,"(Note: For non-inpatient Adm Number is the Booking":
                          " Number)"
.
          DISPLAY   *P1:24,*EL;
.
RPIO9999  RETURN
+
.*****************************************************************************
.
TABH0000  PRINT     *N,ITEMHEAD,*43,ITEMHEAD
          CALL      DRAWLINE
          ADD       TWO,CLNO
TABH9999  RETURN
+
.*****************************************************************************
.                   LIDS0000
.    get instrument or tray description and store in ITEMDESC depending on
.    the ITEMFLAG  this routine also does validation of code entered
.*****************************************************************************
LIDS0000  MATCH     SP70,ITEMCODE       * test if itemcode is blank eg not set
          GOTO      LIDS9990 IF EQUAL
.
          BRANCH    ITEMFLAG,LIDS4444  * 0 tray  1 instrument
.
.----- Tray items ----
.
          PACK      KEY6,ITEMCODE,SP6
          CALL      RDOPTRA1           * get tray description 
          BRANCH    OVRCD,LIDS9990
          MOVE      OPTHDESC,ITEMDESC  * copy the description
          MOVE      ZERO,EXIT
          GOTO      LIDS9999
.
.---- instruments ----
.
LIDS4444  PACK      KEY15,ITEMCODE,SP70
          CALL      RDOPITE1           * get instrument description 
          BRANCH    OVRCD,LIDS9990
          MOVE      OPITDESC,ITEMDESC  * copy the description
          MOVE      ZERO,EXIT
          GOTO      LIDS9999
LIDS9990
          PACK      ITEMDESC,SP20      * clear the old description
          MOVE      ONE,EXIT
.
LIDS9999  RETURN
+
.*****************************************************************************
.                                 HEAD0000
.    Print heading page and new patient
.*****************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD80
.
HEAD1111  PRINT     *N,"Patient    : ",PATNAME,*N:
                    "U/R Number : ",URNUMBER,*29,"Sex : ",SEXDESC:
                    *50,"Age : ",PATAGE,*N:
                    "Adm Number : ",ADMINNUM,*24,"Adm.Date : ",ADMNDATE:
                    *46,"Ward/Bed : ",WARDBED
.
          PRINT     "Sess'n Date: ",NORMDATE,*28,"Time : ",SESNTIME:
                    *48,"Clinic : ",CLINDESC
.
          PRINT     *N,*N,"Theatre        : ",THEATRE,*24,"Case Num : ",CASENUM:
                    *46,"Team Num : ",TEAMNUM:
                    *N,"Chief Surgeon  : ",SURGCODE,SP4,SURGDESC:
                    *N,"Operation desc : ",OPERDES1:
                    *N,*18,OPERDES2:
                    *N,*18,OPERDES3
          ADD       TEN1,CLNO
.
HEAD9999  RETURN
+
.*****************************************************************************
.
DRAWLINE  PRINT     "---------------------------------------------------------":
                    "-----------------------"
RETURN
+
.*****************************************************************************
.                   SURG0000
.    To get the surgeon check the first indicator from the codes file and
.    matched against the operating team file the corresponding doctor
.    code is the chief doctor surgeon code.  If you understand that I'd 
.    advise seeking professional help.
.    Get requisition number as well
.*****************************************************************************
.SURG0000  PACK      KEY26,SESNDATE,SESNTIME,CLINIC,ZERO,CASENUM,SP70
.         CALL      RDOPDEA1                * read operation details file
.         BRANCH    OVRCD,SURG8800
SURG0000  PACK      KEY26,SESNDATE,SESNTIME,CLINIC,CASENUM,ZERO,SP70
.
          CALL      RSOPDEA6
          CALL      RKOPDEA6
          BRANCH    OVRCD,SURG8800
.
          MATCH     SESNDATE,OPDADATE    * same date
          GOTO      SURG8800 IF NOT EQUAL
.
          MATCH     SESNTIME,OPDATIME    * same time
          GOTO      SURG8800 IF NOT EQUAL
.
          MATCH     CLINIC,OPDACLIN      * same clinic
          GOTO      SURG8800 IF NOT EQUAL
.
          MATCH     "0",DOPDASTA        * same status
          GOTO      SURG8800 IF NOT EQUAL
.
          MATCH     CASENUM,OPDACASE     * same case number
          GOTO      SURG8800 IF NOT EQUAL
.
          PACK      KEY11,OPDAUNIQ,TEAMNUM
          CALL      RDOPDEB1                * read operating team file
          BRANCH    OVRCD,SURG8800
.
SURG1000  MOVE      OPDAUNIQ,REQNUM         * copy the unique number
          MOVE      ZERO,INDEX              * zero doctor type counter
.
.         get the surgeon type
.
SURG2222  ADD       ONE,INDEX
          LOAD      ACODE,INDEX,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          MATCH     SP3,ACODE
          GOTO      SURG4444 IF EQUAL       * no code to use
.
          PACK      KEY5,ANSO,ANSP,ACODE
          CALL      RDCODE1                 * get indicator from codes file
          BRANCH    OVRCD,SURG4444
.
          MATCH     ANSS,TCINDC1            * test if this is the surgeon
          GOTO      SURG4444 IF NOT EQUAL   * not a surgeon
.
.         have the first surgeon so use this as the chief surgeon
.
          LOAD      SURGCODE,INDEX,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      SURG5555                * exit loop and get doctors detail
.
SURG4444  COMPARE   EIGHT,INDEX             * test end of list
          GOTO      SURG2222 IF LESS        * more details to check
.
.         get surgeon name
.
SURG5555  PACK      KEY6,SURGCODE,SP6
          CALL      DOCN0000           * check for valid doctor & get name
          BRANCH    EXIT,SURG8000      * tell user his code not on file
.
.         display chief surgeon
.
SURG7777  DISPLAY   *P22:10,*V2LON,SURGCODE,*HOFF,*P35:10,SURGDESC;
          DISPLAY   *V2LON,*P22:11,OPERDES1,*P22:12,OPERDES2,*P22:13,OPERDES3;
          MOVE      ZERO,EXIT
          GOTO      SURG9999
.
SURG8000  DISPLAY   *P1:24,*B,*EL,*V2LON,"Chief Surgeon not on file.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      SURG9999
.
SURG8800  MATCH     "1",TEAMNUM
          GOTO      SURG8880 IF NOT EQUAL
.
.         Check for a default team.
.
          CALL      DEFT0000
          BRANCH    EXIT,SURG8880
          GOTO      SURG1000
.
SURG8880  DISPLAY   *P1:24,*B,*EL,*V2LON,"Operating Team not on file.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
SURG9999  RETURN
+
.*****************************************************************************
.                   DOCN0000
.    setup surgeons or doctors name     KEY6 must be set to the doctor code
.*****************************************************************************
DOCN0000  CALL      RDDOCT1            * check for valid doctor
          BRANCH    OVRCD,DOCN8888     * tell user his code not on file
.
          MOVE      ONE,PACFRMT        * title name surname
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME            * use the pacname routine to set name
          MOVE      PACFNAME,SURGDESC
          MOVE      ZERO,EXIT          * exit=0 doctor code on file
          GOTO      DOCN9999
DOCN8888
          MOVE      SP30,SURGDESC
          MOVE      ONE,EXIT           * exit=1 doctor code not registered
.
DOCN9999  RETURN
+
.*****************************************************************************
.                                 GOPT0000
.  GOPT is a wonderful routine that gets the item selected or a command option
.
.  GOPT0111 ->  Select Item, (F)irst page, (N)ext page, (E)xit :
.******************************************************************************
GOPT0000  BRANCH    EXIT,GOPT0111,GOPT0222,GOPT0333
.
GOPT0111  DISPLAY   *P1:24,*EL,"Select Item, ":
                    "(",*V2LON,"F",*HOFF,")irst page, ":
                    "(",*V2LON,"N",*HOFF,")ext page, ":
                    "(",*V2LON,"E",*HOFF,")xit : ";
          MOVE      "F2N3E1",MASK      * set the replace conversion for GOPT
          MOVE      "50",CCOL
          MOVE      ONE,EXIT
          GOTO      GOPT4444
GOPT0222
GOPT0333
GOPT4444
          KEYIN     *PCCOL:24,*JR,*ZF,*V2LON,REPLY;
          ENDSET    REPLY
          APPEND    SP2,REPLY
          RESET     REPLY
          REPLACE   UPPLOW,REPLY
.
          TYPE      REPLY              * a number was entered
          GOTO      GOPT6666 IF EQUAL
.
          REPLACE   MASK,REPLY 
          TYPE      REPLY
          GOTO      GOPT9000 IF EQUAL
.
GOPT5555  BEEP
          GOTO      GOPT0000
GOPT6666
          MOVE      REPLY,INDEX
          COMPARE   INDEX,ZERO
          GOTO      GOPT5555 IF NOT LESS
          MOVE      ZERO,EXIT          * valid number exit=0
          GOTO      GOPT9999
GOPT9000
          MOVE      REPLY,EXIT
.
GOPT9999  RETURN
+
.*****************************************************************************
.                                 OPENTMP1
.*****************************************************************************
OPENTMP1  DISPLAY   *P1:24,*EL,*V2LON,"** Pre-processing **",*BLINKON:
                                      "    Please wait";
          CALL      DELTEMP1           * delete the temp file
          EXECUTE   DOSCMND1,TASKID    * temp index file for item code order
          OPEN      OPRTMPF1,TEMPNAM1  * open the tempfile file pointer
          OPEN      OPRTMPF2,TEMPNAM1  * open the tempfile file pointer
          DISPLAY   *P1:24,*EL;        * clear notice to users
          RETURN
+
.*****************************************************************************
.
DELTEMP1  CLOSE      OPRTMPF1          * close 1st and 2nd index
          CLOSE      OPRTMPF2          * close 1st and 2nd index
          CLEAR      DELCOMM
          APPEND     "iserase ",DELCOMM 
          APPEND     TEMPNAM1,DELCOMM 
          RESET      DELCOMM 
          EXECUTE    DELCOMM,TASKID
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       OPRCLIDS           * Clinics scan
          INC       PATDOCDS           * Doctors scan
          INC       PATDOCKY           * Doctors scan
          INC       SEXDSCIO
.
          INC       BOKRC1IO/INC       * Booking Master file
          INC       OPRCLIIO/INC       * Clinics file
          INC       OPRDEAIO/INC       * Session Booking Details file
          INC       OPRDEBIO/INC       * Operating Teams file
          INC       OPRITEIO/INC       * Items file
          INC       OPRREQIO/INC       * Instruments and Tray Items Requisition
          INC       OPRSESIO/INC       * Sessions file
          INC       OPRTRAIO/INC       * Tray Header
          INC       OPRTRBIO/INC       * Tray Details
.
          INC       PATCODIO/INC       * Codes file 
          INC       PATDO1IO/INC       * Doctors file
          INC       PATMA1IO/INC       * Patient Master file
          INC       PATMI1IO/INC       * Patient Admissions file
          INC       PMSVX1IO/INC       * Patient Admissions file
          INC       PATNOBIO/INC       * No-Bed file
          INC       PATPR1IO/INC       * Patient Pre-Admissions file
          INC       PATTRNIO/INC       * Patient Tranfers
          INC       PATWR1IO/INC       * Ward/Bed file
