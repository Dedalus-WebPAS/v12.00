.******************************************************************************
.*  Include Name  : IBAPOSDS.PBL                                              *
.*  Description   : Display a postcode                                        *
.*  Modifications :                                                           *
.*        V10.14.01 25/03/2019  Peter Vela      Task 0857392                  *
.*                  Added IBPORRAV - Regional and Remote Areas Value          *
.*        V10.03.01 17/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*                                                                            *
.******************************************************************************
IBAPOSDS
DPCOD000  PACK      KEY56,SP30,SP30
          CALL      RSIBPOS2                * Position on & read a postcode
          CALL      RKIBPOS2                  file record
          BRANCH    OVRCD,DPCOD800
.
          DISPLAY   *P1:3,*EF,*P36:4,*V2LON,*ULON:
                    "POSTCODES",*HOFF:
                    *P1:6,"Postcode Description : ",UNDLN30,UNDLN15;
          PACK      KEY45,SP30,SP30
          KEYIN     *P24:6,*V2LON,KEY45;
          DISPLAY   *P1:8,*ULON,"Code",*P10:8,"Description",SP30,SP4:
                    *P56:8,"State",*P63:8,"ABS ASGC ",*P73:8,"RRAV ";
.
DPCOD100  MOVE      "9",CVERT
          MOVE      "1",CCOL
          DISPLAY   *P1:9,*EF;
.
          PACK      KEY56,KEY45,SP70
          CALL      RSIBPOS2                * Position on & read a postcode
DPCOD200  CALL      RKIBPOS2                  file record
          BRANCH    OVRCD,DPCOD600
.
          RESET     KEY45
          GOTO      DPCOD300 IF EOS         * Display all postcodes
.
          MATCH     KEY45,SP30
          GOTO      DPCOD300 IF EQUAL       * Display all postcodes
.
          MATCH     KEY45,IBPOSUBR
          GOTO      DPCOD600 IF NOT EQUAL   * Different postcode
.
DPCOD300  DISPLAY   *PCCOL:CVERT,*V2LON,IBPOPCOD,*HOFF,SP1,IBPOSUBR:
                    SP1,IBPOSTAT,SP4,IBPOASGC,SP1,IBPORRAV;
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      DPCOD200 IF LESS        * < 24 lines, continue
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, ":
                               "(",*V2LON,"F",*HOFF,")irst screen, ":
                               "(",*V2LON,"N",*HOFF,")ext screen ? ";
.
DPCOD400  KEYIN     *P45:24,*EL,*DV,UNDLN1,*P45:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      DPCOD500 IF EQUAL        * Next screen
.
          MATCH     ANSC,ANS
          GOTO      DPCOD900 IF EQUAL        * Continue
.
          MATCH     ANSF,ANS
          GOTO      DPCOD100 IF EQUAL        * First screen
.
          MATCH     ANSN,ANS
          GOTO      DPCOD500 IF EQUAL        * Next screen
.
          BEEP
          GOTO      DPCOD400
.
DPCOD500  MOVE      "9",CVERT
          MOVE      "1",CCOL
          DISPLAY   *P1:9,*EF;
          GOTO      DPCOD200
.
DPCOD600  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, ":
                               "(",*V2LON,"F",*HOFF,")irst screen ? ";
.
DPCOD700  KEYIN     *P30:24,*DV,UNDLN1,*P30:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     ANSC,ANS
          GOTO      DPCOD900 IF EQUAL        * Continue
.
          MATCH     ANSF,ANS
          GOTO      DPCOD100 IF EQUAL        * First screen
.
          BEEP
          GOTO      DPCOD700
.
DPCOD800  DISPLAY   *P1:24,*EL,*B,"No postcodes on file.  ";
          CALL      HITENTER
          GOTO      DPCOD950
.
DPCOD900  MOVE      ZERO,EXIT
          GOTO      DPCOD999
.
DPCOD950  MOVE      ONE,EXIT
DPCOD999  RETURN
+
.

+
