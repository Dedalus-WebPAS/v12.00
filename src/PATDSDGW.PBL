.
.       PATDSDGW : Display DRG Desription and Weighting file
.       This include displays entries in the DRG file
.       Modifications:
.
.         V9.08.01  19/04/2007  Mike Laming   CAR 137125
.                   Add DRG Version to key of PATDGWaf
.         V5.07.01  25/11/1999  Davin
.                   Changed for 4 character drgs
.
PATDSDGW  CALL      CVAR0000                * Clear storage variables
.
.                                                                     *I-137125
          MATCH     SP3,DRGVERZ
          IF        @EQUAL
            READ      CONTROLF,HUND05;*130,PTCNDGPV
            MOVE      PTCNDGPV,DRGVERZ
          ENDIF
          UNPACK    DRGVERZ,ANS,DIM1,KEY1
          PACK      KEY3,DIM1,DOT,KEY1
.
          MOVE      ONE,CPAGENO             * Set page
          MOVE      "7",CVERT
          MOVE      ZERO,DISPCNT            * Clear count
          MOVE      ZERO,HLEF
          CALL      GETSCR00
          DISPLAY    *P1:SVERT,*EF,*P32:6,*V2LON,"DRG Version ",KEY3; *I-137125
          DISPLAY    *P5:7,*EF,*V2LON,*ULON,"Code  ":
                     *P14:7,"Description":
                     *P25:7,SP30,SP10,SP9
.
. ----- Loop Over File -----
.
          PACK      KEY7,DRGCODE,DRGVERZ,SP10                         *C-137125
          CALL      RDPTDGW1
          COMPARE   ZERO,OVRCD
          GOTO      DSPR1200 IF EQUAL
.
DSPR1000  CALL      RKPTDGW1
          BRANCH    OVRCD,DSPR4000
          MATCH     DRGVERZ,PTDWDRGV                                  *I-137125
          GOTO      DSPR1000 IF NOT EQUAL
.
DSPR1200  ADD       ONE,CVERT               * Increment counters
          COMPARE   "23",CVERT
          GOTO      DSPR3000 IF NOT LESS    * Need a new page
.
          ADD       ONE,DISPCNT             * Add to item counter
.
DSPR2000  DISPLAY   *P1:CVERT,*V2LON,DISPCNT,*HOFF,".  ":
                    *P5:CVERT,PTDWDRGC,*HOFF:
                    *P14:CVERT,PTDWDESC
          PACK      DRGC[DISPCNT],PTDWDRGC
          GOTO      DSPR1000
.
. ----- New Screen Needed -----
.
DSPR3000  BRANCH    CPAGENO,DSPR3200
.
. ----- Middle Pages : (N)ext & (P)revious -----
.
DSPR3100  MOVE      ONE,CALLPOSN            * Set position
          CALL      KEYR0000
          BRANCH    EXIT,DSPR7000,PATDSDGW,DSPR9500
.                        Next     First    Exit
          GOTO      DSPR6000
.
. ----  First Page : (N)ext -----
.
DSPR3200  MOVE      TWO,CALLPOSN
          CALL      KEYR0000
          BRANCH    EXIT,DSPR7000,DSPR5000,DSPR9500
.                        Next     First    Exit
          GOTO      DSPR6000
.
. ----- No More Data To Display -----
.
DSPR4000  COMPARE   ZERO,DISPCNT
          GOTO      DSPR8000 IF EQUAL       * No dates 
.
          BRANCH    CPAGENO,DSPR4200
.
.         last page : (P)revious
.
DSPR4100  MOVE      THREE,CALLPOSN
          CALL      KEYR0000
          BRANCH    EXIT,DSPR5000,PATDSDGW,DSPR9500
.                        Next     First    Exit
          GOTO      DSPR6000
.
. ----- First Page -----
.
DSPR4200  MOVE      FOUR,CALLPOSN
          CALL      KEYR0000
          BRANCH    EXIT,DSPR5000,DSPR5000,DSPR9500
.                        Next     First    Exit
          GOTO      DSPR6000
.
DSPR5000  BEEP   
          BRANCH    CALLPOSN,DSPR3100,DSPR3200,DSPR4100,DSPR4200
.
. ----- Item On Screen Selected -----
.
DSPR6000  UNPACK    DRGC[FORM2],DRGCODE
          MOVE      ZERO,EXIT
          GOTO      DSPR9000
.
. ----- Set Up For A New Page -----
.
DSPR7000  CALL      CVAR0000                * Clear storage variables
          ADD       ONE,CPAGENO             * Add to page counter
          MOVE      ONE,DISPCNT             * Reset item counter
          MOVE      "8",CVERT               * Set row
          DISPLAY   *P1:CVERT,*EF;          * Clear screen
          GOTO      DSPR2000
.
DSPR8000  DISPLAY   *P1:24,*B,"No Records Exist. ",*EL;
          CALL      HITENTER
.
. ----- Set Exit Flags -----
.
DSPR9000  CALL      PUTSCR00
          GOTO      DSPR9999
.
DSPR9500  CALL      PUTSCR00
          MOVE      ONE,EXIT                * Exit selected
DSPR9999  RETURN
+
.******************************************************************************
.*                                 CVAR0000               Called by: DSPR0000 *
.*                        Clear The Storage Variables                         *
.******************************************************************************
CVAR0000  MOVE      "15",FORM2
          WHILE     FORM2
            MOVE      SP30,DRGC[FORM2]
            SUB       ONE,FORM2
          DO
CVAR9999  RETURN
+
.******************************************************************************
.*                                 KEYR0000               Called by: DSPR0000 *
.*                     Keyin Response For Line 24 Question                    *
.*  Para's :  CALLPOSN     Which line 24 prompt                               *
.*  Returns:  EXIT     = 0 Item selected (FORM2)                              *
.*                     = 1 Next screen selected                               *
.*                     = 2 Previous screen selected                           *
.*                     = 3 Nothing entered                                    *
.******************************************************************************
KEYR0000  DISPLAY   *P1:24,*EL,"Select Record";
          MOVE      "13",CCOL
          IF        ((CALLPOSN = ONE) | (CALLPOSN = TWO))
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        ((CALLPOSN = ONE) | (CALLPOSN = THREE))
            DISPLAY   ", (",*V2LON,"F",*HOFF,")irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   " : ",*EL;
          ADD       "4",CCOL
.
KEYR1000  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          RESET     DIM2
          GOTO      KEYR3000 IF EOS         * Nothing entered
.
          TYPE      DIM2
          GOTO      KEYR4000 IF EQUAL       * Number entered
.
. ----- Letter Entered -----
.
          MATCH     SP1,DIM2
          GOTO      KEYR2000 IF NOT EQUAL   * Invalid entry
.
          UNPACK    DIM2,ANS,ANS            * Get required letter in ANS
          REP       UPPLOW,ANS              * Get as uppercase
          REP       "N1F2",ANS              * Turn valid letters into number
          MOVE      ZERO,EXIT               * Clear exit flag
          MOVE      ANS,EXIT                * Set exit flag
          BRANCH    EXIT,KEYR9999,KEYR9999
.
KEYR2000  BEEP
          GOTO      KEYR1000
.
KEYR3000  MOVE      THREE,EXIT              * Nothing entered
          GOTO      KEYR9999
.
. ----- Number Entered -----
.
KEYR4000  MOVE      ZERO,EXIT               * Set exit flag
          MOVE      DIM2,FORM2              * Get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYR2000 IF NOT LESS    * Negative entered
.
          COMPARE   FORM2,DISPCNT
          GOTO      KEYR2000 IF LESS        * Number too large
.
KEYR9999  RETURN
+
