.*****************************************************************************
.*                             SLTCOUNT                                      *
.*              Update the slot stats for a clinic                           *
.*****************************************************************************
.
SLTCOUNT  MOVE      ZERO,OSESLOTB              * slots booked
          MOVE      ZERO,OSESLOTS              * slots scheduled
          MOVE      ZERO,OSESSLTN              * New slots scheduled
          MOVE      ZERO,OSESSLTR              * Review slots scheduled
          MOVE      ZERO,OSESSLTS              * Special slots scheduled
          MOVE      ZERO,OSEBNEW               * New slots scheduled
          MOVE      ZERO,OSEBRV                * Review slots scheduled
          MOVE      ZERO,OSEBSPC               * Special slots scheduled
          MOVE      ZERO,OSEANEW               * New slots scheduled
          MOVE      ZERO,OSEARV                * Review slots scheduled
          MOVE      ZERO,OSEASPC               * Special slots scheduled
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          CALL      RDSBOKA1
SLTCNT10  CALL      RDKBOKA1
          BRANCH    OVRCD,SLTCNT90
.
          MATCH     OSESITE,OBASITE
          GOTO      SLTCNT90 IF NOT EQUAL
.
          MATCH     OSECLIN,OBACLIN
          GOTO      SLTCNT90 IF NOT EQUAL
.
          MATCH     OSEDATE,OBADATE
          GOTO      SLTCNT90 IF NOT EQUAL
.
          MATCH     OSESTRT,OBASTRT
          GOTO      SLTCNT90 IF NOT EQUAL
.
          COMPARE   THREE,OBASTAT
          GOTO      SLTCNT10 IF EQUAL
.
          COMPARE   SEVEN,OBASTAT
          GOTO      SLTCNT10 IF EQUAL
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,SLTCNT10
.
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
          ADD       TCFORM6,OSESLOTS
.
          IF        (OBASTAT=1|OBASTAT=4|OBASTAT=5)
            ADD       TCFORM6,OSESLOTB
            CALL      ADDTIM00
          ENDIF
.
          MATCH     ANSN,TCINDC1
          GOTO      SLTCNT20 IF NOT EQUAL
.
          ADD       ONE,OSESSLTN
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBNEW
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBNEW
            ADD       ONE,OSEANEW
          ENDIF
          GOTO      SLTCNT10
.
SLTCNT20  MATCH     ANSR,TCINDC1
          GOTO      SLTCNT30 IF NOT EQUAL
.
          ADD       ONE,OSESSLTR
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBRV
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBRV
            ADD       ONE,OSEARV
          ENDIF

          GOTO      SLTCNT10
.
SLTCNT30  MATCH     ANSS,TCINDC1
          GOTO      SLTCNT10 IF NOT EQUAL
.
          ADD       ONE,OSESSLTS
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBSPC
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBSPC
            ADD       ONE,OSEASPC
          ENDIF
          GOTO      SLTCNT10
.
SLTCNT90  CALL      UPSESA1
.
SLTCNT99  RETURN
+
