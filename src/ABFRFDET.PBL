.******************************************************************************
.* System         : Patient Billing                                           *
.* Include Name   : ABFRFDET.PBL                                              *
.* Author         : J.Tan                                                     *
.* Date           : 15/09/2022  Task 0906597                                  *
.* Modification   :                                                           *
.*           V11.03.02 07/06/2023  J.Tan    TSK 0923703                       *
.*                     Mod to use 6 decimal places for ABF calculations       *
.*           V11.03.01 11/01/2023 J.Tan    TSK 0906597                        *
.*                     Changed the ABF formula                                *
.*                                                                            *
.******************************************************************************
ABFRFDET  MOVE      ZERO,PWAMOUNT
          MOVE      ZERO,ANMCAMNT
          MOVE      ZERO,AINDAMNT
          MOVE      ZERO,ARESAMNT
          MOVE      ZERO,ATAAMNTS
          MOVE      ZERO,NEPAMNTS
          MOVE      ZERO,ABFCHRGE
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      ABFT9999 IF NOT EQUAL   * Not using ABF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATABFA2,"patabfaf"
          TRAPCLR   IO
          BRANCH    OVRCD,ABFT9999
.
          PACK      KEY5,ANSC,ANSL,ALRECOMP
          CALL      RDCODE1
          BRANCH    OVRCD,ABFT9999
.
          MATCH     "N",TCINDC2              * Check for NWAU Calculations
          GOTO      ABFT9000 IF NOT EQUAL    * Not an ABF Funding
.
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY   * referral date
          CALL      PACDATE                 * Format the admin date
          REP       " 0",CPCDATE
          PACK      KEY16,CPCDATE,SP20
.
          WRITE     HTMLFILE;"<table border=1 width=100%>";
.
          IF        MREFFLAG=1
            WRITE     HTMLFILE;"<tr><td width=30%>Referral Date:</td>";
          ELSE
            WRITE     HTMLFILE;"<tr><td width=30%>Service Date:</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=70%><b>&nbsp;",KEY16,"</b></td>":
                             "</tr>"; 
.
          UNPACK    ENCOUTKY,PTABADMN,PTABENCT
.
          PACK      KEY27,ENCOUTKY,ZERO,SEVENTY1,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Encounter Number:</td>":
                               "<td><b>&nbsp;",PTABENCT,"    ":
                               PTABCDES,"</b></td>":
                             "</tr>";
.
          MOVE      SP70,TDESC
          MATCH     SP70,ALRECOMP
          IF        !@EQUAL & !@EOS
            PACK      KEY5,ANSC,ANSL,ALRECOMP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      SP70,TDESC
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Claim Code:</td>":
                               "<td><b>&nbsp;",ALRECOMP,"</b>":
                               " - <b>",TDESC,"</b></td>":
                             "</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,THIRTY2,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Tier 2:</td>":
                             "<td><b>&nbsp;",PTABCDES,"</b>":
                             "</td>":
                             "</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,TWENTY1,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
.
          MOVE      PTABADJV,FORM10P4
          MOVE      PTABADJV,AINDAMNT
          STRIP     PTABCDES
.
          WRITE     HTMLFILE;"<tr><td>Indigenous Status:</td>":
                            "<td><b>&nbsp;",FORM10P4," ( ",PTABCDES,")</b></td>":
                             "</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,THIRTY3,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
.
          UNPACK    PTABCDES,KEY3
          MOVE      PTABADJV,FORM10P4
          MOVE      PTABADJV,ANMCAMNT
.
          MATCH     "Yes",KEY3
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td>Multidisciplinary Clinic:</td>":
                               "<td><b>&nbsp;",FORM10P4," ( ",KEY3,")</b></td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr><td>Multidisciplinary Clinic:</td>":
                               "<td><b>&nbsp;",KEY3,"</b></td>":
                               "</tr>";
          ENDIF
.
.         Patient Remoteness Area Adjustment
.
          MOVE      SP70,DIM3
          MOVE      ZERO,FORM10P4
          PACK      KEY27,ENCOUTKY,ZERO,TEN6,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ELSE
            MOVE      PTABADJV,FORM10P4
            MOVE      PTABADJV,ARESAMNT
            MOVE      "R2 ",DIM3
          ENDIF
.
          IF        PTABADJV = ZERO
            PACK      KEY27,ENCOUTKY,ZERO,TEN7,SP70
            CALL      RDPTABF2
            IF        OVRCD=ONE
              CALL      CLPATABF
            ELSE
              MOVE      PTABADJV,FORM10P4
              MOVE      PTABADJV,ARESAMNT
              MOVE      "RA3",DIM3
            ENDIF
          ENDIF
.
          IF        PTABADJV = ZERO
            PACK      KEY27,ENCOUTKY,ZERO,TEN9,SP70
            CALL      RDPTABF2
            IF        OVRCD=ONE
              CALL      CLPATABF
            ELSE
              MOVE      PTABADJV,FORM10P4
              MOVE      PTABADJV,ARESAMNT
              MOVE      "RA4",DIM3
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Remote Area Adjustment:</td>":
                             "<td><b>&nbsp;",FORM10P4," ( ",DIM3,")</b></td>":
                             "</tr>";
.
          MOVE      SP70,DIM3
          MOVE      ZERO,FORM10P4
          PACK      KEY27,ENCOUTKY,ZERO,THIRTY6,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ELSE
            MOVE      "RA3",DIM3
          ENDIF
.
          IF        PTABADJV=ZERO
            PACK      KEY27,ENCOUTKY,ZERO,THIRTY7,SP70
            CALL      RDPTABF2
            IF        OVRCD=ONE
              CALL      CLPATABF
            ELSE
              MOVE      "RA4",DIM3
            ENDIF
          ENDIF
.
          MOVE      PTABADJV,FORM10P4
          MOVE      PTABADJV,ATAAMNTS
.
          WRITE     HTMLFILE;"<tr><td>Hospital Remoteness Area:</td>";
.
          MATCH     SP70,DIM3
          IF        @EQUAL
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"</b></td>";
          ELSE
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"(",DIM3,")</b></td>";
          ENDIF
          WRITE     HTMLFILE;"</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,FIFTY3,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
          MOVE      PTABADJV,FORM10P4
.
          WRITE     HTMLFILE;"<tr><td>NWAU:</td>":
                             "<td><b>&nbsp;",FORM10P4,"</b></td>":
                             "</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,SIXTY2,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
          MOVE      PTABADJV,FORM10P4
          MOVE      PTABADJV,PWAMOUNT
          WRITE     HTMLFILE;"<tr><td width=30%>Price Weight:</td>":
                             "<td width=70%><b>&nbsp;",FORM10P4,"</b></td>":
                     "</tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,SIXTY1,SP70
          CALL      RDPTABF2
          IF        OVRCD=ONE
            CALL      CLPATABF
          ENDIF
          MOVE      PTABADJV,FORM10P4
          MOVE      PTABADJV,NEPAMNTS
          WRITE     HTMLFILE;"<tr><td width=30%>NEP:</td>":
                             "<td width=70%><b>&nbsp;",FORM10P4,"</b></td>":
                     "</tr></table>";
.
          CALL      RCAL00000                 * Display ABF calculation
          GOTO      ABFT9999
.
ABFT9000  WRITE     HTMLFILE;"<tr><center><b>ABF/NWAU Details ":
                             "are not applicable to this event</b></td>":
                             "</center></tr>";
.
ABFT9999  RETURN
+
.*******************************************************************************
.         Display ABF Calculation
.*******************************************************************************
RCAL0000  CALL      RFBF0000                  * Calculate ABF amount
          MOVE      ABFCHRGE,FORM12P2
.
.         {PW x APaed x (1 + ARes + AInd + ANMC) x (1 + ATreat)} x NEP
.
          WRITE     HTMLFILE;"<table border=1 width=100%>":
                             "<tr>&nbsp;</tr><tr><td>":
                      "ABF Calculation:</td></tr>":
                      "<tr><td>":
                      "= {PW x APaed x ":
                      "(1 + ARes + AInd + ANMC)} x ":
                      "(1 + ATreat)} x ":
                      "NEP</td></tr>";
.
          PACK      KEY27,ENCOUTKY,ZERO,SEVENTY1,SP70
          CALL      RDPTABF2
          IF        OVRCD=0
            MATCH     "Subsequent Daily",PTABCDES
            IF        @EQUAL
              MOVE      ZERO,FORM12P2
              GOTO      RCAL9000
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>":
                      "= { ",PWAMOUNT," x 1 x (1 + ",ARESAMNT," + ":
                      AINDAMNT," + ",ANMCAMNT," )} x (1 + ",ATAAMNTS,")}":
                      "x $",NEPAMNTS,"</td></tr>";
.
RCAL9000  WRITE    HTMLFILE;"<tr><td>":
                      "= $",FORM12P2,"</td></tr><table>"
.
RCAL9999  RETURN
+
.*******************************************************************************
.         Calculate ABF amount
.         {PW x APaed x (1 + ARes + AInd + ANMC) x (1 + ATreat)} x NEP
.*******************************************************************************
RFBF0000  MOVE      ONE,F106A
          ADD       ARESAMNT,F106A           * A_Res
          ADD       AINDAMNT,F106A           * A_Ind
          ADD       ANMCAMNT,F106A           * + A_NMC
.
          MOVE      ONE,F106B                * 1
          ADD       ATAAMNTS,F106B           * + ATreat
.
          MOVE      PWAMOUNT,FORM106         * PW
          MULT      F106A,FORM106            * x (1+A_Ind+A_res+A_NMC)
          MULT      F106B,FORM106            * x (1 + ATreat)
          MOVE      FORM106,ABFCHRGE
.
          IF        ABFCHRGE<0
            MOVE      ZERO,ABFCHRGE
          ENDIF
          MULT      NEPAMNTS,ABFCHRGE       * NEP
.
RFBF9999  RETURN
+
