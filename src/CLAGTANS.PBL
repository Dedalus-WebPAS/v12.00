.******************************************************************************
.* System         : CLA                                                       *
.*                                                                            *
.* Include Name   : CLAGTANS.PBL                                              *
.*                                                                            *
.* Function       : Extract all relevent anaesthetist information from        *
.*                    OPR files                                               *
.*                                                                            *
.* Author         : Darren Jones    02/03/1993                                *
.*                                                                            *
.* Returns        : CODEANAE, DESCANAE      anaethetist code and desc         *
.*                  CODESUPA, DESCSUPA      supervising anae code and desc    *
.*                  CODETRNA, DESCTRNA      trainee anae code and desc        *
.*                                                                            *
.* Modifications  :                                                           *
.*        V11.03.01 20.03.2023  DASarkies      TSK 0909393                    *
.*                  Added hospital name as key to theatre booking             *
.******************************************************************************
CLAGTANS
GANS0000  OPEN      OPRDETB1,"oprdetbf"
          UNPACK    SP20,CODEANAE,CODETRNA,CODESUPA
          PACK      DESCANAE,SP30,SP30
          PACK      DESCTRNA,SP30,SP30
          PACK      DESCSUPA,SP30,SP30
.
          PACK      KEY11,THETUNIQ,SP1
          CALL      RSOPDEB1
.
GANS1000  CALL      RKOPDEB1
          BRANCH    OVRCD,GANS2000          * end of file
.
          MATCH     THETUNIQ,OPDBUNIQ
          GOTO      GANS2000 IF NOT EQUAL   * different visit
.
          MOVE      ZERO,FORM2
GANS1500  ADD       ONE,FORM2
          COMPARE   NINE,FORM2
          GOTO      GANS1000 IF NOT LESS    * no anaesthetist for this team
.
          LOAD      KEY3,FORM2,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                               OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          MATCH     SP3,KEY3
          GOTO      GANS1500 IF EQUAL       * no doctor type

          PACK      KEY5,ANSO,ANSP,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,GANS1500          * invalid doctor type
.
          MATCH     ANSN,TCINDC1
          GOTO      GANS1500 IF NOT EQUAL   * no anaesthetist
.
          LOAD      KEY6,FORM2,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                               OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          MATCH     SP6,KEY6
          GOTO      GANS1500 IF EQUAL       * no doctor code
.
          CALL      RDDOCT1
          BRANCH    OVRCD,GANS1500          * invalid doctor code
.
. see which type of anaesthetist it is
.
          MOVE      ZERO,FORM1
          REP       "A1S2T3",TCINDC2        * the valid types of anaes.
          MOVE      TCINDC2,FORM1           * set up form1 if valid code
.
          MOVE      SP6,KEY6
          LOAD      KEY6,FORM1,CODEANAE,CODESUPA,CODETRNA
          MATCH     SP6,KEY6
          GOTO      GANS1500 IF NOT EQUAL   * code already found
.
          STORE     DCODE,FORM1,CODEANAE,CODESUPA,CODETRNA
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          STORE     PACFNAME,FORM1,DESCANAE,DESCSUPA,DESCTRNA
          GOTO      GANS1500
.
GANS2000  OPEN      OPRSESA1,"oprsesaf"
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          CALL      RDOPSES1
          BRANCH    OVRCD,GANS9000
.
          MOVE      ZERO,FORM2
GANS2500  ADD       ONE,FORM2
          COMPARE   NINE,FORM2
          GOTO      GANS9000 IF NOT LESS    * no anaesthetist for this team
.
          LOAD      KEY3,FORM2,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                               OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8
          MATCH     SP3,KEY3
          GOTO      GANS2500 IF EQUAL       * no doctor type

          PACK      KEY5,ANSO,ANSP,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,GANS2500          * invalid doctor type
.
          MATCH     ANSN,TCINDC1
          GOTO      GANS2500 IF NOT EQUAL   * no anaesthetist
.
          LOAD      KEY6,FORM2,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                               OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8
          MATCH     SP6,KEY6
          GOTO      GANS2500 IF EQUAL       * no doctor code
.
          CALL      RDDOCT1
          BRANCH    OVRCD,GANS2500          * invalid doctor code
.
. see which type of anaesthetist it is
.
          MOVE      ZERO,FORM1
          REP       "A1S2T3",TCINDC2        * the valid types of anaes.
          MOVE      TCINDC2,FORM1           * set up form1 if valid code
.
          MOVE      SP6,KEY6
          LOAD      KEY6,FORM1,CODEANAE,CODESUPA,CODETRNA
          MATCH     SP6,KEY6
          GOTO      GANS2500 IF NOT EQUAL   * code already found
.
          STORE     DCODE,FORM1,CODEANAE,CODESUPA,CODETRNA
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          STORE     PACFNAME,FORM1,DESCANAE,DESCSUPA,DESCTRNA
          GOTO      GANS2500
.
GANS9000  CLOSE     OPRDETB1
          CLOSE     OPRSESA1
.
GANS9999  RETURN
