.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS21.PBL                                              *
.* Description    : Calculate WIES21 value for an individual admission        *
.******************************************************************************
.* Date           : 21/08/2014                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS21 - Variables required for WIES21                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW21FD  PATW21A1 - Victorian cost weights (for WIES21) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS21DRG - DRG to calculate WIES21 for                    *
.*                                                                            *
.* Returns        : TOTWIS21 - Total WIES21 value                             *
.*                  WIS21DRG - DRG used to calculate WIES21                   *
.*                  WIS21LOS - LOS used to calculate WIES21                   *
.*                  WIS21WGT - Weight used to calculate WIES21                *
.*                  WIS21DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*       V12.00.01  26/05/2025  PJ Le Febour     TSK 0955096 AlphaNum VisitNu *
.*                  amend  COMPARE AADMNO,PTAFADMN to  MATCH  AADMNO,PTAFADMN *
.*                         COMPARE AADMNO,PTEDADMN to  MATCH  AADMNO,PTEDADMN *
.*                         COMPARE AADMNO,PTEOADMN to  MATCH  AADMNO,PTEOADMN *
.*       V10.05.00  21/08/2014  Davin            CAR 296980                   *
.*                  Created for 2014/2015 WIES21 changes:                     *
.******************************************************************************
+
PATWIS21  CALL      IN21W000                * Initialise variables
          BRANCH    EXIT,WIS21950
.
          CALL      C21TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS21950
.
          CALL      C21TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS21950
.
          CALL      C21PI000                * Check Specified Program Identifier
          BRANCH    EXIT,WIS21950
.
          CALL      M21RG000                * Modify DRG if necessary
.
          CALL      C21RG000                * Check for problem DRG
          BRANCH    EXIT,WIS21950
.
          CALL      M21CH000                * Check for Mechanical Ventilation
.
          CALL      T21AL000                * Check for Thalessaemia
.
          CALL      A21AA000                * Check for AAA
.
          CALL      A21SD000                * Check for ASD
.
          CALL      C21PD000                * Check for Cochlear Prosthetic
.
          CALL      B21SW000                * Calculate Base WIES21
.
          CALL      C21AB000                * Check for aboriginality
.
. ---- Calculate WIES21 value ----
.
          MOVE      TOTBWIES,TOTWIS21       * base WIES
          ADD       MVCOPAY,TOTWIS21        * mechv copayment
          ADD       THCOPAY,TOTWIS21        * thal  copayment
          ADD       AAACOPAY,TOTWIS21       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS21       * ASD   copayment
          ADD       CPDCOPAY,TOTWIS21       * CPD   copayment
          ADD       ATSIWIES,TOTWIS21       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS21999
.
WIS21950  MOVE      ONE,EXIT
WIS21999  RETURN
+
.******************************************************************************
.*                                  IN21W000              Called by: PATWIS21 *
.*                         Initialise WIES21 variables                        *
.******************************************************************************
IN21W000  MOVE      ZERO,TOTWIS21
          MOVE      ZERO,WIS21LOS
          MOVE      ZERO,WIS21WGT
          MOVE      SP20,WIS21DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW21A1,"patw21af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN21W950
.
IN21W900  MOVE      ZERO,EXIT
          GOTO      IN21W999
.
IN21W950  MOVE      ONE,EXIT
IN21W999  RETURN
+
.******************************************************************************
.*                                  C21TY000              Called by: PATWIS21 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C21TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C21TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C21TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C21TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "K   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
          MATCH    "U   ",THCSCOD
          GOTO     C21TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C21TY999
.
C21TY900  MOVE     ONE,EXIT                 * Not valid
C21TY999  RETURN
+
.******************************************************************************
.*                                  C21TR000              Called by: PATWIS21 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C21TR000  PACK     KEY25,AADMNO,SP70
          CALL     RSPTASF1
C21TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C21TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C21TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C21TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C21TR900 IF EQUAL
.
C21TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C21TR999
.
C21TR900  MOVE     ONE,EXIT                 * invalid
C21TR999  RETURN
+
.******************************************************************************
.*                                  C21PI000              Called by: PATWIS21 *
.*                        Check Specified Program Identifier                  *
.******************************************************************************
C21PI000  PACK     KEY5,ANSP,ANSK,PMVXPGID  * Program Identifier (Cat PK)
          CALL     RDCODE1
          BRANCH   OVRCD,C21PI800
.
          MATCH    "06",THCSCOD             * competitive elective surgery
          GOTO     C21PI900 IF EQUAL
.
C21PI800  MOVE     ZERO,EXIT                * valid
          GOTO     C21PI999
.
C21PI900  MOVE     ONE,EXIT                 * invalid
C21PI999  RETURN
+
.******************************************************************************
.*                                  M21RG000              Called by: PATWIS21 *
.*                      Modify DRG if necessary for WIES21                    *
.******************************************************************************
M21RG000  CALL      R21IO000           * Radiotherapy
M21RG999  RETURN
+
.******************************************************************************
.*                                  R21IO000              Called by: M21RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R21IO000  MATCH     "B61",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "A40",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "B60",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "B82",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "S65",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "W60",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
          MATCH     "W61",WIS21DRG
          GOTO      R21IO999 IF EQUAL
.
.>>>>     Check for sameday radiotherapy session:
.
          MATCH     ADATE,DDATE
          GOTO      R21IO040 IF NOT EQUAL
.
          MATCH     "R62B",WIS21DRG
          GOTO      R21IO999 IF NOT EQUAL
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R21IO030  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R21IO040
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R21IO040 IF NOT EQUAL
.
          MATCH     "Z51.0",PTEDCODE        * radiotherapy?
          GOTO      R21IO030 IF NOT EQUAL
.
          GOTO      R21IO900                * modify drg for WIES21
.
.>>>>     Check if operation code exists (non-sameday):
.
R21IO040  MATCH     ADATE,DDATE
          GOTO      R21IO999 IF EQUAL       * don't modify sameday
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R21IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R21IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R21IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R21IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R21IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R21IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R21IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R21IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R21IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R21IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R21IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R21IO100 IF NOT LESS
.
R21IO900  MOVE      "R64Z",WIS21DRG         * modified for WIES21
R21IO999  RETURN
+
.******************************************************************************
.*                                  C21RG000              Called by: PATWIS21 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C21RG000  PACK     KEY4,WIS21DRG
          CALL     RDPT211                  * check WIES21 weights file
          BRANCH   OVRCD,C21RG900
.
          MATCH    "960Z",PT21DRGC
          GOTO     C21RG900 IF EQUAL
.
          MATCH    "961Z",PT21DRGC
          GOTO     C21RG900 IF EQUAL
.
          MATCH    "962Z",PT21DRGC
          GOTO     C21RG900 IF EQUAL
.
          MATCH    "963Z",PT21DRGC
          GOTO     C21RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C21RG999
.
C21RG900  MOVE     ONE,EXIT
C21RG999  RETURN
+
.******************************************************************************
.*                                  M21CH000              Called by: PATWIS21 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M21CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M21CH999
.
          MATCH     "D",PT21MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M21CH999
          ENDIF
.
          MATCH     "4",PT21MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M21CH999  RETURN
+
.******************************************************************************
.*                                  T21AL000              Called by: PATWIS21 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T21AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT21CPAY            * check WIES21 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T21AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T21AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T21AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T21AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T21AL999  RETURN
+
.******************************************************************************
.*                                  A21AA000              Called by: PATWIS21 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A21AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT21CPAY          * check WIES21 weights file
          GOTO      A21AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A21AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A21AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A21AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A21AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A21AA999  RETURN
+
.******************************************************************************
.*                                  A21SD000              Called by: PATWIS21 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A21SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT21CPAY          * check WIES21 weights file
          GOTO      A21SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A21SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A21SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A21SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A21SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A21SD999  RETURN
+
.******************************************************************************
.*                                  C21PD000              Called by: PATWIS21 *
.*                 Calculate Cochlear Prosthetic Device Co-payment            *
.******************************************************************************
C21PD000  MOVE      ZERO,CPDCOPAY
          MATCH     "B",PT21CPAY            * check WIES21 weights file
          GOTO      C21PD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
C21PD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,C21PD900
.
          MATCH     AADMNO,PTEOADMN
          GOTO      C21PD900 IF NOT EQUAL
.
          MATCH     "41617-00",PTEOCODE     * check for implant
          IF        @EQUAL
            ADD       ONE,CPDCOPAY
          ENDIF
          MATCH     "41617-01",PTEOCODE     * check for implant removal
          IF        @EQUAL
            SUB       ONE,CPDCOPAY
          ENDIF
          GOTO      C21PD100
.
C21PD900  IF        CPDCOPAY >= 2
            MOVE      "5.0544",CPDCOPAY     * eligible for CPD copayment
          ELSE
            MOVE      ZERO,CPDCOPAY         * not eligible for CPD copayment
          ENDIF
C21PD999  RETURN
+
.******************************************************************************
.*                                  B21SW000              Called by: PATWIS21 *
.*                            Calculate Base WIES21                           *
.******************************************************************************
B21SW000  CALL      D21SC000                * length of stay category
          CALL      C21ST000                * calculate inlier status
.
          MATCH     "L",W21NLIER            * low outlier
          CALL      BW21L000 IF EQUAL       * calculate base WIES21
.
          MATCH     "I",W21NLIER            * inlier
          CALL      BW21N000 IF EQUAL       * calculate base WIES21
.
          MATCH     "H",W21NLIER            * high outlier
          CALL      BW21H000 IF EQUAL       * calculate base WIES21
.
B21SW999  RETURN
+
.******************************************************************************
.*                                  D21SC000              Called by: B21SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D21SC000  MOVE      NBRDAYS,WIS21LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS21LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D21SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS21LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D21SC999
          ENDIF
.
          IF        WIS21LOS > 1825
            MOVE      "1825",WIS21LOS       * maximum LOS is 5 years
          ENDIF
.
D21SC999  RETURN
+
.******************************************************************************
.*                                  C21ST000              Called by: B21SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C21ST000  MOVE      "I",W21NLIER            * set status to inlier
          IF        WIS21LOS < PT21LIBP
            MOVE      "L",W21NLIER          * set status to low outlier
            GOTO      C21ST999
          ENDIF
.
          ASSIGN    (PT21HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS21LOS > FORM12P4
            MOVE      "H",W21NLIER          * set status to high outlier
          ENDIF
C21ST999  RETURN
+
.******************************************************************************
.*                                  BW21L000              Called by: B21SW000 *
.*                     Calculate Base WIES21 for Low Outlier                  *
.******************************************************************************
BW21L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT21TSDW,TOTBWIES             * base WIES21
            ASSIGN    (TOTBWIES/PT21TIMW),TOTIES21  * IES21
.
            MOVE      PT21TSDW,WIS21WGT             * for display
            MOVE      "Same Day Wgt ",WIS21DSC
            GOTO      BW21L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT21TODW,TOTBWIES             * base WIES21
            ASSIGN    (TOTBWIES/PT21TIMW),TOTIES21  * IES21
.
            MOVE      PT21TODW,WIS21WGT             * for display
            MOVE      "One Day Wgt  ",WIS21DSC
            GOTO      BW21L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT21TODW+((WIS21LOS-1)*PT21TLOM)),TOTBWIES
.                                                    * base WIES21
            ASSIGN    (TOTBWIES/PT21TIMW),TOTIES21   * IES21
.
            MOVE      PT21TLOM,WIS21WGT             * for display
            MOVE      "Low Outlr Wgt",WIS21DSC
            GOTO      BW21L999
          ENDIF
BW21L999  RETURN
+
.******************************************************************************
.*                                  BW21N000              Called by: B21SW000 *
.*                      Calculate Base WIES21 for Inlier                      *
.******************************************************************************
BW21N000  MOVE      ONE,TOTIES21          * IES21 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT21TSDW,TOTBWIES   * base WIES21
.
            MOVE      PT21TSDW,WIS21WGT             * for display
            MOVE      "Same Day Wgt ",WIS21DSC
            GOTO      BW21N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT21TODW,TOTBWIES   * base WIES21
.
            MOVE      PT21TODW,WIS21WGT             * for display
            MOVE      "One Day Wgt  ",WIS21DSC
            GOTO      BW21N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT21TIMW,TOTBWIES   * base WIES21
.
            MOVE      PT21TIMW,WIS21WGT             * for display
            MOVE      "Inlier Wgt   ",WIS21DSC
            GOTO      BW21N999
          ENDIF
BW21N999  RETURN
+
.******************************************************************************
.*                                  BW21H000              Called by: B21SW000 *
.*                    Calculate Base WIES21 for High Outlier                  *
.******************************************************************************
BW21H000  ASSIGN    ((WIS21LOS-PT21HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT21THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT21THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT21TIMW+TOTBWIES),TOTBWIES             *base WIES21
          ASSIGN    (TOTBWIES/PT21TIMW),TOTIES21             * IES21
.
          MOVE      PT21THOM,WIS21WGT                        * for display
          MOVE      "High Outl Wgt",WIS21DSC
BW21H999  RETURN
+
.******************************************************************************
.*                                  C21AB000              Called by: PATWIS21 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C21AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C21AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C21AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C21AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C21AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ADD       CPDCOPAY,FORM12P4       * CPD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C21AB999  RETURN
+
