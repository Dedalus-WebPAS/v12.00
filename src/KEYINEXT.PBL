.******************************************************************************
.*  Include Name  : KEYINEXT.PBL                                              *
.*  Description   : Keyin of an Extension Number                              *
.*  Author        : The Lion King                                             *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF4 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  EOSFLG   - Flag for eos input                             *
.*                                                                            *
.*  Returns       : IBEXEXTN   - Keyed in code                                  *
.*                  HFNAME   - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                             or End of string if EOSFLG = 1                 *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          : 23/12/96  ROD                                             *
.*                            Clear code & desc if EOS                        *
.*                                                                            *
.******************************************************************************
KEYINEXT  
KEXTN010  MATCH     SP4,CKYIDEF4                  * using a default ?
          GOTO      KEXTN050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN4:      * no default used
                    *PECOL:EVERT,*V2LON,IBEXEXTN:
                    *PECOL:EVERT,*DV,IBEXEXTN
          GOTO      KEXTN080
.
KEXTN050  MOVE      CKYIDEF4,IBEXEXTN
          KEYIN     *PECOL:EVERT,*DV,IBEXEXTN:     * default used
                    *PECOL:EVERT,*V2LON,*RV,IBEXEXTN:
                    *PECOL:EVERT,*DV,IBEXEXTN
.
.         check what was entered
.
KEXTN080  MOVE      ZERO,EOSFLG
          CALL      EXTCK000
          BRANCH    EXIT,KEXTN100,KEXTN705,KEXTN805,KEXTN905
.                         "?"      valid    nothing exitchar
          GOTO      KEXTN010
.
.         a "?" was input
.
KEXTN100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KEXTN105  MOVE      ONE,CNEWDS
          CALL      IBAEXTDS
.
.         now ask for code while leaving displayed codes on screen
.
KEXTN150  MATCH     SP4,CKYIDEF4                  * using a default
          GOTO      KEXTN200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Extension No. : ":
                    *P17:24,*DV,UNDLN4:      * no default used
                    *P17:24,*V2LON,IBEXEXTN:
                    *P17:24,*DV,IBEXEXTN
          GOTO      KEXTN300
.
KEXTN200  MOVE      CKYIDEF4,IBEXEXTN
          KEYIN     *P1:24,*EL,*DV,"Extension No. : ":
                    *P17:24,*DV,CKYIDEF4:     * default used
                    *P17:24,*V2LON,*RV,IBEXEXTN:
                    *P17:24,*DV,IBEXEXTN
.
.         repeat the validation of the code
.         check what was entered
.
KEXTN300  MOVE      ZERO,EOSFLG
          CALL      EXTCK000
          BRANCH    EXIT,KEXTN105,KEXTN700,KEXTN800,KEXTN900
.                         "?"      valid    nothing exitchar
.
          GOTO      KEXTN150
.
.         a valid code was entered 
. 
KEXTN700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,IBEXEXTN
.
KEXTN705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KEXTN999 
.
.         nothing was input
.
KEXTN800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,IBEXEXTN
.
KEXTN805  MOVE      SP20,IBEXDESC                 * blank out description
          MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      KEXTN999 
.
.         EXITCHAR input
.
KEXTN900  CALL      FRESCR00                      * free the screen saved
.
KEXTN905  MOVE      TWO,EXIT                      * EXITCHAR
.
KEXTN999  RETURN
+
.***************************************************************************
.*                              EXTCK000             Called by: KEYINEXT   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
EXTCK000  RESET     IBEXEXTN
          GOTO      EXTCK550 IF EOS
.
          ENDSET    IBEXEXTN
          APPEND    SP4,IBEXEXTN
          RESET     IBEXEXTN
.
          MATCH     EXITCHAR,IBEXEXTN
          GOTO      EXTCK600 IF EQUAL
.
          MATCH     SP4,IBEXEXTN
          GOTO      EXTCK700 IF EQUAL
.
. check for a "?"
.
          MATCH     QUEST,IBEXEXTN
          GOTO      EXTCK800 IF EQUAL
.
. a code was entered so validate
. 
EXTCK500  PACK      KEY4,IBEXEXTN
          CALL      RDIBEXT1
          BRANCH    OVRCD,EXTCK900
.
          MOVE      TWO,EXIT
          GOTO      EXTCK999
.
.         End of string entered
.
EXTCK550  BRANCH    CKYIMAND,EXTCK950              * mandatory
          MOVE      THREE,EXIT
          MOVE      ONE,EOSFLG
          GOTO      EXTCK700
.
. exitchar entered
.
EXTCK600  MOVE      FOUR,EXIT
          GOTO      EXTCK999
.
. nothing entered
.
EXTCK700  BRANCH    CKYIMAND,EXTCK950             * mandatory
          MOVE      THREE,EXIT
          MOVE      SP20,IBEXDESC                  * clear desc field
          MOVE      SP4,IBEXEXTN
          GOTO      EXTCK999
.
. "?" entered
.
EXTCK800  MOVE      ONE,EXIT
          GOTO      EXTCK999
.
. invalid code entered
.
EXTCK900  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      EXTCK999
.
EXTCK950  BEEP
          MOVE      ZERO,EXIT
.
EXTCK999  RETURN
.
