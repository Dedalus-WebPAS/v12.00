.******************************************************************************
.*  File          :  PATIX2TM.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.12 Coding Maintenance Routines for PATWEB88     *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.*       V11.02.01  19/07/2022  Bella Turco    TSK 0921957                    *
.*                  Added NOMORE00 - Display "No more records" at end of list *
.*                  Added PTCNNRTD - Displays number of rows set by parameter *
.*                  value                                                     *
.*       V11.02.00  28/03/2022  Davin            TSK 0917793                  *
.*                  Created new - cloned from PATIX1TM.PBL                    *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 12 Diagnosis Coding Maintenance: Display, Add, Update, Delete
.------------------------------------------------------------------------------
ICDX2D00  RESET     PTICD001
          MOVE      "12",ICD10EDN
.                                           * make sure files are open
          OPEN      PATD12A1,"patd12af"     * Open ICD10 Ed.12 Diseases
          OPEN      PATD12A2,"patd12af"     * Open ICD10 Ed.12 Diseases
          OPEN      PTDK12A1,"patdk12f"     * ICD10 Ed.12 Key Word Diag.
          OPEN      PTDK12A2,"patdk12f"     * ICD10 Ed.12 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICDX2D40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICDX2D10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICDX2D20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICDX2D30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VALX1C00
            GOTO      ICDX2D99
          ENDIF
.
          GOTO      ICDX2D93
.
ICDX2D10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICDX2D99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPD12A1
          COMPARE   ZERO,OVRCD
          GOTO      ICDX2D92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPD12A1      * Writes away the data
          PROC      PTDK12UP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDX2D99
.
ICDX2D20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPD12A1
          BRANCH    OVRCD,ICDX2D91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICDX2D99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPD12A1      * Writes away the data
          PROC      PTDK12UP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDX2D99
.
ICDX2D30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPD12A1
          BRANCH    OVRCD,ICDX2D91
          CALL      DEPD12A1      * Deletes the data
          PROC      PTDK12UP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICDX2D99
.
ICDX2D40  PACK      KEY9,PTICD001,SP70
          CALL      RDPD12A1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 12 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICDX2D99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICDX2D99
.
ICDX2D91  MOVE      "ICD10 Edition 12 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2D99
.
ICDX2D92  MOVE      "ICD10 Edition 12 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2D99
.
ICDX2D93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2D99
.
ICDX2D99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 12 Procedure Coding Maintenance: Display, Add, Update, Delete
.------------------------------------------------------------------------------
ICDX2P00  RESET     PTICO001
          MOVE      "12",ICD10EDN
.
          OPEN      PATO12A1,"pato12af"     * Open ICD10 Ed.12 Procedures
          OPEN      PATO12A2,"pato12af"     * Open ICD10 Ed.12 Procedures
          OPEN      PTOK12A1,"patok12f"     * ICD10 Ed.12 Key Word Proc.
          OPEN      PTOK12A2,"patok12f"     * ICD10 Ed.12 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICDX2P40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICDX2P10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICDX2P20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICDX2P30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VALX1C00
            GOTO      ICDX2P99
          ENDIF
.
          GOTO      ICDX2P93
.
ICDX2P10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICDX2P99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPO12A1
          COMPARE   ZERO,OVRCD
          GOTO      ICDX2P92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPO12A1      * Writes away the data
          PROC      PTOK12UP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICDX2P99
.
ICDX2P20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPO12A1
          BRANCH    OVRCD,ICDX2P91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICDX2P99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPO12A1      * Writes away the data
          PROC      PTOK12UP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICDX2P99
.
ICDX2P30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPO12A1
          BRANCH    OVRCD,ICDX2P91
          CALL      DEPO12A1      * Deletes the data
          PROC      PTOK12UP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICDX2P99
.
ICDX2P40  PACK      KEY9,PTICO001,SP70
          CALL      RDPO12A1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 12 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICDX2P99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICDX2P99
.
ICDX2P91  MOVE      "ICD10 Edition 12 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2P99
.
ICDX2P92  MOVE      "ICD10 Edition 12 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2P99
.
ICDX2P93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICDX2P99
.
ICDX2P99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 12 Code Validation
.------------------------------------------------------------------------------
VALX2C00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VALX2C90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VALX2C10,VALX2C20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VALX2C90
.
VALX2C10  MOVE      VALDCODE,KEY9
          CALL      RDPO12A1
          IF        OVRCD=0
            MOVE      PT0ODSC2,VALDNAME
          ENDIF
          GOTO      VALX2C90
.
VALX2C20  MOVE      VALDCODE,KEY9
          CALL      RDPD12A1
          IF        OVRCD=0
            MOVE      PT0DDSC2,VALDNAME
          ENDIF
          GOTO      VALX2C90
.
VALX2C90  CALL      OPENHTML
          BRANCH    EXIT,VALX2C99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VALX2C99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 12 Procedure Code: Search Table
.-----------------------------------------------------------
SRHX2P00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDVX,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.12 in use yet?
....      GOTO      SRHX2P90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSOK12A2
SRHX2P10  CALL      RKOK12A2
          BRANCH    OVRCD,SRHX2P90
.
          CALL      SX2PRA00
          BRANCH    OVRCD,SRHX2P10,SRHX2P90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPO12A1
          BRANCH    OVRCD,SRHX2P10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SRHX2P10
          ENDIF
.
          BRANCH    LNK2PRT,SRHX2P20,SRHX2P30
          GOTO      SRHX2P99
.
SRHX2P20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
          GOTO      SRHX2P80
.
SRHX2P30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODSC2:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
SRHX2P80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SRHX2P10 IF NOT EQUAL
          GOTO      SRHX2P99
.
SRHX2P90  CALL      NOMORE00    * Display No More Records Message
.
SRHX2P99  RETURN
+
.------------------------------------------------------------
. Check ICD10 Edition 12 Procedure Record
.------------------------------------------------------------
SX2PRA00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SX2PRA92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SX2PRB00
            BRANCH    EXIT,SX2PRA91
            MOVE      SAVKEY24,KEY24
            CALL      RDPO12A2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SX2PRA91 IF EQUAL
.
          GOTO      SX2PRA90
.
SX2PRA90  MOVE      ZERO,OVRCD
          GOTO      SX2PRA99
SX2PRA91  MOVE      ONE,OVRCD
          GOTO      SX2PRA99
SX2PRA92  MOVE      TWO,OVRCD
SX2PRA99  RETURN
+
.------------------------------------------------------------
. Check for Matching ICD10 Edition 12 Procedure Words
.------------------------------------------------------------
SX2PRB00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SX2PRB10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SX2PRB99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPO12A1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SX2PRB10 IF EQUAL
.
          CALL      RKPO12A1              * Not Exact so Read Next Record
          BRANCH    OVRCD,SX2PRB90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SX2PRB90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SX2PRB90 IF NOT EQUAL * No Match So Exit
          GOTO      SX2PRB10              * Check Next Search Word
.
SX2PRB90  MOVE      ONE,EXIT
SX2PRB99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 12 Diagnosis Code: Search Table
.-----------------------------------------------------------
SRHX2D00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDVX,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.12 in use yet?
....      GOTO      SRHX2D90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSDK12A2
SRHX2D10  CALL      RKDK12A2
          BRANCH    OVRCD,SRHX2D90
.
          CALL      SX2DRA00
          BRANCH    OVRCD,SRHX2D10,SRHX2D90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPD12A1
          BRANCH    OVRCD,SRHX2D10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SRHX2D10
          ENDIF
.
          BRANCH    LNK2PRT,SRHX2D20,SRHX2D30
          GOTO      SRHX2D99
.
SRHX2D20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
          GOTO      SRHX2D80
.
SRHX2D30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDSC2:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
SRHX2D80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SRHX2D10 IF NOT EQUAL
          GOTO      SRHX2D99
.
SRHX2D90  CALL      NOMORE00    * Display No More Records Message
.
SRHX2D99  RETURN
+
.------------------------------------------------------------
. Check ICD10 Edition 12 Diagnosis Record
.------------------------------------------------------------
SX2DRA00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SX2DRA92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SX2DRB00
            BRANCH    EXIT,SX2DRA91
            MOVE      SAVKEY24,KEY24
            CALL      RDDK12A2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SX2DRA91 IF EQUAL
.
          GOTO      SX2DRA90
.
SX2DRA90  MOVE      ZERO,OVRCD
          GOTO      SX2DRA99
SX2DRA91  MOVE      ONE,OVRCD
          GOTO      SX2DRA99
SX2DRA92  MOVE      TWO,OVRCD
SX2DRA99  RETURN
+
.------------------------------------------------------------
. Check for Matching ICD10 Edition 12 Diagnosis Words
.------------------------------------------------------------
SX2DRB00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SX2DRB10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SX2DRB99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDDK12A1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SX2DRB10 IF EQUAL
.
          CALL      RKDK12A1              * Not Exact so Read Next Record
          BRANCH    OVRCD,SX2DRB90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SX2DRB90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SX2DRB90 IF NOT EQUAL * No Match So Exit
          GOTO      SX2DRB10              * Check Next Search Word
.
SX2DRB90  MOVE      ONE,EXIT
SX2DRB99  RETURN
