.******************************************************************************
.*                                                                            *
.* Include Name  : BRHLCOMN.PBL                                               *
.*                                                                            *
.* Function      : Subroutine Include for                                     *
.*                 Broadcast Message Interface                                *
.*                                                                            *
.* Subroutines   : DGMESSID : Get next message ID                             *
.*                 WRINB000 : Write header record (hl7inbaf) for IBA Message  *
.*                 WRCIS000 : Write header record (hl7cisaf) for HL7 Message  *
.*                                                                            *
.* Modifications :                                                            *
.*        06/06/2022  Davin S          TSK 0908745                            *
.*                    Moved read on pmsqpt to always check for message id     *
.*        24/04/2014  Steve Armstrong   CAR 261630                            *
.*                    PORT is now redundant in terms of message header        *
.*                    details, so H7CIPORT & H7INPORT are now space filled    *
.*        10/10/2013  Davin            CAR 291578                             *
.*                    Added read on pmsqpt to check for existing message id   *
.*        02/04/2013  Davin            CAR 270648                             *
.*                    Added read on websec to write user id/hosp to hl7cisaf  *
.*        20/04/2006  Mike Laming      CAR 81335                              *
.*                    Correct output to H7CIACTN (Action flag)                *
.*        27/02/2006  Mike Laming      CAR 81335                              *
.*                    Added H7CIACTN (Action flag)                            *
.*        13/12/2005  Steve Armstrong  CAR 88619                              *
.*                    Added population of H7CIURNO                            *
.*        07/07/2005  Davin            CAR 64087                              *
.*                    Created for CIS interface changes                       *
.******************************************************************************
.
.******************************************************************************
.*                                 DGMESSID                                   *
.*                           Get next message ID                              *
.* Returns:  MESSAGID - Message id                                            *
.******************************************************************************
.
DGMESSID  MOVE      "  0",PRXCODE                 * System Lock Sector 0
          CALL      GETSLK00                      * Get System Lock-Sector 0
          READ      CONTROLF,ZERO;*87,IBCNMESI    * next message id
          MOVE      IBCNMESI,MESSAGID
          ADD       ONE,IBCNMESI
          WRITAB    CONTROLF,ZERO;*87,IBCNMESI
          CALL      RELSLK00                      * Release System Lock-Sector 0
.
.         Check to see if message id already exists on hl7inbaf or hl7cisaf.
.         If it does, then we need to get the next id.
.
          COMPARE   ONE,IBAMFLAG
          IF        @EQUAL
            MOVE      MESSAGID,KEY20
            CALL      RAH7INB1
            COMPARE   ZERO,OVRCD
            GOTO      DGMESSID IF EQUAL
          ENDIF
.
          COMPARE   ONE,CISMFLAG
          IF        @EQUAL
            MOVE      MESSAGID,KEY20
            CALL      RAH7CIS1
            COMPARE   ZERO,OVRCD
            GOTO      DGMESSID IF EQUAL
          ENDIF
.
.         Check to see if message id already exists on pmsqpt
.
          MOVE      MESSAGID,KEY20
          CALL      RAPMQPT1
          COMPARE   ZERO,OVRCD
          GOTO      DGMESSID IF EQUAL
.
DGMESS99  RETURN
+
.*****************************************************************************
.*                            WRINB000                                       *
.* Write a record to the header file (hl7inbaf) for IBA Proprietary messages *
.*****************************************************************************
.
WRINB000  MOVE      MESSAGID,H7INMESI
          MOVE      ANST,H7INSTAT
          CALL      IBACLOCK
          PACK      H7INDTTM,CCC,CYY,CMM,CDD,CTIMEIS
          REP       " 0",H7INDTTM
          MOVE      PASSCODE,H7INOPER
          MOVE      SP2,H7INPORT
          MOVE      PRGID,H7INPGID
          PACK      H7INSPAR,SP30,SP20
.
          CALL      WRH7INB1
.
WRINB999  RETURN
+
.*****************************************************************************
.*                            WRCIS000                                       *
.*        Write a record to the header file (hl7cisaf) for CIS HL7 messages  *
.*****************************************************************************
.
WRCIS000  MOVE      MESSAGID,H7CIMESI
          MOVE      ANSW,H7CISTAT
          CALL      IBACLOCK
          PACK      H7CIDTTM,CCC,CYY,CMM,CDD,CTIMEIS
          REP       " 0",H7CIDTTM
          MOVE      PASSCODE,H7CIOPER
          MOVE      SP2,H7CIPORT
          MOVE      PRGID,H7CIPGID
....      MOVE      PMHCACTN,H7CIACTN       * assigned in DGCLIM02     *C-81335
          PACK      H7CISPAR,SP30,SP20
          MOVE      PURNO,H7CIURNO
.
          OPEN      WEBSECA3,"websecaf"
          PACK      KEY14,PASSCODE,SP70
          CALL      RSWBSE3
          CALL      RKWBSE3
          BRANCH    OVRCD,WRCIS500
.
          MATCH     PASSCODE,WBSEPCD
          GOTO      WRCIS500 IF NOT EQUAL
.
          PACK      H7CIUSID,WBSEUID,SP70
          PACK      H7CIHOSP,WBSEHOSP,SP70
.
WRCIS500  CALL      WRH7CIS1
.
WRCIS999  RETURN
+
