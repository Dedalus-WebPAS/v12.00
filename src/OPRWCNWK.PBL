. *----------------------------------------------------------------------
. * Include Name  :   OPRWCNWK.PBL
. *
. * Function      :   Calculate the theatre week cycle number if required 
. *---------------------------------------------------------------------
.  Modifications Done :
.------------------------------------------------------------------------
.  V11.01.00  03/03/2021  Ebon Clements TSK 0886820
.             Created Include
. *----------------------------------------------------------------------

WKCN0000  MOVE      SP70,WEEKCYCL           * Clear week cycle to display
.
. See if week cycle starts on the session date for the sessions unit
.
          PACK      KEY11,OPSETYPE,OPSEDATE,SP70
          CALL      RDOPWCN2
          IF        OVRCD<>1
            GOTO     WKCN2000               * Week cycle found
          ENDIF
.
. Find earlier week cycle record for session date and unit
.
          PACK      KEY11,OPSETYPE,OPSEDATE,Z70
          CALL      RSOPWCN2
          CALL      RPOPWCN2
          BRANCH    OVRCD,WKCN9999          * No week cycle found
.
          MATCH     OPSETYPE,OPWCUNIT       * Correct Unit
          GOTO      WKCN9999 IF NOT EQUAL
.
. Calculate start and end dates for week 1.
.
WKCN2000  MOVE      OPWCEFFD,STRTWEEK       * Start of week date
          MOVE      STRTWEEK,ENDDWEEK
          ADD       SIX,ENDDWEEK            * End of week date
          MOVE      OPWCWEEK,ROTATION       * How many weeks rotation
          MOVE      ONE,WEEKNUMB            * Week 1
.
WKCN3000  MATCH     STRTWEEK,OPSEDATE       * Session date < start of week date
          GOTO      WKCN4000 IF LESS

          MATCH     OPSEDATE,ENDDWEEK       * Session date <= end of week date
          GOTO      WKCN9000 IF NOT LESS
.
WKCN4000  ADD       ONE,WEEKNUMB            * Add 1 to week number
          IF        WEEKNUMB>ROTATION
            MOVE      ONE,WEEKNUMB          * Start weeks back at 1
          ENDIF
.                                           * Add seven days
          ADD       SEVEN,STRTWEEK          * New start of week date
          ADD       SEVEN,ENDDWEEK          * New end of week date
.
          GOTO      WKCN3000
.
WKCN9000  CLEAR     WEEKCYCL                * Format week number display
          APPEND    "(",WEEKCYCL            * eg (Week 1)
          APPEND    "Week ",WEEKCYCL
          APPEND    WEEKNUMB,WEEKCYCL
          APPEND    ")",WEEKCYCL
          RESET     WEEKCYCL
.
WKCN9999  RETURN
