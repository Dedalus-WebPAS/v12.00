+
.*****************************************************************************
.* System   : Menu Generation                                                *
.* Program  : UMENSKEL                                                       *
.* Desc     : SKELETON PROGRAM FOR USE IN THE GENERATION OF USER MENUS       *
.* %Z%%M%, Version: %I% %E%                                                  *
.*****************************************************************************
.* Date     : 21/04/89 V5.00                                                 *
.* Author   : Darren Jones                                                   *
.* Comments :                                                                *
.* ---------:--------------------------------------------------------------- *
.* Modified : 20/02/90 V5.01                                                 *
.* Author   : Paul Blackmore                                                 *
.* Comments : Add UNIX commands option                                       *
.*          : Changed name from MENUSKEL to UMENSKEL                         *
.* ---------:--------------------------------------------------------------- *
.* Modified : 31/05/90 V5.02                                                 *
.* Author   : Paul Blackmore                                                 *
.* Comments : Now used in conjunction with new menu generator 'gensub'       *
.*          : Complete rewrite of generated source code.                     *
.* ---------:--------------------------------------------------------------- *
.* Modified : 15/08/90 V5.03                                                 *
.* Author   : Paul Blackmore                                                 *
.* Comments : New routine called PFRQ000 to allow payroll program defaults   *
.* ---------:--------------------------------------------------------------- *
.* Modified : 20/08/91 V6.00                                                 *
.* Author   : Steve O'Gorman                                                 *
.* Comments : Modified to Handle the New Common                              *
.* ---------:--------------------------------------------------------------- *
.* Modified : 19/02/91 V6.01                                                 *
.* Author   : Jeni Tan                                                       *
.* Comments : New routine called PATSTYR to check for start of year          *
.* ---------:--------------------------------------------------------------- *
.* Modified : 19/06/91 V6.02                                                 *
.* Author   : Jeni Tan                                                       *
.* Comments : Not allowed to have a blank site code                          *
.* ---------:--------------------------------------------------------------- *
.* Modified : 14/10/1998 V6.03                                               *
.* Author   : Steve Armstrong                                                *
.* Comments : Mods to clear screen after returning from UNIXSHEL.            *
.*            Also changed CSTRTYR from DIM6 to DIM8                         *
.* ---------:--------------------------------------------------------------- *
.* Modified : 30/12/1999 V6.04                                               *
.* Author   : Steve Armstrong                                                *
.* Comments : Removed use of PATSTYR                                         *
.* ---------:--------------------------------------------------------------- *
.* Modified : 04/04/2011 V10.02.01                                           *
.* Author   : Jill Parkinson CAR 239574                                      *
.* Comments : Removed ibaprntf open                                          *
.*****************************************************************************
.*****************************************************************************
.
          INC       STD001FD
.
          INC       IBAIMNFD/INC
          INC       IBAPORFD/INC       **** PORT SCANNING
          INC       IBAPRNFD/INC
          INC       IBASECFD/INC
          INC       IBASYSFD/INC       **** TRACKS USERS ON SYSTEM
.
          INC       OUTCONFD/INC
          INC       OUTSITFD/INC      *** SITE ID TO COMMON/INC IDDD
.
+
.******************************************************************************
.*                           Miscellaneous Variables                          *
.******************************************************************************
ACTION    FORM      2           
ACTION2   FORM      2
ANSWER    DIM       2          
.
CHOSTYP   FORM      1    
CNT       FORM      1
CONAME    DIM       35
CSTRTYR   DIM       8
CHGSTYR   FORM      1
.
DIM3      DIM       3
DIM4      DIM       4
DIM6      DIM       6
DIM8      DIM       8
.
EXECSTR   DIM       80
.
FNAMEM    DIM       12
FORM2A    FORM      2
FORM5     FORM      5
FORM5A    FORM      5
.
NOPASSWD  FORM      1
PASSCHK   DIM       8
PASSTAB   FORM      3
PASSWD    DIM       8
PORTNAME  DIM       11
PROGRAM   DIM       8
PROGNAME  DIM       8
UNIXNAME  DIM       38
.
SPNAME    DIM       8
SPOOLFLG  FORM      1
.
TMPMESG   FILE      ASCII, FIXED=10
.
BOXSIZE   FORM      2
VERT      FORM      2
MMENUOPT  FORM      2
.
.START_VERSION
+
.**************************************************************************
.*                             MAINLINE                                   *
.*                         controlling logic                              *
.**************************************************************************
          CALL      INIT0000                   * initialization
          CALL      HEAD0000
.START_BRANCH
          GOTO      D0010000                   * display main menu
ML9999    CHAIN     SPNAME
+
.**************************************************************************
.*                            INIT0000                                    *
.*                         initialization                                 *
.**************************************************************************
INIT0000  OPEN      CONTROLF,"controlf"
          OPEN      IBACODE1,"ibacodef"
          OPEN      IBAPORT1,"ibaportf"
          OPEN      IBASECU1,"ibasecuf"
          OPEN      IBASECU2,"ibasecuf"
          OPEN      IBASECU2,"ibasecuf"
          OPEN      OUTSITA1,"outsitaf"
.
          READ      CONTROLF,ZERO;*13,CSECCNT
          READ      CONTROLF,TWO;*149,USERFLAG,*154,SPNAME
          READ      CONTROLF,TEN3;*245,CHOSTYP
.
.         userflag = 0 full system tracking
.         userflag = 1 no tracking
.
          BRANCH    USERFLAG,INIT1000
.
          OPEN      IBASYSA1,"ibasysaf"    * system tracking file
          OPEN      IBASYSA2,"ibasysaf"    * system tracking file
          OPEN      IBASYSA3,"ibasysaf"    * system tracking file
          OPEN      IBASYSA4,"ibasysaf"    * system tracking file
.
          CALL      UPSYSA1                * end of last program
.
INIT1000  MOVE      PRGID,IBAPPROG
          CALL      WRPORT1                * in out menu
.
          CALL      IBACLOCK
INIT9999  RETURN
+
.*************************************************************************
.*                          HEAD0000                                     *
.*                display screen heading line 1 & 2                      *
.*************************************************************************
HEAD0000  READ      CONTROLF,TWO;*4,CONAME
          MOVE      CONAME,CNAME
.
          STRIP     CNAME
          MOVELPTR  CNAME,CCOL
          DIV       TWO,CCOL
          MULT      SEQ,CCOL                                     Date : 12/12/12
          ADD       "40",CCOL
          DISPLAY   *ES,*HON,SP30,SP30,SP10,SP10:
                    *P1:1,PRGID,"/",VERSION,*PCCOL:1,CNAME:
                    *P64:1,"Date : ",CDATE
          HLINE     *G37,2,1,80
          DISPLAY   *P65:2," Port : ",PORT,SP1
HEAD9999  RETURN
+
.*************************************************************************
.*                              BOX10000                                 *
.*                       Draw a box for 1 column menu                    *
.*************************************************************************
BOX10000  DISPLAY   *P1:3,*EF
          ASSIGN    4+BOXSIZE,FORM2
          BOX       16,21,3,61,FORM2
BOX19999  RETURN
.
.*************************************************************************
.*                              BOX20000                                 *
.*                       Draw a box for 2 column menu                    *
.*************************************************************************
BOX20000  DISPLAY   *P1:3,*EF
          ASSIGN    4+BOXSIZE,FORM2
          BOX       16,1,3,80,FORM2
BOX29999  RETURN
.
.*************************************************************************
.*                              BOX30000                                 *
.*                       Draw a box for 1 column menu                    *
.*************************************************************************
BOX30000  DISPLAY   *P1:3,*EF
          ASSIGN    4+BOXSIZE,FORM2
          BOX       1,21,3,61,FORM2
BOX39999  RETURN
.
.*************************************************************************
.*                              BOX40000                                 *
.*                       Draw a box for 2 column menu                    *
.*************************************************************************[
BOX40000  DISPLAY   *P1:3,*EF
          ASSIGN    4+BOXSIZE,FORM2
          BOX       1,1,3,80,FORM2
BOX49999  RETURN
.
+
.*************************************************************************
.*                              MENU0000                                 *
.*                        menu generation procedure                      *
.*************************************************************************
.START_MENU
+
.*************************************************************************
.*                              SEL0000                                  *
.*                        Selection procedure                            *
.*************************************************************************
SEL0000  CALL       CHKMS000                * Check for a User Message
.
         MOVE       BOXSIZE TO CVERT
         ADD        THREE TO CVERT
         MOVE       "47" TO CCOL
         DISPLAY    *P31:CVERT," Please select:   "
         CALL       MENUKEY
         ADD        ONE,CMENUOPT
         MOVE       CMENUOPT,MMENUOPT
SEL9999  RETURN
+
.*************************************************************************
.*                             UNIX0000                                  *
.*                      Execute a UNIX Command                           *
.*************************************************************************
.
UNIX0000  DISPLAY   *ES
          EXECUTE   UNIXNAME,TASKID
          KEYIN     *P1:24,*N,*EL,"Press <ENTER> to return to menu:":
                    *P34:24,ANS
          EXECUTE   "clear",TASKID
UNIX9999  RETURN
.
.*************************************************************************
.*                              PROG0000                                 *
.*                check program for payroll, not allowed here            *
.*************************************************************************
.
PROG0000  GOTO      CHECKPW
.
+
.***********************************************************************
.*                               NOCLEAR                               *
.*                      inadequate security clearance                  *
.***********************************************************************
NOCLEAR DISPLAY    *P1:3,*EF,*P15:6,*B,*V2LON:
                   "*** Clearance Level Inadequate ***"
.
        KEYIN      *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                              " to continue ",*EOFF,ANS;
        RETURN
.
+*************************************************************************
.*                              PFRQ0000                                 *
.*                        Keyin pay frequency                            *
.*************************************************************************
.
PFRQ0000  DISPLAY   *P21:18,"****************************************":
                    *P21:19,"*                                      *":
                    *P21:20,"*                                      *":
                    *P21:21,"*                                      *":
                    *P21:22,"*                                      *":
                    *P21:23,"*                                      *":
                    *P21:24,"****************************************"
PFRQ0020  KEYIN     *P26:20,"Enter pay frequency : ",*V2LON,*DV,UNDLN1,*HOFF:
                    *P24:22,"1=Weekly  2=Fortnightly  3=Monthly":
                    *P48:20,*V2LON,CPBS,*P48:20,*DV,CPBS;
          COMPARE   ZERO,CPBS
          GOTO      PFRQ9000 IF EQUAL
          BRANCH    CPBS,PFRQ9990,PFRQ9990,PFRQ9990
          GOTO      PFRQ0020
.
PFRQ9000  MOVE      ONE TO EXIT
          GOTO      PFRQ9999
PFRQ9990  MOVE      ZERO TO EXIT
PFRQ9999  RETURN
.
+*************************************************************************
.*                              SITEPASS                                 *
.*               Keyin site & password for OUT, MAM & RAD                *
.*************************************************************************
.
SITE0000  MOVE      ZERO,NOPASSWD
          MATCH     SP6,OSTSITE
          GOTO      SITE0010 IF EQUAL
          ENDSET    OSTSITE
          APPEND    SP6,OSTSITE
          RESET     OSTSITE
          MOVE      ONE,NOPASSWD
          GOTO      SITE0020
.
SITE0010  DISPLAY   *P21:18,"****************************************":
                    *P21:19,"*                                      *":
                    *P21:20,"*                                      *":
                    *P21:21,"*                                      *":
                    *P21:22,"*                                      *":
                    *P21:23,"*                                      *":
                    *P21:24,"****************************************"
          KEYIN     *P26:20,"Enter Site Code     : ",*V2LON,*DV,UNDLN6,*HOFF:
                    *P48:20,*V2LON,OSTSITE,*P48:20,*DV,OSTSITE;
          ENDSET    OSTSITE
          APPEND    SP6,OSTSITE
          RESET     OSTSITE
          MATCH     SP6,OSTSITE
          GOTO      SITE9100 IF EQUAL
.
SITE0020  MOVE      OSTSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD OF SITE9000
.
          MATCH     SP8,OSTPASS
          GOTO      SITE8000 IF EQUAL
          BRANCH    NOPASSWD,SITE8000
.
          KEYIN     *P26:22,*EOFF,"Enter Site Password  : ",PASSCHK;
          ENDSET    PASSCHK
          APPEND    SP8,PASSCHK
          RESET     PASSCHK
          MATCH     PASSCHK,OSTPASS
          GOTO      SITE9000 IF NOT EQUAL
.
SITE8000  MOVE      OSTSITE,IDDD               ***** COMMON
          MOVE      OSTFILE,UID                ***** COMMON SITE FILE PREFIX
          PACK      CONAME WITH OSTDESC,SP20
          MOVE      CONAME,CNAME
          GOTO      SITE9990
.
SITE9000  BEEP
SITE9100  MOVE      ONE TO EXIT
          GOTO      SITE9999
SITE9990  MOVE      ZERO TO EXIT
SITE9999  RETURN
+
          INC       IBAPRINT
          INC       IBAMNCOM
          INC       IBAWDINC
          INC       CHEKMESS
.
          INC       IBAIMNIO/INC
          INC       OUTSITIO/INC
          INC       IBAPRNIO/INC
          INC       IBAPORIO/INC       **** PORT SCANNING
          INC       IBASECIO/INC       
          INC       IBASYSIO/INC       *** IO FOR SYSTEM AUDIT
.
          INC       STD001IO
