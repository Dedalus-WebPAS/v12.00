. *****************************************************************************
. * Routine Name  : CALCMVHR                                                  *
. *                                                                           *
. * Function      : Calculate MV hours from patvenaf with entry of Cat II     *
. *                 (Initialtion Site) set to Ward (Indicator 1=W)            *
. *                                                                           *
. * Parameters    : PATVENFD record                                           *
. *                                                                           *
. * Modifications :                                                           *
. *                 V11.00.00 01/07/2020 J.Tan   TSK 0886178                  *
. *                 Mods checking for blank LASTDATE                          *
. *                 V10.08.02 05/10/2016 J.Tan   TSK 0826824                  *
. *                 Mods NOT to round up the HOURS calculation                *
. *                 V10.08.01 19/07/2016 J.Tan   TSK 0819914                  *
. *                 Mods to include to MV Hours for blank initiation site     *
. *                 V10.08.00 04/07/2016 J.Tan   TSK 0819914                  *
. *                 Created new routine to calculate Mechanical Hours         *
. *****************************************************************************
          DEFPROC   CALCMVHR
.
          INC       PATVENFD/INC
.
CALCTIME  FORM      6
FIRSTDAT  DIM       8
FIRSTIME  DIM       4
LASTDATE  DIM       8
LASTTIME  DIM       4
MINUTE1   FORM      3
HOUR1     FORM      6
MINUTE2   FORM      3
HOUR2     FORM      6
LASTHOUR  DIM       2
LASTMIN   DIM       2
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
HOUR      DIM       2
MIN       DIM       2
.
.*****************************************************************************
.* Mainline
.*****************************************************************************
MVHR0000  OPEN      PATVENA1,"patvenaf"
.
          MOVE      ZERO,MVENHOUR
          PACK      KEY27,DPTICUAD,SP70
          CALL      RSPTVEN1
MVHR1000  CALL      RKPTVEN1
          BRANCH    OVRCD,MVHR4000
.
          MATCH     DPTICUAD,PTVEVISN      * Same visit number?
          GOTO      MVHR4000 IF NOT EQUAL
.
          MATCH     SP70,PTVEVTYP
          GOTO      MVHR1000 IF EQUAL      * Check for Mechanical Ventilation
.
          PACK      KEY5,ANSV,ANSE,PTVEVTYP
          CALL      RDCODE1
          BRANCH    OVRCD,MVHR1000
.
          MATCH     "2",TCINDC1
          GOTO      MVHR1000 IF NOT EQUAL  * Not MV
.
          MATCH     SP70,PTVEISIT          * Check Initiation Site
          GOTO      MVHR1000 IF EQUAL
.
          PACK      KEY5,ANSI,ANSI,PTVEISIT
          CALL      RDCODE1
          BRANCH    OVRCD,MVHR1000
.
          CMATCH    "W",TCINDC1            * Ward
          GOTO      MVHR1000 IF NOT EQUAL
.
          MOVE      PTVESDAT,FIRSTDAT
          UNPACK    PTVESTIM,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
          REP       " 0",FIRSTDAT
          REP       " 0",FIRSTIME
.
          MOVE      PTVEEDAT,LASTDATE
          REP       " 0",LASTDATE
          MATCH     "00000000",LASTDATE
          GOTO      MVHR1000 IF EQUAL
.
          UNPACK    PTVEETIM,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
          REP       " 0",LASTDATE
          REP       " 0",LASTTIME
          CALL      TIMEDIFF
          ADD       CALCTIME,MVENHOUR     * total duration
.
          GOTO      MVHR1000
.
MVHR4000  COMPARE   ZERO,MVENHOUR
          GOTO      MVHR9999 IF EQUAL
.
.         just take the hours ignore minutes
.
          MOVE      MVENHOUR,CALCTIME
          MOVE      ZERO,MVENHOUR
.
          MOVE      ZERO,FORM6P2
          MOVE      CALCTIME,FORM6P2
          DIV       "60",FORM6P2         * convert to hours
.
          MOVE      SP70,KEY9
          MOVE      FORM6P2,KEY9         * take the hours
          UNPACK    KEY9,KEY6,KEY3
.
          MOVE      ZERO,F6
          MOVE      KEY6,F6
          MOVE      F6,MVENHOUR          * hours
.
.         MULT      "60",F6
.         SUB       F6,CALCTIME          * minutes
.         IF        CALCTIME>0
.           ADD       ONE,MVENHOUR       * round up to an hour
.         ENDIF
.
MVHR9999  GOTO      ENDMVHR
.
.
          INC       TIMEDIFF
          INC       PATVENIO/INC
          INC       IBAOVRIO/INC
.
ENDMVHR   ENDPROC
