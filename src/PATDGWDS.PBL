.
.       PATDGWDS : Display Health Fund File Entries
.
.       This subroutine displays the list of DRG codes
.
.       Mods:
.         V9.08.01  04/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2
.                   Mods to PATDGWFD - Add field PTDWDRGV (add to Keys) 
.                   29/11/1999  Davin
.                   Changed for 4 character drgs
.
PATDGWDS
DGWDS000  MOVE      SP4,DRGCODE
          MOVE      ZERO,F1
          UNPACK    DRGVERZ,ANS,DV1,DV2                               *I-137125
          DISPLAY   *P1:4,*EL,*P32:4,"DRG Version : ",DV1,DOT,DV2;    *I-137125
.
          DISPLAY   *P1:5,*EF,*P2:5,*ULON,*V2LON,"DRG",*P7:5,"Description":
                    *P68:5,"MDC";
          MOVE      SIX,CVERT
          MOVE      SP4,KEY4                 * save field             *I-137125
          PACK      KEY7,DRGCODE,SP10                                 *C-137125
          CALL      RSPTDGW1
DGWDS100  CALL      RKPTDGW1
          BRANCH    OVRCD,DGWDS500
.
          MATCH     KEY4,PTDWDRGC                                     *I-137125
          GOTO      DGWDS100 IF EQUAL       * remove duplicate codes  *I-137125
          MOVE      PTDWDRGC,KEY4                                     *I-137125
.
          MATCH     PTDWDRGV,DRGVERZ                                  *I-137125
          GOTO      DGWDS100 IF NOT EQUAL                             *I-137125
          ADD       ONE,F1
.
          DISPLAY   *P2:CVERT,PTDWDRGC,*HOFF:
                    *P7:CVERT,PTDWDESC:
                    *P68:CVERT,PTDWMDCC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DGWDS100 IF LESS
.
          BRANCH    CNEWDS,DGWDS300
.
DGWDS200  KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      DGWDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DGWDS000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DGWDS400 IF EQUAL
          GOTO      DGWDS200
.
DGWDS300  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      DGWDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DGWDS000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DGWDS400 IF EQUAL
          GOTO      DGWDS300
.
DGWDS400  MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:6,*EF;
          GOTO      DGWDS100
.
DGWDS500  BRANCH    CNEWDS,DGWDS999
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          MOVE      ZERO,OVRCD
          GOTO      DGWDS999
.
.
DGWDS600  COMPARE   ZERO,F1
          GOTO      DGWDS700 IF NOT EQUAL
          DISPLAY   *P1:7,*EL,"No DRG Codes available for Version ":
                           DV1,DOT,DV2;
.
DGWDS700  BRANCH    CNEWDS,DGWDS300
          GOTO      DGWDS200
.
DGWDS999  RETURN
+
.

.
