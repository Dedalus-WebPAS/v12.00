.------------------------------------------------------------
. Function          : Procedure for Seach of 
.                      Fill in description of Search
.
. Parameters        : CKEYSUSP    1=Show Suspended 
.                   : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.                      Fill in Other Parameters Required
.
. Includes Required :
. Open Tables Req   :
. Returns Fields    : OVRCD    0=Valid Record Selected, 1=No Record Selected
.                     Record Read from xxxxxxx Table
.
. Modifications :
.         V5.08.01  21/09/2000  Greg Horvat
.                   Changed to validate the ICD version 2 code when a code is
.                   selected
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach for
.  a coded field via either a Key Word Index or Alphabetical
.  Index and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.
.  patoicaf
.  PATOICA1
.  PATOICA2    
.  
.  PTOIICD     DIM       xx    * Code Field 
.  PTOIKWD     DIM       15    * Key Word
.
.  Index 1   PTOIICD, PTOIKWD
.  Index 2   PTOIKWD, PTOIICD
.
.To convert this Procedure the following should be performed in this sequence
.    Global Change paticdof to name of Alphabetic Search Table   eg patdo1af
.    Global Change PATICDO3 to index of Alphabetic Search Table  eg PATDO1A2
.    Global Change RDICO3  to the Read on Alpha Search Index    eg RDDOCT2
.    Global Change RDKICO3 to the Read Next Alpha Search Index  eg RDKDOCT2
.    Global Change RDPICO3 to the Read Prev Alpha Search Index  eg RDPDOCT2
.    Global Change RDSICO3 to the Read Post Alpha Search Index  eg RDSDOCT2
.    Global Change OPCODE to the Code Field from the Table     eg DCODE
.    Global Change ODESC to the Name Field from the Table     eg DSNAME
.    Global Change KEY79    to the Key for the Aplha Index       eg KEY26
.    Global Change patoicaf to name of Key Word Table            eg patdkiaf
.    Global Change PATOICA1 to index 1 of Key Word Table         eg PATDKIA1
.    Global Change PATOICA2 to index 2 of Key Word Table         eg PATDKIA2
.    Global Change PTOIICD to Key Word Index Code Field         eg PTDKDOC
.    Global Change PTOIKWD to Key Word Index Word Field         eg PTDKKWD
.    Global Change HPTOIA   to Routine Label                     eg HPTDKA00
.    Global Change KEY24    to the Key needed for Key Word Table eg KEY21
.    Global Change KEY9    to the Key for the Code Field        eg KEY6
.    Global Change RDICO1 to the Read on Master Table          eg RDDOCT1
.    Global Change PTOI     to the IO Call ID for the Key Words  eg PTDK
.    Global Change INACTV to the field to Indicate Inactive Code eg DRSTAT
.
.    Modify List Display to Suit
.
.    Note : vi Global change command   :%s/FromString/ToString/g
.  
.  A Alphabetic Index on a Master Table is Defined in the following Manner
.                              
.  OPCODE    DIM       xx    * Code Field 
.  ODESC    DIM       xx    * Name or Description Field
.
.------------------------------------------------------------
. Search 
.------------------------------------------------------------
HPTOIA00  MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATOICA1,"patoicaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patoicaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTOIA99
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      OPICO2
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (paticdof)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTOIA99
          ENDIF
.
          OPEN      PATOICA2,"patoicaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPTOIA10  CALL      KWINT000
          BRANCH    EXIT,HPTOIA90
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Code"            
          MOVE      STDCOL01,STDCOL02
          ADD       "10",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description",*HOFF
          MOVE      STDCOL02,STDCOL03
          ADD       "25",STDCOL03
.
          CALL      SHWFLG00                * Display Flags
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPTOIA20  IF        SEARCHTY=1
            CALL      ALPSRH00              * Alphabetic Search
          ELSE
            CALL      KEYSRH00              * Key Word Search
          ENDIF
          BRANCH    DOAGAIN,HPTOIA10,HPTOIA20
.
          ADD       ONE,CSSCRNUM
          GETSCR    CSSCRNUM,1,24,80,24
          CALL      VOV2C000                * Validate operation ICD10 ver 2 cod
          PUTSCR    CSSCRNUM
          SUB       ONE,CSSCRNUM
          BRANCH    EXIT,HPTOIA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPTOIA90  CLOSE     PATOICA1
          CLOSE     PATOICA2
HPTOIA99  RETURN
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          PACK      KEYWRD00,KEYWRD00,SP30,SP30,SP10
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP9,PTOIICD 
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP30
          CALL      RSPTOI2 
KEYSRH10  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY24,PTOIKWD,PTOIICD
          STORE     KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                      
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTOIICD
          DISPLAY   *PSTDCOL02:STDROW,ODESC
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,NEWDITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,NEWDITEM,DOT
.
KEYSRH20  MOVE      NEWDITEM,DITEM
          CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH98:
                                  KEYSRH91,KEYSRH00,KEYSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY24,PTOIKWD,PTOIICD,SP30                                  
          CALL      RPPTOI2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY24
          CALL      RDPTOI2 
.
KEYSRH36  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY24,PTOIKWD,PTOIICD,SP30                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY24
          CALL      RDPTOI2 
KEYSRH41  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY24,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTOI2 
.
KEYSRH46  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH85  LOAD      KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                         
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTOIKWD,PTOIICD
          CALL      RDPTOI2 
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP30,KEY24
          MOVE      SP9,PTOIICD 
          PACK      PTOIKWD,SP30,SP30,SP10
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP30,KEY24
          MOVE      SP9,PTOIICD 
          LOAD      KEY24,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTOIKWD,PTOIICD
          MOVE      PTOIICD,KEY9
          CALL      RDICO1
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          PACK      KEYWRD00,SP30,SP30,SP10
          RESET     KEYWRD00
.
ALPSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP9,OPCODE 
.
          STRIP     KEYWRD00
          PACK      KEY79,KEYWRD00,SP30,SP30,SP20
          CALL      RDSICO2
ALPSRH10  CALL      RDKICO2
          BRANCH    OVRCD,ALPSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH10,ALPSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY79,ODESC,OPCODE
          STORE     KEY79,SELITMNO,ST1,ST2,ST3,ST4,ST5:                        
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,OPCODE
          DISPLAY   *PSTDCOL02:STDROW,ODESC 
.
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSRH10 IF NOT EQUAL
.
ALPSRH15  MOVE      ONE,NEWDITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,NEWDITEM,DOT
.
ALPSRH20  MOVE      NEWDITEM,DITEM
          CALL      KWSEL000
          BRANCH    EXIT,ALPSRH30,ALPSRH35,ALPSRH40,ALPSRH45,ALPSRH98:
                                  ALPSRH91,ALPSRH00,ALPSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSRH92
.
ALPSRH30  CALL      RDKICO2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH30,ALPSRH85
.
          PACK      KEY79,ODESC,OPCODE,SP30,SP30,SP20
          CALL      RPKICO2
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH10
.
ALPSRH35  MOVE      ST1,KEY79
          CALL      RDICO2
.
ALPSRH36  CALL      RPKICO2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH36,ALPSRH85
          PACK      KEY79,ODESC,OPCODE,SP30,SP30,SP20
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSRH37  CALL      RPKICO2
          BRANCH    OVRCD,ALPSRH38
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH37,ALPSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH37 IF NOT EQUAL
.
ALPSRH38  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH40  MOVE      ST1,KEY79
          CALL      RDICO2
ALPSRH41  CALL      RPKICO2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH41,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH09
.
ALPSRH45  CALL      RDKICO2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH45,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY79,KEYWRD01,Z70,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RDSICO2
.
ALPSRH46  CALL      RPKICO2
          BRANCH    OVRCD,ALPSRH47
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH46,ALPSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH46 IF NOT EQUAL
.
ALPSRH47  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH85  LOAD      KEY79,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY79,ODESC,OPCODE                                       
          CALL      RDICO2
.
ALPSRH90  BEEP
          GOTO      ALPSRH20
.
ALPSRH91  MOVE      SP9,OPCODE 
          PACK      KEY79,SP30,SP30,SP20
          PACK      ODESC,SP30,SP30,SP20
          MOVE      ONE,OVRCD
          GOTO      ALPSRH96
.
ALPSRH92  PACK      KEY79,SP30,SP30,SP20
          MOVE      SP9,OPCODE 
          LOAD      KEY79,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY79,ODESC,OPCODE                                       
          MOVE      OPCODE,KEY9
          CALL      RDICO1
.
ALPSRH96  MOVE      ZERO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH98  MOVE      ONE,DOAGAIN
.
ALPSRH99  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS110  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAGS190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAGS190  ASSIGN    MINITEM+3,F2
FLAGS210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAGS290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAGS290
          CALL      SHWFLG00
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFLG00  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search",FLAGDESC
          ELSE
            APPEND    "Key Word Search",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  BRANCH    SEARCHTY,CHREC100
          MATCH     KEYWRD01,PTOIKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTOIKWD,PTOIICD
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY24,KEY24
            CALL      RDPTOI2
          ENDIF
.
          MOVE      PTOIICD,KEY9
          CALL      RDICO1
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC800
.
CHREC100  MATCH     KEYWRD00,ODESC
          GOTO      CHREC920 IF NOT EQUAL                                       
.
CHREC800  MOVE      ZERO,INACTV
          MOVE      INACTV,CHECKSUS
          BRANCH    SHOWSUSP,CHREC900
          BRANCH    INACTV,CHREC910
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY9,PTOIICD
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,SAVKEY9,KEYWRDXX
          CALL      RDPTOI1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKPTOI1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY9,PTOIICD,SP9
          MATCH     SAVKEY9,KEY9 
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PTOIKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
