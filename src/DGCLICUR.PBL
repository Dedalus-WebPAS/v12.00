.************************************************************************
.*  Procedure: DGCLICUR  -  Send Change U.R for Visit details           *
.*                                                   ~~~~~              *
.*  Author   : Steve Armstrong                                          *
.*                                                                      *
.*  Requires : PATMA1FD record                                          *
.*             PATMI1FD record                                          *
.*             OLDURNO - old U/R number                                 *
.*                                                                      *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.*  V12.00.01 14/05/2025  Davin            TSK 0955096                  *
.*            Changed for Alphanumeric Visit Numbers                    *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.15.01 13/11/2019  Davin             TSK 0861253                 *
.*            Added OUTTHIFD/CLOUTTHI/outthiaf for telehealth fields    *
.************************************************************************
.*  V10.10.01 08/06/2017  Don Nguyen        TSK 0838220                 *
.*            Mods for writing a record to allqueaf                     *
.*  V10.03.03 06/06/2013  Steve Armstrong   CAR 268961                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.*            Mods to handle PVITYPE = 0 from HL7REGEN.                 *
.*  V10.03.02 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.03.01 10/02/2012  Davin             CAR 256006                  *
.*            Added functionality to change WaitList UR (watqueaf)      *
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
          DEFPROC   DGCLICUR
.
          INC       ALLENCFD/INC
          INC       ALLQUEFD/INC
          INC       EMRQUEFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       MEHLERFD/INC
          INC       OUTMA1FD/INC
          INC       OUTQUEFD/INC
          INC       OUTTHIFD/INC
          INC       OUTRSHFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
          INC       WATQUEFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND10;*244,PTCNA44M
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA44M               * yes - sending 434 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending CIS HL7 messages, so first open the relevant
.         holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      OLDURNO,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to W/L watqueaf (holding visit details)
.
          MATCH     "WATWEB01",PRGID
          GOTO      DGCLI100 IF EQUAL
.
          COMPARE   ZERO,PVITYPE                 * merge from HL7REGEN ?
          GOTO      DGCLI300 IF NOT EQUAL        * no
          
DGCLI100  OPEN      WATQUEA1,"watqueaf"
          MOVE      MESSAGID,WTQUMESI
          PACK      WTQUSPAR,SP70
          CALL      WRWTQUE1
          GOTO      DGCLI500
.
.         Write a record to I/P pmsqviaf (holding visit details)
.
DGCLI300  IF        PVITYPE = 3
            OPEN      PMSQVIA1,"pmsqviaf"
            MOVE      MESSAGID,PMQVMESI
            MOVE      ONE,PMQVTRAN
            MOVE      SP3,MHLRCODE
            PACK      PMQVSPAR,SP70
            CALL      WRPMQVI1
          ENDIF
.
.         Write a record to Emergency emrqueaf (holding visit details)
.
          IF        PVITYPE = 1
            OPEN      EMRQUEA1,"emrqueaf"
            MOVE      MESSAGID,EMQUMESI
            PACK      EMQUSPAR,SP70
            CALL      WREMQUE1
          ENDIF
.
.         Write a record to O/P outqueaf (holding visit details)
.
          IF        PVITYPE = 2
            OPEN      OUTQUEA1,"outqueaf"
            OPEN      OUTTHIA1,"outthiaf"
.
            MATCH     ZEROVISN,OBAOUTNO
            IF        @EQUAL
              CALL      CLOUTTHI
            ELSE
              PACK      KEY8,OBAOUTNO,SP70         * Outpatient Telehealth Info
              CALL      RDOTTHI1
              IF        OVRCD=1
                CALL      CLOUTTHI
              ENDIF
            ENDIF
.
            MOVE      MESSAGID,OTQUMESG
            MOVE      PVIBILL,OBAOUTNO
            PACK      OTQUSPAR,SP70
            CALL      WROTQUE1
          ENDIF
.
.         Write a record to Allied Health (allqueaf - holding visit details)
.
          IF        PVITYPE = 9
            OPEN      ALLQUEA1,"allqueaf"
            MOVE      MESSAGID,ALQUMESI
            MOVE      PVIBILL,ALREVISN
            PACK      ALQUSPAR,SP70
            CALL      WRALQUE1
          ENDIF
.
DGCLI500  COMPARE   ONE,CISMFLAG                 * sending HL7 A44 message?
          IF        @EQUAL
            MOVE      "A44",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     OUTTHIA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLOUTTHI
          INC       CLPMSQPX
.
          INC       ALLENCIO/INC
          INC       ALLQUEIO/INC
          INC       EMRQUEIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       OUTQUEIO/INC
          INC       OUTTHIIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
+
