.------------------------------------------------------------
.
. Function      : Procedure for Keyword Seach of doctor file
.
. Parameters    : CKEYSUSP    1=Show Suspended 
.               : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.
. Modifications : 
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach
.  a Key Word Index Table and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PMHCxxxx    DIM       xx    * Code Field Segment 1
.  PMHCyyyy    DIM       xx    * Code Field Segment n
.  PMHKKKWD     DIM       15    * Key Word
.  PMHCSPA     DIM       10    * Spare
.
.  Index 1   PMHKxxxx, PMHKyyyy, PMHKKKWD
.  Index 2   PMHKKKWD, PMHKxxxx, PMHKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change sss to the System ID eg :%s/sss/PAT/g
.    Global Change fff to the File   ID eg :%s/fff/DKI/g
.    Global Change PMHK to the IO Call ID eg :%s/PMHK/PMHK/g
.    Global Change KEYxx to the Key for the Key Word Table eg KEY25
.    Global Change KEYyy to the Key for the Code Field(s) Length eg KEY10
.    Change PMHKxxxx and PMHKyyyy to the Code Field(s) Segments
.    Modify List Display to Suit
.
.------------------------------------------------------------
           DEFPROC   HPMHKA00
.
           INC       STDHLPDF
           INC       STDKWSDF
           INC       PMSHKIFD/INC
           INC       PATCONFD/INC
.
CATEGORY  DIM       2
DOCFNAME  DIM       50
DISPNAME  DIM       50
NAME35    DIM       35
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
SAVKEY25  DIM       25
SAVKEY10  DIM       10
.------------------------------------------------------------
. Search for a Doctor Code
.------------------------------------------------------------
HPMHKA00  OPEN      CONTROLF,"controlf"             *Included here because this
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS  *parameter was sometimes
.                                                   *reset when proc aws called
          MOVE      ONE,CKEYORDR         * Alpabetical search. SRF 646506
          MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSHKIA1,"pmshkiaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Keyword Search Table Missing (pmshkiaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPMHKAZZ
          ENDIF
.
          OPEN      PMSHKIA2,"pmshkiaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPMHKA10  CALL      KWINT000
          BRANCH    EXIT,HPMHKA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Code"            
          MOVE      STDCOL01,STDCOL02
          ADD       "7",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Doctor",*HOFF
          MOVE      STDCOL02,STDCOL03
          ADD       "21",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,SP1,*V2LON,*ULON,"Speciality",*HOFF
          MOVE      STDCOL03,STDCOL04              
          ADD       "12",STDCOL04 
          IF        PTCNDCQS=1
            DISPLAY   *PSTDCOL04:STDROW,SP1,*V2LON,*ULON,"Address",*HOFF
          ENDIF
.
          CALL      SHWFLG00
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPMHKA20  IF        SEARCHTY=1
            CALL      ALPSRH00
          ELSE
            CALL      KEYSRH00
          ENDIF
          BRANCH    DOAGAIN,HPMHKA10,HPMHKA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPMHKA99  CLOSE     PMSHKIA1
          CLOSE     PMSHKIA2
          GOTO      HPMHKAZZ           * End Procedure
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PMHKHCPC 
.
          STRIP     KEYWRD01
          PACK      KEY25,KEYWRD01,SP70
          CALL      RSPMHKI2
KEYSRH10  CALL      RKPMHKI2
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY25,PMHKKKWD,PMHKHCPC
          STORE     KEY25,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PMHKHCPC
.
          CALL      DOCNAM00
          DISPLAY   *PSTDCOL02:STDROW,DOCFNAME
. 
          MOVE      "DT",CATEGORY
          MOVE      PMHCSPC1,KEY3 
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,SP1,TDESC
.
          IF        PTCNDCQS=1
            STRIP     PMHCADR1
            STRIP     PMHCADR2
            CLEAR     KEY32
            APPEND    PMHCADR1,KEY32
            APPEND    ", ",KEY32
            APPEND    PMHCADR2,KEY32
            RESET     KEY32
            DISPLAY   *PSTDCOL04:STDROW,SP1,KEY32  
          ELSE
            MOVE      PMHCSPC2,KEY3
            CALL      GETCOD00
            DISPLAY   *PSTDCOL04:STDROW,SP1,TDESC
          ENDIF
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
KEYSRH20  CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH98:
                                  KEYSRH91,KEYSRH00,KEYSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKPMHKI2
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY25,PMHKKKWD,PMHKHCPC,SP70                                  
          CALL      RPPMHKI2
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY25
          CALL      RDPMHKI2
.
KEYSRH36  CALL      RPPMHKI2
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY25,PMHKKKWD,PMHKHCPC,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPPMHKI2
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY25
          CALL      RDPMHKI2
KEYSRH41  CALL      RPPMHKI2
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKPMHKI2
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY25,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPMHKI2
.
KEYSRH46  CALL      RPPMHKI2
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH85  LOAD      KEY25,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY25,PMHKKKWD,PMHKHCPC                                       
          CALL      RDPMHKI2
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP70,PMHKSPAR
          MOVE      SP70,KEY25
          MOVE      SP70,PMHKHCPC 
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP70,KEY25
          MOVE      SP70,PMHKHCPC 
          LOAD      KEY25,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY25,PMHKKKWD,PMHKHCPC                                       
          MOVE      PMHKHCPC,KEY10
          CALL      RDPMHCP1
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS110  MOVE      "N",ANS
.
.----- Change statement from KEYIN to DISPLAY, user is no longer able 
.----- to select or view an inactive doctor code via the ? option. SRF 646506
.----- Changed to display inactive code but also validate to flag CKYINACT

          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAGS190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAGS190  ASSIGN    MINITEM+3,F2
FLAGS210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAGS290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAGS290  CALL      SHWFLG00
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFLG00  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search",FLAGDESC
          ELSE
            APPEND    "Key Word Search",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  BRANCH    SEARCHTY,CHREC100
          MATCH     KEYWRD01,PMHKKKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY25,PMHKKKWD,PMHKHCPC
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY25,KEY25
            CALL      RDPMHKI2
          ENDIF
.
          MOVE      PMHKHCPC,KEY10
          CALL      RDPMHCP1
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC800
.
CHREC100  MATCH     KEYWRD00,PMHCSNAM
          GOTO      CHREC920 IF NOT EQUAL                                       
.
CHREC800  MOVE      PMHCSTTS,CHECKSUS
          BRANCH    SHOWSUSP,CHREC900
          BRANCH    CHECKSUS,CHREC910
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY10,PMHKHCPC
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY25,SAVKEY10,KEYWRDXX
          CALL      RDPMHKI1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKPMHKI1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY10,PMHKHCPC,SP70
          MATCH     SAVKEY10,KEY10
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PMHKKKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
.------------------------------------------------------------
. Format Doctor Name in Title Text
.------------------------------------------------------------
DOCNAM00  CLEAR     DISPNAME
          MOVE      SP70,DOCFNAME
          STRIP     PMHCSNAM
          PACK      PMHCSNAM,PMHCSNAM,COMMA
          PACK      NAME35,PMHCSNAM,SP70
          CALL      NAMSTR00
          PACK      NAME35,PMHCTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PMHCGNAM,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.-------------------------------------------------
. Format Name
.-------------------------------------------------
NAMSTR00  CMATCH    SP1,NAME35               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME35
            GOTO      NAMSTR00 IF NOT EOS
            GOTO      NAMSTR99               * entire name if blank
          ENDIF
          PACK      KEY35,NAME35,SP30        * reset form pointer
          MOVE      KEY35,NAME35
.
          SCAN      SP1,NAME35             * Locate the blank
          GOTO      NAMSTR20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
.         Check for any more names
.
          SETLPTR   NAME35,35              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME35,CCITEM          * reset to this point
          PACK      KEY35,NAME35,SP70      * reset form pointer
          MOVE      KEY35,NAME35
          GOTO      NAMSTR00
.
.         Rest of the string is one name
.
NAMSTR20  UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
NAMSTR99  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
GETCOD00  MOVE      SP70,TDESC
          MATCH     SP70,KEY3
          GOTO      GETCOD99 IF EQUAL
          PACK      KEY5,CATEGORY,KEY3
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
GETCOD99  RETURN
.
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
.
ALPSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PMHCHCPC 
.
          STRIP     KEYWRD00
          PACK      KEY80,KEYWRD00,SP70
          CALL      RSPMHCP2 
ALPSRH10  CALL      RKPMHCP2 
          BRANCH    OVRCD,ALPSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH10,ALPSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY80,PMHCSNAM,PMHCGNAM,PMHCHCPC
          STORE     KEY80,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PMHCHCPC
.
          CALL      DOCNAM00
          DISPLAY   *PSTDCOL02:STDROW,DOCFNAME
. 
          MOVE      "DT",CATEGORY
          MOVE      PMHCSPC1,KEY3 
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,SP1,TDESC
.
          IF        PTCNDCQS=1
            STRIP     PMHCADR1
            STRIP     PMHCADR2
            CLEAR     KEY32
            APPEND    PMHCADR1,KEY32
            APPEND    ", ",KEY32
            APPEND    PMHCADR2,KEY32
            RESET     KEY32
            DISPLAY   *PSTDCOL04:STDROW,SP1,KEY32
          ELSE
            MOVE      PMHCSPC2,KEY3 
            CALL      GETCOD00 
            DISPLAY   *PSTDCOL04:STDROW,SP1,TDESC  
          ENDIF
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSRH10 IF NOT EQUAL
.
ALPSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
ALPSRH20  CALL      KWSEL000
          BRANCH    EXIT,ALPSRH30,ALPSRH35,ALPSRH40,ALPSRH45,ALPSRH98:
                                  ALPSRH91,ALPSRH00,ALPSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSRH92
.
ALPSRH30  CALL      RKPMHCP2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH30,ALPSRH85
.
          PACK      KEY80,PMHCSNAM,PMHCGNAM,PMHCHCPC,SP70
          CALL      RPPMHCP2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH10
.
ALPSRH35  MOVE      ST1,KEY80
          CALL      RDPMHCP2
.
ALPSRH36  CALL      RPPMHCP2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH36,ALPSRH85
          PACK      KEY80,PMHCSNAM,PMHCGNAM,PMHCHCPC,SP70
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSRH37  CALL      RPPMHCP2 
          BRANCH    OVRCD,ALPSRH38
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH37,ALPSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH37 IF NOT EQUAL
.
ALPSRH38  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH40  MOVE      ST1,KEY80
          CALL      RDPMHCP2
ALPSRH41  CALL      RPPMHCP2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH41,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH09
.
ALPSRH45  CALL      RKPMHCP2
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH45,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY80,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPMHCP2 
.
ALPSRH46  CALL      RPPMHCP2 
          BRANCH    OVRCD,ALPSRH47
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH46,ALPSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH46 IF NOT EQUAL
.
ALPSRH47  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH85  LOAD      KEY80,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY80,PMHCSNAM,PMHCGNAM,PMHCHCPC
          CALL      RDPMHCP2
.
ALPSRH90  BEEP
          GOTO      ALPSRH20
.
ALPSRH91  MOVE      SP70,PMHCHCPC
          MOVE      SP70,KEY80
          MOVE      SP70,PMHCSNAM
          MOVE      ONE,OVRCD
          GOTO      ALPSRH96
.
ALPSRH92  MOVE      SP70,KEY80
          MOVE      SP70,PMHCHCPC
          LOAD      KEY80,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY80,PMHCSNAM,PMHCGNAM,PMHCHCPC
          MOVE      PMHCHCPC,KEY10
          CALL      RDPMHCP1
.
ALPSRH96  MOVE      ZERO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH98  MOVE      ONE,DOAGAIN
.
ALPSRH99  RETURN
.
          INC       PMSHKIIO/INC
          INC       IBAOVRIO/INC
          INC       STDHLPCD
          INC       STDKWSCD
.
HPMHKAZZ  ENDPROC   
