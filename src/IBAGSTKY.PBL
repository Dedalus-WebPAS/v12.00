.---------------------------------------------------------------------------
.   System    :   IBA Software                       
.   Include   :   IBAGSTKY
.   Routine   :   HIBGSA00 GST Code            
.   Return    :           
.   Parameters:   INACTV   = 0 Displays only active codes
.                          = 1 Displays all codes (including inactive)
.-------------------------------------------------------------------------------
HIBGSA00   MOVE     ONE,CKEYORDR
           PROC      HIBGKI00
           RETURN
.---------------------------------------------------------------------------
.   System    :   IBA Software                       
.   Include   :   IBAGSTKY
.   Routine   :   KIBGSA00 Keyin GST Code            
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KIBGSA00 TOPIC     "IBGSCODE"
          MATCH     SP6,IBGSCODE
          IF        @EQUAL
            MOVE      UNDLN70,IBGSCODE
          ENDIF
          KEYIN     *PCCOL:CVERT,*DV,IBGSCODE:                     
                    *PCCOL:CVERT,*RV,*V2LON,IBGSCODE:
                    *PCCOL:CVERT,*DV,IBGSCODE
.
          ENDSET    IBGSCODE
          APPEND    SP70,IBGSCODE
          RESET     IBGSCODE
.
          MATCH     SP70,IBGSCODE
          GOTO      KIBGSA90 IF EQUAL
.
          CMATCH    EXITCHAR,IBGSCODE
          GOTO      KIBGSA95 IF EQUAL
.
          CMATCH    QUEST,IBGSCODE
          GOTO      KIBGSA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HIBGSA00
          MOVE      SCCITEM,CCITEM
.
          ENDSET    IBGSCODE
          APPEND    SP70,IBGSCODE
          RESET     IBGSCODE
.
          MATCH     SP70,IBGSCODE
          GOTO      KIBGSA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,IBGSCODE                                
.
KIBGSA10  PACK      KEY6,IBGSCODE                                               
          CALL      RDIBGS1 
          BRANCH    OVRCD,KIBGSA80
          BRANCH    CKEYTYP,KIBGSA75
.
          COMPARE   ONE,INACTV           * allow inactive codes?
          GOTO      KIBGSA70 IF EQUAL
.
          COMPARE   ONE,IBGSACTV         
          GOTO      KIBGSA85 IF EQUAL     * inactive code
.
KIBGSA70  MOVE      ZERO,EXIT
          GOTO      KIBGSA99
.
KIBGSA75  MOVE      "GST Code Already Exist - ",DISPMSG                                              
          CALL      MESSAGE1
          GOTO      KIBGSA00
.
KIBGSA80  MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KIBGSA99,KIBGSA99
          MOVE      "GST Code Does Not Exist - ",DISPMSG                                             
          CALL      MESSAGE1
          GOTO      KIBGSA00
.
KIBGSA85  MOVE      ZERO,EXIT
          MOVE      "Inactive Code - ",DISPMSG
.
          CALL      MESSAGE1
          GOTO      KIBGSA00
.
KIBGSA90  MOVE      ONE,EXIT
          GOTO      KIBGSA99
KIBGSA95  MOVE      TWO,EXIT
KIBGSA99  TOPIC    PRGID
          RETURN
