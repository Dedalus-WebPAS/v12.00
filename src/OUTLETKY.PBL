.******************************************************************************
.*  Include Name  : OUTLETKY.PBL                                              *
.*  Description   : Keyin of an outpatient letter code                        *
.*  Author        : ROD                                                       *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : DIM3     - Letter Number                                  *
.*                  OTLTTEXT - Letter Name                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          :                                                           *
.*                                                                            *
.*        V11.00.01 20/03/2020 Peter Vela Task 0889143                        *
.*                  Fixed OUTLET IO calls                                     *
.*                                                                            *
.******************************************************************************
OUTLETKY  
KOTLT010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      KOTLT050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,*JR,DIM3:
                    *PECOL:EVERT,*DV,DIM3
          GOTO      KOTLT080
.
KOTLT050  MOVE      CKYIDEF3,DIM3                 * default
          KEYIN     *PECOL:EVERT,*DV,DIM3:     * default used
                    *PECOL:EVERT,*V2LON,*RV,*JR,DIM3:
                    *PECOL:EVERT,*DV,DIM3
.
.         check what was entered
.
KOTLT080  CALL      ILTCK000
          BRANCH    EXIT,KOTLT100,KOTLT705,KOTLT805,KOTLT905
.                        "?"      valid    nothing  exitchar
          GOTO      KOTLT010
.
.         a "?" was input
.
KOTLT100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KOTLT105  CALL      OUTLETDS
          BRANCH    CNEWDS,KOTLT150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      KOTLT010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
KOTLT150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      KOTLT200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Letter  : ":
                    *P11:24,*JR,*DV,UNDLN3:      * no default used
                    *P11:24,*V2LON,DIM3:
                    *P11:24,*DV,DIM3
          GOTO      KOTLT300
.
KOTLT200  MOVE      CKYIDEF3,DIM3
          KEYIN     *P1:24,*EL,*DV,"Letter  : ":
                    *P11:24,*JR,*DV,DIM3:     * default used
                    *P11:24,*V2LON,*RV,DIM3:
                    *P11:24,*DV,DIM3
.
.         repeat the validation of the code
.         check what was entered
.
KOTLT300  CALL      ILTCK000
          BRANCH    EXIT,KOTLT105,KOTLT700,KOTLT800,KOTLT900
.                        "?"      valid    nothing  exitchar
.
          GOTO      KOTLT150
.
.         a valid code was entered 
. 
KOTLT700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DIM3
.
KOTLT705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KOTLT999 
.
.         nothing was input
.
KOTLT800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DIM3
.
KOTLT805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      KOTLT999 
.
.         EXITCHAR input
.
KOTLT900  CALL      FRESCR00                * free the screen saved
.
KOTLT905  MOVE      TWO,EXIT                * EXITCHAR
.
KOTLT999  RETURN
+
.*********************************************************************
.*                  ILTCK000                    Called by : PATILTKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
ILTCK000  ENDSET    DIM3
          APPEND    SP3,DIM3
          RESET     DIM3
.
          UNPACK    DIM3,ANS,ANS,ANS        * Get 3rd Character as *JR
          MATCH     EXITCHAR,ANS
          GOTO      ILTCK600 IF EQUAL       * exit char
.
          MATCH     SP3,DIM3
          GOTO      ILTCK700 IF EQUAL       * nothing
.
          MATCH     "  ?",DIM3
          GOTO      ILTCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
.
ILTCK500  TYPE      DIM3
          IF        !@EQUAL
            BEEP                            * non numeric input
            MOVE      ZERO,EXIT
            GOTO      ILTCK999
          ENDIF
.
          MOVE      DIM3,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO
          CALL      RDOTLET1
          IF        CKYIMODE = 0
            BRANCH    OVRCD,ILTCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      ILTCK900 IF EQUAL
          ENDIF
.
          MOVE      TWO,EXIT                * valid code
          GOTO      ILTCK999
.
.         exitchar entered
.
ILTCK600  MOVE      FOUR,EXIT
          MOVE      SP3,DIM3
          MOVE      SP20,OTLTTEXT              * clear desc field
          GOTO      ILTCK999
.
.         nothing entered
.
ILTCK700  BRANCH    CKYIMAND,ILTCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,DIM3
          MOVE      SP20,OTLTTEXT              * clear desc field
          GOTO      ILTCK999
.
.         "?" entered
.
ILTCK800  MOVE      ONE,EXIT
          GOTO      ILTCK999
.
.         invalid code entered
.
ILTCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Letter Number. ";
            CALL      HITENTER
          ELSE 
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      ILTCK999
.
.         field is mandatory
.
ILTCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
ILTCK999  RETURN
+
          INC       OUTLETDS
