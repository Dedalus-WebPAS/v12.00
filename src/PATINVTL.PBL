.******************************************************************************
.*                                                                            *
.*  PATINVTL - Invoice Tail Fields                                            *
.*                                                                            *
.* &&&&& Make sure the file ibavaraf is updated with the new variables &&&&&  *
.*                                                                            *
.* Modifications  :                                                           *
.*        V11.04.02 30/04/2024  J.Tan             TSK 0945175                 *
.*                  Fixed BPAY and Invoice number for Disc.Billing Statement  *
.*        V11.04.01 01/02/2024  J.Tan             TSK 0936664                 *
.*                  Fixed restoring variables from invoice file (field 73)    *
.*        V11.03.02 08/01/2024  J.Tan             TSK 0936664                 *
.*                  Added PINTL832 to restore var from inv file (field 43)    *
.*        V11.03.01 02/10/2023  Nikitha B         TSK 0927699                 *
.*                  Added CAPPRVNO and PTCNHABN for Hospital Approval and     *
.*                  ABN Number for single Hospital                            *
.*        V11.02.01 24/01/2022  Jacob Jackson     TSK 0864505                 *
.*                  Added preferred name option fields -                      *
.*                  PINTL980, PINTL990, PINT1000, PINT1010, PINT1020          *
.*        V11.01.02 10/09/2021  Davin             TSK 0911277                 *
.*                  Changed field 20 - Balance Payable to FORM12.2 (PINTL600) *
.*        V11.01.01 20/08/2021  Davin             TSK 0897318                 *
.*                  Added field 57 - Adjustment Total (PINTL970)              *
.*----------------------------------------------------------------------------*
.*        V11.00.01 06/04/2020  Thanh T           TSK 0879163                 *
.*                  Added PINTL960 for sex long description                   *
.*----------------------------------------------------------------------------*
.*        V10.08.01 07/11/2016  Ebon Clements     TSK 0827937                 *
.*                  Added field 55 - Hospital approval number - pthsappr      *
.*        V10.03.05 12/09/2013  Peter Vela        CAR 271911                  *
.*                  Fixed PINTL850 Invoice Total print start position         *
.*                  Moved VARIABLE to itself in PINT6000 to fix printing of   *
.*                  form variables                                            *
.*        V10.03.04 17/04/2013  Don Nguyen        CAR 282342                  *
.*                  Corrected pos read for PTCNURBP                           *
.*        V10.03.03 09/04/2013  Don Nguyen        CAR 282342                  *
.*                  Mods to use UR for BPAYINVN (if PTCNURBP=1)               *
.*        V10.03.02 18/07/2012  Don Nguyen        CAR 264561                  *
.*                  Read in Hospital ID before calling GBCRN000               *
.*        V10.03.01 29/11/2010  J.Tan             CAR 254776                  *
.*                  Add variable for Full PRA full address                    *
.*        V10.02.01 29/03/2011  Jill Parkinson   CAR 239574                   *
.*                  Removed open of ibaprntf                                  *
.*        V10.00.02 30/09/2010  J.Tan             CAR 230664                  *
.*                  Mods for HEC invoice layout                               *
.*        V10.00.01 26/03/2010  J.Tan             CAR 174079                  *
.*                  Mods invoice number var.for Future Action letter IBABIL22 *
.*        V9.11.03  11/06/2009  J.Tan         CAR 198713                      *
.*                  Mods printing SX invoice total, balance payable           *
.*        V9.11.02  20/03/2009  Davin             CAR 178015                  *
.*                  Added bpayinvn - generate bpay crn from INP or PRI        *
.*        V9.11.01  05/02/2009  J.Tan         CAR 187842                      *
.*                  Fixed printing SX Net total                               *
.*        V9.10.01  14/05/2008  Ebon Clements CAR 166566                      *
.*                  Added current time                                        *
.*        V9.09.04  27/12/2007  Peter Vela    CAR 155907                      *
.*                  Added new variable: Multi Hospital Code                   *
.*        V9.09.03  11/07/2007  Mike Laming   CAR 140395                      *
.*                  Add test for Statements (STATMENT) at PRITL100 & INVTL100 *
.*        V9.09.02  01/07/2007  Peter Vela    CAR 142173                      *
.*                  Fixed Doctor and Provider Number for Emr Visit and Emr Ev *
.*        V9.09.01  15/06/2007  Mike Laming   CAR 140395                      *
.*                  Add code to print Statements "Net Balance"                *
.*        V9.08.04  08/06/2007  Mike Laming    CAR 125736                     *
.*                  Changes to PATDO1af Address lines - change DSADD3 to      *
.*                  DSADD4 and DSADD2 to DSADD3 (new DSADD2 still to go in)   *
.*        V9.08.03  23/02/07 J.Tan    CAR 120014                              *
.*                  Mods for NZ GST                                           *
.*        V9.08.02  13/12/2006  J.Tan             CAR 127625                  *
.*                  Mods to Singapore HRN                                     *
.*        V9.08.01  16/11/2006  Steve Armstrong   CAR 124160                  *
.*                  Added Singapore HRN                                       *
.******************************************************************************
.*        V9.07.01  30/08/2006 J.Tan      CAR 115853                          *
.*                  Mods checking for 'Using Laser printer' parameter         *
.******************************************************************************
.*        V9.05.02  16/03/2006 Peter Vela CAR 95780                           *
.*                  Added EMG Contact mobile numbers                          *
.*        V9.05.01  27/03/2006 Peter Vela CAR 61299                           *
.*                  Increased field no size from DIM3 to DIM5                 *
.*        V9.04.13  28/09/2005 J.Tan      CAR 77671                           *
.*                  Fixed Printing invoice total variable                     *
.*        V9.04.12  14/07/2005 Peter Vela CAR 62172                           *
.*                  Added invoice total variable                              *
.*        V9.04.11  27/05/2005 J.Tan   CAR 61488                              *
.*                  Added field for page number                               *
.*        V9.04.10  09/12/2004  J.Tan    CAR 55635                            *
.*                  Fixed receipt amount
.*        V9.04.02  26/10/2004  J.Tan    CAR 54398                            *
.*                  Fixed printing invoice number for reprint                 *
.*        V9.04.01  29/09/2004  J.Tan    CAR 53292                            *
.*                  Removed closing pmsvx1af file                             *
.*        V9.03.06  02/04/2004  Peter Vela  CAR 48227                         *
.*                  Added field for BPAY Customer Reference Number (Generated)*
.*        V9.03.05  01/12/2003  J.Tan   CAR 44161                             *
.*                  Mods for credit note                                      *
.*        V9.03.04  04/11/2003  Peter Vela  CAR 44849                         *
.*                  Subracted CSAMT (Cash/Deposit Amount) from NETTOT in      *
.*                  PINTL600                                                  *
.*        V9.03.03  30/10/2003  Peter Vela  CAR 44757                         *
.*                  Added PAYMTTOT to NETTOT in PINTL600                      *
.*        V9.03.02  04/09/2003  CAR 42713   Mike Laming                       *
.*                  Reset NETTOT (with GROSSTOT) at PINTL600 before adding GST*
.*        V9.03.01  27/06/2002 J.Tan                                          *
.*                  Mods for WEB billing printing invoice                     *
.*        V9.02.07  01/05/2003 Dean Cramer CAR 35129                          *
.*                  Add GST to Net Total if GST applicable                    *
.*        V9.02.06  12/10/2002 Dean Cramer                                    *
.*                  Fix St. V's format aftet case payment changes             *
.*        V9.02.05  21/06/2002 Dean Cramer                                    *
.*                  Make so no form feed characters to spool                  *
.*        V9.02.04  16/06/2002 Dean Cramer                                    *
.*                  Read template header again as previous routine loses      *
.*                  form length with another read                             *
.*        V9.02.03  18/04/2002  J.Tan                                         *
.*                  Fixed spelling accommodation                              *
.*        V9.02.02  05/02/2002  Bronko Sosic                                  *
.*                  Added New Variabiles for Provisional diagnosis            *
.*        V9.02.01  18/10/2001  Bronko Sosic                                  *
.*                  Added New Variabiles for printing                         *
.*        V5.08.02  18/07/2000  Davin                                         *
.*                  Added multipage variable for numeric fields               * 
.*        V5.08.01  04/07/2000  Davin                                         *
.*                  - Fixed U*U in endinv17                                   * 
.*                  - Doctor's file variables now available for outpatients   *
.*        V5.08.00  09/03/2000  Davin                                         *
.*                  Copied from PATSHT6 - modified to print invoice fields    * 
.******************************************************************************
.*        Lineup Print for Invoice Tail Fields                                *
.******************************************************************************
.
LINUPITL  IF        WEBFLAG<>1
            CALL      OPNPRINT                 * open spool file
          ENDIF
          MOVE      ONE,CURRROW              * Initialise current row counter
.
INVTL000  COMPARE   CURRROW,IBDTROW          * Current row up to required row ?
          GOTO      INVTL200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else print 1 extra line
          ADD       ONE,CURRROW              * Increment counter
          GOTO      INVTL000                 * Check counter again
.
. ------- Read through the details file -------
.
INVTL100  CALL      RKIBDET1                 * Read next detail record
          BRANCH    OVRCD,INVTL900           * No more records - pad out form
.
.                                                                     *I-140395
          IF        STATMENT = 1
            PACK      KEY6,PTCNSMTT,SP10     * printing statements tail  
          ELSE
            MOVE      SP6,KEY6               * printing Invoice tail  
            LOAD      KEY6,PINVSYS,AECNAEIT,OTCNOTIT,PTCNPAIT,PTCNPAIT
.                                    aae      out      inp      oth
          ENDIF
.
          MATCH     KEY6,IBDTSCOD            * Same form ?
          GOTO      INVTL900 IF NOT EQUAL    * No - pad out rest of form
.
INVTL110  COMPARE   CURRROW,IBDTROW          * Are we at required row ?
          GOTO      INVTL200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else pad out form to req row
          ADD       ONE,CURRROW              * One more row printed
          GOTO      INVTL110                 * Check row count
.
. ------- Print actual data lines -------
.
INVTL200  BRANCH    IBDTINDC,INVTL300:       * Text field
                             INVTL400        * Data field
.
. ------- Print text field -------
.
INVTL300  PRINT     *IBDTCOL,IBDTTXFL;       * Print data
          MOVE      IBDTROW,CURRROW          * Row printed is current row
          GOTO      INVTL100                 * Read next record
.
. ------- Print data field -------
.
INVTL400  MOVE      IBDTTXFL,DIM5            * Data number 1st 3 chars of field
          MOVE      DIM5,FORM5               * Move to a FORM field for BRANCH
.
          BRANCH    FORM5,INVTL480,INVTL450,INVTL450,INVTL450,INVTL450:
                          INVTL450,INVTL450,INVTL480,INVTL450,INVTL480:    10
                          INVTL470,INVTL480,INVTL480,INVTL480,INVTL450:
                          INVTL450,INVTL450,INVTL450,INVTL450,INVTL480:    20
                          INVTL450,INVTL480,INVTL450,INVTL480,INVTL480:
                          INVTL450,INVTL450,INVTL450,INVTL450,INVTL450:    30
                          INVTL450,INVTL450,INVTL470,INVTL470,INVTL450:
                          INVTL450,INVTL450,INVTL450,INVTL450,INVTL450:    40
                          INVTL450,INVTL450,INVTL450,INVTL490,INVTL480:
                          INVTL450,INVTL450,INVTL450,INVTL450,INVTL450:    50
                          INVTL450,INVTL450,INVTL460
.
INVTL450  PACK      VARIABLE,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10
          GOTO      INVTL600
.
INVTL460  MOVE      "99:99:99",VARIABLE          * Time variables
          GOTO      INVTL600
.
INVTL470  IF        IBDTPLEN = 10
            MOVE      "99/99/9999",VARIABLE      * Date with century variables
          ELSE
            MOVE      "99/99/99",VARIABLE        * Date variables
          ENDIF
          GOTO      INVTL600
.
INVTL480  MOVE      "9999999999999999999999999",VARIABLE    * Form Field
          GOTO      INVTL600
.
INVTL490  MOVE      "9",VARIABLE                 * Indicator
          GOTO      INVTL600
.
INVTL520  CLEAR     VARIABLE                     * Address
          APPEND    X10,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X5,VARIABLE
          APPEND    COMMA,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X5,VARIABLE
          APPEND    COMMA,VARIABLE
          APPEND    " 9999",VARIABLE
          RESET     VARIABLE
          GOTO      INVTL600
.
INVTL530  MOVE      "99",VARIABLE            * Year
          GOTO      INVTL600
.
INVTL540  MOVE      "999999.99",VARIABLE     * Daily rate
          GOTO      INVTL600
.
. ------- Actual data print routine -------
.
INVTL600  RESET     VARIABLE,IBDTPLEN        * Reset pointer to required length
          LENSET    VARIABLE                 * Push logical length there
          RESET     VARIABLE                 * Reset form pointer to start
          PRINT     *IBDTCOL,*+,VARIABLE;    * Only print data between FP & LL
          MOVE      IBDTROW,CURRROW          * Move row number to one printed
          GOTO      INVTL100                 * Read next record
.
. ------- Finished all data for this form - pad out rest of form -------
.
INVTL900  COMPARE   CURRROW,IBTHLENG         * End of form ?
          GOTO      INVTL999 IF EQUAL        * Yes - exit
          PRINT     *N;                      * Otherwise pad out 1 more line
          ADD       ONE,CURRROW              * 1 more line printed
          GOTO      INVTL900                 * Test again
.
INVTL999  MOVE      FALSE,EXIT               * Set exit flag
          IF        WEBFLAG<>1
            CALL      CLSPRINT                 * close the spool file
          ENDIF
          RETURN                             * Finished
+
.*******************************************************************************
.*        Actual Print Routine for Patient Invoice Tail Fields                 *
.*******************************************************************************
.
PRTINVTL  IF        WEBFLAG<>1
            CALL      OPNPRINT                 * open spool file
          ENDIF
          MOVE      ONE,CURRROW              * Initialise current row counter
          MOVE      IBDTROW,FORM3
.                                                                     *I-140395
          IF        STATMENT = 1
            PACK      KEY6,PTCNSMTT,SP10     * printing statements tail  
          ELSE
            MOVE      SP6,KEY6               * printing Invoice tail  
            LOAD      KEY6,PINVSYS,AECNAEIT,OTCNOTIT,PTCNPAIT,PTCNPAIT
.                                    aae      out      inp      oth
          ENDIF
.
          CALL      RDIBTH1                   * read header detail file
          BRANCH    OVRCD,PINT9000
.
PINTL000  COMPARE   CURRROW,IBDTROW          * Current row up to required row ?
          GOTO      PINTL200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else print 1 extra line
          ADD       ONE,CURRROW              * Increment counter
          GOTO      PINTL000                 * Check counter again
.
. ------- Read through the details file -------
.
PINTL100  CALL      RKIBDET1                 * Read next detail record
          BRANCH    OVRCD,PINT8000           * No more records - pad out form
.
.                                                                     *I-140395
          IF        STATMENT = 1
            PACK      KEY6,PTCNSMTT,SP10     * printing statements tail  
          ELSE
            MOVE      SP6,KEY6               * printing Invoice tail  
            LOAD      KEY6,PINVSYS,AECNAEIT,OTCNOTIT,PTCNPAIT,PTCNPAIT
.                                    aae      out      inp      oth
          ENDIF
.
          MATCH     KEY6,IBDTSCOD            * Same form ?
          GOTO      PINT8000 IF NOT EQUAL    * No - pad out rest of form
.
PINTL110  COMPARE   CURRROW,IBDTROW          * Are we at the required row ?
          GOTO      PINTL200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else pad out to required row
          ADD       ONE,CURRROW              * One more row printed
          GOTO      PINTL110                 * Check row count
.
. ------- Print actual data lines -------
.
PINTL200  MOVE      IBDTROW,FORM3
          BRANCH    IBDTINDC,PINTL300:       * Text field
                             PINTL400        * Data field
.
. ------- Print text field -------
.
PINTL300  RESET     IBDTTXFL,IBDTPLEN
          LENSET    IBDTTXFL
          RESET     IBDTTXFL
          PRINT     *IBDTCOL,*+,IBDTTXFL;    * Print data
          MOVE      IBDTROW,CURRROW          * Row printed is current row
          GOTO      PINTL100                 * Read next record
.
. ------- Print data field -------
.
PINTL400  MOVE      IBDTTXFL,DIM5            * Data number 1st 3 chars of field
          MOVE      DIM5,FORM5               * Move to a FORM field for BRANCH
.
          PACK      VARIABLE,SP30,SP30,SP30,SP30,SP10  * Initialise variable
          BRANCH    FORM5,PINTL410,PINTL420,PINTL430,PINTL440,PINTL450:
                          PINTL460,PINTL470,PINTL480,PINTL490,PINTL500:   10
                          PINTL510,PINTL520,PINTL530,PINTL540,PINTL550:
                          PINTL560,PINTL570,PINTL580,PINTL590,PINTL600:   20
                          PINTL610,PINTL620,PINTL630,PINTL640,PINTL650:
                          PINTL660,PINTL670,PINTL680,PINTL690,PINTL700:   30
                          PINTL710,PINTL720,PINTL730,PINTL740,PINTL750:
                          PINTL760,PINTL770,PINTL780,PINTL790,PINTL800:   40 
                          PINTL810,PINTL820,PINTL830,PINTL840,PINTL850:
                          PINTL860,PINTL870,PINTL880,PINTL890,PINTL900:   50
                          PINTL910,PINTL920,PINTL930,PINTL940,PINTL950:
                          PINTL960,PINTL970,PINTL980,PINTL990,PINT1000:   60
                          PINT1010,PINT1020,PINT1030,PINT1040,PINT1050
.
PINTL410  MOVE      AADMNO,VARIABLE          * Admission number
          MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL420  MOVE      CHADD1,VARIABLE          * Hospital Address 1
          GOTO      PINT5000
.
PINTL430  MOVE      CHADD2,VARIABLE          * Hospital Address 2
          GOTO      PINT5000
.
PINTL440  MOVE      CHPADD1,VARIABLE         * Hospital Postal Address 1
          GOTO      PINT5000
.
PINTL450  MOVE      CHPADD2,VARIABLE         * Hospital Postal Address 2
          GOTO      PINT5000
.
PINTL460  MOVE      CHPOST,VARIABLE          * Hospital Postcode
          GOTO      PINT5000
.
PINTL470  MOVE      CHPPOST,VARIABLE         * Hospital Postal Postcode
          GOTO      PINT5000
.
PINTL480  MOVE      CHTELEB,VARIABLE         * Hospital Telephone
          GOTO      PINT5000
.
PINTL490  MOVE      CNAME,VARIABLE           * Hospital Name
          GOTO      PINT5000
.
PINTL500  MATCH     "IBABIL21",PRGID         * Invoice Number
          GOTO      PINTL502 IF EQUAL
          MATCH     "PATWEB76",PRGID
          GOTO      PINTL502 IF EQUAL
          MATCH     "IBABIL22",PRGID
          GOTO      PINTL502 IF EQUAL
          MOVE      CNEXTINV,VARIABLE
          GOTO      PINTL504
.
PINTL502  MOVE      PINVNO,VARIABLE
PINTL504  MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL510  CALL      IBACLOCK                 * Current Date
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINTL520  COMPARE   ONE,MPGEFLAG
          GOTO      PINT4500 IF EQUAL
          COMPARE   "23",PTCNINVL            * SX
          GOTO      PINTL522 IF EQUAL        * SX Payment already set
.
          MOVE      PAYMTTOT,FORM62
          SUB       CSAMT,FORM62             * Receipt Amount
PINTL522  MOVE      FORM62,VARIABLE
.
          MOVE      NINE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL530  MOVE      DMEMB,VARIABLE           * DV Membership
          GOTO      PINT5000
.
PINTL540  MOVE      SP70,VARIABLE
          IF        PVITYPE = 2
            PROC      ENDINV17
            MOVE      DPROV,VARIABLE
            GOTO      PINTL549
          ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
          ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
          IF        OVRCD <> 1
            MOVE      DPROV,VARIABLE         * Doctor Provider Number
          ENDIF
PINTL549  GOTO      PINT5000
.
PINTL550  MOVE      SP70,VARIABLE
          IF        PVITYPE = 2
            PROC      ENDINV17
            MOVE      DSADD1,VARIABLE
            GOTO      PINTL559
          ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
          ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
          IF        OVRCD <> 1
            MOVE      DSADD1,VARIABLE        * Surgery Address 1
          ENDIF
PINTL559  GOTO      PINT5000
.
PINTL560  MOVE      SP70,VARIABLE
          IF        PVITYPE = 2
            PROC      ENDINV17
            MOVE      DSADD3,VARIABLE        * Surgery Address 3 - Suburb
            GOTO      PINTL569
          ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
          ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
          IF        OVRCD <> 1
            MOVE      DSADD3,VARIABLE        * Surgery Address 3 - Suburb
          ENDIF
PINTL569  GOTO      PINT5000
.
PINTL570  MOVE      SP70,VARIABLE
          IF        PVITYPE = 2
            PROC      ENDINV17
            MOVE      DSADD4,VARIABLE        * Surgery Address 4 - State
            GOTO      PINTL579
          ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
          ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
          IF        OVRCD <> 1
            MOVE      DSADD4,VARIABLE        * Surgery Address 4 - State
          ENDIF
PINTL579  GOTO      PINT5000
.
PINTL580  MOVE      SP70,VARIABLE
          IF        PVITYPE = 2
            PROC      ENDINV17
            MOVE      DSPOST,VARIABLE
            GOTO      PINTL589
          ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
          ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
          IF        OVRCD <> 1
            MOVE      DSPOST,VARIABLE        * Surgery Postcode
          ENDIF
PINTL589  GOTO      PINT5000
.
PINTL590  MOVE      SP70,VARIABLE
           IF        PVITYPE = 2
               GOTO     PINTL591
           ENDIF
..         IF        PVITYPE = 2
..           PROC      ENDINV17
..           MOVE      PACFNAME,VARIABLE
..           GOTO      PINTL599
..         ELSE
            IF        PVITYPE = 1
              PACK      KEY6,ADADOCT
            ELSE
              PACK      KEY6,ADOCTA
            ENDIF
..         ENDIF
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDDOCT1
PINTL591  IF        OVRCD <> 1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,VARIABLE      * Formatted Doctor's Name
          ENDIF
PINTL599  GOTO      PINT5000
.
PINTL600  COMPARE   ONE,MPGEFLAG
          GOTO      PINT4500 IF EQUAL
.
.                                            * Statements
          MATCH     "IBAHOS20",PRGID         *                        *I-140395
          GOTO      PINTL605 IF EQUAL        * NETTOT already set
          MATCH     "PATWEB72",PRGID         *                        *I-140395
          GOTO      PINTL605 IF EQUAL        * NETTOT already set
          COMPARE   "23",PTCNINVL            * SX
          GOTO      PINTL605 IF EQUAL        * NETTOT already set
.
          MOVE      GROSSTOT,NETTOT                                    *I-42713
          ADD       PAYMTTOT,NETTOT          * I CAR 44757
          SUB       CSAMT,NETTOT             * I CAR 44849
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       TOTGST,NETTOT
          ENDIF
          ADD       CREDTOT,NETTOT
.
PINTL605  MOVE      NETTOT,FORM12P2
          MOVE      FORM12P2,VARIABLE        * Net Balance Payable
.
          MOVE      TEN5,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL610  MOVE      PNAME,VARIABLE           * Patient Name
          GOTO      PINT5000
.
PINTL620  MOVE      PURNO,VARIABLE           * U/R number
          MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL630  MOVE      TDDESC,VARIABLE          * Description of Billing Item
          GOTO      PINT5000
.
PINTL640  COMPARE   ONE,MPGEFLAG
          GOTO      PINT4500 IF EQUAL
.
          MOVE      TPATAMTP,FORM62
          MOVE      FORM62,VARIABLE          * Patient Portion of Charge
          MOVE      NINE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL650  IF        DBSFLAG=1
            MATCH     "1",IVDETAIL
            GOTO      PINT6000 IF NOT EQUAL       * No invoice details
.
            OPEN      PATINVR3,"patinvrf"
            PACK      KEY16,DAADMNO,Z70
            CALL      RDSINV3
PINTL651    CALL      RDPINV3
            IF        OVRCD=0
              MATCH     DPINVADM,DAADMNO
              IF        @EQUAL
                MATCH     "1",PTINCRST
                GOTO      PINTL651 IF EQUAL       * fully credited
                MATCH     "3",DPINVSYS
                GOTO      PINTL651 IF NOT EQUAL   * not inpatient invoice
                GOTO      PINTL652
              ENDIF
            ENDIF
            MOVE    THREE,PINVSYS
            GOTO    PINT6000
          ENDIF
.
          MATCH     "IBABIL21",PRGID         * Invoice Number
          GOTO      PINTL652 IF EQUAL
          MATCH     "PATWEB76",PRGID         * Invoice Number
          GOTO      PINTL652 IF EQUAL
          MATCH     "IBABIL22",PRGID         * Invoice Number
          GOTO      PINTL652 IF EQUAL
          MOVE      CNEXTINV,VARIABLE        * print invoice
          GOTO      PINTL654
.
PINTL652  MOVE      PINVNO,VARIABLE
PINTL654  MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL660  IF        PINVSYS = 2
            MOVE      "DEBTOR NO.",VARIABLE      * Outpatients
          ELSE
            MOVE      "ADM NO.",VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINTL670  MOVE      PFNAME,VARIABLE          * Formatted Patient Name
          GOTO      PINT5000
.
PINTL680  MOVE      PKADD1,VARIABLE          * PRA Address 1
          GOTO      PINT5000
.
PINTL690  MOVE      PKADD2,VARIABLE          * PRA Address 2
          GOTO      PINT5000
.
PINTL700  MOVE      PKSUBR,VARIABLE          * PRA Suburb
          GOTO      PINT5000
.
PINTL710  MOVE      PKPOST,VARIABLE          * PRA Postcode
          GOTO      PINT5000
.
PINTL720  MOVE      PKADD4,VARIABLE          * PRA Address 4
          GOTO      PINT5000
.
PINTL730  MOVE      SP8,KEY8
          PROC      ENDINV17
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY    * Outpatient Date
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINTL740  UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY    * Invoice Date
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINTL750  MOVE      "INVOICE",VARIABLE       * Receipt/Invoice indicator
          MATCH     "IBABIL09",PRGID
          IF        @EQUAL
            MOVE      "RECEIPT",VARIABLE
          ENDIF
          MATCH     "IBABIL21",PRGID
          IF        @EQUAL
            IF        OPTION=3 | OPTION=4
              MOVE      "RECEIPT",VARIABLE
            ENDIF
          ENDIF
          GOTO      PINT5000
.
PINTL760  MOVE      PSEX,VARIABLE            * Patients Sex
          GOTO      PINT5000
.
PINTL770  MATCH     SP70,ADATE
          GOTO      PINTL100 IF EQUAL
          CALL      IBACLOCK                 * Current Date
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          DAYSEP    ADATE,KEY8,DAYSDIFF
          IF        DAYSDIFF > MAXSEPDY
            MOVE      "Long Term Stay",VARIABLE      * Long stay or Short Stay
          ELSE
            MOVE      "              ",VARIABLE      * Long stay or Short Stay
          ENDIF
          GOTO      PINT5000
.
.
. Service
.
PINTL780  COMPARE   ONE,PVITYPE
          IF        @EQUAL
            MOVE      "Emergency   ",VARIABLE
            GOTO      PINT5000
          ENDIF
.
          COMPARE   TWO,PVITYPE
          IF        @EQUAL
            MOVE      "Outpatient  ",VARIABLE
            GOTO      PINT5000
          ENDIF
.
          COMPARE   THREE,PVITYPE
          IF        @EQUAL
            MOVE      "Accommodation",VARIABLE
            GOTO      PINT5000
          ENDIF
          GOTO      PINTL100
.
. Patient Status
.
PINTL790  COMPARE   ONE,PVITYPE
          IF        @EQUAL
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDEMVIS1
            IF        OVRCD=1
              MOVE      "Emergency - ",VARIABLE
              GOTO      PINT5000
            ENDIF
            PACK      TDESC,SP70
            PACK      KEY5,CODECL,EMVICOMP,SP70
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      "Emergency   ",VARIABLE
              PACK      VARIABLE,VARIABLE,TDESC
            ENDIF
            GOTO      PINT5000
          ENDIF
.
          COMPARE   TWO,PVITYPE
          IF        @EQUAL
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDBOKB1
            IF        OVRCD=1
              MOVE      "Outpatient - ",VARIABLE
              GOTO      PINT5000
            ENDIF
            PACK      TDESC,SP70
            PACK      KEY5,CODECL,OBACOMP,SP70
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      "Outpatient - ",VARIABLE
              PACK      VARIABLE,VARIABLE,TDESC
            ENDIF
            GOTO      PINT5000
          ENDIF
.
          COMPARE   THREE,PVITYPE
          IF        @EQUAL
            PACK      TDESC,SP70
            PACK      KEY5,CODECL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      "Inpatient - ",VARIABLE
              PACK      VARIABLE,VARIABLE,TDESC
            ENDIF
            GOTO      PINT5000
          ENDIF
          GOTO      PINT1000
.
. Class
.
PINTL800  COMPARE   ONE,PVITYPE
          IF        @EQUAL
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDEMVIS1
            IF        OVRCD=1
              GOTO      PINTL100
            ENDIF
            MOVE      "EM",VARIABLE
            PACK      VARIABLE,VARIABLE,SP1,EMVICOMP,SP1,EMVICLAS
            GOTO      PINT5000
          ENDIF
.
          COMPARE   TWO,PVITYPE
          IF        @EQUAL
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDBOKB1
            IF        OVRCD=1
              GOTO      PINTL100
            ENDIF
            MOVE      "OP",VARIABLE
            PACK      VARIABLE,VARIABLE,SP1,OBACOMP,SP1,OBACLASS
            GOTO      PINT5000
          ENDIF
.
          COMPARE   THREE,PVITYPE
          IF        @EQUAL
            MOVE      "IP",VARIABLE
            PACK      VARIABLE,VARIABLE,SP1,ACLAIM,SP1,ATYPE
            GOTO      PINT5000
          ENDIF
          GOTO      PINTL100
.
. Provisional DRG 
.
PINTL810  PACK       KEY8,DPVIBILL,SP70
          OPEN       PMSVX1A1,"pmsvx1af"
          CALL       RDPMVX11
          BRANCH     OVRCD,PINTL100
          MOVE       PMVXPICD,VARIABLE
          GOTO       PINT5000
.
. Provisional DRG Description
.
PINTL820  PACK       KEY8,DPVIBILL,SP70
          OPEN       PMSVX1A1,"pmsvx1af"
          CALL       RDPMVX11
          BRANCH     OVRCD,PINTL100
          PACK       KEY9,PMVXPICD,SP70
          OPEN       PATICDD1,"paticddf"
          CALL       RD10T9D1
          IF         OVRCD=0
            MOVE       DDESC,VARIABLE
            GOTO       PINT5000
          ENDIF
          GOTO       PINTL100 
.
. BPAY Reference Number            * I-48227
.
PINTL830  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS   * Multi Hospital
          IF        IBCNMHOS=1
            PACK      KEY8,DPVIBILL,SP70
            OPEN      PMSVX1A1,"pmsvx1af"
            CALL      RDPMVX11
            IF        OVRCD=0
              MOVE      PMVXMHOS,BPAYHOSP
            ENDIF
          ENDIF
.
          READ      CONTROLF,HUND18;*249,PTCNURBP  * Use UR number as BPAYINVN?
          MATCH     "1",PTCNURBP
          IF        @EQUAL
            MOVE      PINVUR,BPAYINVN       * use UR number
          ELSE
            MOVE      PINVNO,BPAYINVN       * use last invno for BPAY CRN
            IF        DBSFLAG=1
              MATCH     "1",IVDETAIL
              GOTO      PINT5000 IF NOT EQUAL       * No invoice details
.
.         Printing Discharged billing with Invoiced details, use last inv.no
.
              OPEN      PATINVR3,"patinvrf"
              PACK      KEY16,DAADMNO,Z70
              CALL      RDSINV3
PINTL832      CALL      RDPINV3
              IF        OVRCD=0
                MATCH     DPINVADM,DAADMNO
                IF        @EQUAL
                  MATCH     "1",PTINCRST
                  GOTO      PINTL832 IF EQUAL       * fully credited
                  MATCH     "3",DPINVSYS
                  GOTO      PINTL832 IF NOT EQUAL   * not inpatient invoice
                  MOVE      PINVNO,BPAYINVN       * use last invno for BPAY CRN
                  GOTO      PINTL834
                ENDIF
              ENDIF
              MOVE      THREE,PINVSYS
              GOTO      PINT5000
            ENDIF
          ENDIF
.
PINTL834  CALL       GBCRN000      * Generate BPAY CRN
          MOVE       BPAYREFN,VARIABLE
          GOTO       PINT5000
.
PINTL840  MOVE       CPAGENO,KEY3
          PACK       VARIABLE,KEY3
          GOTO       PINT5000
.
. Invoice Total
.
PINTL850  COMPARE    ONE,MPGEFLAG
          GOTO       PINT4500 IF EQUAL
.
          MOVE       INVCTOTL,VARIABLE
          IF         PTCNINVL=18
            MOVE       INVCTOTL,FORM62
            MOVE       FORM62,VARIABLE
            MOVE       NINE,PRNTSTRT
            SUB        IBDTPLEN,PRNTSTRT
            GOTO       PINT6000
          ENDIF
          MOVE       TEN5,PRNTSTRT
          SUB        IBDTPLEN,PRNTSTRT
          GOTO       PINT6000
.
. NOK Mobile Number
.
PINTL860  MOVE       PMPXKMOB,VARIABLE
          GOTO       PINT5000
.
. Nearest Relative Mobile Number
.
PINTL870  MOVE       PMPXRMOB,VARIABLE
          GOTO       PINT5000
.
. Emergency Contact Mobile Number
.
PINTL880  MOVE       PMPXCMOB,VARIABLE
          GOTO       PINT5000
.
.         Singapore HRN
.
PINTL890  PROC      GETALIDS
          MOVE      KEY20,VARIABLE
          MOVE      TWENTY,FORM3
          SUB       IBDTPLEN,FORM3
          BUMP      VARIABLE,FORM3
          PACK      VARIABLE,VARIABLE,SP70
          GOTO      PINT5000
.
PINTL900  MOVE      SP70,VARIABLE
          MOVE      SP70,KEY10
          IF        PVITYPE = 1
            OPEN      EMRVISA1,"emrvisaf"
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDEMVIS1
            IF        OVRCD=0
              PACK      KEY10,EMVIDOCT,SP70
            ENDIF
          ELSE
            IF        PVITYPE = 9
              OPEN      PMSEVTA1,"pmsevtaf"
              PACK      KEY8,DPVIBILL,SP70
              CALL      RDPMEVT1
              IF        OVRCD=0
                PACK      KEY10,PMEVACON,SP70
              ENDIF
            ELSE
              PACK      KEY10,ADOCTA
            ENDIF
          ENDIF
          MATCH     SP70,KEY10
          GOTO      PINT5000 IF EQUAL
          OPEN      PMSHCPA1,"pmshcpaf"
          CALL      RDPMHCP1
          IF        OVRCD <> 1
            MOVE      PMHCPRV1,VARIABLE         * Doctor Provider Number
          ENDIF
PINTL909  GOTO      PINT5000
.
PINTL910  MOVE      SP70,VARIABLE
          MOVE      SP70,KEY10
          IF        PVITYPE = 1
            OPEN      EMRVISA1,"emrvisaf"
            PACK      KEY8,DPVIBILL,SP70
            CALL      RDEMVIS1
            IF        OVRCD=0
              PACK      KEY10,EMVIDOCT,SP70
            ENDIF
          ELSE
            IF        PVITYPE = 9
              OPEN      PMSEVTA1,"pmsevtaf"
              PACK      KEY8,DPVIBILL,SP70
              CALL      RDPMEVT1
              IF        OVRCD=0
                PACK      KEY10,PMEVACON,SP70
              ENDIF
            ELSE
              PACK      KEY10,ADOCTA
            ENDIF
          ENDIF
          MATCH     SP70,KEY10
          GOTO      PINT5000 IF EQUAL
          OPEN      PMSHCPA1,"pmshcpaf"
          CALL      RDPMHCP1
          IF        OVRCD <> 1
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,VARIABLE      * Formatted Doctor's Name
          ENDIF
          GOTO      PINT5000
.
PINTL920  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS   * Multi Hospital
          IF        IBCNMHOS=1
            PACK      KEY8,DPVIBILL,SP70
            OPEN      PMSVX1A1,"pmsvx1af"
            CALL      RDPMVX11
            BRANCH    OVRCD,PINT5000
            MOVE      PMVXMHOS,VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINTL930  CLOCK     TIME,VARIABLE
          GOTO      PINT5000
.
PINTL940  CALL      SPRD0000                 * PRA Full Address
          GOTO      PINT5000
.
PINTL950  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS   * Multi Hospital
          IF        IBCNMHOS=1
            PACK      KEY8,DPVIBILL,SP70
            OPEN      PMSVX1A1,"pmsvx1af"
            CALL      RDPMVX11
            BRANCH    OVRCD,PINT5000
.
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            BRANCH    OVRCD,PINT5000
            MOVE      PTHSAPPR,VARIABLE          * Hospital Approval Number
          ENDIF
          GOTO      PINT5000
.
PINTL960  MOVE      PSEX,DSEXCHAR	
          CALL      SEXDSP00	    
          MOVE      DSEXLDES,VARIABLE        * Get sex long description	      
          GOTO      PINT5000	    
.
PINTL970  COMPARE   ONE,MPGEFLAG
          GOTO      PINT4500 IF EQUAL
.
          MOVE      JOURTOT,FORM62           * Journal Total
          ADD       CREDTOT,FORM62           * Credit Note Total
          MOVE      FORM62,VARIABLE          * Adjustment Total
          MOVE      NINE,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PINT6000
.
PINTL980  PACK      VARIABLE,PMPXPFSN,SP70   * Preferred Surname
          GOTO      PINT5000
.
PINTL990  PACK      VARIABLE,PMPXPFGN,SP70   * Preferred Given Name
          GOTO      PINT5000
.
PINT1000  PACK      VARIABLE,PSNAME,SP70     * Pref Surname instead of Actual SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PINT5000
.
PINT1010  PACK      VARIABLE,PGNAME,SP70     * Pref GN instead of Actual GN
          MATCH     "1",PTCNPFGN
          IF        @EQUAL
            MATCH     SP70,PMPXPFGN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFGN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PINT5000
.
PINT1020  PACK      VARIABLE,PTMASNAM,SP70   * Pref Surname instead of 40char SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PINT5000
.
PINT1030  PACK      VARIABLE,CAPPRVNO,SP70   * Hospital Provider Approval Number
          GOTO      PINT5000
.
PINT1040  PACK      VARIABLE,PTCNHABN,SP70   *  Hospital ABN Number
          GOTO      PINT5000
.
PINT1050  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS   * Multi Hospital
          READ      CONTROLF,ZERO;*70,IBCNABNN   * ABN Number
          MOVE      IBCNABNN,VARIABLE
          IF        IBCNMHOS=1
            OPEN      PMSVX1A1,"pmsvx1af"
            MOVE      AADMNO,KEY8
            CALL      RDPMVX11
            BRANCH    OVRCD,PINT5000
            OPEN       PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            BRANCH    OVRCD,PINT5000
            MOVE      PTHSABNN,VARIABLE
          ENDIF
          GOTO      PINT5000 
.
. ---- Not currently templated ----
.
PINT2000  MOVE      CSDAY,CDAY               * Receipt Date
          MOVE      CSMON,CMON
          MOVE      CSCENT,CCENT
          MOVE      CSYEAR,CYEAR
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      PINT5000
.
PINT2010  IF        CASHFLAG = 1
            MOVE      "PAYMENT RECEIVED WITH THANKS",VARIABLE   * Receipt Text
          ENDIF
          GOTO      PINT5000
.
PINT2020  MOVE      CSREPNO,VARIABLE         * Receipt Number
          GOTO      PINT5000
.
.
PINT4500  MOVE      "C/FWD",VARIABLE         * Indicator for multi-page
          GOTO      PINT5000
.
. ------- Format the variable -------
.
PINT5000  RESET     VARIABLE,IBDTPLEN
          LENSET    VARIABLE
          RESET     VARIABLE
          PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      PINTL100                 * Read next record
.
. ------- Formatting for Form fields and right justified variables -------
.
PINT6000  COMPARE   ZERO,PRNTSTRT          * Don't bump if printing full length
          GOTO      PINT7000 IF EQUAL
.
          BUMP      VARIABLE,PRNTSTRT      * bump to starting position
          PACK      VARIABLE,VARIABLE
.
. ------- Print the variable -------
.
PINT7000  PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      PINTL100                 * Read next record
.
.------ end of invoice reached ------
.
PINT8000  MOVE      FALSE,EXIT
.
PINT9000  IF        PTCNLASR=1
            PRINT     *F;
            GOTO      PINT9999
          ENDIF
.
.------ print new-lines until the end of page is reached ------
.
PINT9500  COMPARE   CURRROW,IBTHLENG          * test page length
          GOTO      PINT9999 IF EQUAL
.
          PRINT     *N;
          ADD       ONE,CURRROW
          GOTO      PINT9000
.
PINT9999  MOVE      FALSE,EXIT               * Set exit flag
          IF        WEBFLAG<>1
            CALL      CLSPRINT                 * close the spool file
          ENDIF
          RETURN                             * Finished
+
.*********************************************************************
.         ENDINV17 : Previously called from PATINV17 (PTCNINVL = 8 - MBF).
.         Obtain the referral details from the mamography
.*********************************************************************
          DEFPROC   ENDINV17
.
.         FD's required
.
          INC       OUTXSCFD/INC            * Xtra screen details file
.
. ----- Get the xtra screen details referring doctor -----
.
EINV1700  MOVE      OBAOUTNO,OTXSOUTN       * outpatient number
          PACK      KEY8,OTXSOUTN
.
          PACK      CFNAME,UID,FILXSCA1
          TRAP      EINV1730 IF IO
          OPEN      OUTXSCA1,CFNAME         * open xtra screen details file
          TRAPCLR   IO
.
          CALL      RDOTXSC1                * read xtra screen details file
          BRANCH    OVRCD,EINV1750          * no details
.
          MATCH     SP6,OTXSDOC1
          GOTO      EINV1750 IF EQUAL       * no referring doctor
.
          OPEN      PATDO1A1,"patdo1af"     * open doctor file
          PACK      KEY6,OTXSDOC1
          CALL      RDDOCT1
          BRANCH    OVRCD,EINV1750          * invalid doctor
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      OTXSDTE1,KEY8           * get date for templating
.
          NORETURN
          GOTO      EINV1799
.
EINV1730  NORETURN
.
EINV1750  MOVE      SP70,PACFNAME
          UNPACK    SP70,DSADD1,DSADD2
          UNPACK    SP70,DSADD3,DSADD4
          UNPACK    SP70,DSPOST,DPROV,KEY8
          GOTO      EINV1799
.
.         IO's required
.
OVERCOND  MOVE      ONE,OVRCD
          RETURN
.
          INC       OUTXSCIO/INC            * Xtra screen details file
.
EINV1799  ENDPROC
+
