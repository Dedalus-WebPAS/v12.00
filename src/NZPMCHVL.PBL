.******************************************************************************
.*                                  NZPMCHVL                                  *
.* Validate NZ misc charge effective from and to dates for overlaps           *
.*                                                                            *
.* Requires:   KEY37 populated with -                                         *
.*                      - Claim Code (usually from ACLAIM)                    *
.*                      - Health Fund (usually AFUNDH)                        *
.*                      - H/F Table Type (converted from "patfn1af" -  AFNDTB)*
.*                      - Miscellaneous Charge code                           *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid misc charge effective from and to dates        *
.*                   1 = Invalid misc charge effective from and to dates      *
.*                                                                            *
.* Modifications:                                                             *
.* V10.04.01  07/04/2014  J.Tan           CAR 300260                          *
.*            Mods checking inactive items                                    *
.* V10.04.00  01/04/2014  J.Tan           CAR 299227                          *
.*            Created include                                                 *
.******************************************************************************
.
VNDT0000  MOVE      KEY37,SAVKEY29
.
          MATCH     "N",FILEFLAG
          IF        @EQUAL
            CALL      RDNZMC11              * Record on file?
          ELSE
            CALL      RDNZMCH1              * Record on file?
          ENDIF
          BRANCH    OVRCD,VNDT5000
.
          MATCH     "1",NZMCACFL            * Inactive?
          GOTO      VNDT5000 IF EQUAL       * Yes
.
          MATCH     VALTDATE,NZMCEEDT       * No change in effective from and
          GOTO      VNDT8000 IF EQUAL       * to dates so exit
.
VNDT5000  MATCH     "N",FILEFLAG
          IF        @EQUAL
            CALL      RSNZMC11              * Check for the same misc charge
            CALL      RPNZMC11              * with an earlier effective from
          ELSE
            CALL      RSNZMCH1              * Check for the same misc charge
            CALL      RPNZMCH1              * with an earlier effective from
          ENDIF
VNDT5200  BRANCH    OVRCD,VNDT6000          * date
.
          PACK      KEY29,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     SAVKEY29,KEY29
          GOTO      VNDT6000 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL            * Inactive
          GOTO      VNDT5400 IF NOT EQUAL
.
          MATCH     "N",FILEFLAG
          IF        @EQUAL
            CALL      RPNZMC11              * with an earlier effective from
          ELSE
            CALL      RPNZMCH1              * with an earlier effective from
          ENDIF
          GOTO      VNDT5200
.
VNDT5400  MATCH     SP70,NZMCEEDT           * No effective to date on previous
          GOTO      VNDT8000 IF EQUAL       * misc charge.
.
          MATCH     VALFDATE,NZMCEEDT
          IF        !@LESS
            GOTO      VNDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VNDT6000 IF EQUAL
.
          MATCH     VALTDATE,NZMCEEDT
          IF        !@LESS
            GOTO      VNDT9000              * Effective to date overlap
          ENDIF
.
VNDT6000  MATCH     "N",FILEFLAG
          IF        @EQUAL
            CALL      RSNZMC11              * Check for the same misc charge
            CALL      RKNZMC11              * with an earlier effective from
          ELSE
            CALL      RSNZMCH1              * Check for the same misc charge
            CALL      RKNZMCH1              * with an earlier effective from
          ENDIF
          BRANCH    OVRCD,VNDT8000          * date
.
VNDT6200  PACK      KEY29,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     SAVKEY29,KEY29
          GOTO      VNDT8000 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL            * Inactive
          GOTO      VNDT6400 IF NOT EQUAL   * No
.
          MATCH     "N",FILEFLAG
          IF        @EQUAL
            CALL      RKNZMC11              * with an earlier effective from
          ELSE
            CALL      RKNZMCH1              * with an earlier effective from
          ENDIF
          BRANCH    OVRCD,VNDT8000
          GOTO      VNDT6200
.
VNDT6400  MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VNDT9000 IF EQUAL
.
          MATCH     NZMCEFDT,VALFDATE
          IF        !@LESS
            GOTO      VNDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     NZMCEFDT,VALTDATE
          IF        !@LESS
            GOTO      VNDT9000              * Effective to date overlap
          ENDIF
.
VNDT8000  MOVE      ZERO,EXIT
          GOTO      VNDT9999
.
VNDT9000  MOVE      ONE,EXIT
          GOTO      VNDT9999
.
VNDT9999  RETURN

