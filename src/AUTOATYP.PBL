
.******************************************************************************
.* Include Name   : AUTOATYP.PBL                                              *
.*                                                                            *
.* Function       : Automatic update admission type after entering MBS Items  *
.*                                                                            *
.* Author         : J.Tan       01/12/2001                                    *
.*                                                                            *
.* Parameters     : OPDAADMNO       Admission number                          *
.*                  OPDADATE        Date                                      *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 14/05/2025  J.Tan          TSK 0955096                    *
.*                  Added Alphanumeric visit number changes                   *
.*        V11.01.01 22/02/2021  J.Tan          TSK 0888639                    *
.*                  Changed patmmbs counter record to DIM3                    *
.*                  10/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*                  V10.13.02 26/10/2018  J.Tan          TSK 0854372          *
.*                  Mods to open patmmbsf file                                *
.*                  V10.13.01 22/10/2018  J.Tan          TSK 0854372          *
.*                  Use pmvxud15 for Anaesthetic type if NOT 'Using Theatre'  *
.*                  V10.05.01 12/12/2014  J.Tan          CAR 309912           *
.*                  Mods to check for MBS Amount for Primary MBS if all       *
.*                  theatre fees are zero                                     *
.*                  V10.03.02 02/11/2012  Jill Parkinson CAR 275279           *
.*                  Added RDATRAN2                                            *
.*                  V10.03.01 05/09/2012  J.Tan         CAR 267784            *
.*                  Mods Checking for TCINDC19 Claim Code                     *
.*                  V10.01.02 01/12/2010  Peter Vela    CAR 233148            *
.*                  Initialised pattranf variables to spaces before write     *
.*                  V10.01.01 01/12/2010  J.Tan         CAR 233034            *
.*                  Mods Bed fees to use health fund table type               *
.*                  V10.00.01 16/08/2010  J.Tan         CAR 228192            *
.*                  Mods if not using Thea.Fee to use MBS amount for Exp.Item *
.*                  V9.12.05  09/03/2010  J.Tan         CAR 217017            *
.*                  Mods to set PTTRAUAT for auto update                      *
.*                  V9.12.04  03/03/2010  J.Tan         CAR 217017            *
.*                  Fixed Change record after Disch. for trans. file          *
.*                  V9.12.03  05/11/2009  Ebon Clements CAR 207604            *
.*                  Added call to RCVT0000 - determine time in recovery       *
.*                  V9.12.02 23/10/2008  J.Tan CAR 206764                     *
.*                  Mods NOT to Update Adm.type in trans.file  with TCINDC7=X *
.*                  V9.12.01 19/10/2008 J.Tan  CAR 207359                     *
.*                  Fixed for global bed fee update if PTTREPSD=2             *
.*                  V9.11.01 28/01/2008 J.Tan  CAR 168838                     *
.*                  Mods calculating daycase duration to use system parameter *
.*                  V9.10.01 11/08/2008 J.Tan  CAR 175859                     *
.*                  Added TEAMNUMB=X for Medical Booking (no oprdetaf record) *
.*                  V9.09.01 18/06/2007 J.Tan  CAR 143128                     *
.*                  Mods to use the Suggested classification file             *
.*                  V9.04.03 14/02/2006 J.Tan  CAR 94947                      *
.*                  Mods not to recalculate rate for force stepdown prior oper*
.*                  date                                                      *
.*                  V9.04.02 16/02/2005 J.Tan  CAR 54754                      *
.*                  Mods to update all trans records after oper date with the *
.*                  V9.04.01 06/10/2004 J.Tan  CAR 53798                      *
.*                  Fixed default claim code for multi hospital               *
.*                  V9.03.03 07/06/2004 J.Tan  CAR 50399                      *
.*                  Fixed writting trans record for day surgery patient       *
.*                  V9.03.02 31/10/2003 J.Tan                                 *
.*                  Mods for pmsmtiaf - Misc.theatre item file                *
.*                  V9.03.01 23/07/2003 CAR 41466 Peter Vela                  *
.*                  Fixed Discharge record in pattranf being changed into a   *
.*                  Transfer record                                           *
.*                  V9.02.01 19/02/2003 J.Tan  SRF 36801                      *
.*                  Mods to last record of admission date of trans file       *
.*                  19/04/2002 J.Tan                                          *
.*                  Mods to use MBS fee from item file                        *
.*                  18/01/2002 J.Tan                                          *
.*                  Mods for Daycase Suggested classification                 *
.*                  Mods to use the last MBS item as primary if all charges   *
.*                  are zeros                                                 *
.******************************************************************************
.
          DEFPROC   AUTOATYP  
.
          INC       PATRCAUD/INC
          INC       PATBFEFD/INC
          INC       PATTRNFD/INC
          INC       PATDINVS/INC
.
.*********************************************************************
.         mainline code
.*********************************************************************
.
CALDAYS   FORM      4
CITEMNO   DIM       9
.
DURATION  FORM      4
DIM1      DIM       1
DIM18     DIM       18
DIM18A    DIM       18
DIM30     DIM       30
.
ENDTIME   FORM      4
.
FORM4D    FORM      4
HOURTM    DIM       2
MINTIME   DIM       2
SECTIME   DIM       2
.
OPCNPODY  FORM      2
SAVITEM   DIM       9
SUGGCLSS  DIM       3
SFORM4D   FORM      4
STARTIME  FORM      4
SP9       INIT      "         "
.
AUTAT000  READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*217,CAUDQ
          READ      CONTROLF,TEN5;*189,CTHETR
          READ      CONTROLF,HUND05;*130,PTCNAUAT,*132,PTCNADB1,PTCNADB2:
                                           PTCNADB3,PTCNADB4,PTCNDFAN
          READ      CONTROLF,TWENTY;*193,PTCNDCLM,*211,PTCNCNDY
          READ      CONTROLF,FORTY2;*138,OPCNPODY:
                                    *235,OPCNFTIM,*237,OPCNTTIM
.
          COMPARE   ONE,PTCNAUAT
          GOTO      AUTAT999 IF NOT EQUAL
.
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATBFEE1,"patbfeef"
.
          BRANCH    CAUDQ,AUTAT100
          OPEN      PATRCAUD,"patrcaud"
.
.         Get the most expensive item for each session
.
AUTAT100  CALL      SUGAT000                * Get suggested admission type
          BRANCH    EXIT,AUTAT900
.
          CALL      PODAY000                * Check for no of pre-op days
          CALL      WRTTRN00                * Write transfer record
          CALL      UPDTRN00                * Update transfer file
          CALL      UPDADM00                * Update admission file
.
AUTAT900  MOVE      ZERO,EXIT
          GOTO      AUTAT9999
.
.*************************************************************************
.*                           SUGAT000                                    *
.*           Sub-routine to get the suggested classification             *
.*************************************************************************
SUGAT000  MOVE      OPDAADMN,KEY8           * Admission Number
          CALL      RDMISS1
          BRANCH    OVRCD,SUGAT900
.
          IF        IBCNMHOS=1
            OPEN      PMSVX1A1,"pmsvx1af"
            OPEN      PATHSPA1,"pathspaf"
            MOVE      SP10,PMVXMHOS
            CALL      RDPMVX11
            IF        OVRCD=0
              MOVE      SP10,PTHSDCLM
              PACK      KEY3,PMVXMHOS,SP10
              CALL      RDPTHSP1               * get Hospital Using Theatre Fee
              IF        OVRCD=0
                MOVE      PTHSDCLM,PTCNDCLM
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   ZERO,AINVPRT            * Check if Invioces are > 0       
          GOTO      SUGAT900 IF NOT EQUAL   * invoice printed
.
          MATCH     SP3,ATYPE
          GOTO      SUGAT900 IF EQUAL
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,SUGAT900
.
          MATCH     ANSX,TCINDC7
          GOTO      SUGAT900 IF EQUAL       * Exclude this admission type
.
          CALL      XMBS0000           * Get the primary MBS
          MATCH     SP3,PRIMMBS
          GOTO      SUGAT900 IF EQUAL
.
          PACK      KEY17,PRIMMBS,ADATE,SP70
          CALL      PATITMRD                 * Read on Item File
          BRANCH    OVRCD,SUGAT900 
.
          COMPARE   ONE,PTSGCOPT
          GOTO      SUGAT600 IF NOT EQUAL
.
          OPEN      PATSGCA1,"patsgcaf"
          PACK      KEY18,ACLAIM,AFUNDH,ITEMNO
          CALL      RDPTSGC1              * Read the suggested class file
          COMPARE   ZERO,OVRCD
          GOTO      SUGAT200 IF EQUAL
.
          MATCH     SP70,AFUNDH
          GOTO      SUGAT600 IF EQUAL
.
          MOVE      SP70,PTSGCLMN           * blank claim
          PACK      KEY18,PTSGCLMN,AFUNDH,ITEMNO,SP20  * key of fund and item
          CALL      RDPTSGC1                * read exceptions file
          COMPARE   ZERO,OVRCD
          GOTO      SUGAT200 IF EQUAL
.
          MOVE      SP70,PTSGFUND           * blank health fund
          PACK      KEY18,ACLAIM,PTSGFUND,ITEMNO,SP20  * key of fund and item
          CALL      RDPTSGC1                * read exceptions file
          BRANCH    OVRCD,SUGAT600           * no record so use ICLSS
.
SUGAT200  MOVE      PTSGCLSS,ICLSS      * Admission type
.
SUGAT600  MATCH     SP3,ICLSS
          GOTO      SUGAT900 IF EQUAL        * no suggested classification
.
          CALL      GATYP000                 * Get admission type for a date
.
.  Match Admission Type with that of the Item file & drop out if equal 
.
          MATCH     LATYPE,ICLSS           * Admission Type
          GOTO      SUGAT800 IF NOT EQUAL
.
          MATCH     DDATE,ADATE    
          GOTO      SUGAT820 IF EQUAL
          GOTO      SUGAT900
.
.  Not Equal so set Admission Type to that of Item File
.
SUGAT800  MOVE      ICLSS,SUGGCLSS         * Admission Type
.
.  Get suggested class. for day case
.
SUGAT820  MATCH     DDATE,ADATE    
          IF        @EQUAL
            MATCH     "X",TEAMNUMB
            IF        @EQUAL
              MATCH     SP3,PTITDCSC
              IF        !@EQUAL
                MOVE      PTITDCSC,SUGGCLSS  * Use suggested class.for daycase
                MATCH     LATYPE,SUGGCLSS
                GOTO      SUGAT900 IF EQUAL  * No change of adm type
              ENDIF
            ELSE
              IF        CTHETR=1
                PACK      KEY11,OPDAUNIQ,TEAMNUMB,SP70
                CALL      RDOPSRG1             * Read Operating Surgical details
                IF        OVRCD=0
                  CALL      DAYCAS00           * Doing Day Case Banding
                  MATCH     LATYPE,SUGGCLSS
                  GOTO      SUGAT900 IF EQUAL  * No change of adm type
                ENDIF
              ELSE
                CALL      DAYCAS00           * Doing Day Case Banding
                MATCH     LATYPE,SUGGCLSS
                GOTO      SUGAT900 IF EQUAL  * No change of adm type
              ENDIF
            ENDIF
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      SUGAT999
.
SUGAT900  MOVE      ONE,EXIT
SUGAT999  RETURN
+
.******************************************************************************
.* XMBS000 : Get the primary MBS (most expensive MBS)                         *
.******************************************************************************
XMBS0000  MOVE      ZERO,F1
          MOVE      SP10,PRIMMBS            * initialise primary MBS code
.
XMBS0200  MOVE      SP10,SAVITEM
          MOVE      ZERO,FORM12
          OPEN      PATDTRA3,"patdtraf"
          PACK      KEY18,AADMNO,TWO,SP20
          CALL      RDSDTRN3
XMBS1000  CALL      RDKDTRN3
          BRANCH    OVRCD,XMBS2000
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      XMBS2000 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      XMBS2000 IF NOT EQUAL   * not a theatre item
.
          PACK      KEY8,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",KEY8
          PACK      KEY17,TITEMNO,KEY8,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,XMBS1000          * not a valid item code
.
          MOVE      TITEMNO,SAVITEM         * savitem
          COMPARE   ONE,CMBSFEE
          GOTO      XMBS1600 IF EQUAL       * Overseas and use MBS amount
.
          MOVE      TPATAMTP,FORM12A
          ADD       TREBATE,FORM12A
.
          IF        IBCNMHOS=1
            MATCH     "1",PTHSTFEE
            GOTO      XMBS1800 IF EQUAL       * use theatre fee
          ELSE
            COMPARE   "1",PTCNTFEE
            GOTO      XMBS1800 IF EQUAL       * use theatre fee
          ENDIF
.
XMBS1600  MOVE      QIAMT,FORM12A
.
XMBS1800  COMPARE   FORM12A,FORM12
          GOTO      XMBS1000 IF NOT LESS
.
          MOVE      TITEMNO,PRIMMBS         * set primary MBS code
          MOVE      FORM12A,FORM12
          GOTO      XMBS1000
.
.         Check items in pmsmtiaf
.
XMBS2000  OPEN      PMSMTIA2,"pmsmtiaf"
          MOVE      "2",PMMIRTYP
          PACK      KEY15,AADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2
XMBS3000  CALL      RKPMMTI2
          BRANCH    OVRCD,XMBS9000
.
          MOVE      AADMNO,DAADMNO
          MATCH     DAADMNO,PMMIVISN         * compare admission number
          GOTO      XMBS9000 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      XMBS9000 IF NOT EQUAL
.
          PACK      KEY17,PMMIITEM,PMMITDAT,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,XMBS3000          * not a valid item code
.
          MOVE      PMMIITEM,SAVITEM         * savitem
          COMPARE   ONE,CMBSFEE
          GOTO      XMBS6000 IF EQUAL       * Overseas and use MBS amount
.
          MOVE      PMMIAMTP,FORM12A
          ADD       PMMIRBAT,FORM12A
.
          IF        IBCNMHOS=1
            MATCH     "1",PTHSTFEE
            GOTO      XMBS8000 IF EQUAL       * use theatre fee
          ELSE
            COMPARE   "1",PTCNTFEE
            GOTO      XMBS8000 IF EQUAL       * use theatre fee
          ENDIF
.
XMBS6000  MOVE      QIAMT,FORM12A
.
XMBS8000  COMPARE   FORM12A,FORM12
          GOTO      XMBS3000 IF NOT LESS
.
          MOVE      PMMIITEM,PRIMMBS         * set primary MBS code
          MOVE      FORM12A,FORM12
          GOTO      XMBS3000
.
.         If all MBS items have no charges, Check MBS Amount
.
XMBS9000  MATCH     SP3,PRIMMBS
          GOTO      XMBS9800 IF NOT EQUAL
.
          BRANCH    CMBSFEE,XMBS9800          * Already using MBS Amount
.
          IF        IBCNMHOS=1
            MATCH     "1",PTHSTFEE
            GOTO      XMBS9800 IF NOT EQUAL   * not using theatre fee
          ELSE
            COMPARE   "1",PTCNTFEE
            GOTO      XMBS9800 IF NOT EQUAL   * not using theatre fee
          ENDIF
.
          MOVE      ONE,CMBSFEE               * Use MBS Amount
          MOVE      ONE,F1
          GOTO      XMBS0200
.
.    If all MBS items have no charges and zero MBS amount, use the last MBS item
.
XMBS9800  IF        F1=1
            MOVE      ZERO,CMBSFEE            * Set flag back to NOT use MBS amt
          ENDIF
.
          MATCH     SP70,PRIMMBS
          IF        @EQUAL
            MOVE      SAVITEM,PRIMMBS
          ENDIF
XMBS9999  RETURN
+
.***************************************************************************
.   Get the admission type for the operation date.
.***************************************************************************
GATYP000  MOVE      ATYPE,LATYPE           * default to admissions
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2               * Position on index 2
GATYP100  CALL      RDKTRAN2               * Read Record
          BRANCH    OVRCD,GATYP999
.
          MATCH     AADMNO,TADMN
          GOTO      GATYP999 IF NOT EQUAL
.
          MATCH     TDATE,OPDADATE
          GOTO      GATYP999 IF LESS
.
GATYP200  MOVE      TATYPE,LATYPE
          GOTO      GATYP100
.
GATYP999  RETURN
+
.**************************************************************************
.*                               PODAY000                                 *
.*   The number of pre-op days allowed - n                                *     
.* - if the no of days b/w admission date and operation date > n          *
.*   then suggested classification will start from the operation date     *
.* - if the no of days b/w admission date and operation date <= n         *
.*   then suggested classification will start from the admission date     *
.**************************************************************************
PODAY000  REP       " 0",ADATE
          REP       " 0",OPDADATE
          IF        CTHETR=1
            DAYSEP    ADATE,OPDADATE,F2
            COMPARE   F2,OPCNPODY
            GOTO      PODAY999 IF LESS
          ENDIF
          MOVE      ADATE,OPDADATE           * use adm.date/time
          MOVE      ATIME,OPSGTISS
.
PODAY999  RETURN
+
.**************************************************************************
.*                               WRTTRN00             Called by: VALDAT00 *
.*  Write record for The Uploaded Operation Date & Time                   *
.*  Check if date is equal, if so need to check Operation Time against    *
.*  Transfer Time. Operation Time cannot be equal to the Transfer time of * 
.*  the read record.                                                      *
.*                                                                        *
.**************************************************************************
WRTTRN00  MOVE      SP70,KEY8
          PACK      KEY30,AADMNO,OPDADATE,OPSGTISS,Z70
          CALL      RDSTRAN2          * Position on index 2
WRTTRN10  CALL      RDPTRAN2
          BRANCH    OVRCD,WRTTRN99
.
          MATCH     AADMNO,TADMN
          GOTO      WRTTRN99 IF NOT EQUAL
.
          MOVE      SP70,KEY1
          MOVE      TMOVE,KEY1
          REP       "DXLX",KEY1       * check for disc./leave record
          MATCH     "X",KEY1
          IF        @EQUAL
            MOVE      TTIME,KEY8      * save Discharged or Leave date
            GOTO      WRTTRN10
          ENDIF
.
          MATCH     TDATE,OPDADATE    * If Operation Date = Admission Date
          GOTO      WRTTRN20 IF NOT EQUAL
.
          MATCH     SP70,KEY8         * Check Disc./Leave record
          GOTO      WRTTRN20 IF EQUAL
.
.         Check if Theatre time is after Discharged or leave date
.
          MATCH     KEY8,OPSGTISS
          GOTO      WRTTRN20 IF LESS
          MOVE      KEY8,OPSGTISS   * Use the previous transfer time
.
WRTTRN20  MOVE      AADMNO,TADMN    * Admission No. 
          MOVE      OPDADATE,TDATE  * Transfer Date
          MOVE      OPSGTISS,TTIME  * Transfer Time (surgery start time)
          MOVE      SUGGCLSS,TATYPE * Set Admission Type
.
WRTTRN40  PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP70
          CALL      RDATRAN2
          IF        OVRCD=0
            MOVE      TDATE,DIM8
            UNPACK    TTIME,HOURTM,KEY1,MINTIME,DIM1,SECTIME
            CALL      ASEC0000      * add 1 sec to date/time
            MOVE      SUGGCLSS,TATYPE * Set Admission Type
            MOVE      DIM8,OPDADATE
            MOVE      OPDADATE,TDATE
            MOVE      ":",KEY1
            PACK      TTIME,HOURTM,KEY1,MINTIME,KEY1,SECTIME,SP70
            MOVE      TTIME,OPSGTISS
            GOTO      WRTTRN40
          ENDIF
.
.         Make sure the change record date/time is not after discharge date/time
.
          COMPARE   THREE,ASTAT
          GOTO      WRTTRN60 IF NOT EQUAL
.
          MATCH     TDATE,DDATE
          GOTO      WRTTRN99 IF LESS
          GOTO      WRTTRN60 IF NOT EQUAL
.
          MATCH     DTIME,TTIME
          GOTO      WRTTRN99 IF NOT LESS
.
WRTTRN60  MOVE      SUGGCLSS,TATYPE
          MOVE      "C",TMOVE
          MOVE      "A",PTTRAUAT            * Auto update of admission type
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
.
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP70
          CALL      RDATRAN2
          IF        OVRCD=1
            CALL      WRTRAN2
          ELSE
            DISPLAY   "I*D transfer key AUTOATYP ",KEY30
          ENDIF
.
WRTTRN99  RETURN
+
.**************************************************************************
.*                               UPDTRN00             Called by: VALDAT00 *
.*  Update The Transfer File with new Admission type                       *
.*  Recalculate Bed Fees on the Transfer File if needed                    *
.**************************************************************************
UPDTRN00  MOVE      SP3,LATYPE
          MOVE      ADATE,FROMDATE
.
          PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
UPDTRN10  CALL      RDKTRAN2
          BRANCH    OVRCD,UPDTRN99
.
          MATCH     TADMN,AADMNO  
          GOTO      UPDTRN99 IF NOT EQUAL
.
.         Check if admission type should be excluded
.
          PACK      TCODE,ANSA,SP70
          PACK      KEY5,TCODE,TATYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,UPDTRN10
          MATCH     ANSX,TCINDC7
          GOTO      UPDTRN10 IF EQUAL       * Exclude this admission type
.
          MATCH     OPDADATE,TDATE
          IF        !@LESS
            MOVE      SUGGCLSS,TATYPE
          ELSE
.
.          Check if force stepdown done prior oper.date
.
            BRANCH    PTTREPSD,UPDTRN10,UPDTRN10
          ENDIF
.
          CMATCH    "D",TMOVE          * Use Previous record variables
          GOTO      UPDTRN50 IF EQUAL
.
.         Get the new bed fee rate
.
          MOVE      ZERO,PATFEE
          MOVE      ZERO,HFFEE
          MOVE      SP1,COLFEE
.
          MOVE      TWARD,LWARD
          MOVE      TBED,LBED
          MOVE      TRBTYP,LRBTYP
          MOVE      TRBTYP,SAVBTYP
          MOVE      TATYPE,SAVATYP
.
.  Save the key of trans file
.
          PACK      DIM30,TADMN,TDATE,TTIME,TWARD,TBED
.
.  Check if we have a change in admission type
.
          MATCH     TATYPE,LATYPE
          GOTO      UPDTRN15 IF EQUAL
.
          MOVE      TATYPE,LATYPE
          BRANCH    PTCNCNDY,UPDTRN15     * don't reset day count
          MOVE      TDATE,FROMDATE
.
.  Get the number no of days in hospital up to this trans date,
.  to work out the bed fees
.
UPDTRN15  MOVE      ZERO,TOTDAY
          MATCH     ADATE,DDATE
          GOTO      UPDTRN20 IF EQUAL
.
          MOVE      ADATE,FROMDATE
          MOVE      TDATE,TODATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TOTDAY
          ADD       ONE,TOTDAY
.
UPDTRN20  ADD       ADYHOSP,TOTDAY
.
.  Reposition to the prior trans record,As the NHOSPDAY uses trans file
.
          MOVE      DIM30,KEY30
          CALL      RDTRAN2
.
          MOVE      TDISC,SDISC
          MOVE      TALLW,SALLW
.
UPDTRN40  MOVE      TDATE,DIM8
          MOVE      TOTDAY,TOTDAYS
          MOVE      TATYPE,ATYPE
.
          MOVE      TDATE,CEFFDATE
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,ANSC,ANSL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY27,ACLAIM,AFUNDH,AFNDTB,ATYPE,SAVBTYP,SP70
          CALL      GETBFEES     * Calculate Bed Fees
.
.  Save the computer generated rate for the change rate audit file
.
          MOVE      SBFPAT,RCGPAT
          MOVE      SBFHF,RCGHF
          MOVE      SBFENDDY,REDAYS
          SUB       TOTDAY,REDAYS
          ADD       ONE,REDAYS
.
          MOVE      SBFENDDY,SAVEND
          MOVE      ZERO,SAVAEND
          MOVE      SBFPAT,PATFEE
          MOVE      SBFHF,HFFEE
          MOVE      SDISC,NDISC
          MOVE      SALLW,NLWAMT
          MOVE      SP2,AALLOW
          MOVE      LRBTYP,SAVBTYP
.
UPDTRN50  MOVE      PATFEE,TRATEF
          MOVE      ZERO,PTTRADPT
          MOVE      HFFEE,TRATEH
          MOVE      ZERO,PTTRADRB
          MOVE      ZERO,PTTRAEND
.         MOVE      ZERO,PTTREPSD
          MOVE      NDISC,TDISC
          MOVE      NLWAMT,TALLW
          MOVE      SAVBTYP,TRBTYP
          MOVE      SAVEND,TRBEND
          MOVE      SAVEND,REDAYS
          SUB       TOTDAY,REDAYS
          ADD       ONE,REDAYS
          MOVE      ZERO,TEXTRA
          MOVE      SAVATYP,TATYPE
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
          PACK      DIM30,TADMN,TDATE,TTIME,TWARD,TBED
.
.  Write  to Rate Change Audit file if system parameter is switched on (0)
.
          BRANCH    CAUDQ,UPDTRN80           * added for V5.01.15 - SRF 105195
.
UPDTRN60  CALL      RDSRCA
.
          COMPARE   ZERO,RSTATUS
          GOTO      UPDTRN70 IF EQUAL
.          PRINT     *N,"Rate Change Audit in use, Waiting........";
          GOTO      UPDTRN60
.
UPDTRN70  CALL      WRIRCA
          MOVE      AADMNO,RADMNO
          MOVE      PGNAME,ANS
          PACK      RNAME,ANS,DOT,PSNAME
          MOVE      ACLAIM,RCLAIM
          MOVE      AFUNDH,RFUND
          MOVE      AFNDTB,RHFTAB
          MOVE      SAVBTYP,RBTYPE
.
          MOVE      ZERO,RNPAT        * New rate = rate - disc - allow
          MOVE      PATFEE,RNPAT
          SUB       NDISC,RNPAT
          SUB       NLWAMT,RNPAT
          MOVE      HFFEE,RNFUND     * from  PATDSBFE
          MOVE      TDATE,RDATE
          MOVE      TATYPE,RATYPE
          MOVE      ZERO,ROPAT
          MOVE      SP3,RCLSS
          MOVE      SP3,RCHST
          MOVE      PASSCODE,ROPER       * Operator code
.
          CALL      WRRCA
.
.  Reposition to the previous trans record
.
UPDTRN80  MOVE      DIM30,KEY30
          CALL      RDTRAN2
          GOTO      UPDTRN10  
.
UPDTRN90  RESET     WEBTITLE
          APPEND    "Default Bed Fee Not Set Up Yet - ",WEBTITLE
          APPEND    "Record has partially been updated, ",WEBTITLE
          APPEND    "Please contact Supervisor",WEBTITLE
          APPEND    SP70,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBERR00
          MOVE      ONE,EXIT 
          GOTO      UPDTRN99
.
UPDTRN99  RETURN
+
.**************************************************************************
.*                               DAYCAS00             Called by: VALIDT00 *
.*   Get Admission Type, Based on Rules for Day Case Banding.             *
.*   Admission type will be set to one of the Day case parameters         *
.*   depending on the rule.                                               * 
.*   First up work out operation duration in Mins.                        *
.*   PARAMETERS: PTCNADB1...4 - Day Case Banding                          *
.*               PTCNDFAN - Default Anaesthetic Code for Day Proc         *
.*   RETURNED PARAMETERS: SUGGCLSS - Suggested Class                      *
.**************************************************************************
DAYCAS00  MATCH     SP3,PTITDCSC
          GOTO      DAYCAS10 IF EQUAL
          MOVE      PTITDCSC,SUGGCLSS  * Use suggested class.for daycase
          GOTO      DAYCAS99
.
DAYCAS10  COMPARE   ONE,CTHETR
          GOTO      DAYCAS20 IF NOT EQUAL * Not Using Theatre system
.
          CALL      RCVT0000              * determine time in recovery
.
          LOAD      MMSTIM,OPCNFTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                    OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                    OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                    OPARTIDP,OPARTETC
.                                           * load End time from selected
          LOAD      MMETIM,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                    OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                    OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                    OPARTIDP,OPARTETC
          GOTO      DAYCAS80
.
.         Use pmvxud15 as Anaesthetic type if 'Using Theatre system' set to No
.
DAYCAS20  MOVE      PMVXUD15,OPDAANAE        * Use the user defined field
.
.         Get the start and end time of primary MBS 
.
          OPEN      PATMMBS1,"patmmbsf"
          PACK      KEY11,OPDAADMN,SP10
          CALL      RDSMMBS1                     * posn before visit #
DAYCAS30  CALL      RDKMMBS1                     * read next patmmbaf record
          BRANCH    OVRCD,DAYCAS99               * eof - finished sort
.
          MATCH     OPDAADMN,DMMADMN
          GOTO      DAYCAS99 IF NOT EQUAL
.
          MATCH     PRIMMBS,MMCODE
          GOTO      DAYCAS30 IF NOT EQUAL       * Not a primary MBS
.
DAYCAS80  REP       " 0",MMSTIM
          REP       " 0",MMETIM
          UNPACK    MMSTIM,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes  
          MOVE      FORM4,STARTIME     * Start time now in Minutes
.
          UNPACK    MMETIM,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes
          MOVE      FORM4,ENDTIME      * End Time now in Minutes
.
          ASSIGN    ENDTIME-STARTIME,DURATION  * Get Duration..
.
.  Read Categories Codes file with Anaesthetist Code
.
          MATCH     SP3,OPDAANAE
          IF        @EQUAL
            MOVE      PTCNDFAN,OPDAANAE    * use default anaesthetic
          ENDIF
.
          MOVE      "OA",CATEGORY
          MOVE      OPDAANAE,CODE
          CALL      GETCOD00
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1   * convert to form easier to check on!!!
.
          IF        (FORM1 = 2)|(FORM1 = 3)|(FORM1 = 4)          
            IF        DURATION < SIXTY
              MOVE      PTCNADB3,SUGGCLSS     
              GOTO      DAYCAS99
            ELSE
              MOVE      PTCNADB4,SUGGCLSS     
              GOTO      DAYCAS99
            ENDIF
          ENDIF     
          IF        FORM1 = 1
            MOVE      PTCNADB2,SUGGCLSS     
            GOTO      DAYCAS99
          ENDIF
.
.  Ok if none of the above conditions have been met then Band 1 applies
.
          MOVE      PTCNADB1,SUGGCLSS     
.
DAYCAS99  RETURN
+
.*****************************************************************************
.*                            ASEC0000                                       *
.*               Add one second to date/time                                 *
.* Requires: DIM8-Date & HOURTM/MINTIME/SECTIME-time value (ccyymmddhhmmss)  *
.* Returns : DIM8 & HOURTM/MINTIME/SECTIME- incremented by 1 second          *
.*****************************************************************************
ASEC0000  MATCH     "59",SECTIME
          IF        !@EQUAL
            MOVE      SECTIME,FORM2              * just increment seconds
            ADD       ONE,FORM2
            MOVE      FORM2,SECTIME
            REP       " 0",SECTIME
            GOTO      ASEC9999
          ENDIF
.
.         The seconds were "59", so reset these to "00" and increment the
.         minutes
.
          MOVE      "00",SECTIME
          MATCH     "59",MINTIME
          IF        !@EQUAL
            MOVE      MINTIME,FORM2              * just increment minutes
            ADD       ONE,FORM2
            MOVE      FORM2,MINTIME
            REP       " 0",MINTIME
            GOTO      ASEC9999
          ENDIF
.
.         The minutes were "59", so reset these to "00" and increment the
.         hours
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MATCH     "23",HOURTM
          IF        !@EQUAL
            MOVE      HOURTM,FORM2              * just increment hours
            ADD       ONE,FORM2
            MOVE      FORM2,HOURTM
            REP       " 0",HOURTM
            GOTO      ASEC9999
          ENDIF
.
.         The hours were "59", so reset these to "00" and increment the
.         date
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MOVE      "00",HOURTM
.
.         We need to increment the date by 1 day
.
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julian day
          ADD       ONE,JULDAY                   * increment julian day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                       * get new date
          PACK      DIM8,CC,YY,MM,DD,SP70
          REP       " 0",DIM8
.
ASEC9999  RETURN
+
.***************************************************************************
.*                               UPDADM00             Called by: VALDAT00  *
.*  Update Admission File 
.***************************************************************************
UPDADM00  MOVE      OPDAADMN,KEY8      * Admission Number
          CALL      RLPTMIS1           * Read & Lock Admission File
          BRANCH    OVRCD,UPDADM99
.
          MOVE      SUGGCLSS,ATYPE     * Set Admission type 
          CALL      UPMISS1            * Update & Unlock Admission File
          CALL      UUPTMIS1           * Read & Lock Admission File
.
UPDADM99  RETURN
+
          INC       NHOSPDAY
          INC       OPRECOVT
          INC       GETBFEES
          INC       PATRCAIO/INC
          INC       PATBFEIO/INC
          INC       PATTRNIO/INC
          INC       IBAOVRIO/INC
.
AUTAT999  ENDPROC
