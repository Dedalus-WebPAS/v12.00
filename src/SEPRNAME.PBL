.***************************************************************************
.* System    :   Common                                                    *
.* Program   :   SEPRNAME                                                  *
.* Desc      :   Separate out the first and second name from a combined    *
.*               Given Names field.                                        *
.***************************************************************************
.* Date      :   07/06/2011                                                *
.* Author    :   Steve Armstrong                                           *
.* Function  :   This program will take a combined given name field and    *
.*           :   separate it into first and second given name components.  *
.*           :                                                             *
.* Requires  :   FULLGNAM - Combined given names field     (DIM 80)        *
.*               FORM2    - FORM  2                                        *
.* Returns   :   FRSTGNAM - First given name field         (DIM 40)        *
.*               SCNDGNAM - Second given name field        (DIM 40)        *
.* Mods      :                                                             *
.*        V10.02.00  07/06/2011  Steve Armstrong  CAR 240722/239592        *
.*                   Based on FXLDGNAM and modified to be a generic        *
.*                   subroutine.                                           *
.***************************************************************************
.
SEPRNAME  MOVE      SP70,FRSTGNAM                * initialise first given name
          MOVE      SP70,SCNDGNAM                * initialise second given name
.
          MATCH     SP70,FULLGNAM                * blank full given name ?
          GOTO      SEPNAM99 IF EQUAL            * yes - finished
.
          SCAN      SP1,FULLGNAM                 * space found in full name ?
          IF        !@EQUAL
            MOVE      FULLGNAM,FRSTGNAM          * no - save first name only
            GOTO      SEPNAM15                   * finish
          ENDIF
.
.         We have found the start of the first name, so find the end of the
.         first name , then load it.
.
          MOVEFPTR  FULLGNAM,FORM2               * save length of first name
          RESET     FULLGNAM
          SETLPTR   FULLGNAM,FORM2              * set LL to length of first name
          MOVE      FULLGNAM,FRSTGNAM            * Save first name
.
          SETLPTR   FULLGNAM,80                  * Set LL back to 80
          BUMP      FULLGNAM,FORM2               * Bump to space between names
.
.         Ignore spaces between the first and second given names
.
SEPNAM11  MATCH     SP1,FULLGNAM                 * space found ?
          GOTO      SEPNAM14 IF NOT EQUAL        * no - load second name
.
          BUMP      FULLGNAM,1                   * position on next character
          GOTO      SEPNAM14 IF EOS              * end of string
          GOTO      SEPNAM11                     * check if character is space
.
.         We have found a non-blank character which must be the start of
.         the second name, so load the second name
          
SEPNAM14  MOVE      FULLGNAM,SCNDGNAM
.
SEPNAM15  PACK      FRSTGNAM,FRSTGNAM,SP70
          PACK      SCNDGNAM,SCNDGNAM,SP70
.
SEPNAM99  RETURN
+
