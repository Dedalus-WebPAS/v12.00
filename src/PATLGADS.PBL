.
.       PATLGADS : Display LGA File Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available allowances
.
PATLGADS  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"LGA CODES"
.
.....     TRAP       ENDLGAA0 IF F1
.
          MOVE       SP4,KEY4
          CALL       RDSLGAA1
.
.         Loop over file
.
LGAALP1   CALL       RDKLGAA1
          BRANCH     OVRCD OF ENDLGAA1
.
          DISPLAY    *PCCOL:CVERT,*V2LON,LGACODE,SP2,*HOFF,LGADESC
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       LGAALP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       LGAALP1 IF LESS
.
.....     TRAPCLR    F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSLGA200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      ENDLGA2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATLGADS IF EQUAL
.
          GOTO      DSLGA400
.
.         Use the new standards question
.
DSLGA200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      ENDLGA2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATLGADS IF EQUAL
.
DSLGA400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
.....     TRAP       ENDLGAA0 IF F1
.
          GOTO       LGAALP1
.
ENDLGAA0  NORETURN
.....     TRAPCLR    F1
.
.      End of allowances
.
ENDLGAA1  BRANCH    CNEWDS,ENDLGA2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDLGA2   RETURN
