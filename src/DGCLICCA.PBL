.************************************************************************
.*  Include  : DGCLICCA  -  Send Cancel Admission Details               *
.*                                                                      *
.*  Note     : This should be called before the transfer record is      *
.*             deleted.                                                 *
.*                                                                      *
.*  Author   : Steve Armstrong (28/04/1997)                             *
.*                                                                      *
.*  Mods:                                                               *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.02 06/06/2013  Steve Armstrong   CAR 268961                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.*            Separated out CCA routine from dgcliadm routine.          *
.*            Mods to write directly to holding tables instead of       *
.*            hl7bmtaf.                                                 *
.************************************************************************
.*  V9.02.03  12/02/2002  Davin Sloan                                   *
.*            Mods to not send CCA message if transfer record not found *
.*  V9.02.01  02/10/2001  Davin                                         *
.*            Mods to use message holding file                          *
.*            Rename DGCLICNA to DGCLICCA                               *
.*  V5.       13/10/1997  Steve Armstrong   WHL                         *
.*            Mods for new Master extension file variables              *
.*            (PTMXOPER - PTMXPOST).                                    *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICCA
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,SEVENTY9;*239,PTCNDGAD
          READ      CONTROLF,HUND10;*105,PTCNA11M
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA11M               * yes - sending A11 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGAD <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        PTCNDGAD = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          MOVE      SP3,MHLRCODE
          PACK      PMQVSPAR,SP30,SP30,SP1
          CALL      WRPMQVI1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A11 message?
          IF        @EQUAL
            MOVE      "A11",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGAD                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CCA",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
.
DGCLIEND  ENDPROC
+
