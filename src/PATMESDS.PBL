.
.       PATMESDS : Display Invoice Message Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    10/04/94 Ian Rutt
.                    Renamed to PATMESDS for standards upgrade 
.
.        Subroutine to display the available Invoice Message
.
PATMESDS  MOVE       FIVE,CVERT
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"INVOICE MESSAGES"
.
          MOVE       SP3,KEY3
          CALL       RDSMESG1
.
.         Loop over file
.
MESGLP1   CALL       RDKMESG1
          BRANCH     OVRCD,ENDMESG1
.
MESGDS1   DISPLAY    *P1:CVERT,*V2LON,PMCODE,*HOFF,*P10:CVERT,PMLINE1
          ADD        ONE,CVERT
          DISPLAY    *P10:CVERT,PMLINE2
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       MESGLP1 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSMES200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS 
          CMATCH    ANSE,ANS
          GOTO      ENDMES2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATMESDS IF EQUAL
.
          GOTO      DSMES400
.
.         Use the new standards question
.
DSMES200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      ENDMES2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATMESDS IF EQUAL
.
DSMES400  MOVE       FIVE,CVERT
          DISPLAY    *P1:5,*EF
          GOTO       MESGLP1
.
.      End of messages
.
ENDMESG1  BRANCH    CNEWDS,ENDMES2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDMES2   RETURN
