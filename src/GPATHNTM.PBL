.----------------------------------------------------------------------
. Get file name and extension from full path and file name 
. Input
.   GFILPATH - Full path. (ie. /opt/iba/input/filename01.txt)
. Output
.   GFILDIR  - Directory name (ie. /opt/iba/input/)
.   GFILNAME - File name (ie. filename01)
.   GFILEXTN - extension of the file (ie. txt)  
.
. Working Variables
.   GPATHNTD.INC   
.----------------------------------------------------------------------
. V11.03.00  18/08/2023  Thanh T       TSK 0923938
.            Initial Version.
.----------------------------------------------------------------------
GPATHN00  MOVE      SP70,GFILNAME
          MOVE      SP70,GFILEXTN
          PACK      GFILDIR,SP70,SP70,SP70
          MOVE      GFILPATH,GFILTEM1
          STRIP     GFILTEM1 
          MOVE      GFILTEM1,GFILTEM2
.         
GPATHN20  SCAN      "/",GFILTEM1          
          IF        @EQUAL
            MOVEFPTR  GFILTEM1,F3 
            BUMP      GFILTEM1
            IF        F3 = 1
              MOVE      "/",GFILDIR
            ELSE 
              SCAN      "/",GFILTEM2
              IF        @EQUAL
                LENSET    GFILTEM2
                RESET     GFILTEM2
                PACK      GFILDIR,GFILDIR,GFILTEM2
              ENDIF
            ENDIF
            MOVE      GFILTEM1,GFILTEM2
            GOTO      GPATHN20
          ENDIF
.
GPATHN30  MOVE      GFILTEM1,GFILTEM2
          SCAN      ".",GFILTEM1
          IF        !@EQUAL
            MOVE      GFILTEM1,GFILNAME
            GOTO      GPATHN99
          ENDIF
. 
. 1st dot '.' found
. 
GPATHN40  BUMP      GFILTEM1
          MOVE      GFILTEM1,GFILEXTN
          MOVE      GFILTEM1,GFILTEM3
          SCAN      ".",GFILTEM2
          IF        @EQUAL
            BUMP      GFILTEM2,-1 
            LENSET    GFILTEM2 
            RESET     GFILTEM2
            MOVE      GFILTEM2,GFILNAME
          ENDIF
.
          MOVE      GFILTEM3,GFILTEM1
          MOVE      GFILTEM3,GFILTEM2
.
GPATHN50  SCAN      ".",GFILTEM1
          IF        !@EQUAL
            GOTO      GPATHN99
          ENDIF
.
. dot '.' found
.
GPATHN60  BUMP      GFILTEM1
          MOVE      GFILTEM1,GFILEXTN
          MOVE      GFILTEM1,GFILTEM3
          SCAN      ".",GFILTEM2
          IF        @EQUAL
            BUMP      GFILTEM2,-1
            LENSET    GFILTEM2
            RESET     GFILTEM2
            PACK      GFILNAME,GFILNAME,DOT,GFILTEM2
            MOVE      GFILTEM3,GFILTEM1
            MOVE      GFILTEM3,GFILTEM2
            GOTO      GPATHN50
          ENDIF
.
GPATHN99  RETURN
