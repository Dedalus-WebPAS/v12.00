.********************************************************************** 
.     OUTDSCTY : Display Clinic Type
.
.     This include displays entries in CLINIC TYPE file.  
.
.********************************************************************** 
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                   24/06/1994  bez          acc rehab mods
.                   Rename "Clinic Type" if parameter set
.                   25/01/1995  Steve Armstrong  
.                   Fixed so at end of file goes to ENDCTY1
.********************************************************************** 
.
OUTDSCTY  
.-------- Rename "Clinic Type" if parameter set
          OPEN      CONTROLF,"controlf"          
          READ      CONTROLF,THIRTY2;*3,FORM1:
                                     *4,KEY12
          IF        FORM1=0
            MOVE      "Clinic Type ",KEY12    * parameter not set
          ENDIF
          PACK      KEY20,KEY12,SP20
          STRIP     KEY20  

.
DSCTY000  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,*+,KEY20
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY12 WITH IDDD,SP20
          CALL      RDSCTYA1
.
DISPCTY1  CALL      RDKCTYA1
          BRANCH    OVRCD,ENDCTY1
.
          MATCH     OCTSITE,IDDD
          GOTO      ENDCTY1 IF NOT EQUAL
.
          MOVE      OCTDESC TO DIM14
          DISPLAY   *PCCOL:CVERT,*V2LON,OCTCTYP,SP1,*HOFF,OCTGRP,SP1,DIM14
.
          ADD       ONE,CVERT
          COMPARE   "24",CVERT
          GOTO      DISPCTY1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "26",CCOL
          COMPARE   "70",CCOL
          GOTO      DISPCTY1 IF LESS
.
.         Check for new standards question, or old question
.
DSCTY100  BRANCH    CNEWDS,DSCTY200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ENDCTY2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DSCTY000 IF EQUAL
.
          GOTO      DSCTY400
.
.         Use the new standards question
.
DSCTY200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      ENDCTY2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DSCTY000 IF EQUAL
.
DSCTY400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      DISPCTY1
.
.      End of codes
.
ENDCTY1   BRANCH    CNEWDS,ENDCTY2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
ENDCTY2   MOVE      ZERO,OVRCD
          MOVE      SP30,OCTDESC
          RETURN
