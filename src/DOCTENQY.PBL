.*****************************************************************************
.* Procedure Name: DOCTENQY                                        
.* Function      : Display & Select a Doctor - 1 doctor per line    
.* Mods:
.*        V5.06.01  10/06/1998    Glenn Berry       SRF 644657        
.*                  Added CKYINACT - check if variable is active
.*****************************************************************************
          DEFPROC   DOCTENQY
.
          INC       PATDO1FD/INC
.
DIM4      DIM       4
DIM8      DIM       8
DIM14     DIM       14
DIM15     DIM       15
DIM16     DIM       16
DIM20     DIM       20
DIM79     DIM       79
NUMITEM   FORM      2
ARRYITEM  DIM       26[24]
FIRSTREC  FORM      1
CMDLINE   DIM       120
SCREEN    FORM      2
PREVITEM  FORM      2[99]
COUNTER   FORM      5
FORM2     FORM      2
.
Z10       INIT      "zzzzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.*************************************************************************
.*  DOCTENQY  :  Display the Doctors Mainline   
.*************************************************************************
DOCTENQY  OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          CALL      DDOC0000                      * batch screen processing
.
DENQY999  GOTO      ENDDENQY
.*************************************************************************
.*  DDOC0000  :  Display the Outpatient History 
.*************************************************************************
DDOC0000  MOVE       SP30,DSNAMXX
          MOVE       SP1,ANS
          DISPLAY    *P1:3,*EF:
                     *P1:4,"Doctor Surname : ____________________"
          KEYIN      *P18:4,*V2LON,DSNAMXX:
                     *P18:4,*DV,DSNAMXX
.
          DISPLAY    *P5:6,*EF,*V2LON,*ULON,"Code  ":
                     *P12:6,"Doctors Name                  ":
                     *P43:6,"Type":
                     *P48:6,"Surgery Address                   "
.
          DISPLAY   *P1:7,*EF;
          MOVE      ONE,SCREEN                    * init screen no.
          MOVE      ONE,NUMITEM                   * init Item no.
          MOVE      "7",CVERT
.
          PACK      KEY26,DSNAMXX,SP30
          CALL      RDSDOCT2
.
DDOC1000  CALL      RDKDOCT2  
          BRANCH    OVRCD,DDOC5000          * end of file
.
          IF        DRSTAT=1 & CKYINACT=1
              GOTO      DDOC1000            * ignore non-active codes
          ENDIF
.
          RESET     DSNAMXX
          GOTO      DDOC1100 IF EOS
.
          MATCH     DSNAMXX,SP20
          GOTO      DDOC1100 IF EQUAL      * no search code
.
          MATCH     DSNAME,DSNAMXX
          GOTO      DDOC5000 IF NOT EQUAL
.
DDOC1100  COMPARE   TEN7,NUMITEM  
          GOTO      DDOC3000 IF LESS
.
. we need a new screen
.
DDOC1200  CALL      QNXT0000                     * ask question with Next scrn
          BRANCH    EXIT,DDOC2500:               * Page Forward selected
                         DDOC2000:               * Page Back selected
                         DDOC7000                * Exit
.
          GOTO      DDOC5700                      * item selected
.
. previous screen was selected
.
DDOC2000  PACK      KEY26,ARRYITEM[1]
          CALL      RDSDOCT2                      * pos on 1st key on screen
.
. read back 13 records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
DDOC2200  CALL      RDPDOCT2                      * read back 1 record
          BRANCH    OVRCD,DDOC2300
.
          IF        DRSTAT=1 & CKYINACT=1
              GOTO      DDOC2200                  * ignore non-active codes
          ENDIF
.
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   TEN6,COUNTER    * check if read far enough
          GOTO      DDOC2200 IF NOT EQUAL
.
. we have read back far enough
.
DDOC2300  DISPLAY   *P1:7,*EF                     * erase screen
          MOVE      "7",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
          SUB       ONE,SCREEN                    * update screen no.
          GOTO      DDOC3000
.
. Next screen was selected
.
DDOC2500  ADD       ONE,SCREEN                    * increment screen no.
          DISPLAY   *P1:7,*EF                     * erase screen
          MOVE      "7",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
.
. display record
.
DDOC3000  STRIP     DSNAME
          STRIP     DSADD1
          STRIP     DSADD2
          CLEAR     KEY32
          APPEND    DSADD1,KEY32
          APPEND    ", ",KEY32
          APPEND    DSADD2,KEY32
          RESET     KEY32
          DISPLAY   *P1:CVERT,*V2LON,NUMITEM,*HOFF,".":
                    *P5:CVERT,DCODE,*HOFF:
                    *P12:CVERT,*+,DSNAME,",",SP1,*+,DGNAME:
                    *P43:CVERT,DRTYPE:
                    *P48:CVERT,KEY32
.
          PACK      DSNAME,DSNAME,SP20,SP20
.
.    save each record as it is displayed (for selection purposes)
.
          PACK      ARRYITEM[NUMITEM],DSNAME,DCODE
.
          ADD       ONE,CVERT                     * update row pos
          ADD       ONE,NUMITEM                   * increment counter
          GOTO      DDOC1000                      * get next record
.
. we have no more records to display
.
DDOC5000  CALL      QLST0000
          BRANCH    EXIT,DDOC2500:                * shouldn't get here
                         DDOC2000:                * Page Back selected
                         DDOC7000                 * Exit
.
          GOTO      DDOC5700                      * item selected
.
. Item Selected
.
DDOC5700  UNPACK    ARRYITEM[FORM2],KEY20,DOCCODE
.
. Exit Selected
.
DDOC6500  MOVE      ZERO,EXIT
          GOTO      DDOC9999
.
DDOC7000  MOVE      ONE,EXIT
          GOTO      DDOC9999
.
DDOC9999  RETURN              
+
.**************************************************************************
.*  QNXT0000  :  ask line 24 question with (N)ext                         *
.**************************************************************************
QNXT0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QNXT0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"N",*HOFF,")ext, ":
                    "(",*V2LON,"P",*HOFF,")revious, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "43",CCOL
          GOTO      QNXT1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QNXT0500  DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"N",*HOFF,")ext, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "31",CCOL
.
. now get keyin
.
QNXT1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QNXT1800 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS                  * Change to lower case
          REP       "N1P2E3",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QNXT1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QNXT2000,QNXT3000,QNXT4000,QNXT5000
QNXT1500  BEEP 
          GOTO      QNXT1000
.
QNXT1800  MOVE      DIM2,FORM2 
          COMPARE   ZERO,FORM2
          GOTO      QNXT1500 IF EQUAL
.
          IF        (FORM2>(NUMITEM-1)) | (FORM2<0)
            GOTO      QNXT1500
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      QNXT9999
.
QNXT2000  MOVE      ONE,EXIT                      * Add selected
          GOTO      QNXT9999
.
. Previous was selected so check if it was asked
.
QNXT3000  BRANCH    SCREEN,QNXT5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QNXT9999
.
QNXT4000  MOVE      THREE,EXIT                    * page Forward selected
          GOTO      QNXT9999
.
QNXT5000  MOVE      FOUR,EXIT                    * page Forward selected
          GOTO      QNXT9999
.
. Previous was selected but wasn't asked so error
.
QNXT5200  BEEP
          GOTO      QNXT1000
.
QNXT9999  RETURN
+
.**************************************************************************
.*  QLST0000  :  ask line 24 question without (N)ext                      *
.**************************************************************************
QLST0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QLST0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"P",*HOFF,")revious, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "35",CCOL                     * keyin position
          GOTO      QLST1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QLST0500  DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "23",CCOL                     * keyin position
.
. now get keyin
.
QLST1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QLST1800 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS              
          REP       "P1E2",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QLST1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QLST2000,QLST3000,QLST4000
.
QLST1500  BEEP 
          GOTO      QLST1000
.
QLST1800  MOVE      DIM2,FORM2 
          COMPARE   ZERO,FORM2
          GOTO      QLST1500 IF EQUAL
.
.          IF        (FORM2>(NUMITEM-1)) | (FORM2<0)
.            GOTO      QLST1500
.          ENDIF
.
          IF        FORM2>0 & FORM2<NUMITEM
             MOVE      ZERO,EXIT
             GOTO      QLST9999
          ELSE
             GOTO      QLST1500
          ENDIF
.
. Previous was selected so check if it was asked
.
QLST2000  BRANCH    SCREEN,QLST5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QLST9999
.
QLST3000  MOVE      THREE,EXIT                      * Delete selected
          GOTO      QLST9999
.
QLST4000  MOVE      FOUR,EXIT                      * Delete selected
          GOTO      QLST9999
.
. Previous was selected but wasn't asked so error
.
QLST5200  BEEP
          GOTO      QLST1000
.
QLST9999  RETURN
.
.
          INC       IBAOVRIO/INC
          INC       PATDO1IO/INC
.
ENDDENQY  ENDPROC
