. *----------------------------------------------------------------------
. * Include Name  :   BEDBDPRO.PBL
. *
. * Function      :   Common procedure to perform bed boad and expected    
. *               :   patient udpates                                       
. *----------------------------------------------------------------------
.  Modifications Done :
.------------------------------------------------------------------------
.
.  V12.00.01  15/05/2025 Nikitha B       TSK 0955096                      
.             Alphanumeric changes                                      
.  V11.04.01  10/10/2023  Ebon Clements  TSK 0925492
.             If multiple theatre bookings exist for an admission. Use the
.             first booking from today for the procedure date
.             BBUP1200 - BKREADAT
.  V11.01.03  13/09/2021  Ebon Clements  TSK 0910279
.             Removed V11.01.02 and V11.01.01 changes are restored original
.             functionality for post op ward/bed
.  V11.01.02  22/07/2021  Ebon Clements          TSK 0909330
.             Don't write post op ward expected patient record for current
.             admissions if they are admitted to the post op ward - BBUP1700
.  V11.01.01  07/07/2021  Thanh T.               TSK 0905755
.             Changed BBUP1500 to not clear PMVXPOWD and PMVXPOBD on
.             current admission
.------------------------------------------------------------------------
.  V10.05.01  21/10/2014  Ebon Clements          CAR 299130
.             Clear bed board and expected bed board for deleted bookings
.  V10.01.01  21/12/2010  Ebon Clements          CAR 215701
.             Added H/F stepdown update to current admissions
.  V9.12.02   03/12/2009  Ebon Clements          CAR 204362
.             Clear the post op ward/bed for a current admission if they are
.             admitted to the same ward as the post op ward - CLRP0000
.  V9.12.01   24/06/2009  Ebon Clements          CAR 199232
.             Fixed include name
.  V9.11.02   18/12/2008  Ebon Clements          CAR 174080
.             Fixed save of bed requests                                
.  V9.11.01   16/12/2008  Ebon Clements          CAR 174080
.             Write post op expected bed record if no theatre booking
.  V9.11.00   26/11/2008  Ebon Clements          CAR 174057
.             Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
. Process the bed board expected patient file updates 
. Requires : URNUMBER 
.            ADMISSNO
.------------------------------------------------------------------------
          DEFPROC   BBUP0000
.
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       OPRDEAFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PMSBBDFD/INC
          INC       PMSBRQFD/INC
          INC       PMSEPBFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
ADMIFLAG  FORM      1
BEDRWARD  INIT      "~~B"
FROMDATE  DIM       8
THETFLAG  FORM      1
TODATE    DIM       8
TODAYDAT  DIM       8
THETCNTR  FORM      2
FRSTDATE  DIM       8
.
BBUP1000  OPEN      OPRDETA2,"oprdetaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSBBDA1,"pmsbbdaf"
          OPEN      PMSBBDA2,"pmsbbdaf"
          OPEN      PMSBBDA3,"pmsbbdaf"
          OPEN      PMSEPBA1,"pmsepbaf"
          OPEN      PMSEPBA2,"pmsepbaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      PATMI1A1,"patmi1af"
.
          READ      CONTROLF,HUND12;*105,PTCNMORG
.
          MOVE      ZERO,ADMIFLAG
          MOVE      ZERO,THETFLAG
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,BBUP9000
.
          CALL      RDPMPX21
          BRANCH    OVRCD,BBUP9000
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            CALL      CLPATMIS
          ELSE
            MOVE      ONE,ADMIFLAG
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=1
            CALL       CLPMSVX1
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOKRC1
          ELSE
            MOVE      ONE,THETFLAG
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF 
.
. Check for multiple theatre bookings for this admission from today
.
          MOVE      ZERO,THETCNTR         * Number of theatre bookings
          MOVE      SP70,FRSTDATE         * First theatre booking date 
          CALL      IBACLOCK
          PACK      TODAYDAT,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",TODAYDAT
.
          PACK      KEY31,ADMISSNO,ASTAT,TODAYDAT,SP70
          CALL      RSOPDEA2
BBUP1200  CALL      RKOPDEA2              * Check theatre bookings for
          BRANCH    OVRCD,BBUP1300        * admission status
.
          MATCH     OPDAADMN,ADMISSNO     * Same admission number
          GOTO      BBUP1300 IF NOT EQUAL
.  
          COMPARE   OPDASTAT,ASTAT        * Same status
          GOTO      BBUP1300 IF NOT EQUAL
.
          ADD       ONE,THETCNTR          * Count theatre bookings
          IF        THETCNTR = 1
            MOVE      OPDADATE,FRSTDATE   * First theatre booking date
          ENDIF
          GOTO      BBUP1200
.
BBUP1300  IF        THETCNTR > 1
            MATCH     SP70,FRSTDATE
            IF        !@EQUAL
              MOVE      FRSTDATE,BKREADAT * First theatre booking date
            ENDIF
          ENDIF
.
. Pre Admission
.
          COMPARE   ONE,ASTAT
          GOTO      BBUP1500 IF NOT EQUAL
.
          CALL      BBCL0000              * Bed Board Clear
          CALL      EXCL0000              * Clear adm from expected patients
          CALL      EXAD0000              * Add adm exp ward/bed
          CALL      EXAP0000              * Bed Board Pre Adm Post Op
          GOTO      BBUP1900
.
. Current Admission
.
BBUP1500  COMPARE   TWO,ASTAT
          GOTO      BBUP2500 IF NOT EQUAL
.
          CALL      BBCL0000              * Bed Board Clear
          CALL      EXCL0000              * Clear adm from expected patients
          MATCH     SP70,AWARD
          IF        !@EQUAL
            CALL      CLRP0000             * Check if admitted to post op ward
            CALL      BBAD0000             * Write bed board patient
            GOTO      BBUP1700
          ENDIF
.
. Standy Patient
.
          PACK      KEY14,AADMNO,SP70
          CALL      RDSWARD4
          CALL      RDKWARD4                     * Get standby ward/bed
          BRANCH    OVRCD,BBUP5000
.
          MATCH     AADMNO,WSTBY
          GOTO      BBUP5000 IF NOT EQUAL
.
          BRANCH    WACTIVE,BBUP5000
.
          MOVE      WWARD,AWARD
          MOVE      WWARD,PTMIXWRD
          MOVE      WBED,ABED
          MOVE      WBED,PTMIEBED
.
          CALL      CLRP0000             * Check if admitted to post op ward
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
BBUP1600  CALL      RDPTRAN2
          BRANCH    OVRCD,BBUP1650
.
          MATCH     TADMN,AADMNO
          GOTO      BBUP1650 IF NOT EQUAL
.
          MATCH     ANSC,TMOVE
          GOTO      BBUP1600 IF NOT EQUAL
.
          MATCH     TWARD,PTMIXWRD
          GOTO      BBUP1600 IF NOT EQUAL
.
          MATCH     TBED,PTMIEBED
          GOTO      BBUP1600 IF NOT EQUAL
.
          MOVE      TDATE,ADATE
          MOVE      TTIME,ATIME
.
BBUP1650  CALL      EXAD0000                     * Add expected patient
.
BBUP1700  CALL      EXAP0000            * Write post op exp patient 
.
BBUP1900  PACK      KEY24,AADMNO,ADATE,ATIME,SP70
          CALL      RSPMBRQ1
BBUP2000  CALL      RKPMBRQ1
          BRANCH    OVRCD,BBUP2500
.
          MATCH     DAADMNO,PMBRVISN
          GOTO      BBUP2500 IF NOT EQUAL
.
          MATCH     "1",PMBRRSTA
          IF        @EQUAL
            CALL      EXBR0000                   * Add Bed Request
            GOTO      BBUP2000
          ENDIF
.
          MATCH     "2",PMBRRSTA
          IF        @EQUAL
            CALL      EXAL0000            * Allocate ward to expected patients
          ENDIF
.
          GOTO      BBUP2000
.
. Discharged
.
BBUP2500  COMPARE   THREE,ASTAT
          GOTO      BBUP3000 IF NOT EQUAL
.
          MATCH     "1",PTCNMORG          * Using morgue management
          GOTO      BBUP2550 IF NOT EQUAL
.
          IF        PCEASE=1
            CALL      EXCL0000            * Clear adm from expected patients
            GOTO      BBUP5000
          ENDIF
.
BBUP2550  CALL      BBCL0000              * Bed Board Clear
          CALL      EXCL0000              * Clear adm from expected patients
.         
          GOTO      BBUP5000
.
. On Leave
.
BBUP3000  COMPARE   FOUR,ASTAT
          GOTO      BBUP3500 IF NOT EQUAL
.
          CALL      BBCL0000              * Bed Board Clear
          CALL      EXCL0000              * Clear adm from expected patients
.
          GOTO      BBUP5000
.
. Cancelled Admission
.
BBUP3500  COMPARE   FIVE,ASTAT
          GOTO      BBUP5000 IF NOT EQUAL
.
          CALL      EXCL0000              * Clear adm from expected patients
          CALL      BBCL0000              * Bed Board Clear
          GOTO      BBUP5000
.
. Theatre Booking
.
BBUP5000  IF        BKRESTAT=0 & THETFLAG=1
            CALL      EXCL0000              * Clear adm from expected patients
            CALL      EXBK0000              * Bed Board Booking Exp
            CALL      EXBP0000              * Bed Board Booking Post Op
          ENDIF
.
. Cancelled Theatre Booking
.
          IF        BKRESTAT=2 & THETFLAG=1 & ADMIFLAG=0
            CALL      EXCL0000              * Clear adm from expected patients
          ENDIF
.
. Booking record deleted due to merg with another IP visit in theatre
.
          IF        BKRESTAT=0 & THETFLAG=0 & ASTAT=0 & ADMIFLAG=0
            CALL      BBCL0000              * Bed Board Clear
            CALL      EXCL0000              * Clear adm from expected patients
          ENDIF
.
BBUP9000  CLOSE     PATMA1A1
          CLOSE     PATMX1A1
          CLOSE     PATTRAN2
          CLOSE     PMSBRQA1
          CLOSE     PMSPX2A1
          CLOSE     PMSVX1A1
          CLOSE     PMSBBDA1
          CLOSE     PMSBBDA2
          CLOSE     PMSBBDA3
          CLOSE     PMSEPBA1
          CLOSE     PMSEPBA2
          CLOSE     BOKRC1A1
          CLOSE     BOKRX1A1
          CLOSE     PATMI1A1
          GOTO      BBUP9999
.
          INC       CLBOKRC1
          INC       CLBOKRX1
          INC       BEDBDVCD
          INC       CLPMSVX1
          INC       CLPATMIS
          INC       NHOSPDAY
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       IBAOVRIO/INC
          INC       OPRDEAIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PMSBBDIO/INC
          INC       PMSBRQIO/INC
          INC       PMSEPBIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
.
BBUP9999  ENDPROC
