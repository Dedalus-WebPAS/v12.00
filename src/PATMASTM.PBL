.----------------------------------------------------------------------
. Modifications
.----------------------------------------------------------------------
. V12.00.02  20/06/2025  Ebon Clements   TSK 0961623
.            Remove invalid admission number "" - MASF8100
. V12.00.01  21/05/2025  Ebon Clements   TSK 0955096
.            Alphanueric visit number changes
. V11.04.02  06/03/2024  Don Nguyen      TSK 0943584
.            Modified tag output for Identifying Gender & Identifying Pronoun
.            to use the Free Text value when Indicator 1=O; MASF8455 & MASF8456
. V11.04.01  06.02.2024  DA Sarkies      TSK 0935447
.            Added a procedure to determine the visit type and append it to 
.            a variable to send it to the front end
. V11.03.02  19/04/2023  Don Nguyen      TSK 0931555
.            Appended the description text for Identifying Gender (Cat Gi) &
.            Identifying Pronoun (Cat Gp) to alert string (PatientALT/MASF8400);
.            The appended values will be pipe-delimited.
. V11.03.01  06/03/2023  Jacob Jackson   TSK 0918711
.            Add conditional logic that will not write PADDR1, PADDR2, 
.            PSSUBRB, PADDR4 & PPOST if PMPXSN18 equals 2 and PTELEP will
.            not be written if PMPXSN18 equals 3. 
. V11.01.03  07/03/2022  Alina Bhari     TSK 0900613
.            Checked for category VA , Indicator 3 value for Alert 37  
.            to display the Aboriginality Icon in Patient banner - MASF8453  
. V11.01.02  22/12/2021  Davin           TSK 0882972
.            Changed to use PMPXSN18 for Secure Address
. V11.01.01  18/11/2021  Jacob Jackson   TSK 0882972
.            Added conditional logic to PADDR1, PADDR2, PSSUBRB,
.            PADDR4, PPOST & PTELEP WRITEs. Will not write if
.            PMPXSN20 (secure address) has been selected (equals 1).
.----------------------------------------------------------------------
. V11.00.07  16/11/2020  Thanh T         TSK 0878747
.            Changed MASF8452 to rename the comment for Multiple birth   
. V11.00.06  08/10/2020  Ebon Clements   TSK 0898282
.            Restore logged in users WEBSECAF record after U/R comments     
.            display as the logged in hospital could be incorrect - URCMNT00
. V11.00.05  28/04/2020  Jill Parkinson              
.            Added outputs to alert string -
.            pmpxsn16(My HR)and pmpxsn20-25 free for use in future
. V11.00.04  23/04/2020  Ebon Clements  Task 0850105
.            Added display of theatre status in the patient header
.            MASF6600 - PROC  THETSTHD
. V11.00.03  15/04/2020  Ebon Clements  Task 0890104
.            Added preferred name display to patient header tag - MASF0200
. V11.00.02  02/04/2020  Jill Parkinson Task 0890090
.            Reverted CHKDIS00 back to previous functionality before 10.03.03
. V11.00.01  05/30/2020  Jill Parkinson Task 0879613
.            Changed sex to output TDESC of Category G    
. V10.14.02  19/02/2019  Ebon Clements  TSK 0867703
.            Changed NHI ethnicity file reads to use KEY5
. V10.14.01  11/02/2019  Ebon Clements  TSK 0828972
.            Added PMPXSN17 Scanned Medical Record to alert string - MASF8400
. V10.13.01  24/07/2018  Peter Vela     TSK 0299877
.            Added CHKPMHSR - Active or Waiting Mental Health Referral 
.            to Alert Icons
. V10.12.03  19/06/2018  Nicole Torrisi TSK 0857684
.            Changed URCMNT30 to display Occupational Group Desc instead
.            of PMUHOCCG in comments
. V10.12.02  05/04/2018  Jill Parkinson CAR 0854140
.            Added check for overweight in CHKBMIIN
. V10.12.01  14/03/2018  Ebon Clements  TSK 0851816
.            Increased patient alert string (PatientALT) from 26 to 40
.            characters - MASF8400
.----------------------------------------------------------------------
. V10.11.03  23/11/2017  Thanh T.       TSK 0821710
.            Audit Amission/outpatient/UR comments
. V10.11.02  12/10/2017  Davin          TSK 0837025
.            Check inactive/end date of disability alerts (DSAL0500)
. V10.11.01  19/07/2017  Thanh T.       TSK 0821710
.            Audit Amission/outpatient visit comments
.----------------------------------------------------------------------
. V10.10.05  23/06/2017  Jill Parkinson TSK 0840931 
.            Removed change in 10.10.04                     
. V10.10.04  06/06/2017  Nicole Torrisi TSK 0837025 
.            Fixed DISPALRT - added check of Alert Code
. V10.10.03  02/05/2017  Nicole Torrisi TSK 0837025 
.            Added check on PTALDTIN in DISPALRT
. V10.10.02  18/04/2017  Nicole Torrisi TSK 0837025 
.            Added check on PTALEDAT in DISPALRT
. V10.10.01  21/03/2017  B.G.Ackland    TSK 0835482 
.            FHIR Enable Fields for inclusion in FHIR response templates
. V10.08.02  08/07/2016  Jill Parkinson TSK 0822002
.            Added match of 001 to attribute type in CHKBMIIN
. V10.08.01  15/06/2016  Ania P         CAR 0820893
.            Added RELNRST check in PROC CHRESULT
. V10.06.10  01/09/2015  Jill Parkinson CAR 320331
.            Added check for micro, risk and medical alerts in DISALR00
. V10.06.09  14/08/2015  Peter Vela     CAR 319948
.            Added end date validation to GETSHD00
. V10.06.08  30/07/2015  Jill Parkinson CAR 313323
.            Added NHIM2500 for Home DHB Code
. V10.06.07  11/06/2015  Jill Parkinson CAR 317894
.            Mods to output a 3 from CHKBMIIN if a normal bmi exists
. V10.06.01  12/03/2015  J.Tan          CAR 291492
.            Mods to display Leave type description
. V10.05.04  06/01/2015  J.Tan          CAR 305441
.            Added PCEHR information alert
. V10.05.03  20/10/2014  Ebon Clements  CAR 268970
.            Change to use OUTCLIFD index 1 instead of index 2 - OUTDET00
. V10.05.02  21/08/2014  Don Nguyen        CAR 303878
.            Added NHIM2400 - District Health Board (associated with Domicile)
. V10.05.01  29/07/2014  Peter Vela        CAR 301176
.            Added calculated bmi to CHKBMIIN
. V10.04.05  03/04/2014  Peter Vela        CAR 286158
.            Changed KEY32 to KEY35 in CHKBMIIN
. V10.04.04  23/01/2014  Peter Vela        CAR 286158
.            Changed comparison of BMI calculation to 1 decimal point
.            CHKBMIIN
. V10.04.03  22/01/2014  Don Nguyen        CAR 295972
.            Modified CHPAD000 - Changed KEY16 to KEY24
. V10.04.02  13/01/2014  Peter Vela        CAR 286158
.            Changed validation for overweight to obese in CHKBMIIN
. V10.04.01  20/12/2013  Peter Vela        CAR 286158
.            Added BMI to Alert string
. V10.03.06  19/03/2013  Jill Parkinson    CAR 282814
.            Copied Jay's change from 10.00.04-added Alert Count to Alert string
. V10.03.05  21/11/2012  Don Nguyen        CAR 276754
.            Added IO trap around WRWBLUR1
. V10.03.04  27/01/2012  Ebon Clements     CAR 259160
.            Set name fields back to full length after strip - PATNAM00
. V10.03.03  06/01/2012  Peter Vela        CAR 256563
.            Changed CHKDISCD to check for Disaster Links
. V10.03.02  22/11/2011  Jill Parkinson    CAR 249362
.            Changed alert reads from KEY13 to KEY16
. V10.03.01  10/11/2011  Ebon Clements     CAR 248529
.            Added multiple contacts of the same type functionality
. V10.02.08  30/09/2011  Saroeun Soeur     CAR 208854
.            added Disability Alert Icon to alert String MASF8400
. V10.02.07  13/09/2011  Jill Parkinson    CAR 248562 
.            Fixed GLOCAT31 to have if F1=2 instead of F2=2
. V10.02.06  02/08/2011  Jill Parkinson    CAR 242416 
.            Added ptcnnewc to CHCON000
. V10.02.05  12/07/2011  Peter McMullen    CAR 240725
.            Make ward/bed code/desc in GLOCAT00 sensitive to PTCNWBDS option
. V10.02.04  22/06/2011  Steve Armstrong   CAR 240722
.            Mods to cater for changes to PATGSRFD:
.                 - GSRSNAM & GSRGNAM extended to 30 chars.
.                 - PTGSGNM2 added.
.                 - all indexes changed in length.
. V10.02.03  04/07/2011  Ebon Clements  CAR 240724
.            Mods for MRT hospital/location - Added hospital to location key
. V10.02.02  07/06/2011  Saroeun Soeur  CAR 243557
.            strip first name and surname - PATNAM00
. V10.02.01  19/05/2011  Peter Vela     CAR 239579
.            Added CHKDISCD - Check if patient has disaster code
.----------------------------------------------------------------------
. V10.01.03  13/01/2011  Jill Parkinson CAR 236279      
.            Added output of PTMASNAM
. V10.01.02  15/12/2010  Jill Parkinson CAR 231658      
.            Removed check for indicator 1=I in CHKLEG00
. V10.01.01  17/11/2010  Mike Laming    CAR 233229      
.            Remove PTCNOUTD (no longer used), added comments to all Alert 
.            APPEND statements
.----------------------------------------------------------------------
. V10.00.02  19/08/2010  Mike Laming    CAR 195158 Bugs
.            Rule change - Only display Icon/s if an Active MH Detail exists
. V10.00.01  17/06/2010  Mike Laming    CAR 221911
.            Mods to CHKLEGST for NZ MH where Cat "LS" has indicators "I" of "N"
.            "Informal" - if last session was "Informal" do not show MH Icon
.----------------------------------------------------------------------
. V9.12.03   07/12/2009  Saroeun Soeur  CAR 209737
.            GLOCAT31 and GLOCAT32 - added post ward / bed
.            PMVXPOWD
. V9.12.02   04/12/2009  Mike Laming    CAR 195158
.            Mods to CHKLEGST for NZ Mental Health Legal Status Icon
. V9.12.01   01/10/2009  Ebon Clements  CAR 205398
.            Added PMPXSIN6 to the alert string MASF8400
.----------------------------------------------------------------------
. V9.11.01   08/10/2008  Ebon Clements  CAR 174075
.            Display pre adm ward/bed in patient header when pre admitted
.----------------------------------------------------------------------
. V9.10.02   11/06/2008 Mike Laming        CAR 168794
.            Add NZ Domicile Description at NHIM2300, added PROC NHIDOMDS
.----------------------------------------------------------------------
. 03/06/2008 J.Tan              CAR 166690
.            Mods to Legal status icon for MH
. 05/12/2007 Jill Habasque      CAR 147606
.            Added extra 10 ur and admissions for webluraf
. 12/11/2007 Peter Vela         CAR 151199
.            Added new Emergency Cancellation status 
. 21/02/2007 Mike Laming        CAR 119436
.            Change Alerts routine to use Outstanding Debt Ind. in PMSPX2AF
. 13/09/2006 Jill Habasque      CAR 109852
.            Added output of claim code and Health fund in OUTDET00
. 10/08/2006 Ebon Clements      CAR 114565
.            Fixed tag MASF2600 for PLDDATE
. 06/07/2006 J.Tan              CAR 111685
.            Fixed calculating outstanding debts for alert
. 16/06/2006 Ramesh V.K.        CAR 101964
.            Added PROC TITLEDSC to get the title description.
.            Added PROC RELATDSC to get the relation description.
. 09/06/2006 Manjunath.N       CAR 104812
.            Removed the Carriage return for the Ethnicity field
. 30/03/2006 J.Tan             CAR 90040
.            Initialise overcond (OVRCD) before TRAP
. 20/02/2006 Ebon Clements     CAR 76341
.            Added referral and encounter file audit 
. 16/02/2006 Mike Laming   CAR 92179
.            Add test for Altenate Legacy system at MASF8400
. 16/02/2006 J.Tan         CAR 90040
.            Added PROC CHKLEGST to process Legal status 
. 09/12/2005 Peter Vela    CAR 89839
.            Added CCENT to tag for PFNDCG
. 19/12/2005 Davin         CAR 76341
.            Added PROC CHKSACSR to process 'active SACS referral' alert
. 26/10/2005 Jill Habasque CAR 62834
.            Added output of PTMXDEAF in MASF8400
. 16/09/2005 Jill Habasque CAR 75952
.            Added PROC CHKSUSPT
. 23/02/2005 Jill Habasque CAR 56347
.            Added PROC GETSHD00 for shared care patients
. 27/01/2004 Davin         CAR 56361
.            Added PROC CHKRELTO to process 'release information' alert
. 25/01/2005 J.Tan         CAR 56192
.            Mods to include credit notes for outstanding debt amount
. 19/10/2004 Ebon Clements CAR 54412
.            Added PROC CHKINTRP to process interpreter required
. 12/07/2004 Jill Habasque CAR 51060
.            Changed output of PNNDTE
. 29.06.2004 Peter Vela    CAR 51164
.            Changed display of sex description to DSEXDE13-13 char field
. V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      
.           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"
. 11.06.2004 Jill Habasque  CAR 44641
.            Added CHKALTID - Alternate ID
. 04.06.2004 Sylvek Litewka CAR 49065
.            Added procedure UPDLSTUR (Update Last 10 UR's) to keep track of
.            last 10 UR numbers accessed by a given Web User ID.
. 12.02.2004 Peter Vela     CAR 38290
.            Added functionality for result lists. reslnkaf has now been split
.            into yearly files (e.g. reln1999, reln2000, reln2001, etc...)
. 15.10.2003 Jill Habasque  CAR 44202
.            Added NHIMASFD to CHKDONOR
. 10.07.2003 Sylvek Litewka CAR 39213
.            Added procedures CHKDONOR to output the "Organ Donor" indicator 
.            and CHKRESUS to output the "Not for Resuscitation" indicator.
.            Also extended the alert string from KEY15 to KEY25.
. 08.01.2003 Peter Vela     SRF 23145
.            Added menutype for Current Event Patient
. 28.11.2002 Jill Habasque  SRF 33944
.            Fixed check for unread results
. 11.11.2002 Jill Habasque  SRF 22684
.            Added CHRESULT procedure to output unread result indicator 
. 23.10.2002 Jill Habasque  SRF 23373
.            Changed to compare Y to nhmares for Yes instead of 1       
. 03.12.2001 Katie/Harvinder
.            Opened the files nhimasaf and nhiethaf and included routine
.            NHIM0000 and removed PATNID routine
. 12.11.2001 Greg Horvat                         SRF 10469
.            Mods to default EMR location description to "Other Department"
. 05.10.2001 Jill Habasque
.            Added output of Yes or No for PTMAUKDD (Unknown date of death)    
. 04.10.2001 Jill Habasque
.            Commented out replace of ' with spaces for name and address fields
. 13.09.2001 Jill Habasque
.            Changed CHCON000 to check PKNAME
. 08.09.2001 Jill Habasque
.            Changed CHALI000 to use key70
. 03.09.2001 Jill Habasque
.            Mods spaces to unit
. 17.08.2001 J.Tan
.            Changed privacy indicator to use master extension instead of
.            visit table.
. 16.08.2001 J.Tan
.            Fixed setting up site to open outpatient files
. 13.08.2001 J.Tan
.            Added Unit and Privacy Indicator to Patient Header Details
. 14.06.2001 B.G.Ackland
.            Reorganised Patient Header Details
.            Added Sub Calls to Extension Files 085 & 086
. 27.04.2001 Ebon Clements                                                     
.            Added output of maximum age and flag for invalid age 
. 03.04.2001 Ebon Clements
.            Fixed calculation of age when deceased and unknown date of death
. 19.03.2001 Ebon Clements
.            Fixed PCHGDTE, goto MASF9999 if blank
. 11.01.2000 Bronko Sosic
.            Tags for Medical Records MRTCHK00 
.            Added MASF9000 -1- Home Location
.                           -2- Document Type
.                           -3- Volume
.                           -4- Current Location      
. 18.01.1999 B.G.Ackland
.            Change Patient Name Format to show Surname in Upper Case
.
.------------------------------------------------------------
. Open Files Required for MASF0000
.------------------------------------------------------------
INTMAS00  OPEN      PATALRT1,"patalrtf"
          OPEN      PATALTT1,"pataltaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      PMSUCHA1,"pmsuchaf"
          OPEN      PMSUCDA1,"pmsucdaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATPX1A1,"patpx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          COMPARE   ONE,SYSTNHI
          GOTO      INTMAS99 IF NOT EQUAL
.
          OPEN      NHIMASA2,"nhimasaf"
          OPEN      NHIETHA1,"nhiethaf"
.
INTMAS99  RETURN
.------------------------------------------------------------
. Add Patient Master Field
.------------------------------------------------------------
MASF0000  BRANCH    FLDITMNO,MASF0100,MASF0200,MASF0300,MASF0400,MASF0500:
                             MASF0600,MASF0700,MASF0800,MASF0900,MASF1000:
                             MASF1100,MASF1200,MASF1300,MASF1400,MASF1500:
                             MASF1600,MASF1700,MASF1800,MASF1900,MASF2000:
                             MASF2100,MASF2200,MASF2300,MASF2400,MASF2500:
                             MASF2600,MASF2700,MASF2800,MASF2900,MASF3000:
                             MASF3100,MASF3200,MASF3300,MASF3400,MASF3500:
                             MASF3600,MASF3700,MASF3800,MASF3900,MASF4000:
                             MASF4100,MASF4200,MASF4300,MASF4400,MASF4500:
                             MASF4600,MASF4700,MASF4800,MASF4900,MASF5000:
                             MASF5100,MASF5200,MASF5300,MASF5400,MASF5500:
                             MASF5600,MASF5700,MASF5800,MASF5900,MASF6000:
                             MASF6100,MASF6200,MASF6300,MASF6400,MASF6500:
                             MASF6600,MASF6700,MASF6800,MASF6900,MASF7000:
                             MASF7100,MASF7200,MASF7300,MASF7400,MASF7500:
                             MASF7600,MASF7700,MASF7800,MASF7900,MASF8000:
                             MASF8100,MASF8200,MASF8300,MASF8400,MASF8500:
                             MASF8600,MASF8700,MASF8800,MASF8900,MASF9000:
                             MASF9100,MASF9200,MASF9300,MASF9400,MASF9500:
                             MASF9600,MASF9700,MASF9800,MASF9900
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      MASF9999
.
MASF0100  REP       " +",URNUMBER
          REP       " +",ADMISSNO
          UNPACK    DIM127,D1,KEY8,DIM127
          UNPACK    DIM127,D1,KEY2,DIM127
          UNPACK    DIM127,D1,FLDPARA,DIM127
          WRITE     HTMLFILE;KEY8,".pbl?reportno=",KEY2:
                                 "&template=",FLDPARA:
                                 "&urnumber=",URNUMBER:
                                 "&admissno=",ADMISSNO;
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          GOTO      MASF9999
.
MASF0200  CALL      PATNAM00
          WRITE     HTMLFILE;PATFNAME;
.
          CALL      DSPPNM00     
          GOTO      MASF9999
.
MASF0300  WRITE     HTMLFILE;PURNO;
          GOTO      MASF9999
MASF0400  MATCH     SP70,PCHGDTE
          GOTO      MASF9999 IF EQUAL
          UNPACK    PCHGDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      MASF9999
.
MASF0500  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MASF0501
          MATCH     SP70,PTITL
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTITL;
          GOTO      MASF9999
.
MASF0501  PROC      TITLEDSC                  * .1
          GOTO      MASF9999
.
MASF0600  MOVE      NO,YESNO
          IF        PHOSP=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
MASF0700  WRITE     HTMLFILE;PLDAYS;
          GOTO      MASF9999
MASF0800  MOVE      NO,YESNO
          IF        PBDEBT=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF0900  REP       "#" ",PTMASNAM
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            WRITE     HTMLFILE;PTMASNAM;
            GOTO      MASF9999
          ENDIF
.
          REP       "#" ",PSNAME
          MATCH     SP70,PSNAME
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PSNAME;
          GOTO      MASF9999
MASF1000  
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MATCH     SP70,PGNAME
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PGNAME;
          GOTO      MASF9999
MASF1100  
...       REP       "' ",PPSNAME
          REP       "#" ",PPSNAME
          MATCH     SP70,PPSNAME
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PPSNAME;
          GOTO      MASF9999
MASF1200  MATCH     SP70,PADD1
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "2",PMPXSN18
            IF        !@EQUAL
              REP       "#" ",PADD1
              WRITE     HTMLFILE;PADD1;
            ENDIF
          ENDIF
          GOTO      MASF9999
MASF1300  MATCH     SP70,PADD2
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "2",PMPXSN18
            IF        !@EQUAL
              REP       "#" ",PADD2
              WRITE     HTMLFILE;PADD2;
            ENDIF
          ENDIF
          GOTO      MASF9999
MASF1400  MATCH     SP70,PSUBRB
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "2",PMPXSN18
            IF        !@EQUAL
              REP       "#" ",PSUBRB
              WRITE     HTMLFILE;PSUBRB;
            ENDIF
          ENDIF
          GOTO      MASF9999
MASF1500  MATCH     SP70,PADD4
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "2",PMPXSN18
            IF        !@EQUAL
              REP       "#" ",PADD4
              WRITE     HTMLFILE;PADD4;
            ENDIF
          ENDIF
          GOTO      MASF9999
MASF1600  MATCH     SP70,PPOST
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "2",PMPXSN18
            IF        !@EQUAL
              WRITE     HTMLFILE;PPOST;
            ENDIF
          ENDIF  
          GOTO      MASF9999
MASF1700  MATCH     SP70,PTELEP
          GOTO      MASF9999 IF EQUAL
          MATCH     "1",PMPXSN18
          IF        !@EQUAL
            MATCH     "3",PMPXSN18
            IF        !@EQUAL
              WRITE     HTMLFILE;PTELEP;
            ENDIF
          ENDIF
          GOTO      MASF9999
.
MASF1800  MATCH     SP70,PTELEB
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTELEB;
          GOTO      MASF9999
.
MASF1900  MATCH     SP70,PSEX
          GOTO      MASF9999 IF EQUAL
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.
MASF1950  WRITE     HTMLFILE;TDESC;
          GOTO      MASF9999
.
MASF2000  MATCH     SP70,PBDATE
          GOTO      MASF9999 IF EQUAL
          GOTO      MASF9999 IF EOS
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          MATCH     "00",CDAY
          GOTO      MASF9999 IF EQUAL
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      MASF9999
.
MASF2100  MATCH     SP70,PBDATE
          GOTO      MASF9999 IF EQUAL
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     "00",CDAY
          GOTO      MASF9999 IF EQUAL
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MATCH     SP70,PDECDTE
            IF        @EQUAL
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ELSE
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ENDIF
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      WRTAGE00
          GOTO      MASF9999
.
MASF2200  MOVE      "C ",CATEGORY
          MOVE      PCONT,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF2300  MOVE      "R ",CATEGORY
          MOVE      PREG,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF2400  MOVE      "T ",CATEGORY
          MOVE      PTYPE,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF2500  MATCH     SP70,POCCUP
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;POCCUP;
          GOTO      MASF9999
.
MASF2600  MATCH     SP70,PLDDATE
          GOTO      MASF9999 IF EQUAL
          UNPACK    PLDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      MASF9999
.
MASF2700  MATCH     SP70,PMEDI
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PMEDI;
          GOTO      MASF9999
.
MASF2800  MATCH     SP70,PPENNO
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PPENNO;
          GOTO      MASF9999
.
MASF2900  MATCH     SP70,PPNDTE
          GOTO      MASF9999 IF EQUAL
          UNPACK    PPNDTE,CCENT,CYEAR,CMON
          WRITE     HTMLFILE;CMON,"/",CCENT,CYEAR;
          GOTO      MASF9999
.
MASF3000  MATCH     SP70,PREPAT
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PREPAT;
          GOTO      MASF9999
.
MASF3100  UNPACK    DIM127,D1,KEY1,DIM127 
          MOVE      KEY1,F1
          BRANCH    F1,MASF3140
.
          COMPARE   "1",PSMOK
          GOTO      MASF3110 IF EQUAL
.
          COMPARE   "2",PSMOK
          GOTO      MASF3120 IF EQUAL
.
          MOVE      "Unk",YESNO
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3110  MOVE      "Yes",YESNO  
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3120  MOVE      "No",YESNO  
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3140  COMPARE   "1",PSMOK
          GOTO      MASF3150 IF EQUAL
.
          COMPARE   "2",PSMOK
          GOTO      MASF3160 IF EQUAL
.
          MOVE      "0",YESNO
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3150  MOVE      "1",YESNO
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3160  MOVE      "2",YESNO
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3200  MATCH     SP70,PMICRO
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PMICRO;
          GOTO      MASF9999
.
MASF3300  IF        FHIRTYPE=0
            MOVE      NO,YESNO
            IF        PCEASE=1
              MOVE      YES,YESNO
            ENDIF
            WRITE     HTMLFILE;YESNO;
          ELSE
            IF        PCEASE=1
              WRITE     HTMLFILE;"true";
            ELSE
              WRITE     HTMLFILE;"false";
            ENDIF
          ENDIF
          GOTO      MASF9999
.
MASF3400  MOVE      SP70,CODE
          PROC      PRIVCY00          * Output Privacy Indicator
          MOVE      "PV",CATEGORY
          CALL      CODITM00
          GOTO      MASF9999
.
MASF3500  MOVE      NO,YESNO
          IF        PAUTOPY=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF3600  MOVE      "H1",CATEGORY
          MOVE      PHIGH1,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF3700  MOVE      "H2",CATEGORY
          MOVE      PHIGH2,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF3800  MOVE      "H3",CATEGORY
          MOVE      PHIGH3,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF3900  MATCH     SP70,PLOCDOC
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PLOCDOC;
          GOTO      MASF9999
.
MASF4000  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MASF4010
          MATCH     SP70,PFUNDH
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PFUNDH;
          GOTO      MASF9999
.
MASF4010  PACK      KEY14,PFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            WRITE     HTMLFILE;HFNAME;
          ELSE
            WRITE     HTMLFILE;PFUNDH;
          ENDIF
          GOTO      MASF9999
.
MASF4100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MASF4110
          MATCH     SP70,PFNDTB
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PFNDTB;
          GOTO      MASF9999
.
MASF4110  PACK      KEY14,PFUNDH,PFNDTB,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            WRITE     HTMLFILE;HFNAME;      * Description of Table
          ELSE
            WRITE     HTMLFILE;PFNDTB;      * Table code
          ENDIF
          GOTO      MASF9999
.
MASF4200  MATCH     SP70,PFNDMM
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PFNDMM;
          GOTO      MASF9999
.
MASF4300  MOVE      "P1",CATEGORY
          MOVE      PUSR1,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4400  MOVE      "P2",CATEGORY
          MOVE      PUSR2,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4500  MOVE      "P3",CATEGORY
          MOVE      PUSR3,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4600  MOVE      "P4",CATEGORY
          MOVE      PUSR4,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4700  MOVE      "P5",CATEGORY
          MOVE      PUSR5,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4800  MOVE      "P6",CATEGORY
          MOVE      PUSR6,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF4900  MOVE      NO,YESNO
          IF        PUYN1=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF5000  MOVE      NO,YESNO
          IF        PUYN2=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF5100  MOVE      NO,YESNO
          IF        PUYN3=1
            MOVE      YES,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF5200  WRITE     HTMLFILE;PYRRES;
          GOTO      MASF9999
.
MASF5300  MATCH     SP70,PNKNAME
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKNAME;
          GOTO      MASF9999
MASF5400  MATCH     SP70,PNKADD1
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKADD1;
          GOTO      MASF9999
MASF5500  MATCH     SP70,PNKADD2
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKADD2;
          GOTO      MASF9999
MASF5600  MATCH     SP70,PNKSUBR
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKSUBR;
          GOTO      MASF9999
MASF5700  MATCH     SP70,PNKADD4
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKADD4;
          GOTO      MASF9999
MASF5800  MATCH     SP70,PNKPOST
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKPOST;
          GOTO      MASF9999
MASF5900  MATCH     SP70,PNKTELP
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKTELP;
          GOTO      MASF9999
MASF6000  MATCH     SP70,PNKTELB
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKTELB;
          GOTO      MASF9999
.
MASF6100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MASF6101
          MATCH     SP70,PNKRELP
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PNKRELP;
          GOTO      MASF9999
.
MASF6101  PROC      RELATDSC                 * .1
          GOTO      MASF9999
.
MASF6200  MATCH     SP70,PFNDYY
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PFNDYY;
          GOTO      MASF9999
.
MASF6300  MATCH     SP70,PFNDCG
          GOTO      MASF9999 IF EQUAL
          UNPACK    PFNDCG,CCENT,CYEAR,CMON
          WRITE     HTMLFILE;CMON,SLASH,CCENT,CYEAR;
          GOTO      MASF9999
.
MASF6400  MATCH     SP70,PDECDTE
          GOTO      MASF9999 IF EQUAL
          UNPACK    PDECDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      MASF9999
.
MASF6500  MOVE      "DC",CATEGORY
          MOVE      PDIET,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF6600  
..        WRITE     HTMLFILE;PTMAMCRN;
          CALL      UNIT0000          * Output Unit
          MOVE      "AC",CATEGORY
          CALL      CODITM00
.
          PROC      THETSTHD
.
          GOTO      MASF9999
.
MASF6700  
..        UNPACK    PTMAMCED,CYEAR,CMON,CDAY
..        MOVE      CMON,F2
..        LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
..        WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CYEAR
          GOTO      MASF9999
.
MASF6800  CALL      URCMNT00
          GOTO      MASF9999
.
MASF6900  MOVE      "M ",CATEGORY
          MOVE      PMSTAT,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
MASF7000  UNPACK    DIM127,D1,KEY3,DIM127
          MOVE      KEY3,FLDITMNO
          CALL      NHIM0000
          GOTO      MASF9999
.
MASF7100  MATCH     SP70,PHCNOTES
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PHCNOTES;
          GOTO      MASF9999
.
MASF7200  MATCH     SP70,PINITHOS
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PINITHOS;
          GOTO      MASF9999
.
MASF7300  MATCH     SP70,PHKADUNI
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PHKADUNI;
          GOTO      MASF9999
.
MASF7400  MATCH     SP70,PTMAEMPC
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTMAEMPC;
          GOTO      MASF9999
.
MASF7500  MATCH     SP70,PTMANOKO
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTMANOKO;
          GOTO      MASF9999
.
MASF7600  MATCH     SP70,PTMANOKE
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTMANOKE;
          GOTO      MASF9999
.
MASF7700  MATCH     SP70,PTMADCNO
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTMADCNO;
          GOTO      MASF9999
.
MASF7800  MATCH     SP70,PTMAUKDB
          GOTO      MASF9999 IF EQUAL
          WRITE     HTMLFILE;PTMAUKDB;
          GOTO      MASF9999
.
MASF7900  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MASF7910
          WRITE     HTMLFILE;PTMAUKDD;
          GOTO      MASF9999
.
MASF7910  MATCH     "Y",PTMAUKDD
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
          ELSE
            WRITE     HTMLFILE;"No";
          ENDIF
          GOTO      MASF9999
.
MASF8000  UNPACK    DIM127,D1,CATEGORY,DIM127
          MOVE      SP70,CODE
          CALL      CODITM00
          GOTO      MASF9999
. Visit Number
MASF8100  MATCH     SP70,ADMISSNO
          GOTO      MASF9999 IF EQUAL
.
          MATCH     "''",ADMISSNO
          GOTO      MASF9999 IF EQUAL
.
          MATCH     ZEROVISN,ADMISSNO
          GOTO      MASF9999 IF EQUAL
.
          WRITE     HTMLFILE;ADMISSNO;
          GOTO      MASF9999
.
. Consultant
MASF8200  CALL      GCONSL00     * Output Consultant Details
          GOTO      MASF9999
. Location
MASF8300  CALL      GLOCAT00     * Output Location Details
          GOTO      MASF9999
.
.                                 Alerts String & Last 10 UR's Maintenance
.                                 see Standard.js - AddAlertIcons() PatientALT
MASF8400  CLEAR     KEY40
          PROC      CHRESULT
          IF        EXIT=1
            MOVE      TWO,PTMXSIN2
          ENDIF
          APPEND    PTMXSIN1,KEY40          * Alert 1 - Present/Security 1-4
          APPEND    PTMXSIN2,KEY40          * Alert 2 - Results present
          APPEND    PTMXSIN3,KEY40          * Alert 3 - Clinical notes present
          APPEND    PTMXSIN4,KEY40          * Alert 4 - Clinical Documentation
.
          MATCH     "1",PTMXSIN5
          IF        @EQUAL
            READ      CONTROLF,HUND10;*245,PTCNLEGB  * using Alt Legacy sys?
            IF        PTCNLEGB = 1
              MOVE      TWO,PTMXSIN5
            ENDIF
          ENDIF
          APPEND    PTMXSIN5,KEY40          * Alert 5 - Legacy Visits
          PACK      KEY20,PURNO,SP70
          CALL      RSMRMAS1
          CALL      RKMRMAS1
          BRANCH    OVRCD,MASF8410
          MATCH     PURNO,MRMAURNO
          GOTO      MASF8410 IF NOT EQUAL
          APPEND    ONE,KEY40               * Alert 6 - Medical Records
          GOTO      MASF8420
MASF8410  APPEND    ZERO,KEY40              * Alert 6 - zero 
.
MASF8420  APPEND    PCEASE,KEY40            * Alert 7 - Deseased
          APPEND    PBDEBT,KEY40            * Alert 8 - Bad Debt
          PROC      CHKDEBTS                * get Outstanding Debt Ind.
          APPEND    EXIT,KEY40              * Alert 9  - Outstanding Debt
          APPEND    PUYN1,KEY40             * Alert 10 - 
          APPEND    PUYN2,KEY40             * Alert 11 -
          APPEND    PUYN3,KEY40             * Alert 12 -
          PROC      CHKINTRP
          APPEND    EXIT,KEY40              * Alert 13 - Interpreter Required
          PROC      CHKDONOR
          APPEND    EXIT,KEY40              * Alert 14 - Organ Donor
          PROC      CHKRESUS
          APPEND    EXIT,KEY40              * Alert 15 - Not for Resuscitation
          PROC      CHKALTID
          APPEND    EXIT,KEY40              * Alert 16 - Alternate ID
          PROC      CHKRELTO
          APPEND    EXIT,KEY40              * Alert 17 - Check Consent Info.
          PROC      CHKSUSPT
          APPEND    EXIT,KEY40              * Alert 18 - Patient Suspended
          MATCH     SP70,PTMXDEAF
          GOTO      MASF8425 IF EQUAL
.
          PACK      KEY5,ANSD,ANSK,PTMXDEAF,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MASF8425
.
          MATCH     ANSD,TCINDC1
          IF        @EQUAL
            APPEND    ONE,KEY40             * Alert 19 - Deaf
            GOTO      MASF8430
          ENDIF
.
MASF8425  APPEND    SP1,KEY40               * Alert 19 - zero 
.
MASF8430  PROC      CHKSACSR
          APPEND    EXIT,KEY40              * Alert 20 - Active xx Referral
.
.         EXIT-2 for Historical,  EXIT-1 for Current or Not Close
.
          PROC      CHKLEGST                * check Legal Status
          APPEND    EXIT,KEY40              * Alert 21 - Legal Status 1-2
.
          APPEND    PMPXSIN6,KEY40          * Alert 22 - Transport Required
.
          PROC      CHKDISCD                * check Disaster Code
          APPEND    EXIT,KEY40              * Alert 23 - Disaster Code found
.
          MOVE      ZERO,EXIT               * Disability alert string icon
          PROC      DISPALRT                * Alert 24
          APPEND    EXIT,KEY40
.
MASF8450  PROC      CHKBMIIN                * Body Mass Index -  Alert 25
          APPEND    EXIT,KEY40
.
          APPEND    PMPXSN10,KEY40          * Alert 26 - PCEHR Information 
.
          MATCH     SP70,PMPXUSR9           * Advance Care Plan Flag
          GOTO      MASF8451 IF EQUAL
.
          PACK      KEY5,ANSP,NINE,PMPXUSR9,SP70
          CALL      RDCODE1                 * Read Advance Care Plan Flag
          BRANCH    OVRCD,MASF8451
.
          MATCH     ANSD,TCINDC1
          IF        @EQUAL
            APPEND    TCINDC1,KEY40         * Alert 27 - Advance Care Directive
            GOTO      MASF8452              * Yes display icon
          ENDIF
.
MASF8451  APPEND    SP1,KEY40               * Alert 27 - Advance Care Directive
.
MASF8452  PROC      CHKPMHSR
          APPEND    EXIT,KEY40              * Alert 28 - Active or Waiting
.                                           * Primary Mental Health Referral
          APPEND    PMPXSN17,KEY40          * Alert 29 - Scanned Medical Record
.
          APPEND    PMPXSN16,KEY40          * Alert 30 - My HR
          APPEND    PMPXSN20,KEY40          * Alert 31 - Multiple Birth
          APPEND    PMPXSN21,KEY40          * Alert 32 - Free to use
          APPEND    PMPXSN22,KEY40          * Alert 33 - Free to use
          APPEND    PMPXSN23,KEY40          * Alert 34 - Free to use
          APPEND    PMPXSN24,KEY40          * Alert 35 - Free to use
          APPEND    PMPXSN25,KEY40          * Alert 36 - Free to use
.
MASF8453  MATCH     SP70,PMPXABRG           * Aboriginality (Cat VA) FLAG
          GOTO      MASF8454 IF EQUAL
.
          PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MASF8454
.
          APPEND    TCINDC3,KEY40           * Ind3-D is for Aboriginality Icon
          GOTO      MASF8455
.
MASF8454  APPEND    SP1,KEY40
.
MASF8455  APPEND    SP70,KEY40              * pad out KEY40
          RESET     KEY40
          REP       " 0",KEY40              * Replace space with zero
.
          WRITE     HTMLFILE;KEY40,"|";
.
          MATCH     SP70,PMPXUCC4           * Identifying Gender (Cat Gi)
          GOTO      MASF8456 IF EQUAL
.
          MOVE      "i",D1
          PACK      KEY5,ANSG,D1,PMPXUCC4,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "O",TCINDC1           * Other description
            IF        @EQUAL
              STRIP     PMPXATF1
              WRITE     HTMLFILE;*+,PMPXATF1;  * Identifying Gender Free Text
            ELSE
              STRIP     TDESC
              WRITE     HTMLFILE;*+,TDESC,*-;
            ENDIF
          ENDIF
.
MASF8456  WRITE     HTMLFILE;"|";
.
          MATCH     SP70,PMPXUCC5           * Identifying Pronoun (Cat Gp)
          GOTO      MASF8460 IF EQUAL
.
          MOVE      "p",D1
          PACK      KEY5,ANSG,D1,PMPXUCC5,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "O",TCINDC1           * Other description
            IF        @EQUAL
              STRIP     PMPXATF2
              WRITE     HTMLFILE;*+,PMPXATF2;  * Identifying Pronoun Free Text
            ELSE
              STRIP     TDESC
              WRITE     HTMLFILE;*+,TDESC,*-;
            ENDIF
          ENDIF
.
MASF8460  WRITE     HTMLFILE;"|";

          PROC      DSPVISTP                * Visit number prefix

          PROC      UPDLSTUR
.
          GOTO      MASF9999
.
. Extension File Variable required 
.
MASF8500  UNPACK    DIM127,D1,KEY3,DIM127
          MOVE      KEY3,FLDITMNO
          CALL      MSXF0000
          GOTO      MASF9999
.
. Second Extension File Variable required
.
MASF8600  UNPACK    DIM127,D1,KEY3,DIM127
          MOVE      KEY3,FLDITMNO
          CALL      PMIX0000
          GOTO      MASF9999
.
. Emergency Triage Code
MASF8700  CALL      GTRIG000     * Output Emergency Triage Code
          GOTO      MASF9999
.
. Type of Patient for Menu Option
MASF8800  CALL      MENUTY00
          WRITE     HTMLFILE;MENUTYPE;
          MOVE      "00",MENUTYPE
          GOTO      MASF9999
.
MASF8900  MOVE      YES,YESNO
          IF        PCASE=1
            MOVE      NO,YESNO
          ENDIF
          WRITE     HTMLFILE;YESNO;
          GOTO      MASF9999
.
MASF9000  PROC      MRTCHK00          * Proc for Medical Records details
          GOTO      MASF9999
.
MASF9100  CALL      CHKAGE00            * Output maximum permissible age flag
          GOTO      MASF9999                
.
MASF9200  READ      CONTROLF,TEN8;*246,CMAXAGE     * Maximum permissible age
          WRITE     HTMLFILE;CMAXAGE;
          GOTO      MASF9999                
.
.********************************************************************
.*        MODIFIED BY HPS ODC                                       *
.********************************************************************
MASF9300  PROC      CHALI000          * Output Alias indicator
          GOTO      MASF9999
.
MASF9400  PROC      CHLIN000          * Output Links indicator
          GOTO      MASF9999
.
MASF9500  PROC      CHPAD000          * Output Previous Address indicator
          GOTO      MASF9999
.                
MASF9600  PROC      CHCON000          * Output emergency contact indicato
          GOTO      MASF9999
.                  
MASF9700  WRITE     HTMLFILE;CGENRUR; * U/R Generate Man/Auto sys param
          GOTO      MASF9999
.                  
MASF9800  CALL      CHPAR000          * Output parent contact indicator
          GOTO      MASF9999
.                  
MASF9900  MOVE      "TY",CATEGORY     * Selection List of Documents
          MOVE      SP70,CODE
          CALL      CODITM00
          GOTO      MASF9999
.
.************    Modification ends here ****************************
MASF9999  RETURN
.
.-----------------------------------------------------------------------
. Retrieve the last 5 number of U/R comment details
.-----------------------------------------------------------------------
URCMNT00  MOVE      SP70,KEY3
          UNPACK    DIM127,DIM1,KEY3,DIM127
          MATCH     SP70,KEY3
          IF        @EQUAL
            MOVE      5,URCMNOFS
          ELSE
            MOVE      KEY3,URCMNOFS
          ENDIF
.
          MOVE      ZERO,F3
          PACK      KEY17,PURNO,URCMTTYP,Z70
.
          CALL      RSPMUCH1
URCMNT10  CALL      RPPMUCH1
          BRANCH    OVRCD,URCMNT90
.
          IF        F3 = URCMNOFS 
            GOTO      URCMNT90
          ENDIF
.
          MATCH     PURNO,PMUHURNO
          GOTO      URCMNT90 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      URCMNT90 IF NOT EQUAL
.
          MATCH     "1",PMUHDELT            * is comment deleted 
          GOTO      URCMNT10 IF EQUAL
.
. Retrieve comment header
.    
          MATCH     PMUHCDAT,SP70
          IF        @EQUAL
            MOVE     SP70,URCMDATE
            GOTO     URCMNT20
          ENDIF
.
          UNPACK    PMUHCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN:
                               JUL,AUG,SEP,OCT,NOV,DEC
          PACK      URCMDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
URCMNT20  PACK      KEY10,PMUHCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD = 0
            MOVE      WBSENAM,URCMUSRN
          ELSE
            MOVE      PMUHCUID,URCMUSRN
          ENDIF
          STRIP     URCMUSRN 
.
URCMNT30  PACK      KEY20,SP70
          MATCH     PMUHOCCG,SP70
          IF        !@EQUAL
            MOVE      "w0",KEY2
            PACK      KEY5,KEY2,PMUHOCCG,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              PACK      KEY20,PMUHOCCG,SP70
            ELSE
              PACK      KEY20,TDESC,SP70
            ENDIF
          ENDIF
.
          PACK      URCMLINE,URCMDATE,SP1,PMUHCTIM,SP1,URCMUSRN,SP1,KEY20
          WRITE     HTMLFILE;URCMLINE
          ADD       ONE,F3
.
          CALL      URCMTX00
.
          GOTO      URCMNT10
.
URCMNT90  PACK      KEY10,USERID,SP70       * Restore logged in user
          CALL      RDWBSE1
.
URCMNT99  RETURN
.
.---------------------------------------------------------------------
. Retrieve comment text details
.---------------------------------------------------------------------
. 
URCMTX00  PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      RSPMUCD1
URCMTX20  CALL      RKPMUCD1
          BRANCH    OVRCD,URCMTX99
.
          MATCH     PMUHURNO,PMUCURNO
          GOTO      URCMTX99 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      URCMTX99 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      URCMTX99 IF NOT EQUAL
.
          STRIP     PMUCCOMM
          MOVELPTR  PMUCCOMM,LENGTH
.
          COMPARE   LENGTH,ZERO
          IF        @EQUAL
            MOVE      ONE,LENGTH
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      PMUCCOMM,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,70
.
          GOTO      URCMTX20
.
URCMTX99  RETURN
.
.----------------------------------------------------------------------
. Determine Menu Type to be used
.----------------------------------------------------------------------
MENUTY00  PACK      MENUTYPE,MENUTYPE,SP70
          MATCH     SP70,MENUTYPE
          IF        @EQUAL
            MOVE      "00",MENUTYPE
          ENDIF
          MATCH     SP70,ADMISSNO
          GOTO      MENUTY99 IF EQUAL
.
          MATCH     ZEROVISN,ADMISSNO
          GOTO      MENUTY99 IF EQUAL
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,MENUTY80
          BRANCH    PVITYPE,MENUTY10,MENUTY20,MENUTY30
.
          COMPARE   NINE,PVITYPE                 * I SRF 23145
          GOTO      MENUTY99 IF NOT EQUAL        * Check for current 
          MATCH     " 1",PVISYST                 * event patients
          GOTO      MENUTY40 IF EQUAL            * end I SRF 23145
.
          GOTO      MENUTY99
.         
MENUTY10  MOVE      FOUR,COPTION    * Determine Menu Type from Emergency
          PROC      EMRCHK00
          GOTO      MENUTY99
.
MENUTY20  MOVE      FOUR,COPTION    * Determine Menu Type from Outpatients
          PROC      OUTDET00
          GOTO      MENUTY99
.
MENUTY30  MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MENUTY99
          PACK      MENUTYPE,PVITYPE,ASTAT
          GOTO      MENUTY99
.
.MENUTY40  MOVE      PVIBILL,KEY8            * I SRF 23145
.          CALL      RDPMEVT1                * Check for current event patient
.          BRANCH    OVRCD,MENUTY99
.
.          MOVE      "EV",CATEGORY
.          MOVE      PMEVEVNT,CODE
.          CALL      GETCOD00
.
.          MATCH     "I",TCINDC1             * inpatient event
.          IF        @EQUAL
.            PACK      MENUTYPE,ANSE,SP70    * Pack "E" for Event into MENUTYPE
.            GOTO      MENUTY99              
.          ENDIF                              
.      
MENUTY40  PACK      MENUTYPE,ANSE,SP70                         
          GOTO      MENUTY99                 * end I SRF 23145
.
MENUTY80  MOVE      ZERO,PVITYPE
          MOVE      FOUR,COPTION    * Determine Menu Type from Outpatients
          PROC      OUTDET00
          GOTO      MENUTY99
.
MENUTY99  RETURN
.------------------------------------------------------------
. Format Patient Name in Title Text
.------------------------------------------------------------
PATNAA00  CLEAR     DISPNAME
          MOVE      SP70,PATFNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            PACK      NAME35,PTMASNAM,SP70
            REP       UPPLOW,PTMASNAM
            APPEND    PTMASNAM,DISPNAME
          ELSE
            PACK      NAME35,PSNAME,SP70
            REP       UPPLOW,PSNAME
            APPEND    PSNAME,DISPNAME
          ENDIF
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN             
.------------------------------------------------------------
. Format Patient Name in Title Text
.------------------------------------------------------------
PATNAB00  CLEAR     DISPNAME
          MOVE      SP70,PATFNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            PACK      NAME35,PTMASNAM,SP70
          ELSE
            PACK      NAME35,PSNAME,SP70
          ENDIF
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
..          REP       "' ",PSNAME
          REP       "#" ",PSNAME
          REP       "#" ",PTMASNAM
..        REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          REP       UPPLOW,PTMASNAM
          APPEND    "<b>",DISPNAME
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            STRIP     PTMASNAM
            APPEND    PTMASNAM,DISPNAME
          ELSE
            STRIP     PSNAME
            APPEND    PSNAME,DISPNAME
          ENDIF
          APPEND    " </b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          STRIP     PGNAME
          PACK      NAME35,PGNAME,SP70
          STRIP     NAME35
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
.
          PACK      PTMASNAM,PTMASNAM,SP70       * Set name field back to 
          PACK      PSNAME,PSNAME,SP70           * full length
          PACK      PGNAME,PGNAME,SP70
.
          RETURN
.------------------------------------------------------------
. Consultant
.------------------------------------------------------------
GCONSL00  MATCH    SP70,ADMISSNO
          GOTO     GCONSL90 IF EQUAL
          MOVE     ADMISSNO,KEY8
          CALL     RDVISA1
          BRANCH   OVRCD,GCONSL05
          BRANCH   PVITYPE,GCONSL10,GCONSL20,GCONSL30
          GOTO     GCONSL90
.
GCONSL05  MOVE     ZERO,PVITYPE
          MOVE     ONE,COPTION
          PROC     OUTDET00
          CALL     DOCDET00
          GOTO     GCONSL99

. Emergency
GCONSL10  MOVE     ONE,COPTION
          PROC     EMRCHK00
          CALL     DOCDET00
          GOTO     GCONSL99
. Outpatient
GCONSL20  MOVE     ONE,COPTION
          PROC     OUTDET00
          CALL     DOCDET00
          GOTO     GCONSL99
. Inpatient
GCONSL30  MOVE     PVIDOCT,DOCTCODE
          PROC     GETSHD00
          BRANCH   EXIT,GCONSL99
.
          CALL     DOCDET00
          GOTO     GCONSL99
.
GCONSL90  MOVE     SP70,DOCTCODE
          CALL     DOCDET00
.
GCONSL99  RETURN
.------------------------------------------------------------
. Location 
.------------------------------------------------------------
GLOCAT00  MATCH    SP70,ADMISSNO
          GOTO     GLOCAT99 IF EQUAL
          MOVE     ADMISSNO,KEY8
          CALL     RDVISA1
          BRANCH   OVRCD,GLOCAT05
          BRANCH   PVITYPE,GLOCAT10,GLOCAT20,GLOCAT30
          GOTO     GLOCAT99
.
GLOCAT05  MOVE     ZERO,PVITYPE
          MOVE     TWO,COPTION
          PROC     OUTDET00
          GOTO     GLOCAT99
. Emergency
GLOCAT10  MOVE     TWO,COPTION
          PROC     EMRCHK00
          GOTO     GLOCAT99
. Outpatient
GLOCAT20  MOVE     TWO,COPTION
          PROC     OUTDET00
          GOTO     GLOCAT99
. Inpatient
GLOCAT30  READ     CONTROLF,HUND12;*104,PTCNWBDS
          MOVE     PTCNWBDS,F1    * F1 is display option (code, long, short)
          MOVE     PVIBILL,KEY8
          CALL     RDMISS1
          BRANCH   OVRCD,GLOCAT99
          BRANCH   ASTAT,GLOCAT31,GLOCAT32,GLOCAT33,GLOCAT34,GLOCAT35
.
GLOCAT31  WRITE    HTMLFILE;"Pre-admission ";
.
          MATCH    SP70,PTMIXWRD
          GOTO     GLOCAT99 IF EQUAL
.
          PACK     KEY20,SP70
          PACK     KEY21,SP70
          CLEAR    KEY20         * work field for ward
          CLEAR    KEY21         * work field for bed
          IF       F1 > 0 
            PACK     KEY6,PTMIXWRD,SP70
            CALL     RDWARD1
            IF       OVRCD=1
              APPEND    PTMIXWRD,KEY20
            ELSE
              IF    F1 = 2
                APPEND    PTWDSDES,KEY20
              ELSE
                APPEND    WBDESC,KEY20
              ENDIF
            ENDIF
          ELSE
            APPEND  PTMIXWRD,KEY20
          ENDIF
          RESET     KEY20
          STRIP     KEY20
.
          MATCH    SP70,PTMIEBED
          IF       !@EQUAL
            APPEND   "/",KEY21
            IF       F1 > 0 
              PACK       KEY6,PTMIXWRD,PTMIEBED
              CALL       RDWARD1
              IF        OVRCD<>1
                IF        F1 = 2
                  APPEND    PTWDSDES,KEY21
                ELSE
                  APPEND    WBDESC,KEY21
                ENDIF
              ENDIF
           ELSE
             APPEND     PTMIEBED,KEY21
           ENDIF
          ENDIF
          RESET    KEY21
          STRIP    KEY21
.
          WRITE    HTMLFILE;*+,"<font color=red>(",KEY20,KEY21,")</font>";
.
          CALL     POPWRD00     Mow check for post op ward 
.
          GOTO     GLOCAT99
.
GLOCAT32  PACK      KEY6,AWARD,SP70
          PACK      KEY3,ABED
          PACK      KEY9,SP70
          CLEAR     KEY9 
          MATCH     SP70,AWARD
          IF        @EQUAL
            PACK      KEY14,AADMNO,SP70
            CALL      RDSWARD4
            CALL      RDKWARD4
            BRANCH    OVRCD,GLOCAT99
.
            MATCH     WSTBY,AADMNO
            GOTO      GLOCAT99 IF NOT EQUAL 
.
            PACK      KEY6,WWARD,SP70
            PACK      KEY3,WBED 
            APPEND    "(Standby)",KEY9 
          ENDIF
          RESET     KEY9 

          CALL     RDWARD1
          BRANCH   OVRCD,GLOCAT99
.
          PACK     KEY20,SP70
          PACK     KEY21,SP70
          CLEAR    KEY20         * work field for ward
          CLEAR    KEY21         * work field for bed
          IF       F1 > 0 
            IF    F1 = 2
              APPEND    PTWDSDES,KEY20
            ELSE
              APPEND    WBDESC,KEY20
            ENDIF
          ELSE
            APPEND  WWARD,KEY20
          ENDIF
          RESET     KEY20
          STRIP     KEY20
.
          MATCH    SP70,KEY3 
          IF       !@EQUAL
            APPEND   "/",KEY21
            IF       F1 > 0 
              PACK       KEY6,WWARD,KEY3
              CALL       RDWARD1
              IF         OVRCD<>1
                IF         F1 = 2
                  APPEND    PTWDSDES,KEY21
                ELSE
                  APPEND    WBDESC,KEY21
                ENDIF
              ENDIF
            ELSE
              APPEND     KEY3,KEY21
            ENDIF
          ENDIF
          RESET    KEY21
          STRIP    KEY21
.
          WRITE    HTMLFILE;*+,KEY20,KEY21,KEY9;
.
          CALL     POPWRD00     Mow check for post op ward 
.
          GOTO     GLOCAT99
.
GLOCAT33  WRITE    HTMLFILE;"Discharged";
          GOTO     GLOCAT99
.
GLOCAT34  READ     CONTROLF,HUND20;*172,PTCNHLEA
          MATCH    "1",PTCNHLEA
          IF       @EQUAL
            PROC      LEAV0000
            MOVE      "TL",CATEGORY            * Output Type of Leave
            CALL      GETCOD00
            MOVE      "On Leave - ",KEY11
            PACK      KEY31,KEY11,TDESC,SP70
            WRITE     HTMLFILE;KEY31;
            GOTO      GLOCAT99
          ENDIF
          WRITE    HTMLFILE;"On Leave";
          GOTO     GLOCAT99
.
GLOCAT35  WRITE    HTMLFILE;"Cancelled";
          GOTO     GLOCAT99
.
GLOCAT99  RETURN
.
.------------------------------------------------------------
.  POPWRD00    Write Post-op ward if any 
.-----------------------------------------------------------
POPWRD00  PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,POPWRD99
          MATCH     SP70,PMVXPOWD
          GOTO      POPWRD99 IF EQUAL  
.
          READ     CONTROLF,HUND12;*104,PTCNWBDS
          MOVE     PTCNWBDS,F1    * F1 is display option (code, long, short)
          CLEAR    KEY20
          CLEAR    KEY21
.
          IF       F1 > 0   
            PACK     KEY6,PMVXPOWD
            CALL     RDWARD1
            IF       F1 = 2 
              APPEND   PTWDSDES,KEY20
            ELSE
              APPEND   WBDESC,KEY20
            ENDIF
          ELSE
            APPEND    PMVXPOWD,KEY20
          ENDIF
          RESET    KEY20 
.
          MATCH    SP70,PMVXPOBD
          IF       !@EQUAL
            APPEND   "/",KEY21
            IF       F1 > 0
              PACK     KEY6,PMVXPOWD,PMVXPOBD
              CALL     RDWARD1
              IF       F1 = 2 
                APPEND   PTWDSDES,KEY21
              ELSE
                APPEND   WBDESC,KEY21
              ENDIF
            ELSE
              APPEND   PMVXPOBD,KEY21
            ENDIF
          ENDIF
          RESET KEY21
.
          WRITE    HTMLFILE;*+,"<font color=blue> (",KEY20,KEY21,")</font>";
.
POPWRD99  RETURN
.------------------------------------------------------------
. Procedure to get Doctor Code from Outpatient Clinic ID
. COPTION - 1 Return DOCTCODE
.           2 Output Location
.           3 (N/A)
.           4 Output Menu Type
.           5 Output Unit
.           6 Output Claim code         
.           7 Output Health Fund /Table 
.------------------------------------------------------------
          DEFPROC  OUTDET00   
.
          INC      OUTPREFD/INC
          INC      OUTCLIFD/INC
          INC      OUTBOAFD/INC
          INC      OUTMA1FD/INC
          INC      OUTBB1FD/INC
.
KEYBOA    DIM      28
KEYCLI    DIM      20
TESTSITE  DIM      6
TESTCLIN  DIM      6
.
          MOVE     SP70,DOCTCODE
          COMPARE  ZERO,PVITYPE
          GOTO     OUTDET50 IF EQUAL
.
          PACK     KEY6,PVISITE,SP70
          CALL     RDSITA1
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEYCLI,PVISITE,PVIDOCT,PVIDATE,SP70
          BRANCH   COPTION,OUTDET60
.
          PACK     KEY8,OSTFILE,FILBOKA1 * Determine Location from Masterfile
          TRAP     OVERCOND IF IO
          OPEN     OUTBOKA1,KEY8         * Master
          TRAPCLR  IO
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEYBOA,PVISITE,PVIDOCT,PVIDATE,SP70
          PACK     KEY28,PVISITE,PVIDOCT,PVIDATE,SP70
          PACK     KEY20,PVISITE,PVIDOCT,PVIDATE,SP70
          CALL     RDSBOKA1
OUTDET10  CALL     RDKBOKA1
          BRANCH   OVRCD,OUTDET95
          PACK     KEYBOA,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
          MATCH    KEY20,KEYBOA
          GOTO     OUTDET95 IF NOT EQUAL
.
          MATCH    OBAOUTNO,PVIBILL         * Set up billing number
          GOTO     OUTDET10 IF NOT EQUAL
.
          GOTO     OUTDET80
. 
OUTDET50  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     OUTPREA1,"outpreaf"   * Pre Attendance File
          TRAPCLR  IO
          BRANCH   OVRCD,OUTDET95
          MOVE     ADMISSNO,KEY8
          CALL     RDPREA1               * Check for Pre-Attendance
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEY6,OPRSITE,SP70
          CALL     RDSITA1
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEYCLI,OPRSITE,OPRCLIN,OPRDATE,SP70
          PACK     KEYBOA,OPRSITE,OPRCLIN,OPRDATE,OPRSTRT,OPRSLOT
. 
          BRANCH   COPTION,OUTDET60,OUTDET70,OUTDET70,OUTDET70,OUTDET70:
                           OUTDET70,OUTDET70
.
OUTDET60  MOVE     ZERO,OVRCD
          PACK     KEY8,OSTFILE,FILCLIA1 * Determine Doctor from Clinic ID
          TRAP     OVERCOND IF IO
          OPEN     OUTCLIA1,KEY8         * Clinic Details
          TRAPCLR  IO
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEY20,KEYCLI,SP70
          CALL     RDCLIA1
          IF       OVRCD=1
            PACK     KEY14,KEYCLI,Z70  * Read for Current Effective Clinic
            CALL     RDSCLIA1
            CALL     RDPCLIA1
            BRANCH   OVRCD,OUTDET95
.
            UNPACK    KEYCLI,TESTSITE,TESTCLIN
.
            MATCH     TESTSITE,OCASITE
            GOTO      OUTDET95 IF NOT EQUAL
.
            MATCH     TESTCLIN,OCACLIN
            GOTO      OUTDET95 IF NOT EQUAL
          ENDIF
.
          MOVE     OCADOCT,DOCTCODE
          MATCH    SP70,DOCTCODE
          IF       @EQUAL
            MOVE     OCACCONS,DOCTCODE
          ENDIF
          MOVE     ZERO,EXIT
          GOTO     OUTDET99
.
OUTDET70  PACK     KEY8,OSTFILE,FILBOKA1 * Determine Location from Masterfile
          TRAP     OVERCOND IF IO
          OPEN     OUTBOKA1,KEY8         * Master
          TRAPCLR  IO
          BRANCH   OVRCD,OUTDET95
.
          MOVE     KEYBOA,KEY28
          CALL     RDBOKA1
          BRANCH   OVRCD,OUTDET95
.
OUTDET80  IF       COPTION=4
            PACK     MENUTYPE,TWO,OBASTAT * Outpatient Booking 
            MOVE     ZERO,EXIT
            GOTO     OUTDET99
          ENDIF
.
          IF       COPTION=5
            PACK     CFNAME,OSTFILE,FILBB1A1
            CALL     OPOTBB11
            MOVE     ADMISSNO,KEY8
            CALL     RDBOKB1
            IF       OVRCD=0
              MATCH    SP6,OTBBDEPT
              GOTO     OUTDET95 IF EQUAL
              MOVE     OTBBDEPT,CODE       * Unit code
              MOVE     ZERO,EXIT
            ENDIF
            GOTO     OUTDET99
          ENDIF
.
          IF       COPTION=6
            PACK     CFNAME,OSTFILE,FILBB1A1
            CALL     OPOTBB11
            MOVE     ADMISSNO,KEY8
            CALL     RDBOKB1
            IF       OVRCD=0
              MATCH    SP6,OBACOMP
              GOTO     OUTDET95 IF EQUAL
              MOVE     OBACOMP,CODE
              MOVE     ZERO,EXIT
            ENDIF
            GOTO     OUTDET99
          ENDIF
.
          IF       COPTION=7
            PACK     CFNAME,OSTFILE,FILBB1A1
            CALL     OPOTBB11
            MOVE     ADMISSNO,KEY8
            CALL     RDBOKB1
            IF       OVRCD=0
              MATCH    SP6,OTBBFUND
              GOTO     OUTDET95 IF EQUAL
              PACK     KEY10,OTBBFUND,SLASH,OTBBTBLE
              MOVE     ZERO,EXIT
            ENDIF
            GOTO     OUTDET99
          ENDIF
.
          MOVE     ZERO,OVRCD
          PACK     KEY8,OSTFILE,FILMA1A1  * Determine Location from Masterfile
          TRAP     OVERCOND IF IO
          OPEN     OUTMA1A1,KEY8          * Master
          TRAPCLR  IO
          BRANCH   OVRCD,OUTDET95
.
          PACK     KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL     RDMASA1
          BRANCH   OVRCD,OUTDET95
.
          WRITE    HTMLFILE;OTMACLOC;
          MOVE     ZERO,EXIT
          GOTO     OUTDET99
.
OUTDET95  MOVE     ONE,EXIT
          GOTO     OUTDET99
.
          INC       OUTCLIIO/INC
          INC       OUTPREIO/INC
          INC       OUTBOAIO/INC
          INC       OUTMA1IO/INC
          INC       IBAOVRIO/INC
          INC       OUTBB1IO/INC
.
OUTDET99  ENDPROC
.
.------------------------------------------------------------
. Procedure to get the shared doctor from pmsdtcaf           
.------------------------------------------------------------
          DEFPROC  GETSHD00
.
          INC      PMSDTCFD/INC
          INC      PMSVX1FD/INC
          INC      PMSHCPFD/INC
.
KEY8A     DIM       8
KEY16A    DIM       16
PIPE      INIT      "|"
.
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PMSVX1A1,"pmsvx1af"
          TRAPCLR  IO
          BRANCH   OVRCD,GETSHD95
.
          TRAP     OVERCOND IF IO
          OPEN     PMSHCPA1,"pmshcpaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GETSHD95
.
          TRAP     OVERCOND IF IO
          OPEN     PMSDTCA1,"pmsdtcaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GETSHD95
.
          PACK     KEY8,DPVIBILL,SP70
          CALL     RDPMVX11
          BRANCH   OVRCD,GETSHD95
.
          MATCH    SP70,PMVXEDC1
          GOTO     GETSHD95 IF EQUAL
.
          MATCH    SP70,PMVXEDU1
          GOTO     GETSHD95 IF EQUAL
.
          PACK     KEY24,DPVIBILL,Z70 
          CALL     RSPMDTC1
GETSHD50  CALL     RPPMDTC1
          BRANCH   OVRCD,GETSHD60
.
          MATCH    DPVIBILL,PMDTVISN
          GOTO     GETSHD60 IF NOT EQUAL
.
          MATCH    PMDTDOCT,PMVXEDC1
          GOTO     GETSHD50 IF NOT EQUAL
.
          MATCH    PMDTUNIT,PMVXEDU1
          GOTO     GETSHD50 IF NOT EQUAL
.
          MATCH    SP70,PMDTEDAT
          GOTO     GETSHD60 IF EQUAL           
.
          MOVE     SP70,KEY8A
          MOVE     PMDTETIM,KEY8A
          MATCH    SP70,KEY8A
          IF       @EQUAL
            PACK     KEY8A,ZERO,ZERO,COLON,ZERO,ZERO,COLON,ZERO,ZERO
          ENDIF
.
          PACK     KEY16,PMDTEDAT,KEY8A,SP70
.
          CALL     IBACLOCK
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          PACK     KEY16A,KEY8,CTIMEIS,SP70
.
          MATCH    KEY16A,KEY16
          GOTO     GETSHD95 IF LESS
.
GETSHD60  PACK     KEY10,PMVXEDC1
          CALL     RDPMHCP1
          BRANCH   OVRCD,GETSHD95
.
          PACK     KEY6,DOCTCODE,SP70
          CALL     RDDOCT1
          BRANCH   OVRCD,GETSHD95
.
          UNPACK   DIM127,D1,FLDITEM,DIM127
          MOVE     FLDITEM,FLDITMNO
.
          PACK     DOCFNAME,DTITL,SP1,DSNAME,PIPE,PMHCTITL,SP1,PMHCSNAM
          SQUEEZE  DTITL
          SQUEEZE  DSNAME
          SQUEEZE  PMHCTITL
          SQUEEZE  PMHCSNAM
.
          IF       FLDITMNO=2
            WRITE    HTMLFILE;*+,DTITL,*-,SP1,*+,DSNAME,PIPE,PMHCTITL:
                              *-,SP1,*+,PMHCSNAM;
          ENDIF
.
          IF       FLDITMNO=3
            WRITE    HTMLFILE;DCODE,PIPE,PMHCHCPC;
          ENDIF
.
          MOVE     ONE,EXIT
          GOTO     GETSHD99
.
GETSHD95  MOVE     ZERO,EXIT
          GOTO     GETSHD99
.
          INC       PMSDTCIO/INC
          INC       PMSVX1IO/INC
          INC       PMSHCPIO/INC
          INC       IBAOVRIO/INC
.
GETSHD99  ENDPROC
.
.------------------------------------------------------------
. Routine to get Triage Code
.------------------------------------------------------------
GTRIG000  MOVE      THREE,COPTION
          PROC      EMRCHK00
          GOTO      GTRIG999
.
GTRIG999  RETURN
.------------------------------------------------------------
. Procedure to get Emergency Details
.   COPTION = 1 Return Consultant DOCTCODE
.             2 Output Location
.             3 Output Triage Status
.             4 Output Menu Type 
.             5 Output Unit
.             6 Output Claim code         
.             7 Output Health Fund /Table 
.------------------------------------------------------------
          DEFPROC  EMRCHK00   
.
          INC      EMRVISFD/INC
          INC      EMRLOCFD/INC
.
EMRCHK10  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     EMRVISA1,"emrvisaf"
          TRAPCLR  IO
          BRANCH   OVRCD,EMRCHK90
.
          MOVE     ADMISSNO,KEY8
          CALL     RDEMVIS1
          BRANCH   OVRCD,EMRCHK90
.
          COMPARE  FOUR,EMVISTAT
          GOTO     EMRCHK90 IF EQUAL
.
          MOVE     EMVIDOCT,DOCTCODE
          IF       COPTION=4
            PACK     MENUTYPE,ONE,EMVISTAT
            GOTO     EMRCHK99
          ENDIF
          IF       COPTION=5
            MOVE     EMVIUNIT,CODE
            GOTO     EMRCHK99
          ENDIF
          IF       COPTION=6
            MOVE     EMVICOMP,CODE
            GOTO     EMRCHK99
          ENDIF
          IF       COPTION=7
            MATCH     SP70,EMVIFUND
            IF        !@EQUAL
              PACK      KEY10,EMVIFUND,SLASH,EMVITABL
            ENDIF
          ENDIF
.
          IF       COPTION=2
            IF       EMVISTAT=1
              OPEN     EMRLOCA1,"emrlocaf"
              MOVE     "Other Departments",EMLODESC
              MOVE     EMVILOCN,KEY3
              CALL     RDEMLOC1
              WRITE    HTMLFILE;"Emergency - ",EMLODESC;
            ELSE
               WRITE    HTMLFILE;"Discharged";
            ENDIF
          ENDIF
          IF       COPTION=3
            WRITE    HTMLFILE;EMVITRIG;
          ENDIF
          GOTO     EMRCHK99
.
EMRCHK90  MOVE     PVIDOCT,DOCTCODE
          IF       COPTION=2
            IF       EMVISTAT=4
              WRITE    HTMLFILE;"Emergency - Cancelled";
            ELSE
              WRITE    HTMLFILE;"Emergency";
            ENDIF
          ENDIF
          GOTO     EMRCHK99
.
          INC      EMRVISIO/INC
          INC      EMRLOCIO/INC
          INC       IBAOVRIO/INC
.
EMRCHK99  ENDPROC
.------------------------------------------------------------
. Write Patient Age in Y,M,W or D
.------------------------------------------------------------
WRTAGE00  MATCH     "y",PSAGETYP
          GOTO      WRTAGE10 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEYRS," yrs";
          GOTO      WRTAGE99
.
WRTAGE10  MATCH     "d",PSAGETYP
          GOTO      WRTAGE20 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEDAY," days";
          GOTO      WRTAGE99
.
WRTAGE20  MATCH     "m",PSAGETYP
          GOTO      WRTAGE30 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEMNT," mths";
          GOTO      WRTAGE99
.
WRTAGE30  MATCH     "w",PSAGETYP
          GOTO      WRTAGE99 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEWKS," wks";
.
WRTAGE99  RETURN
.
.------------------------------------------------------------
. Output flag for patient maximum permissible age
.------------------------------------------------------------
CHKAGE00  MATCH     SP70,PBDATE
          GOTO      CHKAGE90 IF EQUAL
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     "00",CDAY
          GOTO      CHKAGE90 IF EQUAL
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MATCH     SP70,PDECDTE
            IF        @EQUAL
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ELSE
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ENDIF
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          COMPARE   PSAGE,CMAXAGE 
          GOTO      CHKAGE95 IF LESS
          GOTO      CHKAGE90
.
CHKAGE90  WRITE     HTMLFILE;ZERO;
          GOTO      CHKAGE99
.
CHKAGE95  WRITE     HTMLFILE;ONE;
          GOTO      CHKAGE99
.
CHKAGE99  RETURN
.
.------------------------------------------------------------
. Output Unit
.------------------------------------------------------------
UNIT0000  MOVE     SP70,CODE
          MATCH    SP70,ADMISSNO
          GOTO     UNIT9500 IF EQUAL
          MOVE     ADMISSNO,KEY8
          CALL     RDVISA1
          BRANCH   OVRCD,UNIT1000
.
          BRANCH   PVITYPE,UNIT9500,UNIT2000,UNIT3000
          COMPARE  NINE,PVITYPE
          GOTO     UNIT9500 IF NOT EQUAL
.
          MATCH    " 1",PVISYST
          GOTO     UNIT4000 IF EQUAL    * Events
          MATCH    " 2",PVISYST
          GOTO     UNIT5000 IF EQUAL    * Allied Health
          GOTO     UNIT9500
.
.   *** outpatient booking ***
.
UNIT1000  MOVE     FIVE,COPTION         * Output Outpatient Clinic
          MOVE     ZERO,PVITYPE
          PROC     OUTDET00
          MOVE     ZERO,EXIT
          GOTO     UNIT9999
.
.         Outpatients
.
UNIT2000  MOVE     FIVE,COPTION         * Output Outpatient Clinic
          PROC     OUTDET00
          MOVE     ZERO,EXIT
          GOTO     UNIT9999
.
.         Inpatients
.
UNIT3000  MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,UNIT9500
.
          BRANCH    ASTAT,UNIT3100,UNIT3200,UNIT3200,UNIT3200
          GOTO      UNIT9500
.
UNIT3100  MOVE      ACLSSFT,CODE       
          MOVE      ZERO,EXIT
          GOTO      UNIT9999
.
UNIT3200  PROC      TRAN0000
          GOTO      UNIT9999
.
.         Event
.
UNIT4000  PROC      EVNT0000
          GOTO      UNIT9999
.
.         Allied Health
.
UNIT5000  PROC      ALHE0000
          GOTO      UNIT9999
.
UNIT9500  MOVE      ONE,EXIT
UNIT9999  RETURN
.
.------------------------------------------------------------
. Procedure to get Medical Records Details
.      KEY1 = 1 Output Home Location
.             2 Output Document Type
.             3 Output Volume
.             4 Output Current Location                     
.------------------------------------------------------------
          DEFPROC  MRTCHK00                                 
.                                                           
          INC      MRTVISFD/INC                             
          INC      MRTMASFD/INC                             
          INC      MRTLOCFD/INC
.                                                           
MRTCHK10  OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      MRTVISA1,"mrtvisaf"
          OPEN      MRTLOCA1,"mrtlocaf"
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,MRTCHK20,MRTCHK30,MRTCHK40,MRTCHK50
.
MRTCHK20  MATCH     SP70,ADMISSNO        * Home Location
          GOTO      MRTCHK25 IF EQUAL
          MOVE      ADMISSNO,KEY8
          CALL      RDMRVS1
          BRANCH    OVRCD,MRTCHK90
          PACK      KEY10,MRVSMKEY,SP70
          CALL      RDMRMAS2
          BRANCH    OVRCD,MRTCHK90
.
          PACK      KEY7,MRMAHHOS,MRMAHLOC,SP70
          CALL      RDMRLOC1
          BRANCH    OVRCD,MRTCHK90
          WRITE     HTMLFILE;MRLODESC;
          GOTO      MRTCHK90
.
MRTCHK25  MATCH     SP70,URNUMBER
          GOTO      MRTCHK90 IF EQUAL
          PACK      KEY20,URNUMBER,Z70
          CALL      RSMRMAS1
          CALL      RPMRMAS1
          BRANCH    OVRCD,MRTCHK90                         
          MATCH     URNUMBER,MRMAURNO                      
          IF        @EQUAL                                 
            PACK      KEY7,MRMAHHOS,MRMAHLOC,SP70
            CALL      RDMRLOC1
            BRANCH    OVRCD,MRTCHK90
            WRITE     HTMLFILE;MRLODESC;
          ENDIF
          GOTO      MRTCHK90
.
MRTCHK30  MATCH     SP70,ADMISSNO     * Document Type
          GOTO      MRTCHK35 IF EQUAL
          MOVE      ADMISSNO,KEY8
          CALL      RDMRVS1
          BRANCH    OVRCD,MRTCHK90
          PACK      KEY10,MRVSMKEY,SP70
          CALL      RDMRMAS2
          BRANCH    OVRCD,MRTCHK90
          MOVE      "TY",CATEGORY
          MOVE      MRMADOTY,CODE
          CALL      GETCOD00
          WRITE     HTMLFILE;TDESC;
          GOTO      MRTCHK90
.
MRTCHK35  MATCH     SP70,URNUMBER      
          GOTO      MRTCHK90 IF EQUAL
          PACK      KEY20,URNUMBER,Z70
          CALL      RSMRMAS1
          CALL      RPMRMAS1
          BRANCH    OVRCD,MRTCHK90
          MATCH     URNUMBER,MRMAURNO
          IF        @EQUAL
            MOVE      "TY",CATEGORY
            MOVE      MRMADOTY,CODE
            CALL      GETCOD00
            WRITE     HTMLFILE;TDESC;
          ENDIF
          GOTO      MRTCHK90
.
MRTCHK40  MATCH     SP70,ADMISSNO       * Volume
          GOTO      MRTCHK45 IF EQUAL
          MOVE      ADMISSNO,KEY8
          CALL      RDMRVS1
          BRANCH    OVRCD,MRTCHK90
          PACK      KEY10,MRVSMKEY,SP70
          CALL      RDMRMAS2
          BRANCH    OVRCD,MRTCHK90
          WRITE     HTMLFILE;MRMAVOLN;
          GOTO      MRTCHK90
.
MRTCHK45  MATCH     SP70,URNUMBER
          GOTO      MRTCHK90 IF EQUAL
          PACK      KEY20,URNUMBER,Z70
          CALL      RSMRMAS1
          CALL      RPMRMAS1
          BRANCH    OVRCD,MRTCHK90
          MATCH     URNUMBER,MRMAURNO
          IF        @EQUAL
            WRITE     HTMLFILE;MRMAVOLN;
          ENDIF
          GOTO      MRTCHK90
.
MRTCHK50  MATCH     SP70,ADMISSNO       * Current Location
          GOTO      MRTCHK55 IF EQUAL
          MOVE      ADMISSNO,KEY8
          CALL      RDMRVS1
          BRANCH    OVRCD,MRTCHK90
          PACK      KEY10,MRVSMKEY,SP70
          CALL      RDMRMAS2
          BRANCH    OVRCD,MRTCHK90
          PACK      KEY7,MRMACHOS,MRMACLOC,SP70
          CALL      RDMRLOC1
          BRANCH    OVRCD,MRTCHK90
          WRITE     HTMLFILE;MRLODESC;
          GOTO      MRTCHK90
.
MRTCHK55  MATCH     SP70,URNUMBER
          GOTO      MRTCHK90 IF EQUAL
          PACK      KEY20,URNUMBER,Z70
          CALL      RSMRMAS1
          CALL      RPMRMAS1
          BRANCH    OVRCD,MRTCHK90
          MATCH     URNUMBER,MRMAURNO
          IF        @EQUAL
            PACK      KEY7,MRMACHOS,MRMACLOC,SP70
            CALL      RDMRLOC1
            BRANCH    OVRCD,MRTCHK90
            WRITE     HTMLFILE;MRLODESC;
          ENDIF
          GOTO      MRTCHK90
.
MRTCHK90  CLOSE    MRTMASA1
          CLOSE    MRTMASA2                                        
          CLOSE    MRTVISA1
          CLOSE    MRTLOCA1
          GOTO     MRTCHK99
.
          INC      MRTVISIO/INC
          INC      MRTMASIO/INC
          INC      MRTLOCIO/INC                                           
          INC      IBAOVRIO/INC
.
MRTCHK99  ENDPROC                                                   
.------------------------------------------------------------
. NHI File Variable Required
.------------------------------------------------------------
NHIM0000  COMPARE   ONE,SYSTNHI
          GOTO      NHIM9999 IF NOT EQUAL
.
          IF       NHIREAD=0
            MOVE     ONE,NHIREAD
            MOVE     PURNO,KEY8
            CALL     RDNHMAS2
            IF       OVRCD=1
              CALL     CLNHIMAS
            ENDIF
          ENDIF
.
          BRANCH    FLDITMNO,NHIM0100,NHIM0200,NHIM0300,NHIM0400,NHIM0500:
                             NHIM0600,NHIM0700,NHIM0800,NHIM0900,NHIM1000:
                             NHIM1100,NHIM1200,NHIM1300,NHIM1400,NHIM1500:
                             NHIM1600,NHIM1700,NHIM1800,NHIM1900,NHIM2000:
                             NHIM2100,NHIM2200,NHIM2300,NHIM2400,NHIM2500
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      NHIM9999
.
NHIM0100  WRITE     HTMLFILE;NHMANMPI;
          GOTO      NHIM9999
.
NHIM0200  WRITE     HTMLFILE;NHMAURNO;
          GOTO      NHIM9999
.
NHIM0300  WRITE     HTMLFILE;NHMASURN;
          GOTO      NHIM9999
.
NHIM0400  WRITE     HTMLFILE;NHMAGIV1;
          GOTO      NHIM9999
.
NHIM0500  WRITE     HTMLFILE;NHMAGIV2;
          GOTO      NHIM9999
.
NHIM0600  WRITE     HTMLFILE;NHMAGIV3;
          GOTO      NHIM9999
.
NHIM0700  WRITE     HTMLFILE;NHMAPREI;
          GOTO      NHIM9999
.
NHIM0800  WRITE     HTMLFILE;NHMAADD1;
          GOTO      NHIM9999
.
NHIM0900  WRITE     HTMLFILE;NHMAADD2;
          GOTO      NHIM9999
.
NHIM1000  WRITE     HTMLFILE;NHMAADD3;
          GOTO      NHIM9999
.
NHIM1100  WRITE     HTMLFILE;NHMAADD4;
          GOTO      NHIM9999
.
NHIM1200  WRITE     HTMLFILE;NHMAADD5;
          GOTO      NHIM9999
.
NHIM1300  UNPACK    NHMADOB,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      NHIM9999
.
NHIM1400  MATCH     SP70,NHMADDTH
          GOTO      NHIM9999 IF EQUAL
          UNPACK    NHMADDTH,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      NHIM9999
.
NHIM1500  WRITE     HTMLFILE;NHMADOMC;
          GOTO      NHIM9999
.
NHIM1600  MATCH     "Y",NHMARES
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
            GOTO      NHIM9999
          ELSE
            WRITE     HTMLFILE;"No";
            GOTO      NHIM9999
          ENDIF
.
NHIM1700  WRITE     HTMLFILE;NHMASEX;
          GOTO      NHIM9999
.
NHIM1800  UNPACK    NHMADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;CCENT,CYEAR,"-",CMON,"-",CDAY;
          ENDIF
          GOTO      NHIM9999
.
NHIM1900  WRITE     HTMLFILE;NHMATIM;
          GOTO      NHIM9999
.
NHIM2000  MOVE      NHMAETH,D5
          CALL      NHIETY00
          GOTO      NHIM9999
.
NHIM2100  MOVE      NHMAETH2,D5
          CALL      NHIETY00
          GOTO      NHIM9999
.
NHIM2200  MOVE      NHMAETH3,D5
          CALL      NHIETY00
          GOTO      NHIM9999
.
NHIM2300  MOVE      NHMADOMC,KEY4
          PROC      NHIDOMDS                * output NHI Domicile Description
          GOTO      NHIM9999
.
NHIM2400  MOVE      NHMADOMC,KEY4
          MOVE      ZERO,F1
          PROC      NHIDODHB                * output description of NHI 
.                                           * Domicile associated DHB
                                            * (District Health Board; Cat dh)
          GOTO      NHIM9999
.
NHIM2500  MOVE      NHMADOMC,KEY4
          MOVE      ONE,F1
          PROC      NHIDODHB                * output code of NHI Domicile 
.                                           * associated DHB
                                            * (District Health Board; Cat dh)
          GOTO      NHIM9999
.
NHIM9999  RETURN
.------------------------------------------------------------
. Show Ethnicity 0=Description, 1=Code, 2=Options
.------------------------------------------------------------
NHIETY00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,NHIETY10,NHIETY20
          MOVE      D5,KEY5
          CALL      RDNHETH1
          IF        OVRCD=0
            WRITE     HTMLFILE;NHETDES;
          ENDIF
          GOTO      NHIETY99
.
NHIETY10  WRITE     HTMLFILE;D5;
          GOTO      NHIETY99
.
NHIETY20  PACK      KEY5,SP70
          CALL      RSNHETH1
NHIETY30  CALL      RKNHETH1
          BRANCH    OVRCD,NHIETY99
          PACK      KEY7,QUOTE,NHETECD,QUOTE
          MATCH     D5,NHETECD
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY7," selected>":
                               NHETDES,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY7,">",NHETDES,"</option>"
          ENDIF
          GOTO      NHIETY30
.
NHIETY99  RETURN
.------------------------------------------------------------
. Output Alias indicator
.------------------------------------------------------------
          DEFPROC   CHALI000   
.
          INC       PATGSRFD/INC
.
CHALI010  CLOSE     CONTROLF
          OPEN      PATGSRN1,"patgsrnf"
          MOVE      ZERO,F1           * No Alias 
          PACK      KEY115,URNUMBER,SP70,SP70
          CALL      RSPTGSR1    
CHALI020  CALL      RKPTGSR1    
          BRANCH    OVRCD,CHALI990
.
          MATCH     GSRURNO,URNUMBER
          GOTO      CHALI990 IF NOT EQUAL
.
          MATCH     GSRSNAM,PTMASNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRDOB,PBDATE
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRSEX,PSEX
          GOTO      CHALI030 IF NOT EQUAL
.
          GOTO      CHALI020
CHALI030  MOVE      ONE,F1            * Yes they have an alias
CHALI990  WRITE     HTMLFILE;F1;      * output alias indicator
CHALI999  CLOSE     PATGSRN1
          OPEN      CONTROLF,"controlf"
          GOTO      CHALIEND
.
          INC       PATGSRIO/INC
          INC       IBAOVRIO/INC
.
CHALIEND  ENDPROC
.------------------------------------------------------------
. Output Links indicator
.------------------------------------------------------------
          DEFPROC   CHLIN000   
.
          INC       PATLINFD/INC
.
CHLIN010  CLOSE     CONTROLF
          OPEN      PATLINK1,"patlinkf"
          MOVE      ZERO,F1           * No linked UR's 
          PACK      KEY16,URNUMBER,SP70 
          CALL      RDSLINK1
          CALL      RDKLINK1
          BRANCH    OVRCD,CHLIN990
          MATCH     LINKFUR,URNUMBER
          GOTO      CHLIN990 IF NOT EQUAL
          MOVE      ONE,F1
CHLIN990  WRITE     HTMLFILE;F1;      * output links indicator
CHLIN999  CLOSE     PATLINK1
          OPEN      CONTROLF,"controlf"
          GOTO      CHLINEND
.
          INC       PATLINIO/INC
          INC       IBAOVRIO/INC
.
CHLINEND  ENDPROC
.------------------------------------------------------------
. Output Previous Address indicator
.------------------------------------------------------------
          DEFPROC   CHPAD000
.
          INC       PATPA1FD/INC
.
CHPAD010  CLOSE     CONTROLF
          OPEN      PATPA1A1,"patpa1af"
          MOVE      ZERO,F1           * No Previous Address  
          PACK      KEY24,URNUMBER,SP70 
          CALL      RDSPADD1
          CALL      RDKPADD1
          BRANCH    OVRCD,CHPAD990
          MATCH     PAURNO,URNUMBER
          GOTO      CHPAD990 IF NOT EQUAL
          MOVE      ONE,F1
CHPAD990  WRITE     HTMLFILE;F1;      * output Previous Address indicator
CHPAD999  CLOSE     PATPA1A1
          OPEN      CONTROLF,"controlf"
          GOTO      CHPADEND 
.
          INC       PATPA1IO/INC
          INC       IBAOVRIO/INC
.
CHPADEND  ENDPROC
.------------------------------------------------------------
. Output parent contact indicator
.------------------------------------------------------------
CHPAR000  MOVE      ZERO,F1
          MATCH     SP70,PMPXMGNA                
          GOTO      CHPAR900 IF NOT EQUAL        * Yes Mother details
.
          MATCH     SP70,PMPXMSNA
          GOTO      CHPAR900 IF NOT EQUAL        * Yes Mother details
.
          MATCH     SP70,PMPXFGNA                
          GOTO      CHPAR900 IF NOT EQUAL        * Yes Father details
.
          MATCH     SP70,PMPXFSNA
          GOTO      CHPAR900 IF NOT EQUAL        * Yes Father details
.
          GOTO      CHPAR990
CHPAR900  MOVE      ONE,F1
CHPAR990  WRITE     HTMLFILE;F1;
CHPAR999  RETURN
.------------------------------------------------------------
. Output emergency contact indicator
.------------------------------------------------------------
          DEFPROC   CHCON000
.
          INC       PATRE1FD/INC
          INC       PMSCEXFD/INC
.
CHCON010  READ      CONTROLF,HUND18;*138,PTCNNEWC
          MATCH     "1",PTCNNEWC
          GOTO      CHCON015 IF NOT EQUAL
.
          OPEN      PMSCEXA1,"pmscexaf"
          PACK      KEY14,URNUMBER,SP70
          CALL      RSPMCEX1
          CALL      RKPMCEX1
          BRANCH    OVRCD,CHCON015
          MATCH     URNUMBER,PMCEURNO
          IF        @EQUAL
            MOVE      ONE,F1
            GOTO      CHCON900
          ENDIF
.
CHCON015  OPEN      PATRE1A1,"patre1af"
          MOVE      ZERO,F1           * No Emergency Contact details   
          MATCH     SP70,PNKNAME            * is there NOK details?
          GOTO      CHCON900 IF NOT EQUAL   * yes, move one to indicator
          MATCH     SP70,PTMXECNM           * is there emergency contact dets?
          GOTO      CHCON900 IF NOT EQUAL   * yes, move one to indicator
          MATCH     SP70,PTMXNRNM           * is there Nearest relative details?
          GOTO      CHCON900 IF NOT EQUAL   * yes, move one to indicator
.
CHCON020  MOVE      ADMISSNO,KEY8
          CALL      RDRESP1   
          BRANCH    OVRCD,CHCON990
.
          MATCH     SP70,PKNAME             * is there person responsible?
          GOTO      CHCON990 IF EQUAL       * no, output indicator with 0 value
.
CHCON900  MOVE      ONE,F1
CHCON990  WRITE     HTMLFILE;F1;      * output Emergency Contact indicator
CHCON999  CLOSE     PATRE1A1
          OPEN      CONTROLF,"controlf"
          GOTO      CHCONEND
.
          INC       PMSCEXIO/INC
          INC       PATRE1IO/INC
          INC       IBAOVRIO/INC
.
CHCONEND  ENDPROC
.*****************************************************************************
.*  CHKDEBTS                                
.*  Procedure to process outstanding debt alert Exit=0 No Debts 1 Has Debts
.*
.*             Original code moved to PATWEB82 as CHKDBT00            *C-119436
.*****************************************************************************
          DEFPROC   CHKDEBTS
.
          INC       PMSPX2FD/INC
.
CHKDD000  MOVE      ZERO,EXIT
.
          OPEN      PMSPX2A1,"pmspx2af"
.
          PACK      KEY8,PURNO,SP20
          CALL      RDPMPX21           * Read PMI Extension Table
          BRANCH    OVRCD,CHKDD999
.
          MOVE      PMPXSN15,EXIT      * get Outstanding Debt Indicator
          GOTO      CHKDD999
.
          INC       PMSPX2IO/INC
          INC       IBAOVRIO/INC
.
CHKDD999  CLOSE     PMSPX2A1
.
          ENDPROC
.
.*****************************************************************************
.*  CHRESULT
.*  Procedure to check for any unread results Exit=0 No Unread Results
.*                                                 1 Has Unread Results
.*****************************************************************************
          DEFPROC   CHRESULT
.
          INC       RESLNKFD/INC
.
DIM4      DIM       4                       * I CAR 38290
FORM4     FORM      4                       * I CAR 38290
FORM4A    FORM      4                       * I CAR 38290
RESLNKFL  DIM       8                       * I CAR 38290
PRELEN    INIT      "reln"                  * I CAR 38290
.
          MOVE      ZERO,EXIT
.         OPEN      RESLNKA2,"reslnkaf"     * D CAR 38290
.
          CALL      IBACLOCK                * I CAR 38290
          PACK      DIM4,CCC,CYY
          REP       " 0",DIM4
          MOVE      DIM4,FORM4
          SUB       ONE,FORM4
.
CHRES050  MOVE      ZERO,OVRCD
          PACK      RESLNKFL,PRELEN,DIM4
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA2,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      CHRES900 IF EQUAL
.                                           * end I CAR 38290
.
          PACK      KEY30,ZERO,ONE,PURNO,SP1,SP1,ZERO,SP70
          CALL      RSRELN2                 * position on the result file
          CALL      RKRELN2 
          BRANCH    OVRCD,CHRES900
          MATCH     PURNO,RELNLKY           * match U/R numbers
          GOTO      CHRES900 IF NOT EQUAL
.
          MATCH     "01",RELNLTY           * match link type  
          GOTO      CHRES900 IF NOT EQUAL
.
          MATCH     "D ",RELNRST
          GOTO      CHRES900 IF EQUAL
.
          MATCH     "0",RELNMSN            * match unread     
          GOTO      CHRES900 IF NOT EQUAL
.
          MOVE      ONE,EXIT               * Unread result found
          GOTO      CHRES999
.
CHRES900  MOVE      DIM4,FORM4A                       * I CAR 38290
          COMPARE   FORM4,FORM4A
          IF        @EQUAL
            GOTO      CHRES999
          ELSE
            SUB       ONE,FORM4A
            MOVE      FORM4A,DIM4
            GOTO      CHRES050                        * end I CAR 38290
          ENDIF
.
          INC       IBAOVRIO/INC
          INC       RESLNKIO/INC
.
CHRES999  CLOSE     RESLNKA2
.
          ENDPROC
.
.*****************************************************************************
.*  CHKDONOR
.*  Procedure to check if a patient is an Organ Donor.
.*  Exit=0  Not a Donor
.*       1  Patient is a Donor 
.*****************************************************************************
          DEFPROC   CHKDONOR
.
          INC       NHIDNRFD/INC
          INC       NHIMASFD/INC
.
          MOVE      ZERO,EXIT
          COMPARE   ONE,SYSTNHI
          GOTO      CHKDNR99 IF NOT EQUAL
.
          OPEN      NHIDNRA1,"nhidnraf"
          OPEN      NHIMASA2,"nhimasaf"
.
          PACK      KEY8,PURNO,SP20
          CALL      RDNHMAS2           * Determine National HCU ID
          BRANCH    OVRCD,CHKDNR99
.
          MOVE      NHMANMPI,KEY7
          CALL      RDNHDNR1           * Read Local Donor Information
          BRANCH    OVRCD,CHKDNR99
.
          MATCH     SP70,NHDNDONR      * No donor details recorded.
          GOTO      CHKDNR99 IF EQUAL
.
          MATCH     "X",NHDNDONR       * Patient marked as "Not a donor".
          GOTO      CHKDNR99 IF EQUAL
.
          MOVE      ONE,EXIT           * Patient is an Organ Donor
          GOTO      CHKDNR99
.
          INC       NHIDNRIO/INC
          INC       NHIMASIO/INC
          INC       IBAOVRIO/INC
.
CHKDNR99  CLOSE     NHIDNRA1
          CLOSE     NHIMASA2
.
          ENDPROC
.
.*****************************************************************************
.*  CHKRESUS
.*  Procedure to check for a "Not for Resuscitation" indicator.
.*  Exit=0  "Not for Resuscitation" indicator is not set. 
.*       1  "Not for Resuscitation" indicator is set. 
.*****************************************************************************
          DEFPROC   CHKRESUS
.
          INC       PMSPX2FD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PMSPX2A1,"pmspx2af"
.
          PACK      KEY8,PURNO,SP20
          CALL      RDPMPX21           * Read PMI Extension Table
          BRANCH    OVRCD,CHKRES99
.
          MATCH     "1",PMPXUYN6       * Check "Not for Resuscitation" indicator
          GOTO      CHKRES99 IF NOT EQUAL

          MOVE      ONE,EXIT          
          GOTO      CHKRES99
.
          INC       PMSPX2IO/INC
          INC       IBAOVRIO/INC
.
CHKRES99  CLOSE     PMSPX2A1
.
          ENDPROC
.
.*****************************************************************************
.*  CHKALTID
.*  Procedure to check if a patient has an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   CHKALTID
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
          CALL      RKPMAID1
          BRANCH    OVRCD,CHKAID99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      CHKAID99 IF NOT EQUAL

          MOVE      ONE,EXIT
          GOTO      CHKAID99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
CHKAID99  CLOSE     PMSAIDA1
.
          ENDPROC
.
.*****************************************************************************
.*  UPDLSTUR
.*  Procedure to keep track of last 10 UR numbers accessed by a particular
.*  WEB User.
.*****************************************************************************
          DEFPROC   UPDLSTUR
.
          INC       WEBLURFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      WEBLURA1,"webluraf"
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBLUR1
          BRANCH    OVRCD,UPDLUR80          * Insert webluraf row.
.
.         Check current UR number and Admission number against 
.         most recent UR number and Admission number stored on file.
.
          MATCH     WBLRUR01,PURNO          * Same as most recent UR?
          GOTO      UPDLUR10 IF NOT EQUAL   * No, Store this UR number
          MATCH     WBLRVS01,ADMISSNO       * Same as most recent Admission?
          GOTO      UPDLUR99 IF EQUAL       * Yes, no update required, exit
.
          MOVE      ADMISSNO,WBLRVS01       * No, update Admission number only.
          GOTO      UPDLUR90
.
UPDLUR10  MATCH     WBLRUR02,PURNO          
          GOTO      UPDLUR55 IF EQUAL
.
          MATCH     WBLRUR03,PURNO          
          GOTO      UPDLUR50 IF EQUAL
. 
          MATCH     WBLRUR04,PURNO
          GOTO      UPDLUR45 IF EQUAL
. 
          MATCH     WBLRUR05,PURNO
          GOTO      UPDLUR40 IF EQUAL
. 
          MATCH     WBLRUR06,PURNO
          GOTO      UPDLUR35 IF EQUAL
. 
          MATCH     WBLRUR07,PURNO
          GOTO      UPDLUR30 IF EQUAL
. 
          MATCH     WBLRUR08,PURNO
          GOTO      UPDLUR25 IF EQUAL
. 
          MATCH     WBLRUR09,PURNO
          GOTO      UPDLUR20 IF EQUAL
. 
          MATCH     WBLRUR10,PURNO
          GOTO      UPDLUR15 IF EQUAL
.
          CALL      UPD2UR00        * check 11-20
.
UPDLUR15  MOVE      WBLRUR09,WBLRUR10
          MOVE      WBLRVS09,WBLRVS10
.
UPDLUR20  MOVE      WBLRUR08,WBLRUR09
          MOVE      WBLRVS08,WBLRVS09
.
UPDLUR25  MOVE      WBLRUR07,WBLRUR08
          MOVE      WBLRVS07,WBLRVS08
.
UPDLUR30  MOVE      WBLRUR06,WBLRUR07
          MOVE      WBLRVS06,WBLRVS07
.
UPDLUR35  MOVE      WBLRUR05,WBLRUR06
          MOVE      WBLRVS05,WBLRVS06          
.
UPDLUR40  MOVE      WBLRUR04,WBLRUR05
          MOVE      WBLRVS04,WBLRVS05
.
UPDLUR45  MOVE      WBLRUR03,WBLRUR04
          MOVE      WBLRVS03,WBLRVS04
.
UPDLUR50  MOVE      WBLRUR02,WBLRUR03
          MOVE      WBLRVS02,WBLRVS03
.
UPDLUR55  MOVE      WBLRUR01,WBLRUR02
          MOVE      WBLRVS01,WBLRVS02
          MOVE      PURNO,WBLRUR01
          MOVE      ADMISSNO,WBLRVS01
          GOTO      UPDLUR90
.
.         Write a row to webluraf for this Web User ID
.
UPDLUR80  MOVE      USERID,WBLRUID          * Set User ID
          MOVE      PURNO,WBLRUR01          * Set UR Number
          MOVE      ADMISSNO,WBLRVS01       * Set Admission Number
          MOVE      SP70,WBLRUR02           * Initialize all other fields
          MOVE      SP70,WBLRVS02
          MOVE      SP70,WBLRUR03
          MOVE      SP70,WBLRVS03
          MOVE      SP70,WBLRUR04
          MOVE      SP70,WBLRVS04
          MOVE      SP70,WBLRUR05
          MOVE      SP70,WBLRVS05
          MOVE      SP70,WBLRUR06
          MOVE      SP70,WBLRVS06
          MOVE      SP70,WBLRUR07
          MOVE      SP70,WBLRVS07
          MOVE      SP70,WBLRUR08
          MOVE      SP70,WBLRVS08
          MOVE      SP70,WBLRUR09
          MOVE      SP70,WBLRVS09
          MOVE      SP70,WBLRUR10
          MOVE      SP70,WBLRVS10
          MOVE      SP70,WBLRUR11
          MOVE      SP70,WBLRVS11
          MOVE      SP70,WBLRUR12
          MOVE      SP70,WBLRVS12
          MOVE      SP70,WBLRUR13
          MOVE      SP70,WBLRVS13
          MOVE      SP70,WBLRUR14
          MOVE      SP70,WBLRVS14
          MOVE      SP70,WBLRUR15
          MOVE      SP70,WBLRVS15
          MOVE      SP70,WBLRUR16
          MOVE      SP70,WBLRVS16
          MOVE      SP70,WBLRUR17
          MOVE      SP70,WBLRVS17
          MOVE      SP70,WBLRUR18
          MOVE      SP70,WBLRVS18
          MOVE      SP70,WBLRUR19
          MOVE      SP70,WBLRVS19
          MOVE      SP70,WBLRUR20
          MOVE      SP70,WBLRVS20
          MOVE      SP70,WBLRSPAR
          PACK      KEY10,USERID,SP70
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRWBLUR1                * Insert webluraf row
          TRAPCLR   IO
          GOTO      UPDLUR99
.
UPDLUR90  CALL      UPWBLUR1                * Update webluraf row
          GOTO      UPDLUR99
.
.
UPD2UR00  MATCH     WBLRUR11,PURNO
          GOTO      UPD2UR65 IF EQUAL
.
          MATCH     WBLRUR12,PURNO
          GOTO      UPD2UR55 IF EQUAL
.         
          MATCH     WBLRUR13,PURNO
          GOTO      UPD2UR50 IF EQUAL
.         
          MATCH     WBLRUR14,PURNO
          GOTO      UPD2UR45 IF EQUAL
.         
          MATCH     WBLRUR15,PURNO
          GOTO      UPD2UR40 IF EQUAL
.         
          MATCH     WBLRUR16,PURNO
          GOTO      UPD2UR35 IF EQUAL
.
          MATCH     WBLRUR17,PURNO
          GOTO      UPD2UR30 IF EQUAL
.
          MATCH     WBLRUR18,PURNO
          GOTO      UPD2UR25 IF EQUAL
.
          MATCH     WBLRUR19,PURNO
          GOTO      UPD2UR20 IF EQUAL
.
          MOVE      WBLRUR19,WBLRUR20
          MOVE      WBLRVS19,WBLRVS20
.
UPD2UR20  MOVE      WBLRUR18,WBLRUR19
          MOVE      WBLRVS18,WBLRVS19
.
UPD2UR25  MOVE      WBLRUR17,WBLRUR18
          MOVE      WBLRVS17,WBLRVS18
.
UPD2UR30  MOVE      WBLRUR16,WBLRUR17
          MOVE      WBLRVS16,WBLRVS17
.
UPD2UR35  MOVE      WBLRUR15,WBLRUR16
          MOVE      WBLRVS15,WBLRVS16
.
UPD2UR40  MOVE      WBLRUR14,WBLRUR15
          MOVE      WBLRVS14,WBLRVS15
.
UPD2UR45  MOVE      WBLRUR13,WBLRUR14
          MOVE      WBLRVS13,WBLRVS14
.
UPD2UR50  MOVE      WBLRUR12,WBLRUR13
          MOVE      WBLRVS12,WBLRVS13
.
UPD2UR55  MOVE      WBLRUR11,WBLRUR12
          MOVE      WBLRVS11,WBLRVS12
.
UPD2UR65  MOVE      WBLRUR10,WBLRUR11
          MOVE      WBLRVS10,WBLRVS11
.
          RETURN
.
          INC       WEBLURIO/INC
          INC       IBAOVRIO/INC
.
UPDLUR99  CLOSE     WEBLURA1
          ENDPROC
.
.*****************************************************************************
.*  PRIVCY00                                
.*  Output Privicy Indicator 
.*****************************************************************************
          DEFPROC   PRIVCY00
.
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
.
CODEPV    INIT      "PV"
.
          OPEN      PMSPX2A1,"pmspx2af"
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,PRIV9500
.
          MOVE      PMPXPRVI,CODE         * Privacy
          MOVE      ZERO,EXIT
          GOTO      PRIV9999
.
.          The following privacy indicator is from visit file (phase 2)
.
.          MOVE      ZERO,EXIT
.          MOVE      SP10,CODE
.          MATCH     SP70,ADMISSNO
.          GOTO      PRIV9500 IF EQUAL
..
.          OPEN      PMSVX1A1,"pmsvx1af"
.          MOVE      ADMISSNO,KEY8
.          CALL      RDPMVX11
.          BRANCH    OVRCD,PRIV9500
..
.          MATCH     SP3,PMVXPIND          * Check Privacy Indicator
.          GOTO      PRIV9500 IF EQUAL
..
.          MOVE      PMVXPIND,CODE         * Privacy
.          MOVE      ZERO,EXIT
.          GOTO      PRIV9999
.
PRIV9500  MOVE      ONE,EXIT
          GOTO      PRIV9999
.
          INC       IBAOVRIO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
.
PRIV9999  CLOSE     PMSVX1A1
.
          ENDPROC
.
.*****************************************************************************
.*  TRAN0000                                
.*  Get Unit from transfer file
.*****************************************************************************
          DEFPROC   TRAN0000
.
          INC       PATTRNFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PATTRAN2,"pattranf"
.
          MOVE      SP10,CODE
          PACK      KEY30,PVIBILL,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,TRAN9500
.
          MATCH     PVIBILL,TADMN          * Same visit number ?
          GOTO      TRAN9500 IF NOT EQUAL  * No
.
          MATCH     SP3,TDEPT
          GOTO      TRAN9500 IF EQUAL
          MOVE      TDEPT,CODE             * Unit code
          MOVE      ZERO,EXIT
          GOTO      TRAN9999
.
TRAN9500  MOVE      ONE,EXIT
          GOTO      TRAN9999
.
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
.
TRAN9999  CLOSE     PATTRAN2
          ENDPROC
+
.*****************************************************************************
.*  EVNT0000                                
.*  Get Unit from Event file
.*****************************************************************************
          DEFPROC   EVNT0000
.
          INC       PMSEVTFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PMSEVTA1,"pmsevtaf"
.
          MOVE      SP10,CODE
          MOVE      ADMISSNO,KEY8
          CALL      RDPMEVT1
          BRANCH    OVRCD,EVNT9500
.
          MATCH     ADMISSNO,PMEVVISN      * Same visit number ?
          GOTO      EVNT9500 IF NOT EQUAL  * No
.
          MATCH     SP3,PMEVUNIT
          GOTO      EVNT9500 IF EQUAL
          MOVE      PMEVUNIT,CODE          * Unit Code
          MOVE      ZERO,EXIT
          GOTO      EVNT9999
.
EVNT9500  MOVE      ONE,EXIT
          GOTO      EVNT9999
.
          INC       IBAOVRIO/INC
          INC       PMSEVTIO/INC
.
EVNT9999  CLOSE     PMSEVTA1
          ENDPROC
+
.*****************************************************************************
.*  ALHE0000                                
.*  Get Unit from Allied Health file
.*****************************************************************************
          DEFPROC   ALHE0000
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      ALLREFA1,"allrefaf"
.
          MOVE      SP10,CODE
          PACK      KEY8,ADMISSNO
          CALL      RDALREF1
          BRANCH    OVRCD,ALHE9500
.
          MATCH     ADMISSNO,ALREVISN      * Same visit number ?
          GOTO      ALHE9500 IF NOT EQUAL  * No
.
          MATCH     SP3,ALREUNIT
          GOTO      ALHE9500 IF EQUAL
          MOVE      ALREUNIT,CODE          * Unit code
          MOVE      ZERO,EXIT
          GOTO      ALHE9999
.
ALHE9500  MOVE      ONE,EXIT
          GOTO      ALHE9999
.
          INC       IBAOVRIO/INC
          INC       ALLREFIO/INC
.
ALHE9999  CLOSE     ALLREFA1
          ENDPROC
.
.*****************************************************************************
.*  CHKINTRP
.*  Procedure to check for interpreter required
.*  Exit=0  Interpreter required = No
.*       1  Interpreter required = Yes
.*****************************************************************************
          DEFPROC   CHKINTRP
.
          INC       PMSPX2FD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PMSPX2A1,"pmspx2af"
.
          PACK      KEY8,PURNO,SP20
          CALL      RDPMPX21           * Read PMI Extension Table
          BRANCH    OVRCD,CHKINT99
.
          MATCH     "1",PMPXINTR       * Check if interpreter required
          GOTO      CHKINT99 IF NOT EQUAL

          MOVE      ONE,EXIT
          GOTO      CHKINT99
.
          INC       PMSPX2IO/INC
          INC       IBAOVRIO/INC
.
CHKINT99  CLOSE     PMSPX2A1
.
          ENDPROC
+
.*****************************************************************************
.*  CHKRELTO
.*  Procedure to check release information ('release to' fields)
.*  Exit=0  None of the 'release to' fields are set to no
.*       1  At least one 'release to' field is set to no
.*****************************************************************************
          DEFPROC   CHKRELTO
.
          INC       PMSVX1FD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PMSVX1A1,"pmsvx1af"
.
          PACK      KEY8,ADMISSNO,SP20
          CALL      RDPMVX11           * Read Visit Extension Table
          BRANCH    OVRCD,CHKREL99
.
          MATCH     "0",PMVXRPOA       * Check if Release to Enduring POA=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRCN1       * Check if Release to Contact 1=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRCN2       * Check if Release to Contact 2=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRHFU       * Check if Release to Health Fund=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRDVA       * Check if Release to DVA=no
          GOTO      CHKREL99 IF NOT EQUAL
.
CHKREL95  MOVE      ONE,EXIT
          GOTO      CHKREL99
.
          INC       PMSVX1IO/INC
          INC       IBAOVRIO/INC
.
CHKREL99  CLOSE     PMSVX1A1
.
          ENDPROC
+
.*****************************************************************************
.*  CHKSUSPT
.*  Procedure to check release information ('release to' fields)
.*  Exit=0  None of the 'release to' fields are set to no
.*       1  At least one 'release to' field is set to no
.*****************************************************************************
          DEFPROC   CHKSUSPT
.
          INC       PATSUSFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PATSUSA1,"patsusaf"
.
          CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY16,URNUMBER,KEY8,Z70
          CALL      RDPTSUS1           * Read suspension table
          BRANCH    OVRCD,CHKSUS10
          GOTO      CHKSUS90
.
CHKSUS10  PACK      KEY16,URNUMBER,KEY8,Z70
          CALL      RSPTSUS1           * Read suspension table
          CALL      RPPTSUS1
          BRANCH    OVRCD,CHKSUS99
.
          MATCH     URNUMBER,PTSSURNO
          GOTO      CHKSUS95 IF NOT EQUAL
.
          MATCH     SP70,PTSSTDAT
          GOTO      CHKSUS90 IF EQUAL
.
          MATCH     PTSSTDAT,KEY8
          GOTO      CHKSUS95 IF NOT LESS
.
CHKSUS90  MOVE      ONE,EXIT
          GOTO      CHKSUS99
.
CHKSUS95  MOVE      ZERO,EXIT
          GOTO      CHKSUS99
.
          INC       PATSUSIO/INC
          INC       IBAOVRIO/INC
.
CHKSUS99  CLOSE     PATSUSA1
.
          ENDPROC
+
.*****************************************************************************
.*  CHKSACSR
.*  Procedure to check for active SACS referral
.*  Exit=0  No active SACS referral exists
.*       1  An active SACS referral exists
.*****************************************************************************
          DEFPROC   CHKSACSR
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALLREFA2,"allrefaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKSAC99
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSALREF2
CHKSAC10  CALL      RKALREF2                * Read through referral table
          BRANCH    OVRCD,CHKSAC99
.
          MATCH     URNUMBER,ALREURNO       * Check for same ur number
          GOTO      CHKSAC99 IF NOT EQUAL
.
          MATCH     "1",ALRESTAT            * Check for active referral
          GOTO      CHKSAC10 IF NOT EQUAL
.
          MATCH     "1",ALREUYN4            * Check for SACS referral
          GOTO      CHKSAC10 IF NOT EQUAL
.
CHKSAC95  MOVE      ONE,EXIT
          GOTO      CHKSAC99
.
          INC       ALLREFIO/INC
          INC       IBAOVRIO/INC
.
CHKSAC99  CLOSE     ALLREFA2
.
          ENDPROC
+
.*****************************************************************************
.*  CHKLEGST
.*  Procedure to check for Legal Status
.*  Exit=0  No Legal Status
.*       1  With Legal Status
.*       2  With Historical data
.*****************************************************************************
          DEFPROC   CHKLEGST
.
          INC       MEHHLSFD/INC
          INC       MEHDLSFD/INC
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MEHHLSA2,"mehhlsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKLEG99
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MEHDLSA1,"mehdlsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKLEG99
.
          MOVE      ZERO,EXIT
          PACK      KEY32,URNUMBER,Z70
.
          CALL      RSMHHLS2
CHKLEG10  CALL      RPMHHLS2
          BRANCH    OVRCD,CHKLEG99
.
          MATCH     URNUMBER,MHHLURNO       * Check for same ur number
          GOTO      CHKLEG99 IF NOT EQUAL
.
          MATCH     "1",MHHLSTAT
          GOTO      CHKLEG10 IF EQUAL       * status "Inactive"
          MATCH     SP70,MHHLEDAT
          GOTO      CHKLEG10 IF NOT EQUAL   * End date not blank
          MATCH     SP70,MHHLETIM
          GOTO      CHKLEG10 IF NOT EQUAL   * End time not blank
.
.                                           * Header stil active - check Details
          PACK      KEY32,URNUMBER,MHHLUNIQ,Z70
          CALL      RSMHDLS1
CHKLEG20  CALL      RPMHDLS1
          BRANCH    OVRCD,CHKLEG10
.
          MATCH     URNUMBER,MHDLURNO       * Same U/R Number?
          GOTO      CHKLEG10 IF NOT EQUAL
          MATCH     MHHLUNIQ,MHDLUNIQ       * Same Uniq number?
          GOTO      CHKLEG10 IF NOT EQUAL
.
.                                           * now check each Detail record
          MATCH     "1",MHDLACTV            * In-Active?
          GOTO      CHKLEG20 IF EQUAL
.
          MATCH     SP70,MHDLEDAT
          GOTO      CHKLEG20 IF NOT EQUAL   * End date not blank
          MATCH     SP70,MHDLETIM
          GOTO      CHKLEG20 IF NOT EQUAL   * End time not blank
.
.    
.                                           * read Details to check Cat "LS"
          PACK      KEY5,ANSL,ANSS,MHDLSCOD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKLEG20
.    
.         If Indicator 1 = I, do not display Legal status icon        *I-195158
.....     MATCH     "I",TCINDC1    * Removed for CAR 231658
.....     GOTO      CHKLEG90 IF EQUAL
.    
.         If Indicator 3 = N, do not display Legal status icon
          MATCH     "N",TCINDC3
          GOTO      CHKLEG90 IF EQUAL
.    
.                                         * found an Active LS Detail
          MOVE      ONE,EXIT              * set to "Legal status" Icon
          GOTO      CHKLEG99
.
CHKLEG90  MOVE      ZERO,EXIT             * set to no LS Icon
          GOTO      CHKLEG99
.
          INC       MEHHLSIO/INC
          INC       MEHDLSIO/INC
          INC       IBAOVRIO/INC
.
CHKLEG99  CLOSE     MEHHLSA2
          CLOSE     MEHDLSA1
.
          ENDPROC
.
.*****************************************************************************
.                 TITLEDSC
.        Procedure to display for Title Description
.***************************************************************************
          DEFPROC   TITLEDSC
.
          INC       PMSTLEFD/INC
.
          OPEN      PMSTLEA1,"pmstleaf" 
.
TITLED00  PACK      KEY4,PTITL,SP70
          CALL      RDPMTLE1
          BRANCH    OVRCD,TITLED99
.
          WRITE     HTMLFILE;PMTLDESC;
          GOTO      TITLED99
.
          INC       PMSTLEIO/INC
          INC       IBAOVRIO/INC
.
TITLED99  ENDPROC   
.
.*****************************************************************************
.                 RELATDSC
.        Procedure to display for Relation Description
.***************************************************************************
          DEFPROC   RELATDSC
.
          INC       PMSRELFD/INC
.
          OPEN      PMSRELA1,"pmsrelaf"
.
RELATD00  PACK      KEY10,PNKRELP,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,RELATD99
.
          WRITE     HTMLFILE;PMRLDESC;
          GOTO      RELATD99
.
          INC       PMSRELIO/INC
          INC       IBAOVRIO/INC
.
RELATD99  ENDPROC
.
.*****************************************************************************
.                 NHIDOMDS
.        Procedure to display for NHI Domicile Description
.***************************************************************************
          DEFPROC   NHIDOMDS
.
          INC       NHIDMCFD/INC
.
          OPEN      NHIDMCA1,"nhidmcaf"
.
NHIDOM00  PACK      NHDMDES,SP70            * KEY4 already populated
          CALL      RDNHDM1
          BRANCH    OVRCD,NHIDOM99
.
          WRITE     HTMLFILE;NHDMDES;
          GOTO      NHIDOM99
.
          INC       NHIDMCIO/INC
          INC       IBAOVRIO/INC
.
NHIDOM99  ENDPROC
+
.******************************************************************************
.                   NHIDODHB
.         Procedure to display the NHI 'District Health Board' for an
.         associated Domicile code.
.******************************************************************************
          DEFPROC   NHIDODHB
.
          INC       NHIDMCFD/INC
.
CATdh     INIT      "dh"
.
          OPEN      NHIDMCA1,"nhidmcaf"
.
NHIDHB00  CALL      RDNHDM1                 * KEY4 already populated
          BRANCH    OVRCD,NHIDHB99
.
          PACK      KEY5,CATdh,NHDMDHB,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            IF        F1=1
              WRITE     HTMLFILE;NHDMDHB;
            ELSE
              WRITE     HTMLFILE;TDESC;
            ENDIF
          ENDIF
          GOTO      NHIDHB99
.
          INC       NHIDMCIO/INC
          INC       IBAOVRIO/INC
.
NHIDHB99  ENDPROC
+
.*******************************************************************************
.*                CHKDISCD                                                     *
.*       Check for Disaster Code                                               *
.*******************************************************************************
          DEFPROC   CHKDISCD
.
          INC       DISMASFD/INC
          INC       DISPATFD/INC
          INC       DISPTLFD/INC
.
KEY25     DIM       8
.
          OPEN      DISMASA1,"dismasaf"
          OPEN      DISPATA1,"dispataf"
          OPEN      DISPATA2,"dispataf"
          OPEN      DISPTLA1,"disptlaf"
.
CHKDIS00  MOVE     ZERO,EXIT
          PACK     KEY17,URNUMBER,SP70
          CALL     RSDSPAT1
CHKDIS10  CALL     RKDSPAT1
          BRANCH   OVRCD,CHKDIS99

          MATCH    URNUMBER,DSPTURNO
          GOTO     CHKDIS99 IF NOT EQUAL

          MATCH    SP70,DSPTEDAT
          IF       @EQUAL | @EOS
            MOVE     ONE,EXIT
            GOTO     CHKDIS99  
          ENDIF
.
          GOTO     CHKDIS10
.
.CHKDIS00  MOVE     ZERO,EXIT
.          PACK     KEY25,URNUMBER,SP70
.          CALL     RSDSPTL1
.          CALL     RKDSPTL1
.          BRANCH   OVRCD,CHKDIS99
.
.          MATCH    URNUMBER,DSPLURNO
.          GOTO     CHKDIS99 IF NOT EQUAL
.
.          MOVE     ONE,EXIT
.
.          GOTO     CHKDIS99
.
          INC      DISMASIO/INC
          INC      DISPATIO/INC
          INC      DISPTLIO/INC 
          INC      IBAOVRIO/INC
.
CHKDIS99  ENDPROC
.
.*****************************************************************************
.         DSAL0000 - disability alert string
.*****************************************************************************
          DEFPROC   DISPALRT
.
          INC       PATALRFD/INC
.
CURRDATE  DIM       8
.
          OPEN      PATALRT1,"patalrtf";
          OPEN      PATMWSA1,"patmwsaf";
.
          READ      CONTROLF,HUND14;*250,PTCNCUDA
.
          MATCH     SP70,PTCNCUDA
          GOTO      DSAL9000 IF EQUAL
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
. TSK 0837025 - determine if at least one active disability alert exists
.
          PACK      KEY16,PURNO,ANSH,PTCNCUDA,SP70
          CALL      RSPTALR1
DSAL0500  CALL      RKPTALR1
          BRANCH    OVRCD,DSAL9000
.
          MATCH     PURNO,PTALURNO
          GOTO      DSAL9000 IF NOT EQUAL
.
          PACK      DIM2,ANSH,PTCNCUDA
          MATCH     PTALCATG,DIM2
          GOTO      DSAL9000 IF NOT EQUAL        * disability alert ?
.
          MATCH     SP70,PTALEDAT
          GOTO      DSAL0800 IF EQUAL            * no alert end date
.
          MATCH     PTALEDAT,CURRDATE
          GOTO      DSAL0500 IF NOT LESS         * alert end date passed ?
.
DSAL0800  MATCH     SP70,PTALDTIN
          GOTO      DSAL0900 IF EQUAL            * no alert inactive date
.
          MATCH     PTALDTIN,CURRDATE
          GOTO      DSAL0500 IF NOT LESS         * alert inactive date passed ?
.
DSAL0900  MOVE      ZERO,FORM1
.
DSAL1000  PACK      KEY16,PURNO,SP70
          CALL      RSPTALR1
DSAL2000  CALL      RKPTALR1
          BRANCH    OVRCD,DSAL3000
.
          MATCH     PTALURNO,PURNO
          GOTO      DSAL3000 IF NOT EQUAL
.
. CAR 320331 - ignore micro, risk and medical alerts as these already have an
.              icon appearing in the banner elsewhere.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,DSAL2000
.
          MATCH     "M",TCINDC5
          GOTO      DSAL2000 IF EQUAL
.
          MATCH     "B",TCINDC5
          GOTO      DSAL2000 IF EQUAL
.
          MATCH     "R",TCINDC5
          GOTO      DSAL2000 IF EQUAL
.
          ADD       ONE,FORM1
          COMPARE   TWO,FORM1
          GOTO      DSAL9200 IF EQUAL
          GOTO      DSAL2000
.
DSAL3000  COMPARE   ONE,FORM1
          GOTO      DSAL9100 IF EQUAL
.
          GOTO      DSAL9000
.
DSAL9000  MOVE      ZERO,EXIT                      * zero
          GOTO      DSAL9999
.
DSAL9100  MOVE      ONE,EXIT                       * one
          PACK      KEY14,PURNO,SP70
          CALL      RSPTMWS1
          CALL      RKPTMWS1
          BRANCH    OVRCD,DSAL9999
.
          MATCH     PURNO,PTMWURNO
          GOTO      DSAL9200 IF EQUAL
.
          GOTO      DSAL9999
.
DSAL9200  MOVE      TWO,EXIT                      * two
          GOTO      DSAL9999
.
          INC       PATALRIO/INC
          INC       IBAOVRIO/INC
.
DSAL9999  CLOSE     PATALRT1
          CLOSE     PATMWSA1
          ENDPROC

+
.*******************************************************************************
.*                CHKBMIIN                                                     *
.*       Check for Patient Body Mass Index                                     *
.*******************************************************************************
          DEFPROC   CHKBMIIN
.
          INC       PATATRFD/INC
.
KEY35     DIM       35
BODMASIN  DIM       5
FORM2P1   FORM      2.1
FORM2P1A  FORM      2.1
FORM4P2   FORM      4.2
FORM6P1   FORM      6.1
FORM6P1A  FORM      6.1

.
          MOVE      SP5,D5
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATATRA1,"patatraf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKBMI90
.
          READ      CONTROLF,HUND20;*132,PTCNUNDW,*137,PTCNHEFR:
                                    *142,PTCNHETO,*147,PTCNOVRW,*152,PTCNOBES
.
CHKBMI00  MATCH     SP70,URNUMBER
          GOTO      CHKBMI90 IF EQUAL
          GOTO      CHKBMI90 IF EOS
.
          PACK      KEY35,URNUMBER,Z70
          CALL      RSPTATR1
CHKBMI10  CALL      RPPTATR1
          BRANCH    OVRCD,CHKBMI90
.
          MATCH     URNUMBER,PTARURNO
          GOTO      CHKBMI90 IF NOT EQUAL
.
          MATCH     "1",PTARDELR
          GOTO      CHKBMI10 IF EQUAL
.
          MATCH     "001",PTARTYPE
          GOTO      CHKBMI10 IF NOT EQUAL
.
          MOVE      SP70,BODMASIN
          MOVE      ZERO,FORM6P1                    * Height work variable
          MOVE      ZERO,FORM6P1A                   * Weight work variable
          MOVE      ZERO,FORM2P1                    * BMI work variable
          MOVE      ZERO,FORM4P2                    * Height work variable
.
          MATCH     SP70,PTARVAL3
          IF        !@EQUAL
            SQUEEZE   PTARVAL3
            MOVE      PTARVAL3,BODMASIN
            GOTO      CHKBMI50
          ENDIF
.
          MATCH     SP70,PTARVAL1                   * Height (cms)
          GOTO      CHKBMI90 IF EQUAL
          GOTO      CHKBMI90 IF EOS
.
          MATCH     SP70,PTARVAL2                   * Weight (kgs)
          GOTO      CHKBMI90 IF EQUAL
          GOTO      CHKBMI90 IF EOS
.
          SQUEEZE   PTARVAL1
          SQUEEZE   PTARVAL2
          MOVE      PTARVAL1,FORM6P1                * Move height into work
          MOVE      PTARVAL2,FORM6P1A               * Move weight into work
.
          COMPARE   ZERO,FORM6P1
          GOTO      CHKBMI90 IF EQUAL
.
          COMPARE   ZERO,FORM6P1A
          GOTO      CHKBMI90 IF EQUAL
.
          ASSIGN    (FORM6P1 / 100),FORM4P2         * Convert height cm to m
          ASSIGN    (FORM4P2 * FORM4P2),FORM4P2     * Square of height m
.
          ASSIGN    (FORM6P1A / FORM4P2),FORM2P1    * BMI Calculation
.
          MOVE      FORM2P1,BODMASIN
CHKBMI50  PACK      BODMASIN,BODMASIN,SP70
          LJUSTIFY  BODMASIN
.
          MATCH     SP70,BODMASIN
          GOTO      CHKBMI90 IF EQUAL
          GOTO      CHKBMI90 IF EOS
.
          MOVE      ZERO,FORM2P1
          MOVE      ZERO,FORM2P1A
          SQUEEZE   PTCNUNDW
          SQUEEZE   BODMASIN
          MOVE      PTCNUNDW,FORM2P1
          MOVE      BODMASIN,FORM2P1A
.
          COMPARE   FORM2P1A,FORM2P1
          GOTO      CHKBMI95 IF NOT LESS
.
.  * patient is not underweight - check if they are less than overweight
. (this will put them in the normal range)
.
          MOVE      ZERO,FORM2P1
          MOVE      ZERO,FORM2P1A
          SQUEEZE   PTCNOVRW
          SQUEEZE   BODMASIN
          MOVE      PTCNOVRW,FORM2P1
          MOVE      BODMASIN,FORM2P1A
.
          COMPARE   FORM2P1,FORM2P1A
          GOTO      CHKBMI97 IF LESS
.
.  * patient is not in the normal range  - check if they are less than obese
. (this will put them in the overweight range)
.
          MOVE      ZERO,FORM2P1
          MOVE      ZERO,FORM2P1A
          SQUEEZE   PTCNOBES
          SQUEEZE   BODMASIN
          MOVE      PTCNOBES,FORM2P1
          MOVE      BODMASIN,FORM2P1A
.
          COMPARE   FORM2P1,FORM2P1A
          GOTO      CHKBMI98 IF LESS
.
          GOTO      CHKBMI96
.
CHKBMI90  CALL      CLPATATR
.
          MOVE      ZERO,EXIT
          GOTO      CHKBMI99
.
CHKBMI95  MOVE      ONE,EXIT           * underweight bmi exists
          MOVE      BODMASIN,D5
          GOTO      CHKBMI99
.
CHKBMI96  MOVE      TWO,EXIT           * obese bmi exists
          MOVE      BODMASIN,D5
          GOTO      CHKBMI99
.
CHKBMI97  MOVE      THREE,EXIT         * normal bmi exists
          MOVE      BODMASIN,D5
          GOTO      CHKBMI99
.
CHKBMI98  MOVE      FOUR,EXIT          * overweight bmi exists
          MOVE      BODMASIN,D5
          GOTO      CHKBMI99
.
          INC       CLPATATR
          INC       PATATRIO/INC
          INC       IBAOVRIO/INC
.
CHKBMI99  ENDPROC
+
.******************************************************************************
.*  LEAV0000
.*  Get Type of Leave from transfer file
.*****************************************************************************
.
          DEFPROC   LEAV0000
.
          INC       PATTRNFD/INC
.
          MOVE      ZERO,EXIT
          OPEN      PATTRAN2,"pattranf"
.
          MOVE      SP10,CODE
          PACK      KEY30,PVIBILL,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,LEAV9500
.
          MATCH     PVIBILL,TADMN          * Same visit number ?
          GOTO      LEAV9500 IF NOT EQUAL  * No
.
          MATCH     SP3,PTTRLTYP
          GOTO      LEAV9500 IF EQUAL
          MOVE      PTTRLTYP,CODE          * Type of Leave
          MOVE      ZERO,EXIT
          GOTO      LEAV9999
.
LEAV9500  MOVE      ONE,EXIT
          GOTO      LEAV9999
.
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
.
LEAV9999  CLOSE     PATTRAN2
          ENDPROC
+
.*****************************************************************************
.*  CHKPMHSR
.*  Procedure to check for active or waiting primary mental health referral
.*  Exit=0  No active or waiting primary mental health referral
.*       1  An active or waiting primary mental health referral
.*****************************************************************************
          DEFPROC   CHKPMHSR
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       ALLDEPFD/INC
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALLREFA2,"allrefaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKPMH99
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ALLDEPA1,"alldepaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CHKPMH99
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSALREF2
CHKPMH10  CALL      RKALREF2                * Read through referral table
          BRANCH    OVRCD,CHKPMH99
.
          MATCH     URNUMBER,ALREURNO       * Check for same ur number
          GOTO      CHKPMH99 IF NOT EQUAL
.
          MATCH     "1",ALRESTAT            * Check for active referral
          GOTO      CHKPMH50 IF EQUAL
.
          MATCH     "0",ALRESTAT            * Check for waiting referral
          GOTO      CHKPMH50 IF EQUAL
.
          GOTO      CHKPMH10
.
CHKPMH50  MATCH     "1",ALREUYN4            * Check for SACS referral
          GOTO      CHKPMH10 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKPMH10
.
          MATCH     "A",TCINDC3
          IF        @EQUAL
            MATCH     "M",TCINDC4
            IF        @EQUAL
              GOTO      CHKPMH95
            ENDIF
          ENDIF
.
          GOTO      CHKPMH10
.
CHKPMH95  MOVE      ONE,EXIT
          GOTO      CHKPMH99
.
          INC       ALLDEPIO/INC
          INC       ALLREFIO/INC
          INC       IBAOVRIO/INC
.
CHKPMH99  CLOSE     ALLREFA2
.
          ENDPROC
+
.*****************************************************************************
.*  CHKABOIN
.*  Procedure to check for category VA, Ind 4 and Aboriginality
.*  Exit=0  Either not Aboriginal or category VA not set to D
.*       1  Aboriginal and category VA indicatory 4 value set to D
.*****************************************************************************
          DEFPROC   CHKABOIN
.         
.
          ENDPROC
+
.------------------------------------------------------------
. If the patient name format parameter is set to display the
. preferred name, display it for the patient header
. Add <b>[Preferred Surname, Preferred Given Name]</b>
.------------------------------------------------------------
DSPPNM00  READ      CONTROLF,HUND01;*45,CWEBPNAM
          MOVE      ZERO,F2
          MOVE      CWEBPNAM,F2
.
          IF        F2<29 | F2 > 42
            GOTO      DSPPNM99           * Not displaying preferred name
          ENDIF
.
          MOVE      PMPXPFSN,KEY20       * Preferred surname in KEY20
          REP       UPPLOW,PTMASNAM      * Upper case
          REP       "#" ",KEY20          * Remove double quotes
          STRIP     KEY20
.
          MOVE      PMPXPFGN,KEY25       * Preferred given name in KEY25
          REP       "#" ",KEY25          * Remove double quotes
          CLEAR     DISPNAME
          PACK      NAME35,KEY25,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,KEY25
          STRIP     KEY25
.
          CLEAR     FORMATNM
          MOVE      ZERO,F1              * No preferred name printed
.
          MATCH     SP70,KEY20           * Preferred surname populated
          IF        !@EQUAL & !@EOS
            APPEND    "<b>[",FORMATNM
            APPEND    KEY20,FORMATNM
            MOVE      ONE,F1             * Preferred name printed
          ENDIF
.
          MATCH     SP70,KEY25
          IF        !@EQUAL & !@EOS
            IF        F1=1
              APPEND    SP1,FORMATNM     * preferred given name
            ELSE
              APPEND    "<b>[",FORMATNM  * Start preferred name
            ENDIF
.
            APPEND    KEY25,FORMATNM
            MOVE      ONE,F1             * Preferred name printed
          ENDIF
.
          IF        F1=1
            APPEND    "]</b>",FORMATNM  * End printed preferred name
          ENDIF

          RESET    FORMATNM
          STRIP    FORMATNM
.
          IF       F1=1
            WRITE    HTMLFILE;*+,FORMATNM;
          ENDIF
.
DSPPNM99  RETURN
.------------------------------------------------------------
. Procedure to display the theatre status in the patient
. header after the unit (if any) is displayed
.------------------------------------------------------------
          DEFPROC   THETSTHD
.
          INC       PMSCURFD/INC
          INC       OPRCONFD/INC
          INC       OPROPRFD/INC
. 
F2        FORM      2
THETSTAT  DIM       15
THETST01  INIT      "Holding"
THETST02  INIT      "Anaes"
THETST03  INIT      "Theatre"
THETST04  INIT      "Abandoned"
THETST05  INIT      "Recovery-Front"
THETST06  INIT      "Recovery-Back"
THETST07  INIT      "Recovery-Day"
THETST08  INIT      "Ready to Depart"
THETST09  INIT      "Exit"
THETST10  INIT      "ICU"
THETST11  INIT      "Deceased"
THETST12  INIT      "Transferred"
THETST13  INIT      "Cancelled"
.
.
THETST00  READ      CONTROLF,HUND13;*67,OPCNTHED
          MATCH     "1",OPCNTHED             * Display theatre status in
          GOTO      THETST99 IF NOT EQUAL    * the patient header
.
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      OPROPRA1,"opropraf"

          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMCUR1                 * Read current patient file
          BRANCH    OVRCD,THETST90
.
          MATCH     "3",PMCUSYST             * Inpatient Visis
          GOTO      THETST90 IF NOT EQUAL
.
          MATCH     SP70,PMCUTSTA            * Theatre Status
          GOTO      THETST90 IF EQUAL
.
          MOVE      SP70,THETSTAT
          MOVE      ZERO,F2
          MOVE      PMCUTSTA,F2
          LOAD      THETSTAT,F2,THETST01,THETST02,THETST03,THETST04,THETST05:
                               THETST06,THETST07,THETST08,THETST09,THETST10:
                               THETST11,THETST12,THETST13
.
          MATCH     SP70,THETSTAT            * Invalid status
          GOTO      THETST90 IF EQUAL
.
          MOVE      SP70,OPRMDESC          
          MATCH     SP70,PMCUTLOC
          IF        !@EQUAL
            PACK      KEY6,PMCUTLOC,SP70
            CALL      RDOPOPR1               * Read operating room        
            IF        OVRCD=1
              MOVE      PMCUTLOC,OPRMDESC          
            ENDIF
          ENDIF
. 
          STRIP     THETSTAT
          WRITE     HTMLFILE;*+,"<span style='color:red;'>(",THETSTAT;
.
          MATCH     SP70,OPRMDESC
          IF        !@EQUAL
            STRIP     OPRMDESC
            WRITE     HTMLFILE;*+," - ",OPRMDESC;
          ENDIF
.
          WRITE     HTMLFILE;")</span>";
.
THETST90  CLOSE     PMSCURA1
          CLOSE     OPROPRA1
                                       
          GOTO      THETST99
.
          INC       PMSCURIO/INC
          INC       OPROPRIO/INC
          INC       IBAOVRIO/INC
.
THETST99  ENDPROC

.------------------------------------------------------------
. Procedure to display the patient visit type in the patient
. header before the visit number               
.------------------------------------------------------------
          DEFPROC   DSPVISTP
.
          INC       WATTR1FD/INC
          INC       BOKRC1FD/INC
          INC       PATVISFD/INC
          INC       OUTPREFD/INC
          INC       OUTCANFD/INC
          INC       OUTSITFD/INC
          INC       PMSEVTFD/INC
.
SAVSPRFX  DIM       3                            * Save site prefix
.
          OPEN      WATTR1A1,"wattr1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      OUTPREA1,"outpreaf"
          OPEN      OUTSITA3,"outsitaf"
          OPEN      PMSEVTA1,"pmsevtaf"
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDVISA1                      * Read Visit File
          BRANCH    OVRCD,DSPVIS10

          BRANCH    PVITYPE,DSPVIS50,DSPVIS55,DSPVIS60,DSPVIS99,DSPVIS99:
                            DSPVIS99,DSPVIS99,DSPVIS99,DSPVIS70
.
DSPVIS10  PACK      KEY19,URNUMBER,SP70          * Read Waiting List
          CALL      RDSTREA1
DSPVIS11  CALL      RDKTREA1 
          BRANCH    OVRCD,DSPVIS20
.
          MATCH     URNUMBER,WMURNO
          GOTO      DSPVIS20 IF NOT EQUAL
.
          PACK      KEY8,WMBOOK,SP70
          MATCH     ADMISSNO,KEY8  
          GOTO      DSPVIS11 IF NOT EQUAL
.
          WRITE     HTMLFILE;"W/L";
          GOTO      DSPVIS99
.
DSPVIS20  PACK      KEY8,ADMISSNO,SP70           * Read Booking file
          CALL      RDBKREC1
          BRANCH    OVRCD,DSPVIS30    
.
          WRITE     HTMLFILE;"Book";
          GOTO      DSPVIS99
.
DSPVIS30  PACK      KEY8,ADMISSNO,SP70           * Read OP Booking
          CALL      RDPREA1     
          BRANCH    OVRCD,DSPVIS40
.
          WRITE     HTMLFILE;"OP";
          GOTO      DSPVIS99
.
DSPVIS40  MOVE      "out",OSTFILE                * Try most likely site
          PACK      KEY8,OSTFILE,FILCANA1        * prefix out
.
          TRAP      OVERCOND IF IO
          OPEN      OUTCANA1,KEY8
          TRAPCLR   IO
          BRANCH    OVRCD,DSPVIS45
.
          PACK      KEY8,ADMISSNO,SP70           * Read OP Cancellation
          CALL      RDOTCAN1     
          BRANCH    OVRCD,DSPVIS45
.
          WRITE     HTMLFILE;"OP";
          GOTO      DSPVIS99
.
DSPVIS45  MOVE      SP70,SAVSPRFX                * Clear saved prefix
          PACK      KEY9,SP70                    * Check all site prefixes
          CALL      RDSSITA3
DSPVIS46  CALL      RDKSITA3
          BRANCH    OVRCD,DSPVIS99
.
          MATCH     SP70,OSTFILE                 * Skip if prefix is blank
          GOTO      DSPVIS46 IF EQUAL 
.
          MATCH     OSTFILE,SAVSPRFX             * Skip if prefix already
          GOTO      DSPVIS46 IF EQUAL            * checked
.
          MOVE      OSTFILE,SAVSPRFX             * Save site prefix
          PACK      KEY8,OSTFILE,FILCANA1        * try site prefix
.
          CLOSE     OUTCANA1
.
          TRAP      OVERCOND IF IO
          OPEN      OUTCANA1,KEY8
          TRAPCLR   IO
          BRANCH    OVRCD,DSPVIS46
.
          PACK      KEY8,ADMISSNO,SP70           * Read OP Cancellation
          CALL      RDOTCAN1     
          BRANCH    OVRCD,DSPVIS46
.
          WRITE     HTMLFILE;"OP";
          GOTO      DSPVIS99
.
DSPVIS50  WRITE     HTMLFILE;"EMG";              * Emergency
          GOTO      DSPVIS99 
.
DSPVIS55  WRITE     HTMLFILE;"OP";               * Outpatient
          GOTO      DSPVIS99 
.
DSPVIS60  WRITE     HTMLFILE;"IP";               * Inpatient
          GOTO      DSPVIS99 
.
DSPVIS70  MATCH     " 2",PVISYST
          IF        @EQUAL
            WRITE     HTMLFILE;"RF";             * A/H Referral
            GOTO      DSPVIS99
          ENDIF
.
          MATCH     " 1",PVISYST                 * Event
          GOTO      DSPVIS99 IF NOT EQUAL
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMEVT1                     * Read Event
          BRANCH    OVRCD,DSPVIS99
.
          MOVE      "EV",CATEGORY
          MOVE      PMEVEVNT,CODE
          CALL      GETCOD00
.
          MATCH     "E",TCINDC1                   * Emergency Event
          IF        @EQUAL
            WRITE     HTMLFILE;"EME";
            GOTO      DSPVIS99
          ENDIF
.
          MATCH     "I",TCINDC1                  * Inpatient Event
          IF        @EQUAL
            WRITE     HTMLFILE;"IPE";
            GOTO      DSPVIS99
          ENDIF
.
          MATCH     "O",TCINDC1                  * Outpatient Event
          IF        @EQUAL
            WRITE     HTMLFILE;"OPE";
            GOTO      DSPVIS99
          ENDIF
.
          GOTO      DSPVIS99
.
          INC       WATTR1IO/INC
          INC       BOKRC1IO/INC
          INC       PATVISIO/INC
          INC       OUTPREIO/INC
          INC       OUTCANIO/INC
          INC       OUTSITIO/INC
          INC       PMSEVTIO/INC
          INC       IBAOVRIO/INC

DSPVIS99  CLOSE     WATTR1A1                     * Close files opened in proc
          CLOSE     BOKRC1A1
          CLOSE     PATVISA1
          CLOSE     OUTPREA1
          CLOSE     OUTSITA3
          CLOSE     PMSEVTA1
          CLOSE     OUTCANA1
.
          ENDPROC
