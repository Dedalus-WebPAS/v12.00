.***************************************************************************
.*   System          : Theatre System                                      *
.*   Include Function: OPUPDURA.PBL                                        *
.*   Description     : Update the U/R audit file                           *
.***************************************************************************
.*   Date            : 21/12/1990                                          *
.*   Author          : Graeme Williams                                     *
.*   Modifications   :                                                     *
.*         V5.07.00    14/10/1998  Jim Stathopoulos                        *
.*                     507 Changes                                         *
.***************************************************************************
.*                     14/07/1993  Steve Armstrong                         *
.*                     Changed KEY13 to KEY15 for second index of paturadf *
.***************************************************************************
.*   Parameters      : PURNO    U/R number                                 .
.*                     CPMIAD   System parameter to say if U/R audit is on .
.***************************************************************************
.*   Open Files      : PATURAD1                                            .
.***************************************************************************
.*   Function        : Updates the U/R audit (if switched on) with a new   .
.*                     change record.                                      .
.*                                                                         .
.***************************************************************************
OPUPDURA  BRANCH    CPMIAD,OURAD010          * Check if system parameter is on
          GOTO      OURAD999                 * Parameter is off.
.
.         Find the last record in the U/R audit file for this U/R
.
OURAD010  OPEN      PATURAD2,"paturadf"      * Open U/R index of file
          MOVE      SP8,XDATE                * Initialise date of last change
          PACK      KEY17,PURNO,ANSC,SP10
          CALL      RDSURAD2                 * Position at start of U/R
OURAD100  CALL      RDKURAD2                 * Read the next record
          BRANCH    OVRCD,OURAD200           * Finished U/R number
.
          MATCH     PURNO,URURNO             * Still same U/R number ?
          GOTO      OURAD200 IF NOT EQUAL    * No. Finished U/R number
.
          MATCH     ANSC,URRTYPE             * Still change record ?
          GOTO      OURAD200 IF NOT EQUAL    * No. Finished U/R number
.
          MOVE      URDATE,XDATE             * Save the date of the change
          GOTO      OURAD100
.
.         We have found the last change record. Now update it with the
.         current date, or post a new record, as required.
.
OURAD200  CLOSE     PATURAD2                 * Close U/R index of file
          OPEN      PATURAD1,"paturadf"
.
          MATCH     SP8,XDATE                * Do we have a saved date ?
          GOTO      OURAD500 IF EQUAL        * No. Write a new record.
.
.         Update the last "C" record with the current date
.
          PACK      KEY17,ANSC,XDATE,PURNO
          CALL      RDURAD1
          BRANCH    OVRCD,OURAD500           * Should never occur
.
          PACK      URDATE,CCC,CYY,CMM,CDD       * Change to the current date
          REP       " 0",URDATE
          MOVE      THREE,URSYST             * Changed by inpatient system
.
          CALL      UPURAD1                  * Update the changes
          GOTO      OURAD999                 * Finished
.
.         Post a new record to the U/R audit file
.
OURAD500  MOVE      PURNO,URURNO             * Set up U/R number
          MOVE      ANSC,URRTYPE             * Write a change record
          PACK      URDATE,CCC,CYY,CMM,CDD       * Change on the current date
          REP       " 0",URDATE
          MOVE      THREE,URSYST             * Changed by inpatient system
.
          PACK      KEY17,URRTYPE,URDATE,URURNO
          CALL      RDURAD1                  * Check if it already exists
          COMPARE   ZERO,OVRCD
          GOTO      OURAD999 IF EQUAL        * Already exists. Do nothing
.
          CALL      WRURAD1
.
OURAD999  CLOSE     PATURAD1
          RETURN
