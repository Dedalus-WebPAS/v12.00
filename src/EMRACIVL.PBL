.******************************************************************************
.*                                  EMRACIVL                                  *
.* Validate EMRACIFD effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY38 populated with -                                         *
.*                      - ECDG Subcode                                        *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid effective from and to dates                    *
.*                   1 = Invalid effective from and to dates                  *
.*             ENDKEY38 = Key to record that requires an auto end date update *
.*                                                                            *
.* Modifications:                                                             *
.* V11.01.01  20/08/2021  Thanh T         TSK 0905305                         *
.*            Renamed EMRACHVL.PBL to EMRACIVL.PBL to be consistent with      *
.*            table name                                                      *
.* V11.01.00  02/08/2021  DA Sarkies      TSK 0905305                         *
.*            Created include                                                 *
.******************************************************************************
.
RAVL0000  MOVE      SP70,ENDKEY38           * Clear auto end key
          MOVE      KEY38,SAVKEY30          * Save ECDG Subcategory
.
          CALL      RDEMACI1                * Record on file so must be an
          BRANCH    OVRCD,RAVL5000          * update
.
          MATCH     VALTDATE,EMRATDAT       * No change in effective from and
          GOTO      RAVL8000 IF EQUAL       * to dates so exit
.
RAVL5000  CALL      RSEMACI1                * Check for the same ECDG sub cat
          CALL      RPEMACI1                * with an earlier effective from
          BRANCH    OVRCD,RAVL6000          * date
.
          MATCH     SAVKEY30,EMRASUBV       * Same ECDG Subcategory
          GOTO      RAVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMRATDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY38,EMRASUBV,EMRAFDAT,SP70 * auto end date record key
            GOTO      RAVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMRATDAT
          IF        !@LESS
            GOTO      RAVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RAVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMRATDAT
          IF        !@LESS
            GOTO      RAVL9000              * Effective to date overlap
          ENDIF
.
RAVL6000  CALL      RSEMACI1                * Check for the same ECDG sub cat
          CALL      RKEMACI1                * with an earlier effective from
          BRANCH    OVRCD,RAVL8000          * date
.
          MATCH     SAVKEY30,EMRASUBV       * Same ECDG Subcategory
          GOTO      RAVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RAVL9000 IF EQUAL
.
          MATCH     EMRAFDAT,VALFDATE
          IF        !@LESS
            GOTO      RAVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMRATDAT,VALTDATE
          IF        !@LESS
            GOTO      RAVL9000              * Effective to date overlap
          ENDIF
.
RAVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      RAVL9999
.
RAVL9000  MOVE      SP70,ENDKEY38
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      RAVL9999
.
RAVL9999  RETURN
