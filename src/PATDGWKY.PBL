.******************************************************************************
.*  Include Name  : PATDGWKY.PBL                                              *
.*  Description   : Keyin of a Health fund                                    *
.*  Author        : Wilma Flintstone                                          *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF4 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory   (0=No  1=Yes)             *
.*                                                                            *
.*  Returns       : DRGCODE - Keyed in code                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing/Spaces input                           *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          :                                                           *
.*        V9.08.01  19/04/2007  Mike Laming   CAR 137125 HDP 2007             *
.*                  Add DRG Version to Key fields for reads to PATDGWaf       *
.*        V5.08.B01 25/02/2000  Greg Horvat      SRF 637565                   *
.*                  Changes for 4 character DRGs                              *
.*                                                                            *
.******************************************************************************
PATDGWKY
KYDGW010  MATCH     SP6,CKYIDEF4                  * using a default ?
          GOTO      KYDGW050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN4:      * no default used
                    *PECOL:EVERT,*V2LON,DRGCODE:
                    *PECOL:EVERT,*DV,DRGCODE
          GOTO      KYDGW080
.
KYDGW050  MOVE      CKYIDEF4,DRGCODE
          KEYIN     *PECOL:EVERT,*DV,CKYIDEF4:     * default used
                    *PECOL:EVERT,*V2LON,*RV,DRGCODE:
                    *PECOL:EVERT,*DV,DRGCODE
.
.         check what was entered
.
KYDGW080  CALL      DGWCK000
          BRANCH    EXIT,KYDGW100,KYDGW705,KYDGW805,KYDGW905,KYDGW805
.                         "?"      valid    nothing exitchar spaces
          GOTO      KYDGW010
.
.         a "?" was input
.
KYDGW100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KYDGW105  MOVE      ONE,CNEWDS
          CALL      PATDGWDS
.
.         now ask for code while leaving displayed codes on screen
.
KYDGW150  MATCH     SP6,CKYIDEF4                  * using a default
          GOTO      KYDGW200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"DRG Code : ":
                    *P12:24,*DV,UNDLN4:      * no default used
                    *P12:24,*V2LON,DRGCODE:
                    *P12:24,*DV,DRGCODE
          GOTO      KYDGW300
.
KYDGW200  MOVE      CKYIDEF4,DRGCODE
          KEYIN     *P1:24,*EL,*DV,"DRG Code : ":
                    *P12:24,*DV,DRGCODE:     * default used
                    *P12:24,*V2LON,*RV,DRGCODE:
                    *P12:24,*DV,DRGCODE
.
.         repeat the validation of the code
.         check what was entered
.
KYDGW300  CALL      DGWCK000
          BRANCH    EXIT,KYDGW105,KYDGW700,KYDGW800,KYDGW900,KYDGW800
.                         "?"      valid    nothing exitchar spaces
.
          GOTO      KYDGW150
.
.         a valid code was entered 
. 
KYDGW700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DRGCODE
.
KYDGW705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KYDGW999 
.
.         nothing/spaces was input
.
KYDGW800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DRGCODE
.
KYDGW805  MOVE      SP70,PTDWDESC
          MOVE      SP4,DRGCODE
          MOVE      ONE,EXIT                       * Nothing entered
          GOTO      KYDGW999 
.
.         EXITCHAR input
.
KYDGW900  CALL      FRESCR00                      * free the screen saved
.
KYDGW905  MOVE      TWO,EXIT                      * EXITCHAR
.
KYDGW999  RETURN
+
.***************************************************************************
.*                              DGWCK000             Called by: PATFNDKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.*                          = 5   spaces                                   *
.***************************************************************************
DGWCK000  RESET     DRGCODE
          GOTO      DGWCK700 IF EOS
.
          ENDSET    DRGCODE
          APPEND    SP6,DRGCODE
          RESET     DRGCODE
.
          MATCH     EXITCHAR,DRGCODE
          GOTO      DGWCK600 IF EQUAL
.
          MATCH     SP4,DRGCODE
          GOTO      DGWCK750 IF EQUAL
.
          MATCH     QUEST,DRGCODE
          GOTO      DGWCK800 IF EQUAL
.
DGWCK500  MOVE      "999",KEY3                                        *I-137125
          PACK      KEY7,DRGCODE,KEY3,SP10       * was KEY4           *C-137125
          CALL      RSPTDGW1
          CALL      RPPTDGW1                                          *I-137125
          IF        CKYIMODE=ZERO
            BRANCH    OVRCD,DGWCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      DGWCK900 IF EQUAL
          ENDIF
.         
          IF        CKYIMODE=ZERO
            MATCH     PTDWDRGC,DRGCODE                                *I-137125
            GOTO      DGWCK900 IF NOT EQUAL                           *I-137125
          ELSE
            MATCH     PTDWDRGC,DRGCODE                                *I-137125
            GOTO      DGWCK900 IF EQUAL                               *I-137125
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      DGWCK999
.
. exitchar entered
.
DGWCK600  MOVE      FOUR,EXIT
          GOTO      DGWCK999
.
. nothing entered
.
DGWCK700  BRANCH    CKYIMAND,DGWCK950             * mandatory
          MOVE      SP70,PTDWDESC
          MOVE      SP4,DRGCODE
          MOVE      THREE,EXIT
          GOTO      DGWCK999
.
. spaces entered
.
DGWCK750  BRANCH    CKYIMAND,DGWCK950             * mandatory
          MOVE      SP70,PTDWDESC
          MOVE      SP4,DRGCODE
          MOVE      FIVE,EXIT
          GOTO      DGWCK999
.
. "?" entered
.
DGWCK800  MOVE      ONE,EXIT
          GOTO      DGWCK999
.
. invalid code entered
.
DGWCK900  DISPLAY   *P1:24,*EL,*B,"Invalid Code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      DGWCK999
.
DGWCK950  DISPLAY   *P1:24,*EL,*B,"Field is mandatory.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      DGWCK999
.
DGWCK999  RETURN
.
          INC       PATDGWDS
+
.

