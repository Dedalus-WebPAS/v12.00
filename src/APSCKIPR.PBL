.------------------------------------------------------------
.  File          :  
.
.  Function      :  Update Keyword Index File for 
.
.  Parameters    : 
.
.  Modifications :
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  APCKxxxx    DIM       xx    * Code Field Segment 1
.  APCKyyyy    DIM       xx    * Code Field Segment n
.  APCKKEYW     DIM       15    * Key Word
.  APCKSPA     DIM       10    * Spare
.
.  Index 1   APCKxxxx, APCKyyyy, APCKKEYW
.  Index 2   APCKKEYW, APCKxxxx, APCKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change APS to the System ID eg :%s/APS/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/DKI/g
.    Global Change APCK to the IO Call ID eg :%s/APCK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY20 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   APSCKIUP
.
          INC       APSCKIFD/INC
.
KEYINDEX  DIM       5         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSCKIA1,"apsckiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     APMACRD,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
.
          MOVE     APMACRD,KEY5      * Validate Record on File
          CALL     RDAPMA1 
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          PACK     KEY35,APMACRD,SP70
          CALL     RSAPMX2
GENR1100  CALL     RKAPMX2
          BRANCH   OVRCD,GENR1900
          MATCH    APMACRD,APMXCRD
          GOTO     GENR1900 IF NOT EQUAL
.
          MOVE     APMACRD,KEYINDEX
          MOVE     APMXREF,KEYWORDS
          CALL     BWORD000
.
          GOTO     GENR1100
.
GENR1900  CLOSE    APSCKIA1
.
GENR9999  GOTO     APSCKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY20,KEYINDEX,SP70
          CALL     RSAPCK1
          CALL     RKAPCK1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,APCKCRD
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY20,APCKCRD,APCKKWD,SP70
          CALL     DEAPCK1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY20,KEYINDEX,KEY15
          UNPACK    KEY20,APCKCRD,APCKKWD
          CALL      RDAPCK1
          IF        OVRCD=1
            CALL      WRAPCK1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       APSCKIIO/INC
          INC       IBAOVRIO/INC
.
APSCKIEP  ENDPROC                          * End of Procedure
.
