.----------------------------------------------------------------------
. SINLABCD - Print standard label include
.            Programs : IBASIN95  (Print Cost Centre Labels)
.
.            Requires : SINLABDF  (data definition)
.
.            Routines : 
.                       STLAB000  (store and print label data)
.                       PREND000  (end of  label print)
.                       LULAB000  (print lineup)
.                       PRLAB000  (print labels)
.
.----------------------------------------------------------------------
.----------------------------------------------------------------------
. Store Label Data (and print if required)
.   Requires : LLINE[A,B,1,2,3,4,5,6,7,8]        * data lines
.              NOCOPIES                          * number of copies
.   Uses     : LABNUM                            * current label number
.              [A,B,C]LINE[A,B,1,2,3,4,5,6,7,8]  * data lines
.              F2                                * label counter
.----------------------------------------------------------------------
STLAB000  MOVE      ZERO,F2            * clear copy counter
.
          WHILE     NOCOPIES>F2
            ADD       ONE,F2           * increment copy counter
            ADD       ONE,LABNUM       * increment label counter
.                                      * store data
            STORE     LLINEA,LABNUM,ALINEA,BLINEA,CLINEA
            STORE     LLINEB,LABNUM,ALINEB,BLINEB,CLINEB
            STORE     LLINE1,LABNUM,ALINE1,BLINE1,CLINE1
            STORE     LLINE2,LABNUM,ALINE2,BLINE2,CLINE2
            STORE     LLINE3,LABNUM,ALINE3,BLINE3,CLINE3
            STORE     LLINE4,LABNUM,ALINE4,BLINE4,CLINE4
            STORE     LLINE5,LABNUM,ALINE5,BLINE5,CLINE5
            STORE     LLINE6,LABNUM,ALINE6,BLINE6,CLINE6
            STORE     LLINE7,LABNUM,ALINE7,BLINE7,CLINE7
            STORE     LLINE8,LABNUM,ALINE8,BLINE8,CLINE8
.                                      * print data if up to req no. of labels
            IF        LABNUM=HLABPG
              MOVE      LABNUM,NOLABELS
              CALL      PRLAB000       * print labels
              MOVE      ZERO,LABNUM    * reset label no
            ENDIF
          DO
.
STLAB999  RETURN
.----------------------------------------------------------------------
. Print Labels
.   Requires : [A,B,C]LINE[A,B,1,2,3,4,5,6,7,8]  * data lines
.              NOLABELS                          * number of labels to print
.   Uses     : TABPOS2, TABPOS3                  * label collumns
.----------------------------------------------------------------------
PRLAB000
          BRANCH    NOLABELS,PRLAB100,PRLAB200,PRLAB300
          GOTO      PRLAB999           * no labels to print
.
.---- print 1 label ----
.
PRLAB100  
          IF        LENIND=2
            PRINT     *N,ALINEA:
                      *N,ALINEB
          ENDIF
          PRINT        ALINE1:
                    *N,ALINE2:
                    *N,ALINE3:
                    *N,ALINE4:
                    *N,ALINE5:
                    *N,ALINE6:
                    *N,ALINE7:
                    *N,ALINE8,*N
          GOTO      PRLAB999
.
.---- print 2 labels ----
.
PRLAB200  
          IF        LENIND=2
            PRINT     *N,ALINEA,*TABPOS2,BLINEA:
                      *N,ALINEB,*TABPOS2,BLINEB
          ENDIF
          PRINT        ALINE1,*TABPOS2,BLINE1:
                    *N,ALINE2,*TABPOS2,BLINE2:
                    *N,ALINE3,*TABPOS2,BLINE3:
                    *N,ALINE4,*TABPOS2,BLINE4:
                    *N,ALINE5,*TABPOS2,BLINE5:
                    *N,ALINE6,*TABPOS2,BLINE6:
                    *N,ALINE7,*TABPOS2,BLINE7:
                    *N,ALINE8,*TABPOS2,BLINE8,*N
          GOTO      PRLAB999
.
.---- print 3 labels ----
.
PRLAB300  
          IF        LENIND=2
            PRINT     *N,ALINEA,*TABPOS2,BLINEA,*TABPOS3,CLINEA:
                      *N,ALINEB,*TABPOS2,BLINEB,*TABPOS3,CLINEB
          ENDIF
          PRINT        ALINE1,*TABPOS2,BLINE1,*TABPOS3,CLINE1:
                    *N,ALINE2,*TABPOS2,BLINE2,*TABPOS3,CLINE2:
                    *N,ALINE3,*TABPOS2,BLINE3,*TABPOS3,CLINE3:
                    *N,ALINE4,*TABPOS2,BLINE4,*TABPOS3,CLINE4:
                    *N,ALINE5,*TABPOS2,BLINE5,*TABPOS3,CLINE5:
                    *N,ALINE6,*TABPOS2,BLINE6,*TABPOS3,CLINE6:
                    *N,ALINE7,*TABPOS2,BLINE7,*TABPOS3,CLINE7:
                    *N,ALINE8,*TABPOS2,BLINE8,*TABPOS3,CLINE8,*N
          GOTO      PRLAB999
.
PRLAB999  RETURN
.----------------------------------------------------------------------
.         Subroutine to set up a lineup print
.----------------------------------------------------------------------
LULAB000  
          IF        LENIND=2
            MOVE      "XX/XX/XX   XXXXXX    XXX.XX",ALINEA
            MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINEB
          ELSE
            MOVE      "                           ",ALINEA
            MOVE      "                           ",ALINEB
          ENDIF
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE1
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE2
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE3
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE4
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE5
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE6
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE7
          MOVE      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",ALINE8
.
          MOVE      ALINEA,BLINEA
          MOVE      ALINEB,BLINEB
          MOVE      ALINE1,BLINE1
          MOVE      ALINE2,BLINE2
          MOVE      ALINE3,BLINE3
          MOVE      ALINE4,BLINE4
          MOVE      ALINE5,BLINE5
          MOVE      ALINE6,BLINE6
          MOVE      ALINE7,BLINE7
          MOVE      ALINE8,BLINE8
.
          MOVE      ALINEA,CLINEA
          MOVE      ALINEB,CLINEB
          MOVE      ALINE1,CLINE1
          MOVE      ALINE2,CLINE2
          MOVE      ALINE3,CLINE3
          MOVE      ALINE4,CLINE4
          MOVE      ALINE5,CLINE5
          MOVE      ALINE6,CLINE6
          MOVE      ALINE7,CLINE7
          MOVE      ALINE8,CLINE8
.
          MOVE      HLABPG,NOLABELS
          CALL      PRLAB000           * print labels
.
LULAB999  RETURN
.----------------------------------------------------------------------
. Print end of labels routine
.   Requires : LLINE[A,B,1,2,3,4,5,6,7,8]        * data lines
.              LABNUM                            * current label number
.              [A,B,C]LINE[A,B,1,2,3,4,5,6,7,8]  * data lines
.----------------------------------------------------------------------
PREND000  
          MOVE      LABNUM,NOLABELS    * set up no. of labels left to print
          CALL      PRLAB000           * print labels
          MOVE      ZERO,LABNUM        * reset label number
.
PREND999  RETURN
.***************************************************************************
.          Subroutine to find the end of the string in LLINE1
.***************************************************************************
FINDEND   ENDSET    LLINE1
FINDLP1   CMATCH    SP1,LLINE1
          RETURN    IF NOT EQUAL
          BUMP      LLINE1,-1
          GOTO      FINDLP1 IF NOT EOS
.
          RESET     LLINE1
          RETURN
.
.***************************************************************************
.         Set up line 1
.***************************************************************************
SETLINE   PACK      LLINEA,CDATE,SP3,MODHOS,SP4,AMOUNT1
          RETURN
