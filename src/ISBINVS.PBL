.
.         ISBINVS
.         This include controls the printing of invoices.
.
.         MODIFICATIONS:
.         V12.00.02 23/09/2025  J.Tan          TSK 0966921
.                   Mod to remove tempfiles (KT2P0000)
.         V12.00.01 22/05/2025  J.Tan          TSK 0955096
.                   Added Alphanumeric visit number changes
.         V11.05.01 09/01/2025  J.Tan          TSK 0955356
.                   Mod to set FGSTFLAG for GST
.                   Added FININVN,FINADMN,FINURNO
.         V10.03.00 12/09/2013 J.Tan          CAR 283618
.                   Created new incluse for printing Independence Services bill
.
.*******************************************************************************
.         Printing Invoice for Independence services billing
.*******************************************************************************
PRIS0000
.         Check for a "A" record in the transfer file - No "A" rec., No invoice
.         Unless it is an outstanding debt
.
          MATCH     "00000000",ADATE
          GOTO      PRIS0100 IF EQUAL
.
          PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD OF PRIS9300
.
          MATCH     TADMN,AADMNO
          GOTO      PRIS9300 IF NOT EQUAL
.
          CMATCH    "A",TMOVE
          GOTO      PRIS9300 IF NOT EQUAL
.
PRIS0100  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSIDEA1,"pmsideaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSIDEA2,"pmsideaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSIDEA3,"pmsideaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSPBRA4,"pmspbraf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSPYRA1,"pmspyraf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSIVBA1,"pmsivbaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          TRAP      OVERCOND IF IO
          OPEN      PMSIVBA2,"pmsivbaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PRIS9400
.
          IF        IBCNUGST=2
            CALL      PMBS0000          * Check with primary MBS
            BRANCH    EXIT,PRIS9000     * yes, can not print invoice
          ENDIF
.
.         If there is no Payer breakdown, invoice will be raised as normal
.
          MOVE      SP70,PMIDINVN
          PACK      KEY34,AADMNO,PMIDINVN,SP70
          CALL      RSPMIDE3
          CALL      RKPMIDE3
          BRANCH    OVRCD,PRIS9400       * Print normal invoice
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      PRIS9400 IF NOT EQUAL   * print normal invoice
          MATCH     SP70,PMIDINVN
          GOTO      PRIS9400 IF NOT EQUAL   * print normal invoice
.
          CALL      SPYR0000            * set up payer for accomm.
.
          CALL      CIBR0000            * Check for Payers invoice breakdown
          BRANCH    EXIT,PRIS9400       * Print normal invoice
.
.
          COMPARE   TOTDAYS,CMAXINV
          GOTO      PRIS9100 IF LESS    * Invoice for too many days
          MOVE      THREE,PROGFLAG
.
          MOVE      ZERO,STRATEF
          MOVE      ZERO,STRATEH
          MOVE      ZERO,SPTTRLSR
          MOVE      ZERO,SPTTRLSP
          MOVE      ZERO,STEXTRA
          MOVE      ZERO,SAMTG
          UNPACK    SP70,LSDESC1,LSDESC2
          MOVE      ZERO,LOWFLAG
          MOVE      ZERO,SPTTRADP
          MOVE      ZERO,SPTTRADR
          MOVE      ZERO,SPTTRITF
.
          MOVE      ZERO,INVPRTD
          MOVE      ZERO,GSTFLAG
          MOVE      ZERO,GOVTOTAL
.
          MOVE      SP10,PTDSDDRG
          MOVE      AADMNO,KEY8
          MOVE      SP8,DDATE
          CALL      RDDSCH1
          MOVE      OVRCD,DISFLG
.
          OPEN      PATDO1A1,"patdo1af"
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=1
            UNPACK    SP70,DSNAME,DGNAME,DTITL
          ENDIF
.
          CALL      GETPNAM                 * Get the patient's name
.
          MOVE      ZERO,COMPFLG            * flag for processing cash
          CALL      GETCASH                 * Get any pending cash receipts
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,A10DATE
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
.         Use the discharge diagnosis if available
.
          BRANCH    DISFLG OF PRIS0200
          MATCH     SP70,DDIAG
          GOTO      PRIS0200 IF EQUAL
          MOVE      DDIAG,ADIAG1
          MOVE      DDIAG2,ADIAG2
.
          MATCH     SP9,DFMBS
          GOTO      PRIS0200 IF EQUAL
          MOVE      DFMBS,AMBS
.
PRIS0200  CALL      SETUPIV     * set up special variable for printing invoice
          CALL      ISBC0000    * set up invoice date
.
          CALL      GUNQ0000    * Get a Unique ID for all invoice raised
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
.         Loop through invoices payer breakdown file
.
          MOVE      SP70,SAVACLM
          MOVE      ZERO,LINCOUNT          * No of invoices printed
.
PRIS1000  MOVE      SP70,PMIDINVN
          PACK      KEY26,DAADMNO,PMIDINVN,SP70
          CALL      RSPMIDE2
          CALL      RKPMIDE2
          BRANCH    OVRCD,PRIS6000
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      PRIS6000 IF NOT EQUAL
          MATCH     SP70,PMIDINVN
          GOTO      PRIS6000 IF NOT EQUAL   * already been invoiced
.
          CALL      INRG0000                * Generate invoice range
          BRANCH    EXIT,PRIS9200
.
          MOVE      PMIDCLTY,SAVACLM        * save claim type
          CALL      PRINV000                * print invoice 
          ADD       ONE,LINCOUNT            * No of invoices printed
.
          CALL      CLSPRINT
          CALL      OPNPRINT
.
          GOTO      PRIS1000
.
.         Check if any Cash pending
.
PRIS6000  BRANCH    COMPFLG,PRIS8000
.
          CALL      CASHUPD                 * Cash update to default payer
.
.         Update the admission record with the changes
.
PRIS8000  PACK      KEY8,AADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,PRIS9999
.
          ADD       LINCOUNT,AINVPRT        * No of invoices
          ADD       ONE,ATRANNO             * Last transaction number
.
          MOVE      SP8,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          IF        OVRCD = 0
            MATCH     PINVDTE,DDATE
            IF        @EQUAL
              MOVE        ONE,ADSCHI
            ENDIF
          ENDIF
.
          MOVE      BACKDATE,ALACDTE      * end of Accommodation date
          IF        ASTAT=3
            MATCH     BACKDATE,DDATE
            IF        @LESS
              MOVE      DDATE,ALACDTE     * use discharge date
            ENDIF
          ENDIF
          REP       " 0",ALACDTE
.
          IF        ASTAT = 3
            MATCH     ALACDTE,DDATE
            IF        @EQUAL
             MOVE      ONE,ADSCHI
            ENDIF
          ENDIF
          CALL      UPLMISS1             * Update the admission record
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          IF        OVRCD=0
            MATCH     DDATE,BACKDATE
            IF        !@LESS
              CALL      DELIPEND         * remove invoice pending record
            ENDIF
          ENDIF
          MOVE      ZERO,EXIT
.
PRIS9000  MOVE      ZERO,ERRFLAG
          GOTO      PRIS9999
.
PRIS9100  MOVE      ONE,ERRFLAG          * Invoice more than max days
          GOTO      PRIS9999
.
PRIS9200  MOVE      TWO,ERRFLAG          * Invalid invoice range
          GOTO      PRIS9999
.
PRIS9300  MOVE      THREE,ERRFLAG        * Missing Transfer record
          GOTO      PRIS9999
.
PRIS9400  MOVE      FOUR,ERRFLAG         * Print normal invoice
          GOTO      PRIS9999
.
PRIS9999  RETURN
+
.*******************************************************************************
.         Get Unique ID
.*******************************************************************************
GUNQ0000  MOVE      ZERO,F10
          PACK      KEY26,DAADMNO,Z70
          CALL      RSPMIVB2
          CALL      RPPMIVB2
          BRANCH    OVRCD,GUNQ8000
.
          MATCH     DAADMNO,PMIVADMN         * Same admission no?
          GOTO      GUNQ8000 IF NOT EQUAL
.
          MOVE      PMIVUNIQ,F10
.
GUNQ8000  ADD       ONE,F10
          MOVE      F10,SAVEUNIQ
.
GUNQ9999  RETURN
+
.*******************************************************************************
.         Check Invoice payers breakdown 
.*******************************************************************************
CIBR0000  MOVE      SEVEN,PROGFLAG
.
.         Generate accommodation for Default payer for any reminder accom.charge
.
          MOVE      SP70,DEFPAYER
          MOVE      ZERO,DEFPAYCL
          MOVE      ZERO,TOTDAYS
.
          CALL      GDPY0000          * get Def.payer to pay reminder of inv.
.
.         Check for any reminder Accom.invoice amt to be paid by Default payer
.
          MOVE      ALACDTE,SFRDATE   * Accom.start date
          MOVE      ONE,PROGFLAG
          CALL      ISAC0000          * Get Contract & inv.total from all payers
.
          CALL      REIN00000
          BRANCH    EXIT,CIBR1000     * Payer(s) paid all accommodation
.
          MOVE      ONE,DEFPAYCL
          MOVE      SEVEN,PROGFLAG
          MOVE      DEFPAYER,PMPBCLTY    * Default payer
          MOVE      "1",PMPBDEFT
          CALL      ISAC0000
.
.         Processing Misc. item
.         If theatre fees file is not used for charging
.         item not need to be sorted.
.
CIBR1000 MOVE     ONE,TFEEIND              * Default to use theatre fee
.
         IF        IBCNMHOS=1
           MATCH     "1",PTHSTFEE
           GOTO      CIBR5000 IF EQUAL
         ELSE
           COMPARE   "1",PTCNTFEE
           GOTO      CIBR5000 IF EQUAL
         ENDIF
.
          MOVE      ZERO,TFEEIND            * Not using theatre fee flag
.
CIBR5000  MOVE      SEVEN,PROGFLAG
          CALL      GMSC0000                * Processing items
.
          CALL      KTMP0000
          CALL      KLMV0000
          CALL      KT2P0000
.
          MOVE      ZERO,EXIT
          GOTO      CIBR9999
.
CIBR9000  MOVE      ONE,EXIT
CIBR9999  RETURN
+
.*******************************************************************************
.         Get default payer who will pay Accommodation
.*******************************************************************************
GDPY0000  PACK      KEY26,DAADMNO,SP70
          CALL      RSPMPBR4
GDPY2000  CALL      RKPMPBR4
          BRANCH    OVRCD,GDPY9000
.
          MATCH     DAADMNO,PMPBADMN
          GOTO      GDPY9000 IF NOT EQUAL
.
          MATCH     "1",PMPBDEFT
          GOTO      GDPY2000 IF NOT EQUAL * Not default
.
          MOVE      PMPBCLTY,DEFPAYER     * Default payer
          GOTO      GDPY9999
.
GDPY9000  MOVE      ACLAIM,DEFPAYER       * Default to Admission claim code
.
GDPY9999  RETURN
+
.*******************************************************************************
.         Check for any reminder Accom.invoice amt to be paid by Default payer
.*******************************************************************************
REIN0000  MOVE      ZERO,FORM12P2
          MOVE      SP70,PMIDINVN
          MOVE      ONE,PMIDRTYP
          PACK      KEY34,AADMNO,PMIDINVN,PMIDRTYP,SP70
          CALL      RSPMIDE3
REIN1000  CALL      RKPMIDE3
          BRANCH    OVRCD,REIN3000
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      REIN3000 IF NOT EQUAL
          MATCH     SP70,PMIDINVN
          GOTO      REIN3000 IF NOT EQUAL
          MATCH     "1",PMIDRTYP
          GOTO      REIN3000 IF NOT EQUAL     * Not Accommodation
.
          ADD       PMIDAMNT,FORM12P2
          GOTO      REIN1000
.
REIN3000  COMPARE   ACCMTOT,FORM12P2          * Check with Total billing
          GOTO      REIN9000 IF NOT LESS      * payer paying all Accomm.
.
          MOVE      ZERO,EXIT                 * there is still reminder inv.amt
          GOTO      REIN9999
.
REIN9000  MOVE      ONE,EXIT                  * Payer paid all Accom.charges
.
REIN9999  RETURN
+
.*******************************************************************************
.         Print invoice for each payer
.*******************************************************************************
PRINV000  CALL      ZEROINV
          MOVE      PMIDCLTY,ACLAIM           * payer claim type
          CALL      PYRA0000                  * Get payer name as PRFA
.
.         Print the invoice heading section
.
          MOVE      AADMNO,PINVADM
          MOVE      AURNO,PINVUR
          MOVE      SAVACLM,PINVCLM
          MOVE      THREE,PINVSYS
          MOVE      SP6,PINVSITE
          MOVE      ZERO,ERRFLAG
          CALL      HEAD
.
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,TOTGST
          MOVE      ZERO,NODAYS
          MOVE      ZERO,TOTDAYS
          MOVE      ZERO,REBTOT
          MOVE      SP70,CONTRCID
          MOVE      ONE,ISBLFLAG
.
.         Loop through invoice breakdown
.
PRINV100  PACK      KEY25,DAADMNO,SAVACLM,SP70
          CALL      RSPMIDE1
          CALL      RKPMIDE1
          BRANCH    OVRCD,PRIS9999
.
          MATCH     DAADMNO,PMIDADMN
          GOTO      PRINV800 IF NOT EQUAL
          MATCH     PMIDCLTY,SAVACLM
          GOTO      PRINV800 IF NOT EQUAL   * different payer
          MATCH     SP70,PMIDINVN
          GOTO      PRINV800 IF NOT EQUAL
.
          CALL      CLPATDTR
.
          MOVE      PMIDNDAY,NODAYS
.
          MOVE      ZERO,LSTRATE
          IF        PMIDAMNT<>0
            MOVE      PMIDAMNT,LSTRATE
            DIV       NODAYS,LSTRATE
          ENDIF
          MOVE      PMIDAMNT,LINETOT
          MOVE      PMIDGSTM,LINEGST
          MOVE      PMIDDESC,TDDESC
          MOVE      PMIDCNID,SAVCONTR
.
          MATCH     "1",PMIDRTYP
          GOTO      PRINV200 IF EQUAL       * Accommodation record
.
          MOVE      PMIDRTYP,KEY1           * Misc.item record type
          MATCH     "2",PMIDRTYP
          GOTO      PRINV300 IF EQUAL       * Theatre Item record
          MATCH     "3",PMIDRTYP
          GOTO      PRINV300 IF EQUAL       * Misc.Item record
          GOTO      PRINV800
.
.         Print the accommodation line
.
PRINV200  MOVE      PMIDFDAT,DFDATE
          MOVE      PMIDTDAT,DTDATE
          UNPACK    PMIDFDAT,FCENT1,FYEAR1,FMON1,FDAY1
          UNPACK    PMIDTDAT,TCENT1,TYEAR1,TMON1,TDAY1
          MOVE      PMIDCNID,CONTRCID
.
          MOVE      ONE,RECFLAG     * flag for printing accommodation
          CALL      PRLN0000        * Print line of invoice
.
          CALL      RTRN0000        * Read transfer file for fields to print
          MOVE      PMIDDESC,TDDESC
          MOVE      PMIDAMNT,TPATAMTT
          MOVE      PMIDAMNT,TPATAMTP
          DIV       PMIDNDAY,TPATAMTP
          MOVE      ZERO,TREBATE
          ADD       LINETOT,GROSSTOT
          ADD       LINEGST,TOTGST
.
          CALL      WADT0000        * Write DTR fro accommodation
          CALL      WAFN0000        * Set up key for a call to PATCALF
.
          GOTO      PRINV600
.
.         Print Items
.
PRINV300  CALL      RMTI0000        * Read pmsmtiaf file for fields to print
.
          UNPACK    PMIDFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
.
          MOVE      PMIDAMNT,TPATAMTT
          MOVE      PMIDAMNT,TPATAMTP
          DIV       PMIDNDAY,TPATAMTP
          MOVE      ZERO,TREBATE
          ADD       LINETOT,GROSSTOT
          ADD       LINEGST,TOTGST
          MOVE      PMIDNDAY,PMMISERV
          MOVE      PMIDDESC,TDDESC
.
          CALL      SMTI0000        * Setup items
          MOVE      PMMISERV,TSERVS
          MOVE      TPATAMTP,PMMIAMTP
          MOVE      TREBATE,PMMIRBAT
          CALL      PITM0000        * Print Item line
.
          CALL      WRDT0000        * Write to DTR file
          CALL      UPFN0000        * Update Financial summary file
.
PRINV600  PACK      KEY25,PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,SP70
          CALL      RDPMIDE1
          IF        OVRCD=0
            MOVE      PINVNO,PMIDINVN         * Update Invoice no
          ENDIF
          CALL      UPPMIDE1
.
          GOTO      PRINV100
.
PRINV800  CALL      UIVR0000                * Update invoice record
.
          MOVE      GROSSTOT,NETTOT
          MOVE      ZERO,REBTOT
.
          MOVE      GROSSTOT,XGROSTOT
          MOVE      TOTGST,XTOTGST
          MOVE      "Invoice Total ",IVTOTDES
          MOVE      THREE,RECFLAG
          CALL      PRLN0000                  * Print line of total invoice
.
          MOVE      GROSSTOT,INVCTOTL        * save total for tail template
          MOVE      GROSSTOT,NETTOT
          IF        IBCNUGST=2
            ADD       TOTGST,NETTOT
          ENDIF
.
.         If this is the default payer, check if there is any deposit
.
          IF        PMIDDEFT=1
            SUB       CSAMT,NETTOT
            CALL      ENDPRT00
          ELSE
            OPEN      CONTROLF,"controlf"
            READ      CONTROLF,HUND05;*189,PTCNPRCL
            IF        PTCNPRCL<>0
              CALL      PRTCLMS                 * print claim code
            ENDIF
            IF        CLAIM=1 | CLAIM=2
              PROC      PRNTICDC              * Print ICD10 Codes for TAC or WC
            ENDIF
            MOVE      ZERO,PAYMTTOT
            MOVE      ZERO,CREDTOT
            CALL      NTAIL000
          ENDIF
.
PRINV999  RETURN
+
.*******************************************************************************
.         Get payer name as Person responsible for Account
.*******************************************************************************
PYRA0000
          PACK      KEY19,AURNO,PMIDCLTY,SP70
          CALL      RSPMPYR1
PYRA1000  CALL      RKPMPYR1
          BRANCH    OVRCD,PYRA8000
.
          MATCH     AURNO,PMPYURNO
          GOTO      PYRA8000 IF NOT EQUAL
          MATCH     PMIDCLTY,PMPYCLTY
          GOTO      PYRA8000 IF NOT EQUAL
.
.         only Payers with effective for the Visit
.
          MATCH     PMPYEFDT,ALACDTE
          GOTO      PYRA1000 IF LESS
.
          MATCH     SP70,PMPYETDT
          GOTO      PYRA2000 IF EQUAL            * Blank Effective to date
.
          MATCH     ALACDTE,PMPYETDT
          GOTO      PYRA1000 IF LESS
.
PYRA2000  RESET     PMPYGNAM
          MOVE      PMPYGNAM,ANS
          MATCH     SP70,ANS
          IF        @EQUAL
            PACK      DIM8 WITH SP1,PMPYTITL,SP70
          ELSE
            PACK      DIM8 WITH SP1,PMPYTITL,SP1,ANS,DOT
          ENDIF
.
PYRA2200  CMATCH    SP1,DIM8
          GOTO      PYRA3000 IF NOT EQUAL
          BUMP      DIM8
          GOTO      PYRA2200 IF NOT EOS
.
PYRA3000  PACK      PFNAME WITH DIM8,PMPYSNAM
          REP       ", ",PFNAME
          MOVE      PFNAME,PKNAME
          MOVE      PMPYADD1,PKADD1
          MOVE      PMPYADD2,PKADD2
          MOVE      PMPYADD3,PKSUBR
          MOVE      PMPYADD4,PKADD4
          MOVE      PMPYPOST,PKPOST
          MOVE      PMPYRELP,PKRELP
          GOTO      PYRA9999
.
PYRA8000  CALL      GPRESP00                 * Set up Patient as PRFA
PYRA9999  RETURN
+
.*******************************************************************************
.         Get last transfer record of the Accommdation date
.*******************************************************************************
RTRN0000  PACK     KEY30 WITH AADMNO,PMIDFDAT,Z70   * last change of Fromdate
          CALL     RDSTRAN2
RTRN1000  CALL     RDPTRAN2
          BRANCH   OVRCD,RTRN2000
.
          MATCH    TADMN,AADMNO
          GOTO     RTRN2000 IF NOT EQUAL
.
          MATCH    TDATE,PMIDFDAT
          GOTO     RTRN9000 IF EQUAL
.
          MATCH    TDATE,PMIDTDAT
          GOTO     RTRN9000 IF NOT LESS
.
.         Use the admission trans record
.
RTRN2000  PACK     KEY30,AADMNO,SP70
          CALL     RDSTRAN2
          CALL     RDKTRAN2
          BRANCH   OVRCD,RTRN9999
.
          MATCH    TADMN,AADMNO
          GOTO     RTRN9999 IF NOT EQUAL
.
.         Save the rate and ward/bed information for writing to DTR file
.
RTRN9000  MOVE      TWARD,STWARD
          MOVE      TBED,STBED
          MOVE      TRATEF,STRATEF
          MOVE      TRATEG,STRATEG
          MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
          MOVE      TATYPE,STATYPE
          MOVE      TADOCT,STADOCT
          MOVE      TRBTYP,STRBTYP
          MOVE      TDATE,STDATE
          MOVE      TEXTRA,STEXTRA
          MOVE      TCHSTAT,STCHSTAT
          MOVE      PTTREPSD,SPTTREPS
.
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
.
          MOVE      TMOVE,STMOVE
RTRN9999  RETURN
+
.*******************************************************************************
.         Get item details from pmsmtiaf file
.*******************************************************************************
RMTI0000  PACK      KEY15,AADMNO,KEY1,SP70
          CALL      RSPMMTI2
RMTI1000  CALL      RKPMMTI2
          BRANCH    OVRCD,RMTI9000
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      RMTI9000 IF NOT EQUAL
          MATCH     KEY1,PMMIRTYP
          GOTO      RMTI9000 IF NOT EQUAL    * Not a misc.item record
.
          MATCH     PMMIITEM,PMIDITEM
          GOTO      RMTI1000 IF NOT EQUAL
.
          MOVE      PMMITRAN,SAVTRNNO
          CALL      MTDT0000               * Move pmsitm to dtraf fields
          GOTO      RMTI9999
.
RMTI9000  MOVE      SP70,SAVTRNNO
RMTI9999  RETURN
+
.*******************************************************************************
.         Set up pmsmtiaf file
.*******************************************************************************
SMTI0000 MOVE      SP70,MBSCONTR
         MOVE      SP70,PTITTCER
         MOVE      SP70,PTITIWRN
.
.        IF        TRECTYPE=2
.          MOVE      ONE,CBFLAG
.          CALL      GETIT00           * re-calculate theatre fees
.          MOVE      ZERO,CBFLAG
.        ENDIF
.
         MOVE      SP9,PRTITCOD
.
.        If this is a theatre charge, do any theatre charge processing
.
.        Check "Hospital ID', if MBS Code should be printed on invoice
.
         IF        TRECTYPE=2
           MOVE      TITEMNO,PRTITCOD     * Print MBS item
           IF        IBCNMHOS=1
             MATCH     "1",PTHSPMBS
             IF        !@EQUAL
               MOVE      SP10,PRTITCOD
             ENDIF
           ENDIF
         ENDIF
.
.        If this is a miscellaneous charge, do any miscellaneous processing
.
         IF        TRECTYPE=3
           MOVE      SP10,PRTITCOD
           CALL      PROCMISC
           IF        PTCNPMIS=1
             MOVE      TITEMNO,PRTITCOD     * Print miscellaneous item
           ENDIF
         ENDIF
          GOTO      SMTI9999
.
SMTI9999  RETURN
+
.*******************************************************************************
.        Write to DTR File
.*******************************************************************************
WRDT0000  MOVE      CNEXTINV,TREF        * Update invoice number
          MOVE      ONE,TINVPRT          * Flag as being printed
          PACK      PTDTEFFD,PINVDTE     * Effective date to fees invoiced
          MOVE      ZERO,PTDTGSTM
          MOVE      ZERO,PTDTGSTL
          IF        IBCNUGST=2 & PTDTGSTA=1
            CALL      IGST0000
            MOVE      TPATAMTT,PTDTGSTM
            MULT      IBGEAMNT,PTDTGSTM  * Item GST Amount
            DIV       "100.00",PTDTGSTM
          ENDIF
.
WRDT1000  CALL      NXTTRAN1      * Get the next transaction number
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1
          COMPARE   ZERO,OVRCD
          GOTO      WRDT1000 IF EQUAL
.
          PACK      PTDTCNID,MBSCONTR,SP70   * Contract ID
          CALL      WRDTRN1              * Write Misc.Charge
.
.         If this is a item split invoice, delete item later when default payer
.         invoice raised
.
          MATCH     "1",PMIDSPLT
          GOTO      WRDT9999 IF EQUAL
.
          MATCH     SP70,SAVTRNNO
          IF        !@EQUAL
            PACK      KEY14,TADMNO,SAVTRNNO
            CALL      DEPMMTI1             * remove item pending recod
          ENDIF
.
WRDT9999 RETURN
+
.*******************************************************************************
.        Update Financial summary file
.*******************************************************************************
UPFN0000  UNPACK    SP70,FININVN,FINADMN,FINURNO
          IF        PROGFLAG=3
            PACK      FININVN,CNEXTINV,SP70
            MOVE      AADMNO,FINADMN
            MOVE      AURNO,FINURNO
          ENDIF
.
          MOVE      LINETOT,FINAMT
          MOVE      "A",FINTYPE
.
          MOVE      "M",ANS
          IF        PTCNMFEE = 1
            PACK      FINCODE WITH ANS,ACLAIM,TMISGRP,SP20
          ELSE
            PACK      FINCODE WITH ANS,TMISGRP,TITEMNO,SP20
          ENDIF
.
          UNPACK    PINVDTE WITH XCENT,XYEAR,XMON,XDAY
          PACK      FINDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",FINDATE
          MOVE      THREE,FINSYS
          MOVE      SP6,FINSITE
          MOVE      PMVXMHOS,FINHOSP
          CALL      PATCALF
.
          IF        IBCNUGST=2 & LINEGST<>0
            MOVE      ONE,FGSTFLAG
            PACK      FINDATE,XCENT,XYEAR,XMON,XDAY
            REP       " 0",FINDATE
            MOVE      LINEGST,FINAMT
            MOVE      PMVXMHOS,FINHOSP
            CALL      PATCALF
            MOVE      ZERO,FGSTFLAG
          ENDIF
UPFN9999  RETURN
+
.*******************************************************************************
.         Update the invoice record with the appropriate details
.*******************************************************************************
UIVR0000  MOVE      CNEXTINV,KEY8
          CALL      RDINV1
.
          MOVE      AADMNO,KEY8                     * Make sure we have got
          CALL      RDMISS1                         * the correct U/R number
.
          MOVE      AADMNO,PINVADM
          MOVE      AURNO,PINVUR
          MOVE      PSNAME,PINALPHA
          MOVE      GROSSTOT,PINVAMT
          MOVE      REBTOT,PINVREB
          MOVE      ZERO,PTINGSTA
          MOVE      ZERO,PTINDISC
          MOVE      ZERO,PTINROUN
          MOVE      ZERO,PINVTYP
          IF        IBCNUGST=2
            ADD       TOTGST,PINVAMT
            MOVE      TOTGST,PTINGSTA      * GST amount
          ENDIF
          MOVE      THREE,PINVSYS
          MOVE      SP6,PINVSITE
          MATCH     SP70,SAVACLM
          IF        @EQUAL
            MOVE      ACLAIM,PINVCLM       * Patient claim code
          ELSE
            MOVE      SAVACLM,PINVCLM      * Payer claim code
          ENDIF
.
          PACK      PTINCNID,CONTRCID,SP70    * Contract ID
          PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      WBSEUID,PTINUUID          * web user id
.
          CALL      UPINV1
.
          MATCH     PINVCLM,DEFPAYER
          IF        @EQUAL
            CALL      CASHUPD                 * Cash update to default payer
            MOVE      ONE,COMPFLG             * flag for processing cash
          ENDIF
.
          PACK      KEY26,PINVADM,SAVEUNIQ,PINVNO,SP70
          CALL      RDPMIVB1
          COMPARE   ZERO,OVRCD
          GOTO      UIVR8000 IF EQUAL
.
          MOVE      PINVADM,PMIVADMN
          MOVE      SAVEUNIQ,PMIVUNIQ
          MOVE      PINVNO,PMIVINVN
          MOVE      "0",PMIVSTAT            * invoice raised
.
          PACK      PMIVCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMIVCDAT           * date created
          CLOCK     TIME,PMIVCTIM           * time created
          MOVE      WBSEUID,PMIVCUID         * web user id
          UNPACK    SP70,PMIVUUID,PMIVUDAT,PMIVUTIM
          PACK      PMIVSPAR,SP70,SP70
          CALL      WRPMIVB1
.
UIVR8000  PROC      FLAGDEBT                * set outstanding debt indicator
.
UIVR9999  RETURN
+
.---------------------------------------------------------------------
.        Delete existing invoice breakdown for selected claim type
.---------------------------------------------------------------------
DEBR0000  MOVE      ZERO,FORM3A
          MOVE      SP70,KEY8              * Not invoiced
.
DEBR1000  PACK      KEY25,DAADMNO,KEY3,KEY8,SP70
          CALL      RSPMIDE1
          CALL      RKPMIDE1
          BRANCH    OVRCD,DEBR9999
.
          PACK      KEY19,PMIDADMN,PMIDCLTY,PMIDINVN,SP70
          MATCH     KEY19,KEY25
          GOTO      DEBR9999 IF NOT EQUAL
.
          PACK      KEY25,PMIDADMN,PMIDCLTY,PMIDINVN,PMIDTRNO,SP70
          CALL      DEPMIDE1
          GOTO      DEBR1000
.
DEBR9999  RETURN
+
.*******************************************************************************
.         set up payer for this accommodation
.*******************************************************************************
SPYR0000  MOVE      "1",KEY1
          MOVE      SP70,PMIDINVN
          PACK      KEY34,AADMNO,PMIDINVN,KEY1,SP70
          CALL      RSPMIDE3
SPYR1000  CALL      RKPMIDE3
          BRANCH    OVRCD,SPYR9000
.
          PACK      KEY17,PMIDADMN,PMIDINVN,PMIDRTYP,SP70
          MATCH     KEY17,KEY34
          GOTO      SPYR9000 IF NOT EQUAL
.
          COMPARE   ZERO,PMIDAMNT
          GOTO      SPYR1000 IF EQUAL       * No Accom.bill, try next record
.
          MOVE      ZERO,EXIT
          GOTO      SPYR9999
.
SPYR9000  MOVE      ZERO,PMIDAMNT
          MOVE      SP70,PMIDDEFT
          MOVE      ONE,EXIT
SPYR9999  RETURN
+
.*******************************************************************************
.         This section of the include determines the date range for the invoice
.*******************************************************************************
ISBC0000  MOVE      ZERO,DCFLAG
          MOVE      ZERO,PCFLAG
          MOVE      ZERO,CRITDAYS
          MOVE      ZERO,CPAGENO
.
          MOVE      ONE,TFEEIND              * Default to use theatre fee
          MOVE      THREE,PINVSYS
          MOVE      SP70,CMENDDTE
          MOVE      ZERO,STEPNO
          MOVE      ZERO,SAVETRN
          MOVE      ZERO,SAVTBEND
.
          MOVE      ZERO,FDAY
          MOVE      ZERO,FMON
          MOVE      ZERO,FYEAR
          MOVE      ZERO,FCENT
          MOVE      ZERO,TDAY
          MOVE      ZERO,TMON
          MOVE      ZERO,TYEAR
          MOVE      ZERO,TCENT
.
          CALL      INITIVAR                 * Initialise any special vari
.
          MOVE      ZERO,ACDAYCS             * Assume not a day case
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
          CALL      FRSDAY00                 * Check for 1st day of period
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,ACDAYCS            * This is a day case
          ENDIF
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND14;*221,PTCNCALM
          IF        IBCNMHOS=1
            MOVE      SP70,PTHSTFEE
            MOVE      SP70,PTHSPMBS
            MOVE      SP70,PTHSDCLM
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
          ENDIF
.
.         Check claim type indic.if this is a claim
.
          MOVE      ZERO,CLAIM
          PACK      KEY5 WITH CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD OF ISBC9999
.
.         Check the indicators for a W, M, or V (these are the claims)
.
          MOVE      ZERO,FORM2
ISBC6000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      ISBC7000 IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     "W",ANS
          GOTO      ISBC7000 IF EQUAL
          MATCH     "M",ANS
          GOTO      ISBC7000 IF EQUAL
          MATCH     "V",ANS
          GOTO      ISBC7000 IF EQUAL
          GOTO      ISBC6000
.
.         We have a claim
.
ISBC7000  REP       "W1M2V3",ANS
          MOVE      ANS,CLAIM
ISBC9999  RETURN
+
.*******************************************************************************
.         Get the next invoice number
.*******************************************************************************
INRG0000  OPEN      PATINVR1,"patinvrf"
          OPEN      PATIRNG1,"patirnge"
.
INRG1000  MOVE      CINVRIP,KEY3
          CALL      TRIRNG1
          BRANCH    OVRCD,INRG9000,INRG9000
.
          MOVE      IRNEXT,CNEXTINV
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          COMPARE   ZERO,OVRCD
          GOTO      INRG1000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      INRG9999
.
INRG9000  MOVE      ONE,EXIT
INRG9999  RETURN
+
