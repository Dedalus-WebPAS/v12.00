.------------------------------------------------------------------------
.                            AD1SEC00                                       
.               Add/subtract one second to date/time                                 
.
. Requires: ADATTIME - Date/time value (ccyymmddhhmmss)                     
. Returns : ADATTIME - Date/time value (ccyymmddhhmmss) 
.           incremented/decrement by 1 sec
.------------------------------------------------------------------------
. Modifications
.------------------------------------------------------------------------
. V11.01.01  13/09/2021  Thanh T.       TSK 0889595
.            Added SU1SEC00 for subtracting one second from date/time
. V10.14.00  16/08/2019  Thanh T.       TSK 0837991
.            Initial version
.------------------------------------------------------------------------
. Add 1 second to date/time field
.------------------------------------------------------------------------
AD1SEC00  UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          MATCH     "59",ASECSS
          IF        !@EQUAL
            MOVE      ASECSS,FORM2              * just increment seconds
            ADD       ONE,FORM2
            MOVE      FORM2,ASECSS
            REP       " 0",ASECSS
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      AD1SEC99
          ENDIF
.
. The seconds were "59", so reset these to "00" and increment the minutes
.
          MOVE      "00",ASECSS
          MATCH     "59",ASECMM
          IF        !@EQUAL
            MOVE      ASECMM,FORM2              * just increment minutes
            ADD       ONE,FORM2
            MOVE      FORM2,ASECMM
            REP       " 0",ASECMM
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      AD1SEC99
          ENDIF
.
. The minutes were "59", so reset these to "00" and increment the hours
.
          MOVE      "00",ASECSS
          MOVE      "00",ASECMM
          MATCH     "23",ASECHH
          IF        !@EQUAL
            MOVE      ASECHH,FORM2              * just increment hours
            ADD       ONE,FORM2
            MOVE      FORM2,ASECHH
            REP       " 0",ASECHH
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      AD1SEC99
          ENDIF
.
. The hours were "59", so reset these to "00" and increment the date
.
          MOVE      "00",ASECSS
          MOVE      "00",ASECMM
          MOVE      "00",ASECHH
.
. We need to increment the date by 1 day
.
          UNPACK    ASECDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julian day
          ADD       ONE,JULDAY                   * increment julian day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                       * get new date
          PACK      ADATTIME,CC,YY,MM,DD,ASECHH,ASECMM,ASECSS
          REP       " 0",ADATTIME
.
AD1SEC99  RETURN
.
.------------------------------------------------------------------------
. Subtract 1 second to date/time field
.------------------------------------------------------------------------
SU1SEC00  UNPACK    ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
          MATCH     "00",ASECSS
          IF        !@EQUAL
            MOVE      ASECSS,FORM2              * just decrement seconds
            SUB       ONE,FORM2
            MOVE      FORM2,ASECSS
            REP       " 0",ASECSS
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      SU1SEC99
          ENDIF
.
. The seconds were "00", so reset these to "59" and decrement the minutes
.
          MOVE      "59",ASECSS
          MATCH     "00",ASECMM
          IF        !@EQUAL
            MOVE      ASECMM,FORM2              * just decrement minutes
            SUB       ONE,FORM2
            MOVE      FORM2,ASECMM
            REP       " 0",ASECMM
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      SU1SEC99
          ENDIF
.
. The minutes were "00", so reset these to "59" and decrement the hours
.
          MOVE      "59",ASECMM
          MATCH     "00",ASECHH
          IF        !@EQUAL
            MOVE      ASECHH,FORM2              * just decrement hours
            SUB       ONE,FORM2
            MOVE      FORM2,ASECHH
            REP       " 0",ASECHH
            PACK      ADATTIME,ASECDATE,ASECHH,ASECMM,ASECSS
            GOTO      SU1SEC99
          ENDIF
.
. The hours were "00", so reset these to "23" and decrement the date
.
          MOVE      "23",ASECHH
.
. We need to increment the date by 1 day
.
          UNPACK    ASECDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julian day
          SUB       ONE,JULDAY                   * decrement julian day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                       * get new date
          PACK      ADATTIME,CC,YY,MM,DD,ASECHH,ASECMM,ASECSS
          REP       " 0",ADATTIME
.
SU1SEC99  RETURN
.
