.******************************************************************************
.* System         : Visit System                                              *
.*                                                                            *
.* Include Name   : DISPVISN.PBL                                              *
.*                                                                            *
.* Function       : Displays Visit Notes for an admission                     *
.*                                                                            *
.* Author         : Don Nguyen                                                *
.*                                                                            *
.* Display Visit Notes Tag   - %SERVERNM.AA.BBB.xxx.y.z:                      *
.*                                                                            *
.* Parameters     : ADMISSNO - Admission Number                               *
.*                  NOTEFILT - xxx - Note Type Filter                         *
.*                  NOTEORDR -   y - Note Order (0-From Newest, 1-From Oldest)*
.*                  NOTEDETL -   z - Note Details:                            *
.*                                   0-Include header (date/time/userid)      *
.*                                   1-Include note text only                 *
.*                                   2-Include blank line in place of header  *
.*                                                                            *
.* Modifications  :                                                           *
.*      V11.02.01   28/09/2022  Davin            TSK 0919110                  *
.*                  Added NOTEDETL=2 (Include blank line in place of header)  *
.*      V10.14.00   08/02/2019  Don Nguyen       TSK 0833548                  *
.*                  Created NEW                                               *
.******************************************************************************
.
          DEFPROC   DISPVISN
.
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
.
.
. LOCAL VARIABLES
. ----------------
.
VISNOTES  DIM       100
TEMPDATE  DIM       12
.
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
.
          CALL      VISNOT00
          GOTO      DISPVN99
.
.****************************************************************************
.*        Output Visit Notes free text (lines)                              *
.*                                                                          *
.****************************************************************************
VISNOT00  IF        NOTEORDR=0
            PACK      KEY17,ADMISSNO,NOTEFILT,Z70
          ELSE
            PACK      KEY17,ADMISSNO,NOTEFILT,SP70
          ENDIF
.
          CALL      RSVSMDT1
VISNOT10  IF        NOTEORDR=0
            CALL      RPVSMDT1
          ELSE
            CALL      RKVSMDT1
          ENDIF
          BRANCH    OVRCD,VISNOT99
.
          MATCH     ADMISSNO,VSMDVISN
          GOTO      VISNOT99 IF NOT EQUAL
.
          MATCH     NOTEFILT,VSMDTYPE
          GOTO      VISNOT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT            * Deleted?
          GOTO      VISNOT10 IF EQUAL
.
          MOVE      SP70,TEMPDATE
          MATCH     SP70,VSMDDATE
          IF        !@EQUAL
            UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      TEMPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MOVE      ZERO,F1
          MOVE      NOTEDETL,F1
.
          IF        F1 = 1
            GOTO      VISNOT19
          ENDIF
.
          IF        F1 = 2
            CLEAR     VISNOTES
            APPEND    "  ",VISNOTES
            RESET     VISNOTES
            GOTO      VISNOT18
          ENDIF
.
          CLEAR     VISNOTES
          APPEND    TEMPDATE,VISNOTES
          APPEND    "  ",VISNOTES
          APPEND    VSMDTIME,VISNOTES
          APPEND    "  ",VISNOTES
          PACK      KEY10,VSMDUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            STRIP     WBSENAM
            APPEND    WBSENAM,VISNOTES
          ELSE
            APPEND    VSMDUSER,VISNOTES
          ENDIF
          APPEND    "  ",VISNOTES
          APPEND    VSMDOCCG,VISNOTES
          RESET     VISNOTES
.
          STRIP     VISNOTES
VISNOT18  MOVELPTR  VISNOTES,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      VISNOTES,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
VISNOT19  PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VISNOT20  CALL      RKVSMTX1
          BRANCH    OVRCD,VISNOT10
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VISNOT10 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VISNOT10 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VISNOT10 IF NOT EQUAL
.
          STRIP     VSMTCMNT
          MOVELPTR  VSMTCMNT,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      VSMTCMNT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      VISNOT20
.
VISNOT99  RETURN
+
          INC       IBAOVRIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
.
DISPVN99  ENDPROC
+
