. **************************************************************
. *              I N C L U D E  S U M M A R Y                  *
. *              -------------  -------------                  *
. *                                                            *
. *     INCLUDE NAME:     RTHIDAYS                             *
. *                                                            *
. *     FUNCTION:         CALCULATE DAYS IN HOSPITAL           *
. *                                                            *
. *     V5.01.01 11/06/92 J.Habasque SRF 17573                 *
. *                       Copy of NHOSPDAY with RTIO0000 added *
. *                                                            *
. *     V12.00.01 23/05/2025  J.Tan          TSK 0955096       *
. *               Added Alphanumeric visit number changes      *
. *                                                            *
. **************************************************************
.
.                READ TRANSFER FILE
.
RTIODAYS MOVE           SP8,STDATE
        MOVE            SP1,STMOVE
        MOVE            ZERO,NBRDAYS
        MOVE            ZERO,CALDAYS
.
        COMPARE         THREE,ASTAT
        GOTO            RTD0100 IF NOT EQUAL
.
        MATCH           ADATE,DDATE
        GOTO            RTD0100 IF NOT EQUAL
.
        MATCH           FROMDATE,ADATE
        RETURN          IF LESS
.
        MATCH           ADATE,TODATE
        RETURN          IF LESS
.
        MOVE            ZERO,NBRDAYS        * Sameday patient LOS = 0 days
        RETURN
.
RTD0100 PACK            KEY30 WITH AADMNO,SP20
        CALL            RDSTRAN2
RTD0200 CALL            RDKTRAN2
        BRANCH          OVRCD OF RTD0800
.
        MATCH           TADMN,AADMNO
        GOTO            RTD0800 IF NOT EQUAL
.
        MOVE            TMOVE,STMOVE
        REP             " 0",TDATE
.
        MATCH           "A",TMOVE
        GOTO            RTD0300 IF EQUAL
        MATCH           "R",TMOVE
        GOTO            RTD0300 IF EQUAL
        MATCH           "L",TMOVE
        GOTO            RTD0600 IF EQUAL
        MATCH           "D",TMOVE
        GOTO            RTD0200 IF NOT EQUAL
.
        MATCH           FROMDATE,TDATE
        GOTO            NHOSP99 IF LESS
        MATCH           TODATE,TDATE
        GOTO            RTD0800 IF LESS
        MOVE            TODATE,TDATE  
        GOTO            RTD0800
.
.
.
RTD0300 MATCH           "A",TMOVE
        GOTO            RTD0400 IF NOT EQUAL
.
        MATCH           TDATE,ADATE
        GOTO            RTD0400 IF EQUAL
.
.       PRINT           *N,"*** Admission : ",AADMNO," Date is ",ADATE:
.                       "   Transfer File Admit Date is ",TDATE:
.                       "Contact I.B.A *** NOW ***":
.                       " (NHOSPDAY)"
.
        DISPLAY         *P1:23,*EL,*V2LON,*B:
                        "*** Admission : ",AADMNO:
                        "   Transfer and Admission date DIFFERENT ***",*W
.
RTD0400 MATCH           TODATE,TDATE
        GOTO            NHOSP99 IF NOT LESS
        MATCH           FROMDATE,TDATE
        GOTO            RTD0500 IF NOT LESS
        MOVE            FROMDATE,STDATE
        GOTO            RTD0200
.
RTD0500  MOVE            TDATE,STDATE
        GOTO            RTD0200
.
RTD0600 MATCH           FROMDATE,TDATE
        GOTO            RTD0200 IF LESS
.
        MATCH           TODATE,TDATE
        GOTO            RTD0700 IF LESS
.
          DAYSEP    STDATE,TODATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
        GOTO        NHOSP99
.
RTD0700   DAYSEP    STDATE,TDATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
        GOTO            RTD0200
.
RTD0800 MATCH           "L",STMOVE
        GOTO        NHOSP99 IF EQUAL
        MATCH           " ",STMOVE
        GOTO        NHOSP99 IF EQUAL
.
.         this is the last record
.
        MATCH           "D",STMOVE
        GOTO            RTD0900 IF NOT EQUAL
.
.       PATIENT IS DISCHARGED
.
          DAYSEP    STDATE,TDATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
          GOTO      NHOSP99
.
.        PATIENT IS CURRENTLY ADMITTED
.
RTD0900   DAYSEP    STDATE,TODATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
.
NHOSP99   CALL      RTIO0000
          ADD       RTDAYS,NBRDAYS
          RETURN
+
.**************************************************************************
.*  Calculate RTIO leave days                                             *
.**************************************************************************
RTIO0000  MOVE      ZERO,RTDAYS
          PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2
RTIO1000  CALL      RDKTRAN2
          BRANCH    OVRCD,RTIO9999
.
          MATCH     TADMN,AADMNO
          GOTO      RTIO9999 IF NOT EQUAL
.
          MATCH     ANSL,TMOVE
          GOTO      RTIO1000 IF NOT EQUAL
.
          MATCH     SP70,PTTRLTYP
          GOTO      RTIO1000 IF EQUAL
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,RTIO1000
.
          MATCH     "3",TCINDC2
          GOTO      RTIO1000 IF NOT EQUAL
.
          PACK      SKEY30,DTADMN,TDATE,TTIME,TWARD,TBED,SP70
          PACK      STDATE,TDATE
          PACK      STTIME,TTIME
.
          MATCH     TDATE,ADATE
          IF        @EQUAL
            ADD       ONE,RTDAYS
            GOTO      RTIO1000
          ENDIF
.
          PACK      KEY30,DTADMN,TDATE,SP70
          CALL      RDSTRAN2
RTIO2000  CALL      RDKTRAN2
          BRANCH    OVRCD,RTIO3000
.
          MATCH     TADMN,AADMNO
          GOTO      RTIO3000 IF NOT EQUAL
.
          MATCH     TDATE,STDATE
          GOTO      RTIO3000 IF NOT EQUAL
.
          MATCH     STTIME,TTIME
          GOTO      RTIO3000 IF NOT LESS
.
          MATCH     ANSR,TMOVE
          GOTO      RTIO2000 IF NOT EQUAL
.
          ADD       ONE,RTDAYS
.
RTIO3000  MOVE      SKEY30,KEY30
          CALL      RDTRAN2
          GOTO      RTIO1000
.
RTIO9999  RETURN
+
