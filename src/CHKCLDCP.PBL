.--------------------------------------------------------------------------
. CHKCLDCP - Check Claim Type/Clinical Document Mapping table to determine
.            if the Raise Invoice button should be inactive
. Return: EXIT 0 - OK
.              1 - clinical document missing 
.--------------------------------------------------------------------------
. Modifications
.--------------------------------------------------------------------------
. V11.02.01 11/10/2022  J.Tan           TSK 0908346 
.           Mod to initialise EXIT at the start of routine
. V11.02.00 06/09/2022  Thanh T         TSK 0908346 
.           Initial version.
.--------------------------------------------------------------------------
          DEFPROC   CHKCLDCP
.
          INC       ACCDMAFD/INC
          INC       OBSPCOFD/INC
.
ZERO8     INIT      "00000000"
.
CLDM1000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACCDMAA1,"accdmaaf"
          TRAPCLR   IO
          IF        OVRCD=1
            GOTO      CTDM9999
          ENDIF
          OPEN      ACCDMAA2,"accdmaaf"  
          OPEN      OBSPCOA1,"obspcoaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND25;*240,PTCNLDII,*241,PTCNLDEI,*242,PTCNLDOI:
                                    *243,PTCNLDPI
.
          MATCH     "I",INDMSYST
          IF        @EQUAL
            MATCH     "1",PTCNLDII
            GOTO      CTDM9999 IF NOT EQUAL
          ENDIF
.
          MATCH     "E",INDMSYST
          IF        @EQUAL
            MATCH     "1",PTCNLDEI
            GOTO      CTDM9999 IF NOT EQUAL
          ENDIF
.
          MATCH     "O",INDMSYST
          IF        @EQUAL
            MATCH     "1",PTCNLDOI
            GOTO      CTDM9999 IF NOT EQUAL
          ENDIF
.
          MATCH     "P",INDMSYST
          IF        @EQUAL
            MATCH     "1",PTCNLDPI
            GOTO      CTDM9999 IF NOT EQUAL
          ENDIF
.
          IF        IBCNMHOS=1
            PACK      KEY16,INDMHOSP,INDMCLAM,INDMSYST,INDMCTYP,INDMILOS,SP70
            CALL      RSACDMA1
          ELSE
            PACK      KEY16,INDMCLAM,INDMSYST,INDMCTYP,INDMILOS,SP70
            CALL      RSACDMA2
          ENDIF 
CTDM2000  IF        IBCNMHOS=1
            CALL      RKACDMA1
          ELSE
            CALL      RKACDMA2
          ENDIF 
          BRANCH    OVRCD,CTDM9000
.
          MATCH     INDMHOSP,ACDMHOSP
          GOTO      CTDM9000 IF NOT EQUAL
.
          MATCH     INDMCLAM,ACDMCCLM
          GOTO      CTDM9000 IF NOT EQUAL
.
          MATCH     INDMSYST,ACDMSYST
          GOTO      CTDM9000 IF NOT EQUAL
.
          MATCH     INDMCTYP,ACDMCTYP
          GOTO      CTDM9000 IF NOT EQUAL
.
          MATCH     INDMILOS,ACDMILOS
          GOTO      CTDM9000 IF NOT EQUAL
.
          MATCH     "1",ACDMIACT
          GOTO      CTDM2000 IF EQUAL
.
          MATCH     "1",ACDMDELT
          GOTO      CTDM2000 IF EQUAL
.
          MOVE      "wi",DIM2
          PACK      KEY5,DIM2,ACDMDTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CTDM2000
.
          MATCH     ANSY,TCINDC6     
          GOTO      CTDM2000 IF NOT EQUAL
.
. Check existence of clinical document
.
          PACK      KEY20,INDMURNO,INDMADNO,Z70
          CALL      RSOBPC1
CTDM3000  CALL      RPOBPC1
          BRANCH    OVRCD,CTDM4000
.
          MATCH     INDMURNO,OBPCURNO
          GOTO      CTDM4000 IF NOT EQUAL
.
          MATCH     INDMADNO,OBPCCVIS
          GOTO      CTDM4000 IF NOT EQUAL
.
          MATCH     OBPCCTYP,ACDMDTYP
          GOTO      CTDM3000 IF NOT EQUAL
.
          MATCH     "1",OBPCMDEL
          GOTO      CTDM3000 IF EQUAL
. 
          GOTO      CTDM2000 
.
. Check existence of clinical document for blank admission number

CTDM4000  MATCH     SP70,INDMADNO
          GOTO      CTDM8000 IF NOT EQUAL
.
          PACK      KEY20,INDMURNO,ZERO8,Z70
          CALL      RSOBPC1
CTDM4200  CALL      RPOBPC1
          BRANCH    OVRCD,CTDM8000
.
          MATCH     INDMURNO,OBPCURNO
          GOTO      CTDM8000 IF NOT EQUAL
.
          MATCH     ZERO8,OBPCCVIS
          GOTO      CTDM8000 IF NOT EQUAL
.
          MATCH     OBPCCTYP,ACDMDTYP
          GOTO      CTDM4200 IF NOT EQUAL
.
          MATCH     "1",OBPCMDEL
          GOTO      CTDM4200 IF EQUAL
.
          GOTO      CTDM2000
.
CTDM8000  MOVE      ONE,EXIT
          GOTO      CTDM9999 
.
CTDM9000  MOVE      ZERO,EXIT 
          GOTO      CTDM9999 
.
          INC       ACCDMAIO/INC
          INC       OBSPCOIO/INC
          INC       IBAOVRIO/INC
.
CTDM9999  CLOSE     ACCDMAA1
          CLOSE     ACCDMAA2
          CLOSE     OBSPCOA1     
          ENDPROC
