.******************************************************************************
.*  Include Name  : PATICOKY.PBL                                              *
.*  Description   : Keyin of operation codes                                  *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  OPICDDTE - Date (e.g. discharge date) used to determine   *
.*                             which version of ICD10 is valid.               *
.*                                                                            *
.*  Returns       : OPCODE   - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*        V9.09.01  19/03/2008  Mike Laming     CAR 163918    HDP 2008        *
.*                  Add ICD10 Ed.6 Go-live Date PTCNGDV6 (Sect.112 Pos.239) - *
.*        V9.06.01  09/05/2006  Mike Laming     CAR 105244                    *
.*                  Added 2006 ICD10 Ed5 - go-live Date PTCNGDV5 & PT0OV5CD   *
.*        V9.04.01  08/07/2005  Mike Laming   CAR 63036                       *
.*                  1) Add ICD Edition 4 changes from V5.13.00                *
.*                  2) at ICOCK500, change RDICO1 to RDPTICO1                 *
.*        V5.08.02  14/08/2000  Greg Horvat                                   *
.*                  Changed the ICD10 V2 validation - if before the ICD10 V2  *
.*                  go live date only ICD10 V1 codes are valid, if on or after*
.*                  only ICD10 V2 codes are valid                             *
.*        V5.08.01  27/06/2000  Greg Horvat                                   *
.*                  Added code to validate ICD10 version 2 codes              *
.*        V6.05.02  25/09/1998    Katie Nelson                                *
.*                  Added procedure HPTOIA00 to key word search               *
.*        V6.05.01  03/06/1998  Greg Horvat                                   *
.*                  Changed to auto allocate the - for ICD10 codes            *
.*        V6.03.00  26/09/1996  Greg Horvat      Holy Spirit Mods             *
.*                  Ported from release 5.04                                  *
.*                                                                            *
.******************************************************************************
PATICOKY
PTIOKY00  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      PTIOKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,OPCODE:
                    *PECOL:EVERT,*DV,OPCODE
          GOTO      PTIOKY20
.
PTIOKY10  MOVE      CKYIDEF9,OPCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,OPCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,OPCODE:
                    *PECOL:EVERT,*DV,OPCODE
.
.         check what was entered
.
PTIOKY20  CALL      ICOCK000
          BRANCH    EXIT,PTIOKY25,PTIOKY31,PTIOKY41,PTIOKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTIOKY00
.
.         a "?" was input
.
PTIOKY25  CALL      HPTOIA00
          BRANCH    OVRCD,PTIOKY00
          IF        CKYIMODE=1
            CALL      REXISTS
            GOTO      PTIOKY00
          ENDIF
PTIOKY31  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,OPCODE
          GOTO      PTIOKY99 
.
.         nothing was input
.
PTIOKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTIOKY99 
.
.         EXITCHAR input
.
PTIOKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTIOKY99  RETURN
+
.*********************************************************************
.*                  ICOCK000                    Called by : PATICOKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
ICOCK000  ENDSET    OPCODE
          APPEND    SP9,OPCODE
          RESET     OPCODE
.
          MATCH     EXITCHAR,OPCODE
          GOTO      ICOCK600 IF EQUAL       * exit char
.
          MATCH     SP9,OPCODE
          GOTO      ICOCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,OPCODE
          GOTO      ICOCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
.
ICOCK500  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     PTCNI10D,KEY8
          CALL      FTICO000 IF NOT LESS
. 
          PACK      KEY9,OPCODE,SP9
          CALL      RDPTICO1                        * was RDICO1       *C-63036
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ICOCK900          * invalid code
.
            CALL      VOV2C000              * Validate opr ICD10 version 2 code
            BRANCH    EXIT,ICOCK905
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      ICOCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      ICOCK999
.
.         exitchar entered
.
ICOCK600  MOVE      FOUR,EXIT
          MOVE      SP9,OPCODE
          GOTO      ICOCK999
.
.         nothing entered
.
ICOCK700  BRANCH    CKYIMAND,ICOCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,OPCODE
          GOTO      ICOCK999
.
.         "?" entered
.
ICOCK800  MOVE      ONE,EXIT
          GOTO      ICOCK999
.
.         invalid code entered
.
ICOCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Operation Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
.
ICOCK905  MOVE      ZERO,EXIT
          GOTO      ICOCK999
.
.         field is mandatory
.
ICOCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
ICOCK999  RETURN
+
.******************************************************************************
.*                                  FTICO000              Called by: ICOCK000 *
.*                              Format ICD10 Code                             *
.******************************************************************************
FTICO000  SCAN      "-",OPCODE
          GOTO      FTICO100 IF EQUAL       * Code already formatted
.
          MOVE      "-",KEY1
          UNPACK    OPCODE,KEY5,KEY4
          PACK      OPCODE,KEY5,KEY1,KEY4
.
FTICO100  RESET     OPCODE
FTICO900  MOVE      ZERO,EXIT
FTICO999  RETURN
+
.******************************************************************************
.*                                  VOV2C000              Called by: ICOCK000 *
.*                  Validate Operation ICD10 (Edition 1/2/3) Code             *
.******************************************************************************
VOV2C000  MATCH     SP8,OPICDDTE
          GOTO      VOV2C900 IF EQUAL     * Blank ICD date
          GOTO      VOV2C900 IF EOS       * Blank ICD date
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*217,PTCNGDV4,*227,PTCNGDV5       *C-105244
          READ      CONTROLF,HUND12;*239,PTCNGDV6                     *I-163918
.
.                                           ***** ICD10 Ed.6          *I-163918
          MATCH     PTCNGDV6,OPICDDTE
          IF        !@LESS
            IF        PT0OV6CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 Ed. 6 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950            * not valid ICD10 Ed.6 code
            ENDIF
            GOTO      VOV2C900              * valid ICD10 Ed.6 code
          ENDIF
.
.                                           ***** ICD10 Ed.5          *I-105244
          MATCH     PTCNGDV5,OPICDDTE
          IF        !@LESS
            IF        PT0OV5CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 Ed. 5 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950            * not valid ICD10 Ed.5 code
            ENDIF
            GOTO      VOV2C900              * valid ICD10 Ed.5 code
          ENDIF
.
          MATCH     PTCNGDV4,OPICDDTE
          IF        !@LESS
            IF        PT0OV4CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 Ed. 4 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950            * not valid ICD10 e4 code
            ENDIF
            GOTO      VOV2C900              * valid ICD10 e4 code
          ENDIF
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*134,PTCNGDV3
          MATCH     PTCNGDV3,OPICDDTE
          IF        !@LESS
            IF        PT0OV3CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 Ed. 3 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950            * not valid ICD10 v3 code
            ENDIF
            GOTO      VOV2C900              * valid ICD10 v3 code
          ENDIF
.
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          MATCH     PTCNGDV2,OPICDDTE
          IF        @LESS
            IF        PT0OV1CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 version 1 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950
            ENDIF
          ELSE
            IF        PT0OV2CD<>1
              DISPLAY   *P1:24,*EL,*B,"This operation code is not a valid ":
                        "ICD10 version 2 code.  ";
              CALL      HITENTER
              GOTO      VOV2C950
            ENDIF
          ENDIF
.
VOV2C900  MOVE      ZERO,EXIT
          GOTO      VOV2C999
.
VOV2C950  MOVE      ONE,EXIT
VOV2C999  RETURN
.
..         INCLUDE   PATICODS
          INC       ICOKYWSR
+
