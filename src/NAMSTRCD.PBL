.-------------------------------------------------
. NAMSTRCD.PBL
.      V10.10.01  28/03/2017  Don Nguyen     TSK 0828266
.                             Added UPCHYP00 to uppercase letter after hyphen
.       V9.11.01  25/02/2009  Jill Habasque  CAR 190842
.                             Mods to leave first letter upcase if begins with '
.       V9.09.02  15/02/2008  Jill Habasque  CAR 155123
.                             Rewrote namasi00 as a copy of namstr00 without
.                             changing cases
.       V9.09.01  18/12/2007  Peter McMullen CAR 155123
.                             Added support for text case as entered
.-------------------------------------------------
. Format Name 
.-------------------------------------------------
NAMSTR00  RESET     NAME35
          GOTO      NAMSTR99 IF EOS
.
NAMSTR10  CMATCH    SP1,NAME35               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME35
            GOTO      NAMSTR10 IF NOT EOS
            GOTO      NAMSTR99               * entire name if blank
          ENDIF
          PACK      KEY35,NAME35,SP30        * reset form pointer
          MOVE      KEY35,NAME35
.
          SCAN      SP1,NAME35             * Locate the blank
          GOTO      NAMSTR20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME35,KEY1,KEY50
          MATCH     "'",KEY1
          IF        @EQUAL
            UNPACK    NAME35,KEY2,KEY50  * save first 2 letters (preserve the ')
            REP       DOWNCASE,KEY50     * to lowercase remaining letters
            APPEND    KEY2,DISPNAME      * append first 2 letters
          ELSE
            REP       DOWNCASE,KEY50     * to lowercase remainder of name
            APPEND    KEY1,DISPNAME      * append first letter (uppercase)
          ENDIF
.
          CALL      UPCHYP00             * to uppercase 1st letter after hyphen
.
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
.         Check for any more names
.
          SETLPTR   NAME35,35              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME35,CCITEM          * reset to this point
          PACK      KEY35,NAME35,SP70      * reset form pointer
          MOVE      KEY35,NAME35
          GOTO      NAMSTR10
.
.         Rest of the string is one name
.
NAMSTR20  UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
NAMSTR99  RETURN
+
.-------------------------------------------------
. Format Name as is - same as namstr but without formatting case
.-------------------------------------------------
NAMASI00  RESET     NAME35
          GOTO      NAMASI99 IF EOS
.
NAMASI10  CMATCH    SP1,NAME35               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME35
            GOTO      NAMASI10 IF NOT EOS
            GOTO      NAMASI99               * entire name if blank
          ENDIF
          PACK      KEY35,NAME35,SP30        * reset form pointer
          MOVE      KEY35,NAME35
.
          SCAN      SP1,NAME35             * Locate the blank
          GOTO      NAMASI20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME35,KEY1,KEY50
.....     REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
.         Check for any more names
.
          SETLPTR   NAME35,35              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME35,CCITEM          * reset to this point
          PACK      KEY35,NAME35,SP70      * reset form pointer
          MOVE      KEY35,NAME35
          GOTO      NAMASI10
.
.         Rest of the string is one name
.
NAMASI20  UNPACK    NAME35,KEY1,KEY50
.....     REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
NAMASI99  RETURN
+
.------------------------------------------------------------------------------
.         Convert the first letter after the hyphen ("-") to uppercase
.------------------------------------------------------------------------------
UPCHYP00  SCAN      "-",KEY50          * Locate the hyphen char
          GOTO      UPCHYP90 IF NOT EQUAL
.
          BUMP      KEY50              * go forward 1 after "-"
          REP       UPPLOW,KEY50       * to uppercase the letter
          BUMP      KEY50              * go forward 1
          REP       LOWUPP,KEY50       * to lowercase the remaining letters
.
UPCHYP90  RESET     KEY50
UPCHYP99  RETURN
+
