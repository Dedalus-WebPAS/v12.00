. *****************************************************************************
. *                                                                           *
. * Include Name  : PATALERT.PBL                                              *
. *                                                                           *
. * Function      : Subroutine Include for                                    *
. *                 Displaying Patient Alerts on Line 3                       *
. *                                                                           *
. * Subroutines   : PATALERT : Display Patient Alerts                         *
. *                                                                           *
. * Files needed  : PATMA1FD.INC   Patient Master File                        *
. *                 PATCODFD.INC   Patient Codes File                         *
. *                                                                           *
. * Modifications : 05/02/90 Steve O'Gorman                                   *
. *                           Include Written                                 *
. *                 08/02/91 Graeme Williams                                  *
. *                           Clear line 3 first                              *
. *                 28/08/91 Justin Coates                                    *
. *                           Mental Health changes - display S for special & *
. *                           X for committed patients                        *
. *                 22/04/93 DIG                                              *
. *                           Added PTCNOUTD alert for Outstanding Debts.     *
. *                 27/05/1993  Graeme Williams            SRF 120813         *
. *                           Make PTCNOUTD blink                             *
. *                 03/08/1993  ROD                                           *
. *                           Added new Medical Warning Alert                 *
. *                 17/11/1993  Paul Howells                                  *
. *                           Added new Occurance Screening                   *
. *       V5.02.01  09/05/1995  Greg Horvat      SRF 135708                   *
. *                 Changed the OUTSDEBT procedure to not open controlf as an *
. *                 I*M occurs. This include will be recompiled into all      *
. *                 programs with the 5.03 recompile.                         *
. *       V5.03.01  21/11/1995    Justin Coates                               *
. *                 make this read CALRDESC                                   *
. *       V10.03.01 29/11/2011 Jill Parkinson CAR 249362                      *
. *                 Changed alert key                                         *
. *       V12.00.01 22/05/2025  J.Tan          TSK 0955096                    *
. *                 Added Alphanumeric visit number changes                   *
. *                                                                           *
. *****************************************************************************
.
. *****************************************************************************
. * PATALERT   : Display Patient Alerts ( On Line 3 of the Screen )           *
. *                                                                           *
. * Files Open : patcodes                                                     *
. *                                                                           *
. * Parameters : ALRTMASK   Indicator Field to Say which alerts to display    *
. *                         Position   Description                            *
. *                         --------   ------------------------------------   *
. *                            1       Medical Alert                          *
. *                            2       Prisoner                               *
. *                            3       Legal Status flag                      *
. *                            3a      Purchaser Alert (conpural) Pos 23-27   *
. *                            4       Occurance Screening                    *
. *                            5       Outstanding Debt                       *
. *                            6       Medical Warning Alert                  *
. *                            7       Bad Debt                               *
. *                            8       Deceased                               *
. *                                                                           *
. *           .  A Valid Read on the Patient Master File ( patma1af )         *
. *                                                                           *
. *****************************************************************************
.
PATALERT  DISPLAY   *P1:3,*EL;                   * Clear any previous alerts
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*204,PTCNAWRN,*226,PTCNMWAD
          READ      CONTROLF,FIFTY9;*132,CALRDESC
          READ      CONTROLF,SEVENTY7;*202,PTCNOCSC
.
.         check on first field - Alerts
.
          UNPACK    ALRTMASK,ANS                 * Validate that the Medical 
          MATCH     "1",ANS                      * Alert Flag is Set
          GOTO      ALERT200 IF NOT EQUAL
.
          OPEN      PATALRT1,"patalrtf"
.
          PACK      KEY16,PURNO,SP20             * Check all Record in the
          CALL      RSPTALR1                     * patalrtf file for a record
ALERT100                                         * with the first indicator
          CALL      RKPTALR1                     * not set to an "N" which
          BRANCH    OVRCD,ALERT200               * means that the category
.                                                * and code indicates that
          MATCH     PURNO,PTALURNO               * this code is cause for a
          GOTO      ALERT200 IF NOT EQUAL        * medical alert
.
          PACK      KEY5,PTALCATG,PTALCODE
          CALL      RDCODE1
          BRANCH    OVRCD,ALERT100
.
          MATCH     ANSN,TCINDC1
          GOTO      ALERT100 IF EQUAL
.
          DISPLAY   *P1:3,*V2LON,*BLINKON,CALRDESC;
.
.         check on second field - Prisoner
.
ALERT200  UNPACK    ALRTMASK,ANS,ANS             * Validate that the Prisoner
          MATCH     "1",ANS                      * Alert Flag is Set
          GOTO      ALERT300 IF NOT EQUAL
.
          MOVE      "T ",TCODE                   * Category for patient type
          PACK      KEY5,TCODE,PTYPE             * Check Patient Type from the
          CALL      RDCODE1                      * Master file
          BRANCH    OVRCD,ALERT300
.
          MATCH     "3",TCINDC1                  * A "3" in the first indicator
          GOTO      ALERT300 IF NOT EQUAL        * indicates prisoner
.
          DISPLAY   *P11:3,*V2LON,"Prisoner";
.
.         check on third field - Legal Status
.
ALERT300  UNPACK    ALRTMASK,ANS,ANS,ANS         * get third flag
          MATCH     "1",ANS
          GOTO      ALERT400 IF NOT EQUAL        * Alert Flag not set
.
          MATCH     SP3,PTMXLS
          GOTO      ALERT400 IF EQUAL            * no legal status
.
          PACK      KEY5,ANSL,ANSS,PTMXLS        * legal status
          CALL      RDCODE1
          BRANCH    OVRCD,ALERT400               * invalid legal status
.
          MOVE      "X",ANS                      * set default value
          MATCH     ANSC,TCINDC1
          GOTO      ALERT350 IF EQUAL            * leave as X
.
          MOVE      "S",ANS                      * set default value
          MATCH     ANSS,TCINDC1
          GOTO      ALERT350 IF EQUAL            * leave as X
.
          MOVE      SP1,ANS                      * dont display anything
.
ALERT350  DISPLAY   *P21:3,*V2LON,ANS            * display flag if required
.
.         check on fourth field - Occurance Screening
.
ALERT400  UNPACK    ALRTMASK,ANS,ANS,ANS,ANS     * get fourth flag
          MATCH     "1",ANS
          GOTO      ALERT500 IF NOT EQUAL        * Alert Flag not set
.
          PACK      KEY5,ANSP,FOUR,PUSR4,SP10        
          CALL      RDCODE1
          BRANCH    OVRCD,ALERT500               * invalid code 
.
          MATCH     "1",TCINDC1
          GOTO      ALERT500 IF NOT EQUAL
.
          DISPLAY   *P31:3,*V2LON,PTCNOCSC       * display flag if required
.
.------ check on fifth field - Outstanding Debt ------
.
ALERT500  PROC      OUTSDEBT                     * do outstanding debt alert
.
.----- check on New Medical Warnings Alert ------
.
          COMPARE   ZERO,PTCNAWRN                 * using New Medical Warnings?
          CALL      CHKMWS00 IF NOT EQUAL         * 0=Off  1,2=On
.
.         check on seventh field - Bad Debt
.
          UNPACK    ALRTMASK,DISPOPT,ANS         * Validate that the Bad Debt
          MATCH     "1",ANS                      * Alert Flag is Set
          GOTO      ALERT800 IF NOT EQUAL
.
          COMPARE   ZERO,PBDEBT                  * Check the Bad Debt Indicator
          GOTO      ALERT800 IF EQUAL            * from the master file
. 
          DISPLAY   *P61:3,*V2LON,"Bad Debt";
.
.         check on eighth field - Deceased
.
ALERT800  UNPACK    ALRTMASK,DISPOPT,ANS,ANS     * Validate that the Deceased
          MATCH     "1",ANS                      * Alert Flag is Set
          GOTO      ALERT999 IF NOT EQUAL
.
          COMPARE   ONE,PCEASE                   * Check the Deceased Indicator
          GOTO      ALERT999 IF NOT EQUAL        * on the master file
. 
          DISPLAY   *P71:3,*V2LON,"Deceased";
.
ALERT999  CLOSE     PATALRT1
          RETURN
+
.----------------------------------------------------------------------------
. Check for Medical Warnings NZ NHI/MWS System
.
. NZHISWRN  FORM      1        * KURN National Read 0-Read Failed (Check Local)
.                              *                    1-Read OK Warning Set
.                              *                    2-Read OK Warning Not Set
.----------------------------------------------------------------------------
CHKMWS00  BRANCH    NZHISWRN,CHKMWS90,CHKMWS99 
.
CHKMWS50  OPEN      PATMWSA1,"patmwsaf" * Can't read national system so
          PACK      KEY14,PURNO,SP20    * Check Local File for MW Records
          CALL      RSPTMWS1
          CALL      RKPTMWS1
          BRANCH    OVRCD,CHKMWS99
          MATCH     PURNO,PTMWURNO
          GOTO      CHKMWS99 IF NOT EQUAL
.
CHKMWS90  DISPLAY   *P41:3,*V2LON,*BLINKON,PTCNMWAD
.
CHKMWS99  RETURN
.*****************************************************************************
.*                                   OUTSDEBT                                *
.*         Procedure to process outstanding debt alert - field no 5.         *
.*****************************************************************************
          DEFPROC   OUTSDEBT
.
          INC       PATINVFD/INC
          INC       PATVISFD/INC
.
PTCNOUTD  DIM       9
PAIDTOT   FORM      8.2
.
          READ      CONTROLF,TWENTY1;*123,PTCNOUTD
.
          MATCH     SP9,PTCNOUTD            * see if we are using the 
          GOTO      OUTSD999 IF EQUAL         outstanding debt alert
.
          UNPACK    ALRTMASK,ANS,ANS,ANS,ANS,ANS 
          MATCH     "1",ANS                 * check to see if we are using the
          GOTO      OUTSD999 IF NOT EQUAL     fifth alert
.
          OPEN      PATINVR3,"patinvrf"
          OPEN      PATVISA2,"patvisaf"
.
          PACK      KEY24,PURNO,SP20
          CALL      RDSVISA2                * position on the visit file
.
.------ read through the visit file ------
.
OUTSD100  CALL      RDKVISA2
          BRANCH    OVRCD,OUTSD999
.
          MATCH     PURNO,PVIURNO           * match U/R numbers
          GOTO      OUTSD999 IF NOT EQUAL
.
          PACK      KEY16,PVIBILL,SP8       * position on the invoice file
          CALL      RDSINV3                   for this admission number 
.
.------ read through the invoice file for this admission number ------
.
OUTSD200  CALL      RDKINV3                
          BRANCH    OVRCD,OUTSD100
.
          MATCH     PVIBILL,PINVADM         * compare admission numbers
          GOTO      OUTSD100 IF NOT EQUAL
.
          ASSIGN    PINVAMT + PINVPATA + PINVHFDA + PINVOTHA + PINVJOUR,PAIDTOT
          IF        PAIDTOT > 0
            DISPLAY   *P31:3,*V2LON,*BLINKON,PTCNOUTD;
            GOTO      OUTSD999
          ELSE
            GOTO      OUTSD200
          ENDIF
.
          INC       IBAOVRIO/INC
          INC       PATINVIO/INC
          INC       PATVISIO/INC
.
OUTSD999  CLOSE     PATINVR3
          CLOSE     PATVISA2
.
          ENDPROC
