.**********************************************************************
.  CHKAP - Check if payments file exists              Called By lots
.        Returns  - OVRCD    (1=doesn't exist)
.**********************************************************************
CHKAP000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSAPYA1,APAPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHKAP100
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSAPYA2,APAPFILE
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSAPYA3,APAPFILE
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSAPYA4,APAPFILE
          TRAPCLR   IO
          GOTO      CHKAP999
.
CHKAP100  MOVE      "9999999999999999999999999999999999999999",KEY40
          MOVE      ZERO,APIMTYP
          CALL      RDAPIM1
.
CHKAP999  RETURN
.**********************************************************************
.  RDPAR - Read selection parameters                  Called By lots
.        Returns  - CREDF,CREDT,TYPEF,TYPET,DATEF,DATET,TERMF,TERMT
.                   OVRCD (1=no data found)
.**********************************************************************
RDPAR000  UNPACK    SP70,CREDF,CREDT,TYPEF,TYPET,DATEF,DATET,TERMF,TERMT
          CLOSE     APSPYDAF
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSPYDAF,"apspydaf"
          TRAPCLR   IO
          BRANCH    OVRCD,RDPAR999     * file not found ?
.
          READ      APSPYDAF,SEQ;CREDF,CREDT,TYPEF,TYPET:
                                 DATEF,DATET,TERMF,TERMT
          CALL      OVERCOND IF OVER   * record not found ?
.
RDPAR999  RETURN
.**********************************************************************
.  MAKAP - Create payments file (if possible)         Called By lots
.        Returns  - EXIT     (1=already exists, 2=cant create)
.**********************************************************************
MAKAP000  CALL      CHKAP0000          * check if file exists
          COMPARE   ZERO,OVRCD
          GOTO      MAKAP950 IF EQUAL
.
          DISPLAY   *P1:20;            * move cursor so error displayed to scrn
          EXECUTE   APAPMAKE,TASKID    * create files
          EXECUTE   APAPADD,TASKID     * add 3rd index
          EXECUTE   APAPADD2,TASKID    * add 4th index
.
          CALL      CHKAP0000          * check if file was created
          COMPARE   ONE,OVRCD
          GOTO      MAKAP970 IF EQUAL
.
          MOVE      "9999999999999999999999999999999999999999",KEY40
          UNPACK    KEY40,APIMBCH,APIMCRD,APIMREF,APIMDOC
          CALL      RDAPIM1
          IF        OVRCD=1
            MOVE      ZERO,APIMTYP
            CALL      WRAPIM1
          ENDIF
.
MAKAP900  MOVE      ZERO,EXIT          * continue
          GOTO      MAKAP999
.
MAKAP950  MOVE      ONE,EXIT           * already exists
          GOTO      MAKAP999
.
MAKAP970  MOVE      TWO,EXIT           * cant create !!
          GOTO      MAKAP999
.
MAKAP999  RETURN
.**********************************************************************
.  DELAP - Delete payments file (if possible)         Called By lots
.        Returns  - EXIT     (1=doesn't exist, 2=cant delete)
.**********************************************************************
DELAP000  
          MOVE      "9999999999999999999999999999999999999999",KEY40
          MOVE      ZERO,APIMTYP
          CALL      RDAPIM1
          IF        OVRCD=0
            CALL      DEAPIM1
          ENDIF
.
          CALL      CHKAP0000          * check if file exists
          COMPARE   ONE,OVRCD
          GOTO      DELAP950 IF EQUAL
.
          DISPLAY   *P1:20;            * move cursor so error displayed to scrn
          PREP      LOKFILE,APAPLOK    * delete lok file for FUJITSI machines
          CLOSE     LOKFILE
.
          CLOSE     APSAPYA1
          CLOSE     APSAPYA2
          CLOSE     APSAPYA3
          CLOSE     APSAPYA4
          EXECUTE   APAPKILL,TASKID    * delete files
          CLOSE     APSPYDAF
          PREP      APSPYDAF,"apspydaf"
          CLOSE     APSPYDAF
.
          CALL      CHKAP0000          * check if file was deleted
          COMPARE   ZERO,OVRCD
          GOTO      DELAP970 IF EQUAL
.
.
DELAP900  CALL      DELCA000           * try to delete cheque audit
          MOVE      ZERO,EXIT          * continue
          GOTO      DELAP999
.
DELAP950  MOVE      ONE,EXIT           * doesn't exist
          GOTO      DELAP999
.
DELAP970  
          MOVE      "9999999999999999999999999999999999999999",KEY40
          UNPACK    KEY40,APIMBCH,APIMCRD,APIMREF,APIMDOC
          CALL      RDAPIM1
          IF        OVRCD=1
            CALL      WRAPIM1
          ENDIF
.
          MOVE      TWO,EXIT           * cant delete !!
          GOTO      DELAP999
.
DELAP999  RETURN
.**********************************************************************
.  CHKCA - Check if cheque audit file exists              Called By lots
.        Returns  - OVRCD    (1=doesn't exist)
.**********************************************************************
CHKCA000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSCAFA1,APCAFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHKCA999
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSCAFA2,APCAFILE
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSCAFA3,APCAFILE
          TRAPCLR   IO
.
CHKCA999  RETURN
.**********************************************************************
.  MAKCA - Create cheque audit file (if possible)         Called By lots
.        Returns  - EXIT     (1=already exists, 2=cant create)
.**********************************************************************
MAKCA000  CALL      CHKCA0000          * check if file exists
          COMPARE   ZERO,OVRCD
          GOTO      MAKCA950 IF EQUAL
.
          DISPLAY   *P1:20;            * move cursor so error displayed to scrn
          EXECUTE   APCAMAKE,TASKID    * create file
.
          CALL      CHKCA0000          * check if file was created
          COMPARE   ONE,OVRCD
          GOTO      MAKCA970 IF EQUAL
.
.
MAKCA900  MOVE      ZERO,EXIT          * continue
          GOTO      MAKCA999
.
MAKCA950  MOVE      ONE,EXIT           * already exists
          GOTO      MAKCA999
.
MAKCA970  MOVE      TWO,EXIT           * cant create !!
          GOTO      MAKCA999
.
MAKCA999  RETURN
.**********************************************************************
.  DELCA - Delete cheque audit file (if possible)         Called By lots
.        Returns  - EXIT     (1=doesn't exist, 2=cant delete)
.**********************************************************************
DELCA000  CALL      CHKCA0000          * check if file exists
          COMPARE   ONE,OVRCD
          GOTO      DELCA950 IF EQUAL
.
          DISPLAY   *P1:20;            * move cursor so error displayed to scrn
          PREP      LOKFILE,APCALOK    * delete lok file for FUJITSI machines
          CLOSE     LOKFILE
.
          CLOSE     APSCAFA1
          CLOSE     APSCAFA2
          CLOSE     APSCAFA3
          EXECUTE   APCAKILL,TASKID    * delete file
.
          CALL      CHKCA0000          * check if file was deleted
          COMPARE   ZERO,OVRCD
          GOTO      DELCA970 IF EQUAL
.
.
DELCA900  MOVE      ZERO,EXIT          * continue
          GOTO      DELCA999
.
DELCA950  MOVE      ONE,EXIT           * doesn't exist
          GOTO      DELCA999
.
DELCA970  MOVE      TWO,EXIT           * cant delete !!
          GOTO      DELCA999
.
DELCA999  RETURN
.
.**********************************************************************
.  CRDOF - Create DOF file if possible                Called By lots
.        Returns  - OVRCD    (1=couldn't create)
.**********************************************************************
CRDOF000  CALL      RDFMCO65           * read parameters
          CALL      RDFMCO66
.
          PACK      FMCODFDI,FMCODFDI,SP70
          MATCH     SP70,FMCODFDI                * is a directory set up ?
          IF        @EQUAL
            PACK      DOFFILE,APCADOFF,SLASH,APAPDOF
          ELSE
            STRIP     FMCODFDI
            PACK      DOFFILE,FMCODFDI,APCADOFF,DOT,APAPDOF
          ENDIF
          REP       " 0",DOFFILE
.
          CLEAR     DISPMSG
          APPEND    "Creating ",DISPMSG
          APPEND    DOFFILE,DISPMSG
          CALL      MESSAGE2
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      APSDOFF1,DOFFILE
          TRAPCLR   IO
.
          CALL      MESSAGE3
.
CRDOF999  RETURN
