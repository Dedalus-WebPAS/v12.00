.
.       PATDRGDS : Display Date Range Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available date ranges
.
PATDRGDS  MOVE       FIVE,CVERT         * Starting line number
          MOVE       ONE,CCOL           * Starting column
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"DATE RANGE ENTRIES"
.
          PACK       KEY6,SIXZ          * Start from beginning of file
          CALL       RDSDRGA1           * Position to end of file
.
.         Loop over file
.
DRGALP1   CALL       RDPDRGA1           * Read the next date range record
          BRANCH     OVRCD,ENDDRGA1     * No more records. Finish.
.
          UNPACK     DRGFDTE,CCENT,CYEAR,CMON
          DISPLAY    *PCCOL:CVERT,*V2LON,CMON,SLASH,CCENT,CYEAR
.
          UNPACK     DRGFDTE,CCENT,CYEAR,CMON,CDAY
          ADD        TEN1,CCOL          * Shift across
          MOVE       ONE,CHIGHLT        * No highlighting
          CALL       DSPDATE            * Display the from date
.
          ADD        TEN1,CCOL          * Shift across
          DISPLAY    *PCCOL:CVERT,"to "
.
          UNPACK     DRGTDTE,CCENT,CYEAR,CMON,CDAY
          ADD        THREE,CCOL         * Shift across
          MOVE       ONE,CHIGHLT        * No highlighting
          CALL       DSPDATE            * Display the to date
.
          SUB        TWENTY5,CCOL       * Shift back
.
          ADD        ONE,CVERT
.
          COMPARE    TWENTY4,CVERT
          GOTO       DRGALP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       DRGALP1 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSDRG200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      ENDDRG2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATDRGDS IF EQUAL
.
          GOTO      DSDRG400
.
.         Use the new standards question
.
DSDRG200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      ENDDRG2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATDRGDS IF EQUAL
.
DSDRG400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
          GOTO       DRGALP1
.
.      End of rates
.
ENDDRGA1  BRANCH     CNEWDS,ENDDRG2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDDRG2   RETURN
