. *****************************************************************************
. *                                                                           *
. * Include Name :  GETCHSCR.PBL                                              *
. *                                                                           *
. * Author       :  J.Tan  TSK 0886178                                        *
. *                                                                           *
. * Function     :  Subroutine for Automate Calculate Charlson score          *
. *                 - By adding up the value of Charles scores that are       *
. *                   mapped to each ICD Codes                                *
. * Modification :                                                            *
. *      V11.04.01 04/06/2024 Ebon Clements TASK 0947060                      *
. *                Added trap to paticuaf write to prevent I * D              *
. *      V11.02.01 31/03/2022 J.Tan 0918410                                   *
. *                Mod to pack KEY8 with admission no & spaces (I*U paticu)   *
. *                                                                           *
. *****************************************************************************
GETCHSCR  MOVE      ZERO,F2                 * total score
          OPEN      PMSVMTA1,"pmsvmtaf"
.
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
CHASC100  CALL      RKPTECD1
          BRANCH    OVRCD,CHASC400
.
          MATCH     DAADMNO,DPTEDADM
          GOTO      CHASC400 IF NOT EQUAL
.
.         Check Charlson score mapping
.
          MOVE      "pat10dxf",KEY8          * Default to 10
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          UNPACK    PTCNGDX1,KEY4,KEY5           * ICD10 Ed.11
          MATCH     "9999",KEY4                  * is year = "9999"?
          IF        !@EQUAL
            MOVE      "patd11af",KEY8
          ENDIF
.
          PACK      KEY30,PTEDCODE,SP70      * Disease code
          MOVE      "CHA",PMVMMVSI           * Set ID
          PACK      KEY41,KEY8,KEY30,PMVMMVSI
          CALL      RDPMVMT1
          BRANCH    OVRCD,CHASC100           * Next disease code
.
          MOVE      ZERO,FORM2
          MOVE      SP70,KEY2
          UNPACK    PMVMMCOD,KEY2            * Charlson score
          SQUEEZE   KEY2
          MOVE      KEY2,FORM2
          ADD       FORM2,F2           * Accummulated the scores
.
.         Maximum Charlson score is 16
.
          COMPARE   "16",F2
          GOTO      CHASC100 IF LESS
.
          MOVE      "16",F2           * Maximum score
.
CHASC400  PACK      KEY8,AADMNO,SP70
          CALL      RDPTICU1                * Read the patient ICU file
          BRANCH    OVRCD,CHASC900
.
          PACK      PTICCHSC,F2,SP70       * Charlson Score
          CALL      UPPTICU1
          GOTO      CHASC9999
.
CHASC900  MOVE      AADMNO,PTICUADM
          MOVE      ZERO,PTICUINT          * Hrs in Intensive care
          MOVE      ZERO,PTICUMEC          * MV Hours
          MOVE      ZERO,PTICUCCU          * Hrs in Critical Care
          MOVE      SP70,PTICAMBN          * Ambulance client number
          MOVE      ZERO,PTICCPAP          * Hrs on CPAP
          MOVE      ZERO,PTICHNCU          * Hrs in NCU
          MOVE      ZERO,PTICINTM          * Minutes of ICU
          MOVE      ZERO,PTICMECM          * MV Minutes
          PACK      PTICCHSC,F2,SP70       * Charlson Score
          MOVE      SP70,PTICUSPR
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO        * Trap to stop I * D
          CALL      WRPTICU1
          TRAPCLR   IO
.
CHASC999  RETURN


