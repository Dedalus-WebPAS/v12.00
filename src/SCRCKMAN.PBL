.******************************************************************************
.* System         : ALL with screen painting.                                 *
.*                                                                            *
.* Procedure Name : SCRCKMAN.PBL                                              *
.*                                                                            *
.* Function       : Check that all Mandatory fields have been input when      *
.*                  posting data for Screen painted screens.                  *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 19/07/1991                                                *
.*                                                                            *
.* Parameters     : SCRID        the screen id for other files                *
.*                                                                            *
.* Ext. Routines  : SPCK0000      load the required variable into DIM80       *
.*                                                                            *
.* Returns        : EXIT = 1      a mandatory field hasn't been entered.      *
.*                                                                            *
.* Modifications  : 14/04/1992    Justin Coates                               *
.*                  changed to a procedure and added read on SCRMASFD         *
.*                  26/10/1992    Justin Coates                               *
.*                  display a highlight bar in items keyin position           *
.******************************************************************************
.
          DEFPROC   SCRCKMAN
.
          INC       SCRMASFD/INC            * screen master file
          INC       SCRFLDFD/INC            * field file
.
PROGRAM   DIM       8
.
.*********************************************************************
.         check all fields
.*********************************************************************
CKMAN000  PACK      KEY15,PRGID,SCRID,SP20
          CALL      RSSCPS1
CKMAN100  CALL      RKSCPS1                 * next read
          BRANCH    OVRCD,CKMAN900          * end of file
.
          MATCH     SCPSPID,PRGID
          GOTO      CKMAN900 IF NOT EQUAL   * different program
.
          MATCH     SCPSSID,SCRID
          GOTO      CKMAN900 IF NOT EQUAL   * different screen
.
          COMPARE   ONE,SCPSMAN
          GOTO      CKMAN100 IF NOT EQUAL   * not a mandatory field
.
. ------- now get the appropriate field -------
.
          PACK      DIM80,SP30,SP30,SP20    * clear load variable
          CALL      SPCK0000                * get the value of field to check
.
          MATCH     SP70,DIM80
          GOTO      CKMAN100 IF NOT EQUAL   * there is data there
.
. ------- one of the mandatory fields isn't entered so so give error
. ------- see if there is a description on the selections file for error
.
          MOVE      SCPSCOL,CCOL
          MOVE      SCPSROW,CVERT
          MOVE      SCPSLEN,LLCNT
          PACK      DIM80,SP30,SP30,SP20
          SETLPTR   DIM80,LLCNT
          DISPLAY   *PCCOL:CVERT,*V2LON,*HON,*+,DIM80
          SETLPTR   DIM80
.
          PACK      KEY18,PRGID,SCRID,SCPSITM,SP3
          CALL      RSSCSL2
          CALL      RKSCSL2
          BRANCH    OVRCD,CKMAN200          * no on selections file
.
          MATCH     SP30,SCFLDES
          GOTO      CKMAN200 IF EQUAL       * no description
.
          STRIP     SCSLDES
          DISPLAY   *P1:24,*B,"Field ",*V2LON,*+,SCSLDES,*HOFF:
                    " is mandatory.  ",*EL;
          GOTO      CKMAN890
.
. ------- no description on selections file so use field file desc -------
. ------- have to see which field file to use                      -------
.
CKMAN200  PACK      KEY10,PRGID,SCRID
          OPEN      SCRMASA1,"scrmasaf"
          CALL      RDSCMA1
          CLOSE     SCRMASA1
.
          PACK      SCMAFPR,SCMAFPR,SP8
          MATCH     SP8,SCMAFPR
          IF        @EQUAL
            MOVE      PRGID,PROGRAM
          ELSE
            MOVE      SCMAFPR,PROGRAM
          ENDIF
.           
          MATCH     SP2,SCMAFSC
          IF        @EQUAL
            MOVE      SCRID,SCMAFSC
          ENDIF
.
          PACK      KEY15,PROGRAM,SCMAFSC,SCPSITM
          OPEN      SCRFLDA1,"scrfldaf"
          CALL      RDSCFL1                 * get item description
          CLOSE     SCRFLDA1
          STRIP     SCFLDES
          DISPLAY   *P1:24,*B,"Field ",*V2LON,*+,SCFLDES,*HOFF:
                    " is mandatory.  ",*EL;
.
CKMAN890  CALL      HITENTER 
          MOVE      ONE,EXIT
          GOTO      CKMAN999
.
CKMAN900  MOVE      FALSE,EXIT
          GOTO      CKMAN999
.
          INC       SCRFLDIO/INC            * field file
          INC       SCRMASIO/INC            * master file
          INC       IBAOVRIO/INC            * overcond etc
.
CKMAN999  ENDPROC
...............................................................................
