.******************************************************************************
.*                                 SJOGLABL                                   *
.*                    Label 27 for SJOG Geelong I/P labels                    *
.*                    Label 15 for SJOG Geelong O/P labels                    *
.*                    Label 1 (AECNLABL) for SJOG Geelong ED labels           *
.*                                                                            *
.* Requires: SJOGFLAG 1 = Emergency Visit                                     *
.*                    2 = Outpatient Visit                                    *
.*                    3 = Inpatient Visit                                     *
.*           PMI and Visit records read                                       *
.* Mods:                                                                      *
.*        V11.04.01 05.02.2024 DA Sarkies     TSK 0936282                     *
.*                  Increased size of variable to hold Healthfund Number      *
.*        V10.10.01 22/03/2017 Richa Phogat   TSK 0832066                     *
.*                  Changed DVA Card Number to be collected from              *
.*                  PMCDCNUM.PMSCCDFD instead of PREPAT.PMIMA1FD              * 
.*        V10.05.05 22/10/2014  Steve Armstrong   CAR 306544                  *
.*                  Mods to SJOG ED barcoded labels to use PREPAT if there    *
.*                  is no DVA number recorded on the concession card file.    *
.*        V10.05.04 26/09/2014  Steve Armstrong   CAR 306544                  *
.*                  Mods to SJOG ED barcoded labels to print Local GP name.   *
.*        V10.05.03 23/09/2014  Steve Armstrong   CAR 294204                  *
.*                  Further label alignment changes.                          *
.*                  Also fixed the printing of HF and HF Membership No to     *
.*                  default back to the PMI fields if both visit level fields *
.*                  are blank.                                                *
.*        V10.05.02 15/09/2014  Steve Armstrong   CAR 294204                  *
.*                  Fixed setting of Stroke Weight to be at end of line 2     *
.*                  instead of start of line 3.                               *
.*        V10.05.01 10/09/2014  Steve Armstrong   CAR 294204                  *
.*                  Fixed setting of font after printing of left label barcode*
.*        V10.05.00 28/07/2014  Steve Armstrong   CAR 294204                  *
.*                  Include created.                                          *
.******************************************************************************
.
.         Set up the label variables for printing
.
SETSJOGL  PACK      SJOGLIN1,SP70,SP70
          PACK      SJOGLIN2,SP70,SP70
          MOVE      SP70,SJOGLN3A
          MOVE      SP70,SJOGLN3B
          PACK      SJOGLIN4,SP70,SP70
          MOVE      SP70,SJOGLN5A
          MOVE      SP70,SJOGLN5B
          MOVE      SP70,SJOGLN6A
          MOVE      SP70,SJOGLN6B
          MOVE      SP70,SJOGLN6C
          PACK      SJOGLIN7,SP70,SP70
          PACK      SJOGLIN8,SP70,SP70
.
.         Line 1 - Surname (18 chars)
.
          MOVE      PSNAME,SJOGLIN1
          STRIP     SJOGLIN1
.
.         Line 2 - Given Name (10 chars) and Title (4 chars)
.
          UNPACK    PMPXFNAM,ANS,DIM9
          MOVE      ANS,SJOGLIN2
          ENDSET    SJOGLIN2
          REP       UPPTOLOW,DIM9
          STRIP     DIM9
          APPEND    DIM9,SJOGLIN2
          MATCH     PMPXSNAM,SP70
          IF        !@EQUAL
            APPEND    SP1,SJOGLIN2
            UNPACK    PMPXSNAM,ANS,DIM9
            APPEND    ANS,SJOGLIN2
            REP       UPPTOLOW,DIM9
            STRIP     DIM9
            APPEND    DIM9,SJOGLIN2
          ENDIF
          RESET     SJOGLIN2
          SETLPTR   SJOGLIN2,10
          ENDSET    SJOGLIN2
          APPEND    SP70,SJOGLIN2
          RESET     SJOGLIN2
          STRIP     SJOGLIN2
          ENDSET    SJOGLIN2
          APPEND    ", ",SJOGLIN2
          UNPACK    PTITL,ANS,DIM4
          APPEND    ANS,SJOGLIN2
          REP       UPPTOLOW,DIM4
          STRIP     DIM4
          APPEND    DIM4,SJOGLIN2
          RESET     SJOGLIN2
          STRIP     SJOGLIN2
.
.         Line 3 - U/R (8 chars), Sex (3 chars), Date of Birth (dd/mm/ccyy) and
.                  Age (4 chars including trailing "D", "M" or "Y")
.
          MOVE      PURNO,SJOGLN3A                    * U/R
          SQUEEZE   SJOGLN3A
.
          MOVE      "DOB ",SJOGLN3B
          ENDSET    SJOGLN3B
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY      * DOB
          CALL      PACDATE
          APPEND    CPCDATE,SJOGLN3B
          ENDSET    SJOGLN3B
          CALL      IBACLOCK                          * Age
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          APPEND    SP1,SJOGLN3B
          APPEND    PSAGECON,SJOGLN3B
          REP       UPPLOW,PSAGETYP
          APPEND    PSAGETYP,SJOGLN3B
          APPEND    SP1,SJOGLN3B
          APPEND    LBRACK,SJOGLN3B                   * Sex
          APPEND    PSEX,SJOGLN3B
          APPEND    RBRACK,SJOGLN3B
          RESET     SJOGLN3B
.
.         Line 4 - Address Line 1 (35 chars), Suburb (35 chars), State
.                  (35 chars) and Postcode (4 chars)
.
          MOVE      PADD1,SJOGLIN4
          STRIP     SJOGLIN4
          ENDSET    SJOGLIN4
          APPEND    SP1,SJOGLIN4
          APPEND    PSUBRB,SJOGLIN4
          RESET     SJOGLIN4
          STRIP     SJOGLIN4
          ENDSET    SJOGLIN4
          APPEND    SP1,SJOGLIN4
          APPEND    PADD4,SJOGLIN4
          RESET     SJOGLIN4
          STRIP     SJOGLIN4
          ENDSET    SJOGLIN4
          APPEND    SP1,SJOGLIN4
          APPEND    PPOST,SJOGLIN4
          RESET     SJOGLIN4
          STRIP     SJOGLIN4
.
.         Line 5 - Health Fund Code (3 chars), HF Member Number (19 chars)
.                  and Concession Number (10 chars)
.
          LOAD      DIM3,SJOGFLAG,EMVIFUND,OTBBFUND,AFUNDH       * HF
          MOVE      PMVXFNDM,DIM19
          MATCH     SP3,DIM3                     * blank visit fund ?
          IF        @EQUAL
            MATCH     DIM19,SP70                 * yes - blank HF Memb. No.?
            IF        @EQUAL
              MOVE      PFUNDH,DIM3              * yes - use PMI HF Details
              MOVE      PFNDMM,DIM19
            ENDIF
          ENDIF
          MOVE      DIM3,SJOGLN5A
          ENDSET    SJOGLN5A
          APPEND    SP1,SJOGLN5A
          APPEND    DIM10,SJOGLN5A
          RESET     SJOGLN5A
.
          CALL      GCCSJG00
          MATCH     SP20,PMCDCNUM
          IF        !@EQUAL
            PACK      SJOGLN5B,ANSC,PMCDCNUM
            SQUEEZE   SJOGLN5B
            RJUSTIFY  SJOGLN5B
          ENDIF
.
.         Line 6 - Medicare Number (12 chars), Medicare Expiry Date (5 chars),
.                  Religion Code (3 chars) and Visit Date (8 chars)
.
          MATCH     SP10,PMEDI                   * Medicare Number
          IF        !@EQUAL
            MOVE      PMEDI,SJOGLN6A
            ENDSET    SJOGLN6A
            MATCH     SP2,PTMXMCCD               * Medicare Reference
            IF        @EQUAL
              APPEND    SP2,SJOGLN6A
            ELSE
              APPEND    "/",SJOGLN6A
              MOVE      PTMXMCCD,DIM2
              SQUEEZE   DIM2
              APPEND    DIM2,SJOGLN6A
            ENDIF
            APPEND    SP1,SJOGLN6A
            MATCH     SP8,PMPXMEDC               * Medicare Expiry Date
            IF        @EQUAL
              APPEND    SP5,SJOGLN6A
            ELSE
              UNPACK    PMPXMEDC,CCENT,CYEAR,CMON,CDAY
              PACK      DIM5,CMON,SLASH,CYEAR
              REP       " 0",DIM5
              APPEND    DIM5,SJOGLN6A
            ENDIF
          ENDIF
.
          MOVE      PREG,SJOGLN6B                * Religion
.
          LOAD      DIM8,SJOGFLAG,EMVIDATE,OBADATE,ADATE      * Visit Date
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,SJOGLN6C
.
.         Line 7 - Claim Type Code (3 chars) and DVA Number (11 chars)
.
          LOAD      DIM3,SJOGFLAG,EMVICOMP,OBACOMP,ACLAIM
          MOVE      DIM3,SJOGLIN7
          ENDSET    SJOGLIN7
          APPEND    SP1,SJOGLIN7
          MOVE      THREE,DIM1
          CALL      CONCAD00
          APPEND    PMCDCNUM,SJOGLIN7
          RESET     SJOGLIN7
          STRIP     SJOGLIN7
.
.         Line 8 - Attending Doctor/Clinic
.
          BRANCH    SJOGFLAG,STSJOG10:           * ED label
                             STSJOG20:           * O/P label
                             STSJOG30            * I/P label
.
.         ED Label
.
STSJOG10  MOVE      "GP:",SJOGLIN8
          MATCH     SP10,PMPXRHC1                * blank Local GP ?
          GOTO      STSJOG90 IF EQUAL            * yes
.
          MOVE      PMPXRHC1,KEY10
          CALL      RDPMHCP1                     * no - HCP on file ?
          IF        OVRCD = 0
            ENDSET    SJOGLIN8                   * * yes
            MOVE      PMHCSNAM,KEY19
            STRIP     KEY19
            APPEND    KEY19,SJOGLIN8
            APPEND    COMMA,SJOGLIN8
            APPEND    SP1,SJOGLIN8
            MOVE      PMHCGNAM,KEY19
            STRIP     KEY19
            APPEND    KEY19,SJOGLIN8
            RESET     SJOGLIN8
          ENDIF
          GOTO      STSJOG90
.
.         O/P Label
.
STSJOG20  MOVE      OBACLIN,SJOGLIN8
          GOTO      STSJOG90
.
.         I/P Label
.
STSJOG30  MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD = 0
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,SJOGLIN8
          ENDIF
.
.         Now make sure that everything is in uppercase, except line 2 & 3
.
STSJOG90  REP       UPPLOW,SJOGLIN1
          REP       UPPLOW,SJOGLIN4
          REP       UPPLOW,SJOGLN5A
          REP       UPPLOW,SJOGLN5B
          REP       UPPLOW,SJOGLN6A
          REP       UPPLOW,SJOGLN6B
          REP       UPPLOW,SJOGLN6C
          REP       UPPLOW,SJOGLIN7
          REP       UPPLOW,SJOGLIN8
.
          RETURN
+
.****************************************************************************
.*   Print the A4 label sheet (7 x 2 labels) for SJOG Geelong on HP Laser   *
.*   using BarDIMM barcodes                                                 *
.****************************************************************************
.
PRTSJOGL  COMPARE   ZERO,NOLABEL            * any labels to print ?
          GOTO      PRTSJ999 IF EQUAL       * no
.
.         Note: ESC sequences can only be combined where the first two
.               characters after the ESC are the same.
.               Where escape sequences are combined, use a lowercase termination
.               character except for the last sequence, where the termination
.               character must be uppercase.
.               There are 300 Dots/Inch.
.               Barcodes are Code 128.
.               After each barcode is printed, we need to reset height, font
.               and stroke weight as printing the barcode results in the
.               loss of all these settings.
.
.         Work out how many sheets are being printed based on 14 labels/sheet
.
          ASSIGN    (NOLABEL/14),SHTCOUNT        * total sheets to print
          ASSIGN    (NOLABEL%14),REMNDER
          IF        REMNDER <> 0
            ADD       ONE,SHTCOUNT
          ENDIF
.
.         Set up the U/R # for a barcode.
.         To right justify the U/R barcode, we need to determine how
.         many digits in the U/R, then allow 32 dots between each
.         character, plus the offset for the left label and right label.
.
          MOVE      PURNO,DIM8
          SQUEEZE   DIM8
          MOVELPTR  DIM8,FORM1
.>>>>>>   ASSIGN    (820+((8-FORM1)*32)),URNOLEFT   * left barcode position
.>>>>>>   ASSIGN    (2023+((8-FORM1)*32)),URNORIGT  * right barcode position
          LOAD      URNOLEFT,FORM1,URLENGL1,URLENGL2,URLENGL3,URLENGL4,URLENGL5:
                                   URLENGL6,URLENGL7,URLENGL8
          LOAD      URNORIGT,FORM1,URLENGR1,URLENGR2,URLENGR3,URLENGR4,URLENGR5:
                                   URLENGR6,URLENGR7,URLENGR8
.         ASSIGN    (414+((8-FORM1)*46)),URNOLEFT   * left barcode position
.         ASSIGN    (1614+((8-FORM1)*46)),URNORIGT  * right barcode position
.
.         Keep the following piece of code to use when testing with Code
.         3/9 (as we don't have BarDIMM fonts).
.         The asterisk is required at the end to signify the end of the
.         barcode.
.
          MOVE      PURNO,WRCHURNT
          ENDSET    WRCHURNT
          APPEND    ASTRIX,WRCHURNT
          RESET     WRCHURNT
          SQUEEZE   WRCHURNT
.
.         Set up the visit # for a barcode.
.         To right justify the visit barcode, we need to determine how
.         many digits in the visit, then allow 32 dots between each
.         character, plus the offset for the left label and right label.
.
          LOAD      KEY8,SJOGFLAG,EMVIADMN,OBAOUTNO,AADMNO
          SQUEEZE   KEY8
          MOVELPTR  KEY8,FORM1
.>>>>>>>  ASSIGN    (759+((8-FORM1)*32)),VISNLEFT   * left barcode position
.>>>>>>>  ASSIGN    (1955+((8-FORM1)*32)),VISNRIGT  * right barcode position
          LOAD      VISNLEFT,FORM1,VSLENGL1,VSLENGL2,VSLENGL3,VSLENGL4,VSLENGL5:
                                   VSLENGL6,VSLENGL7,VSLENGL8
          LOAD      VISNRIGT,FORM1,VSLENGR1,VSLENGR2,VSLENGR3,VSLENGR4,VSLENGR5:
                                   VSLENGR6,VSLENGR7,VSLENGR8
.         ASSIGN    (400+((8-FORM1)*47)),VISNLEFT   * left barcode position
.         ASSIGN    (1600+((8-FORM1)*47)),VISNRIGT  * right barcode position
.
.         Keep the following piece of code to use when testing with Code
.         3/9 (as we don't have BarDIMM fonts).
.         The asterisk is required at the end to signify the end of the
.         barcode.
.
          LOAD      DIM9,SJOGFLAG,EMVIADMN,OBAOUTNO,AADMNO
          ENDSET    DIM9
          APPEND    ASTRIX,DIM9
          RESET     DIM9
          SQUEEZE   DIM9
.
.         Set default printer values
.
PRTSJ010  PRINT     *1,ESC,"E":             * Reset
                    ESC,"&l1e":             * Top Margin (1 line)
                    "&l8f":                 * Text Length (8 lines)
                    "&l6D":                 * Line Spacing (6 lines/inch)
                    ESC,"(8U":              * Symbol set (Roman 8)
                    ESC,"(s1p":             * Spacing (Proportional)
                    "0s":                   * Style (Upright)
                    "4T"                    * Typeface (Helvetica)
.
.         Work out how many labels are required to print on a sheet
.
          IF        SHTCOUNT = 1
            ASSIGN    (NOLABEL/2),LOOPCNT
            ASSIGN    (NOLABEL%2),REMNDER
            ADD       REMNDER,LOOPCNT
          ELSE
            MOVE      ZERO,REMNDER
            MOVE      SEVEN,LOOPCNT
          ENDIF
.
.         Set the absolute start position for the Y axis for the first line
.         of the new sheet
.
          PRINT     ESC,"*p100Y"            * Abs. Vert. Cursor Pos'n (100 dots)
.
          WHILE     LOOPCNT > 0
.
.           Set the height and stroke weight for the first line
.
            PRINT     ESC,"(s12.00v":       * Height (12 point)
                      "3B":                 * Stroke Weight (Bold)
                      ESC,"*p28X"           * Abs. Hor. Cursor Pos'n (28 dots)
.
.           Line 1    (Left Label)
.
            PRINT     SJOGLIN1;
.
.           Line 1    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLIN1;
            ENDIF
.
.           Now print each subsequent line and adjusting printer values
.           when required.
.
.           Line 2    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      ESC,"(s10.00v":       * Height (10 point)
                      "0B":                 * Stroke Weight (Medium)
                      SJOGLIN2:
                      ESC,"*p",URNOLEFT,"X":  * Abs. Hor. Cursor Pos'n
.>>>>>>>>>            ESC,"(5U",WRCHURNT:   * Print U/R Barcode Code 3/9
                      ESC,"(s2p18v24700T",*+,DIM8,*-:   * Print U/R Barcode
                      ESC,"(8U":            * Symbol set (Roman 8) (Barcode Off)
                      ESC,"(s1p":           * Spacing (Proportional)
                      "4t":                 * Typeface (Helvetica)
                      "10.00v":             * Height (10 point)
                      "0B";                 * Stroke Weight (Medium)
.
.           Line 2    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLIN2:
                        ESC,"*p",URNORIGT,"X":  * Abs. Hor. Cursor Pos'n
.>>>>>>>>>              ESC,"(5U",WRCHURNT: * Print U/R Barcode
                        ESC,"(s2p18v24700T",*+,DIM8,*-:   * Print U/R Barcode
                        ESC,"(8U":          * Symbol set (Roman 8) (Barcode Off)
                        ESC,"(s1p":         * Spacing (Proportional)
                        "4t":               * Typeface (Helvetica)
                        "10.00v":           * Height (10 point)
                        "3B";               * Stroke Weight (Bold)
            ENDIF
.
.           Line 3    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      SJOGLN3A:
                      ESC,"(s0B":           * Stroke Weight (Medium)
                      ESC,"*p613X":         * Abs. Hor. Cursor Pos'n (613 dots)
                      SJOGLN3B;
.
.           Line 3    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        ESC,"(s3B":         * Stroke Weight (Bold)
                        SJOGLN3A:
                        ESC,"(s0B":         * Stroke Weight (Medium)
                        ESC,"*p1816X":      * Abs. Hor. Cursor Pos'n (1816 dots)
                        SJOGLN3B;
            ENDIF
.
.           Line 4    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      ESC,"(s8.00V":        * Height (8 point)
                      SJOGLIN4;
.
.           Line 4    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLIN4;
            ENDIF
.
.           Line 5    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      ESC,"(s10.00V":       * Height (10 point)
                      SJOGLN5A:
                      ESC,"*p720X":         * Abs. Hor. Cursor Pos'n (720 dots)
                      SJOGLN5B;
.
.           Line 5    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLN5A:
                        ESC,"*p1920X":      * Abs. Hor. Cursor Pos'n (1920 dots)
                        SJOGLN5B;
            ENDIF
.
.           Line 6    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      SJOGLN6A:
                      ESC,"*p490X":         * Abs. Hor. Cursor Pos'n (490 dots)
                      SJOGLN6B:
                      ESC,"*p936X":         * Abs. Hor. Cursor Pos'n (936 dots)
                      SJOGLN6C;
.
.           Line 6    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLN6A:
                        ESC,"*p1690X":      * Abs. Hor. Cursor Pos'n (1690 dots)
                        SJOGLN6B:
                        ESC,"*p2136X":      * Abs. Hor. Cursor Pos'n (2136 dots)
                        SJOGLN6C;
            ENDIF
.
.           Line 7    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      SJOGLIN7;
.
.           Line 7    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLIN7;
            ENDIF
.
.           Line 8    (Left Label)
.
            PRINT     ESC,"*p+50y":         * Rel. Vert. Cursor Pos'n (50 dots)
                      "28X":                * Abs. Hor. Cursor Pos'n (28 dots)
                      ESC,"(s3B":           * Stroke Weight (Bold)
                      SJOGLIN8:
                      ESC,"(s0B":           * Stroke Weight (Medium)
                      ESC,"*p",VISNLEFT,"X":  * Abs. Hor. Cursor Pos'n
.>>>>>>>>>            ESC,"(5U",ANSE,ANSP,DIM9:   * Print Visit Barcode
                      ESC,"(s2p18v24700T",ANSE,ANSP,*+,KEY8,*-:  * Print Visit Barcode
                      ESC,"(8U":            * Symbol set (Roman 8) (Barcode Off)
                      ESC,"(s1p":           * Spacing (Proportional)
                      "4t":                 * Typeface (Helvetica)
                      "10.00v":             * Height (10 point)
                      "3B";                 * Stroke Weight (Bold)
.
.           Line 8    (Right Label)
.
            IF        !(REMNDER = 1 & NOLABEL = 1)
              PRINT     ESC,"*p1228X":      * Abs. Hor. Cursor Pos'n (1228 dots)
                        SJOGLIN8:
                        ESC,"(s0B":         * Stroke Weight (Medium)
                        ESC,"*p",VISNRIGT,"X":  * Abs. Hor. Cursor Pos'n
.>>>>>>>>>              ESC,"(5U",ANSE,ANSP,DIM9:   * Print Visit Barcode
                        ESC,"(s2p18v24700T",ANSE,ANSP,*+,KEY8,*-: *Print Visit Barcode
                        ESC,"(8U":          * Symbol set (Roman 8) (Barcode Off)
                        ESC,"(s1p":         * Spacing (Proportional)
                        "4T"                * Typeface (Helvetica)
            ELSE
              PRINT     SP1
            ENDIF
.
            SUB       ONE,LOOPCNT           * decrement loop counter
            SUB       TWO,NOLABEL           * decrement no of labels
          DO
.
          SUB       ONE,SHTCOUNT            * decrement sheet count
          COMPARE   ZERO,SHTCOUNT           * last sheet printed ?
          GOTO      PRTSJ010 IF NOT EQUAL   * no - print next sheet
.
PRTSJ999  RETURN
+
