.******************************************************************************
.* System         : Operating Rooms / Patient Management                      *
.*                                                                            *
.* Include Name   : CMGETTHR.PBL                                              *
.*                                                                            *
.* Function       : To get the casemix theatre fees for an item               *
.*                                                                            *
.* Author         : Jeni Tan (11/03/97)                                       *
.*                                                                            *
.* Parameters     : ACLAIM        claim code                                  *
.*                  AFUNDH        health fund code                            *
.*                  AFUNDTB       health fund table                           *
.*                  PTCTDCAS      Daycase                                     *
.*                  PTCTITMS      Item sequence                               *
.*                  MBSFLAG       MBS Item                                    *
.*                  USEDEF        Use default                                 *
.*                  TSRVDATE      Service date                                *
.*                                                                            *
.* Returns        : PTCTDREB,PTCTDPAT,OVRCD,NOFEE                             *
.*                                                                            *
.* Variables reqd : FORM2     FORM      2                                     *
.*                  DIM2      DIM       2                                     *
.*                  DIM8      DIM       8                                     *
.*                  THCFLG    FORM      2                                     *
.*                  ZERO8     INIT      "0000    "                            *
.*                                                                            *
.* Modifications  :                                                           *
.*        V11.04.01 08/03/2024  J.Tan     TSK 0919335                         *
.*                  Mod checking for HF History file                          *
.*        V9.12.01  09/09/2009  J.Tan     CAR 205303                          *
.*                  Added Contract ID to Casemix Theatre fee file             *
.*        V9.04.02  06/10/2004  J.Tan     CAR 53798                           *
.*                  Mods default claim code for multi hospital                *
.*        V9.04.01  30/08/2004  J.Tan     CAR 52997                           *
.*                  Mods to use default claim code                            *
.*        V9.03.01  10/11/2003  J.Tan                                         *
.*                  Mods to check for item with blank banding                 *
.*        V9.02.02  22/04/2002  J.Tan                                         *
.*                  Ported from r6.09                                         *
.*        V6.09.04  19/03/2002  J.Tan  SRF = 16281                            *
.*                  Fixed setting up DRG code                                 *
.*        V6.09.03  19/03/2002  J.Tan  SRF = 16281                            *
.*                  Fixed reading the next record                             *
.*        V6.09.02  02/01/2002  J.Tan                                         *
.*                  Mods for item seq greater than one to zero if not setup   *
.*        V6.09.01  27/10/2001  J.Tan                                         *
.*                  Mods to setup daycase                                     *
.*        V6.09.00  14/12/2000  Jill Habasque                                 *
.*                  Removed Episode Type - 609 Mods                           *
.******************************************************************************
CMGETTHR
CMTH0000  MOVE      ZERO,FORM3
          MOVE      ZERO,NOFEE
          MOVE      PTCTDCAS,DIM1           * daycase
          MOVE      PTCTITMS,FORM2D         * Item sequence
          MOVE      ACLAIM,PTCTCLMN         * claim code
          MOVE      AFUNDH,PTCTHFND         * health fund
          MOVE      AFNDTB,DIM8             * hf table
          OPEN      PATCTFA1,"patctfaf"
          OPEN      PATCTFA2,"patctfaf"
.
CMTH2200  MOVE      DIM1,PTCTDCAS           * Restore daycase
          MOVE      FORM2D,PTCTITMS          * Restore item sequence
          PACK      KEY5,ANSC,ANSL,PTCTCLMN
          CALL      RDCODE1
          MOVE      TCFORM6,HFBAND          * default banding from claim code
.
          MATCH     SP70,PTCTHFND
          IF        !@EQUAL
            PACK      KEY14 WITH PTCTHFND,ZERO,ZERO,ZERO,ZERO,SP20
            CALL      RDFUND1
.
.           Check HF history file
.
            MOVE      "03",KEY2        * Theatre charging indicator
            PACK      KEY17,PTCTHFND,KEY2,TSRVDATE,SP70
            CALL      PATDDHRD
          ENDIF
.
          MOVE      HFBAND,THCFLG           * HF banding
.
          MOVE      SP10,PTHFBCAT
          MOVE      SP10,PTCTTABT
          MATCH     SP6,PTCTHFND
          GOTO      CMTH2220 IF EQUAL
.
          PACK      KEY14 WITH PTCTHFND,DIM8
          CALL      RDFUND1
          MOVE      PTHFBCAT,PTCTTABT       * health fund table type
.
CMTH2220  MOVE      SP10,PTCTCASM
          MOVE      SP10,PTCTMBSB
          BRANCH    MBSFLAG,CMTH3000        * MBS Item
          GOTO      CMTH3200                * DRG Code
.
.         MBS Item -  use banding
.
CMTH3000  LOAD      PTCTMBSB,THCFLG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5,IBAND6:
                                IBAND7,IBAND8,IBAND9,IBAND10,IBAND11,IBAND12:
                                IBAND13,IBAND14,IBAND15,IBAND16
          MATCH     SP10,PTCTMBSB
          GOTO      CMTH3400 IF EQUAL
.
          PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTMBSB:
                          PTCTCASM,PTCTDCAS,PTCTITMS
          CALL      RDPTCTF2
          BRANCH    OVRCD,CMTH3400          * Try default
          GOTO      CMTH9999
.
.         DRG Code 
.
CMTH3200  PACK      PTCTCASM,TITEMNO,SP20
          PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTCASM:
                          PTCTMBSB,PTCTDCAS,PTCTITMS
          CALL      RDPTCTF1
          BRANCH    OVRCD,CMTH3400          * Try default
          MOVE      ZERO,IAMT
          GOTO      CMTH9999
.
.         if the first item exists then the following items of the combination
.         must be zeros
.
CMTH3400  BRANCH    PTCTITMS,CMTH3420
          MOVE      SP10,KEY2
          MOVE      ONE,FORM2
          MOVE      FORM2,KEY2         * set to the first item
          IF        MBSFLAG=1
            PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTMBSB:
                            PTCTCASM,PTCTDCAS,KEY2
            CALL      RDPTCTF2
          ELSE
            PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTCASM:
                          PTCTMBSB,PTCTDCAS,KEY2
            CALL      RDPTCTF1
          ENDIF
          BRANCH    OVRCD,CMTH3420
.
          MOVE      ZERO,IAMT
          MOVE      ZERO,PTCTDREB
          MOVE      ZERO,PTCTDPAT
          GOTO      CMTH9999
.
CMTH3420  ADD       ONE,FORM3
          BRANCH    FORM3,CMTH3600,CMTH6400
          GOTO      CMTH9000
.
.         Use default
.
CMTH3600  MOVE      ACLAIM,PTCTCLMN
          MOVE      SP10,PTCTHFND           * Try no health fund
          MOVE      SP10,PTCTTABT
          GOTO      CMTH2200
.
CMTH6400  IF        IBCNMHOS=1
            MOVE      PTHSDCLM,PTCTCLMN
          ELSE
            MOVE      PTCNDCLM,PTCTCLMN     * Try default claim
          ENDIF
          MOVE      SP10,PTCTHFND           * Try no health fund
          MOVE      SP10,PTCTTABT
          GOTO      CMTH2200
.
CMTH9000  MOVE      ONE,OVRCD
CMTH9999  RETURN
+
.**************************************************************************
.*  NOCM0000  :  no casemix record found, so display warning message      *
.* no record exists so display warning and write zero amount to dtraf     *
.**************************************************************************
NOCM0000  DISPLAY   *P1:24,*EL,*B,"Warning: No Theatre Fee for this case ":
                    "payment. ";
          CALL      HITENTER
.
.         ----- Set up variables & write to the DTR file -----
.
          MOVE      ZERO,PTCTDPAT
          MOVE      ZERO,PTCTDREB
          MOVE      ZERO,OVRCD
NOCM9999  RETURN
+
