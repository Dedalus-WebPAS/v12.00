.******************************************************************************
.
.       WATDSTRE : Display TREATMENT code Entries for U/R PURNO  WAITING LISTS
.
.       This include is used to display TREATMENT codes and descriptions 
.
.       Modifications :
.         V5.01     20/06/89  M.Ohleiter 
.                   Changes for new release 5.01
.         V5.01.01  25/08/89 K.Peak     
.                   Changed Treatment to Procedure
.
.                   02/12/89 Graeme Williams
.                   Added the parameter CNEWDS to determine if the include
.                   is to use the new standards for "?" routines
.                   06/08/90 Justin Coates
.                   Added WTCNTPDS : Treatment/Procedure description
.                   17/08/1990 Bill Dew
.                   Change procedure description to WMDESC from wattr1af
.         V5.01.02  07/11/1990 Karin Peak
.                   Fixed the display of the screen                        
.
.******************************************************************************
WATDSTRE  DISPLAY    *P1:3,*EF,*P20:4,*V2LON:
                               *ULON," ",WTCNTPDS," codes for U/R : ",PURNO,SP1
.
          MOVE       SIX,CVERT
          MOVE       ONE,CCOL
.
          MOVE       PURNO,WMURNO             
          PACK       KEY19 WITH WMURNO,SP20
          CALL       RDSTREA1
.
TRE100    CALL       RDKTREA1
          BRANCH     OVRCD OF TRE998
.
          MATCH      PURNO,WMURNO
          GOTO       TRE998 IF NOT EQUAL
.
          COMPARE    ONE,WMSTAT
          GOTO       TRE100 IF NOT EQUAL
.
          DISPLAY    *PCCOL:CVERT,*V2LON,WMCODE,SLASH,WMCNT,SP1,*HOFF,WMDESC
.
          ADD        ONE,CVERT
          COMPARE    "24",CVERT
          GOTO       TRE100 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSTRE200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      TRE999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATDSTRE IF EQUAL
.
          GOTO      DSTRE400
.
.         Use the new standards question
.
DSTRE200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      TRE999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATDSTRE IF EQUAL
.
DSTRE400  MOVE      ONE,CCOL
          MOVE      SIX,CVERT
          DISPLAY   *PCCOL:CVERT,*EF
          GOTO      TRE100
.
.         End of TREcedure codes
.
TRE998    BRANCH    CNEWDS,TRE999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
TRE999    RETURN
