.******************************************************************************
.* LABELERC - This is a proc to display the variables painted for ERC Labels  *
.*            This will print 1 label across the page
.*                                                                            *
.* PARAMETERS  : This procedure requires that the master file record  has     *
.*               been read.                                                   *
.*               IBLPSKEY = Photo Number                                      *
.*               URNUMBER = UR Number                                         *
.*               LABELTYP = Label Type                                        *
.*               USINGELT - Using Eltron printer for labels                   *
.*                      0 = No                                                *
.*                      1 = Yes                                               *
.* RETURNS     : EXIT 0 = Labels Printed / Zero Labels Selected               *
.*                    1 = Not Screen Painted                                  *
.*               NOLABEL = Number of labels to print                          *
.*                                                                            *
.* Modifications :                                                            *
.* V9.12.01  17/12/2009  Peter McMullen CAR 210130                            *
.*           Convert to newer Dr name format routine DOCNM000                 *
.*                                                                            *
.*                V9.05.B01 20/12/2005 Ebon Clements CAR 76341                *
.*                          Key length changes for encounter number           *
.*                V9.02.03  06/07/2002 Ebon Clements SRF 19957                *
.*                          Added ALREHCP and ALENHCP                         *
.*                V9.02.02  07/06/2002 Ebon Clements 18422                    *
.*                          Added ALRERDAT and ALENSDAT                       *
.*                V9.02.01  27/03/2002 Ebon Clements                          *
.*                          Added ALERSTFF,ALERDSNT and format patient name   *
.*                V9.00.00  09/08/2001 Ebon Clements                          *
.*                          Created program from WEBLBPMI.PBL                 *
.*                                                                            *
.******************************************************************************
          DEFPROC   LABELERC
.
          INC       IBAPASFD/INC
.
FORM3     FORM      3
.
DIM80     DIM       80
DIM3      DIM       3
DIM6      DIM       6
FIELDNO   FORM      5
PROCADMN  DIM       8
NMPNUMB   DIM       20
NOTSCPT   FORM      1          * not screen painted flag
PAGELIMT  FORM      "55"
.
CYRSFDTE  DIM       8
CYRSTDTE  DIM       8
CYRSYEAR  FORM      3
.
CODEC     INIT      "C "
CODEDA    INIT      "DA"
CODEDC    INIT      "DC"
CODEH1    INIT      "H1"
CODEH2    INIT      "H2"
CODEH3    INIT      "H3"
CODEM     INIT      "M "
CODEP1    INIT      "P1"
CODEP2    INIT      "P2"
CODEP3    INIT      "P3"
CODEP4    INIT      "P4"
CODEP5    INIT      "P5"
CODEP6    INIT      "P6"
CODER     INIT      "R "
CODET     INIT      "T "
CODEV6    INIT      "V6"
CODEV7    INIT      "V7"
CODEV8    INIT      "V8"
.
+
MAIN0000  CALL      INIT0000
          BRANCH    CEXIT,MAIN9999          * invalid details
.
          CALL      LABELCOM                * process screen painted labels
.
MAIN9999  GOTO      LABERC99
.******************************************************************************
.*        INIT0000  Initalise routine                                         *
.******************************************************************************
INIT0000  UNPACK    LABELTYP,PRGID,SCRID
.
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
.          READ      CONTROLF,TEN;*126,CLABPG
          READ      CONTROLF,TEN3;*143,CSIZLAB
.
.          MOVE      CLABPG,LABCOS
          MOVE      ONE,LABCOS              * ERC hard coded to 1 across page
          MOVE      CSIZLAB,LABSIZE
.
.         set the encounter details
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,INIT9200
.
.         set the patient details
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,INIT9100
.
.         set the ERC details
.
          PACK      KEY8,IBLPSKEY
          CALL      RDALERC1
          BRANCH    OVRCD,INIT9300
.
.         set the encounter details
.
          PACK      KEY16,ADMISSNO,ALERENCT,SP70
          CALL      RDALENC1
          BRANCH    OVRCD,INIT9400
.
.         See if using Eltron printer
.
          READ      CONTROLF,EIGHTY8;*1,PTCNPELT
          MOVE      PTCNPELT,USINGELT       * save parameter
.
          MOVE      ZERO,CEXIT
          GOTO      INIT9999
.
INIT9100  MOVE      "Invalid U/R Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9200  MOVE      "Invalid Referral/Visit Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9300  MOVE      "Invalid ERC/Photo Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9400  MOVE      "Invalid Encounter Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.*****************************************************************
.*        DISPVARS  Display Items            Called By :         *
.*****************************************************************
DISPVARS  MOVE      SCPSITM,FIELDNO
          MOVE      SCPSLEN,LLCNT
          CALL      ERCVR000
DVARS999  RETURN
+
.*****************************************************************
.*        ERCVR000  Set up display variables Called By : DISPVARS*
.*****************************************************************
ERCVR000  PACK      VAR,SP30,SP30,SP20
          MOVE      SCPSITM,FIELDNO
          BRANCH    FIELDNO,ERCVR001,ERCVR002,ERCVR003,ERCVR004,ERCVR005:
                            ERCVR006,ERCVR007,ERCVR008,ERCVR009,ERCVR010:
                            ERCVR011,ERCVR012,ERCVR013,ERCVR014,ERCVR015:
                            ERCVR016,ERCVR017,ERCVR018,ERCVR019,ERCVR020:
                            ERCVR021,ERCVR022,ERCVR023,ERCVR024,ERCVR025:
                            ERCVR026,ERCVR027
.
          GOTO      ERCVR999
.
ERCVR001  PACK      VAR,PTITL,SP70,SP70     * title
          GOTO      ERCVR995
.
ERCVR002  PACK      VAR,PSNAME,SP70,SP70    * surname
          GOTO      ERCVR995
.
ERCVR003  PACK      VAR,PGNAME,SP70,SP70    * given name
          GOTO      ERCVR995
.
ERCVR004  PACK      VAR,PADD1,SP70,SP70     * address 1
          GOTO      ERCVR995
.
ERCVR005  PACK      VAR,PADD2,SP70,SP70     * address 2
          GOTO      ERCVR995
.
ERCVR006  PACK      VAR,PSUBRB,SP70,SP70    * address 3
          GOTO      ERCVR995
.
ERCVR007  PACK      VAR,PADD4,SP70,SP70     * address 4
          GOTO      ERCVR995
.
ERCVR008  PACK      VAR,PPOST,SP70,SP70     * Post code
          GOTO      ERCVR995
.
ERCVR009  PACK      VAR,PURNO,SP70,SP70     * U/R Number  
          GOTO      ERCVR995
.
ERCVR010  PACK      VAR,ALERPHOT,SP70,SP70  * Photo Number
          GOTO      ERCVR995
.
ERCVR011  PACK      VAR,ALERVISN,SP70,SP70  * Referral / Visit Number
          GOTO      ERCVR995
.
ERCVR012  PACK      VAR,ALERENCT,SP70,SP70  * Encounter number
          GOTO      ERCVR995
.
ERCVR013  PACK      VAR,ALREPRO1,SP70,SP70  * Problem number 1
          GOTO      ERCVR995
.
ERCVR014  PACK      VAR,SP70,SP70 
.
          MATCH     SP70,ALREDEPT
          GOTO      ERCVR995 IF EQUAL
.
          MATCH     SP70,ALREPRO1
          GOTO      ERCVR995 IF EQUAL
.
          PACK      KEY12,ALREDEPT,ALREPRO1,SP70
          CALL      RDALPRR1
          BRANCH    OVRCD,ERCVR995
.
          PACK      VAR,ALPRDESC,SP70,SP70 * Porblem 1 Description    
          GOTO      ERCVR995
.
ERCVR015  PACK      VAR,ALREPRO2,SP70,SP70  * Problem number 2
          GOTO      ERCVR995
.
ERCVR016  PACK      VAR,SP70,SP70 
.
          MATCH     SP70,ALREDEPT
          GOTO      ERCVR995 IF EQUAL
.
          MATCH     SP70,ALREPRO2
          GOTO      ERCVR995 IF EQUAL
.
          PACK      KEY12,ALREDEPT,ALREPRO2,SP70
          CALL      RDALPRR1
          BRANCH    OVRCD,ERCVR995
.
          PACK      VAR,ALPRDESC,SP70,SP70 * Porblem 2 Description    
          GOTO      ERCVR995
.
ERCVR017  PACK      VAR,ALREPRO3,SP70,SP70  * Problem number 3
          GOTO      ERCVR995
.
ERCVR018  PACK      VAR,SP70,SP70 
.
          MATCH     SP70,ALREDEPT
          GOTO      ERCVR995 IF EQUAL
.
          MATCH     SP70,ALREPRO3
          GOTO      ERCVR995 IF EQUAL
.
          PACK      KEY12,ALREDEPT,ALREPRO3,SP70
          CALL      RDALPRR1
          BRANCH    OVRCD,ERCVR995
.
          PACK      VAR,ALPRDESC,SP70,SP70 * Porblem 3 Description    
          GOTO      ERCVR995
.
ERCVR019  MOVE      PSNAME,PACSNAME       * Formatted Name (for LABELS)
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          PACK      VAR,PACFNAME,SP70,SP10
          GOTO      ERCVR995
.
ERCVR020  PACK      VAR,ALERSTFF,SP70,SP70  * Sent to free format          
          GOTO      ERCVR995
.
ERCVR021  MOVE      SP20,CPCDATE            * Date Photo Sent
          MATCH     SP8,ALERDSNT
          IF        !@EQUAL
            UNPACK    ALERDSNT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PACK      VAR,CPCDATE,SP70,SP10

          GOTO      ERCVR995
.
ERCVR022  PACK      VAR,ALREUFR1,SP70,SP70  * User Defined Free Format Text 1
          GOTO      ERCVR995
.
ERCVR023  PACK      VAR,ALREUFR2,SP70,SP70  * User Defined Free Format Text 2
          GOTO      ERCVR995
.
ERCVR024  MOVE      SP20,CPCDATE            * Referral Date
          MATCH     SP8,ALRERDAT
          IF        !@EQUAL
            UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PACK      VAR,CPCDATE,SP70,SP10

          GOTO      ERCVR995
.
ERCVR025  MOVE      SP20,CPCDATE            * Encounter Date
          MATCH     SP8,ALENSDAT
          IF        !@EQUAL
            UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PACK      VAR,CPCDATE,SP70,SP10

          GOTO      ERCVR995
.
ERCVR026  MOVE      SP70,DOCFNAME
          PACK      KEY10,ALREHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD = 0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ENDIF
          PACK      VAR,DOCFNAME,SP70
          GOTO      ERCVR995
.
ERCVR027  MOVE      SP70,DOCFNAME
          PACK      KEY10,ALENHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD = 0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ENDIF
          PACK      VAR,DOCFNAME,SP70
          GOTO      ERCVR995
.
ERCVR995  CALL      DISPITEM
.
ERCVR999  RETURN

.         I/O includes needed
. 
          INC       AGECHK
          INC       CALCYRS
.
          INC       DSPFIELD
          INC       IBAOVRIO/INC
          INC       IBAPASIO/INC
          INC       WEBLBCOM    
          INC       PMIGTNID
.
LABERC99  ENDPROC
................................................................................
