.******************************************************************************
.         AAECMNT.PBL: COMPENSATION CLAIM COMMENTS - FREE FORMAT
.                      DISPLAY,MAINTENANCE & PRINT
.
.         MODIFICATIONS: 
.
.            V12.00.01  30/05/2025  Don Nguyen   TSK 0955096
.                       Alphanumeric visit number changes around ADANUMB
.            V5.01.00   19/04/89 K.Peak
.                       Added Date on Comment for Epworth SRF 100180
.            V5.01.01   12/09/89 M.Ohleiter
.                       Included word processor
.            V5.01.02   20/09/89 Graeme Williams
.                       Re-wrote code for invoking word-processor
.                       and posting new comments. Deleted the
.                       subroutine INSCOMM, as you can use CHGCOMM
.                       instead.
.            V5.01.03   30/10/1992    Justin Coates
.                       Made it use new wordprocessor
.
.           ADANUMB ......ARE REQUIRED
.
.         The following includes are assumed to be in the main program
.
.         AAEDETFD & AAEIODEB
.
.         This include may be called either by:
.
.         CALL       CHGCOMM    for AAE enquiry and maintenance
.
.         or by:
.
.         CALL       PRTAAECM   for printing AAE data
.******************************************************************************
.
DSPAAECM
.
.    execute the word processor
.
WP0000    CLOSE     TEMP1 
          MOVE      TWO,FORM1
          WORD      FNAMER,99,5,7,76,22,FORM1
          RETURN
.
DSPAAEC4  DISPLAY   *P1:24,*EL,"Posting Changes ",*BLINKON,*V2LON:
                    " - Please Wait"
          CLOSE     TEMP1
          OPEN      TEMP1,FNAMER
          MOVE      ZERO,LINENO
DSPAAEND  RETURN
.
.         Changing comments.
.
CHGCOMM   PREP      TEMP1,FNAMER             * Initialise word processor
.
.         Get any existing comments
.
          MOVE      ONE,ADBCLNO              * Check for the first line
          PACK      KEY10,ADANUMB,ADBCLNO    * Key for the first line
          CALL      RDDETB1                  * Read in the first line
          BRANCH    OVRCD,CHGCOMM9           * No first line -> no comments
.
.         Copy the existing comments to the temp file for the W.P.
.
CHGCOMM1  MOVE      ADBLINE,DIM70            * Save the comment line
          WRITE     TEMP1,SEQ;DIM70          * Write comment line to temp file
          PACK      DIM70,SP30,SP30,SP10
          CALL      RDKDETB1                 * Get the next comment line
          BRANCH    OVRCD,CHGCOMM9           * No more comments
.
          MATCH     ADANUMB,ADBNUMB          * Still the same patient ?
          GOTO      CHGCOMM9 IF NOT EQUAL    * No. Finished getting old cmnts
.
          GOTO      CHGCOMM1                 * Post comment line to file
.
.         Invoke the word processor on current temp file
.
CHGCOMM9  CALL      WP0000
          COMPARE   ZERO,FORM1
          GOTO      MODCOMM9 IF EQUAL       * abort
.
.         Check if the changes should be posted
.          
INSCOMM   CALL      POSTQST                  * Ask if ok to post
          MOVE      ANS,FORM1                * Change answer to a form field
          BRANCH    FORM1,INSCOMM1,MODCOMM9,MODCOMM9
          BEEP                               * Invalid answer
          GOTO      INSCOMM
.
.         Post the comments
.
INSCOMM1  DISPLAY   *P1:24,*EL,"Posting Comments",*BLINKON,*V2LON:
                    " - Please Wait";
.
.         Step 1. Delete the old comments
.
MODCOMM1  PACK      KEY10,ADANUMB,SP2        * Key for start of pats comments
          CALL      RDSDETB1                 * Position at first comment line
          CALL      RDKDETB1                 * Get the first comment line
          BRANCH    OVRCD,MODCOMM5           * No more comments.
.
          MATCH     ADBNUMB,ADANUMB          * Still the correct patient ?
          GOTO      MODCOMM5 IF NOT EQUAL    * No. Finished deleting comments
.
          PACK      KEY10,ADBNUMB,ADBCLNO    * Key for the record to delete
          CALL      DEDETB1                  * Delete this record
          GOTO      MODCOMM1                 * Check for more records to delete
.
.         Step 2. Add the new comments
.
MODCOMM5  CLOSE     TEMP1                    * Close the temp file
          OPEN      TEMP1,FNAMER             * Re-open to start of temp file
          MOVE      ZERO,LINENO              * Initialise line count
.
MODCOMM6  READ      TEMP1,SEQ;DIM70          * Read in the next comment line
          GOTO      MODCOMM9 IF OVER         * No more comment lines. Finished
          ADD       ONE,LINENO               * Increment line count
          MOVE      LINENO,ADBCLNO           * Set up comment line number
          MOVE      ADANUMB,ADBNUMB          * Set up comment A&E number
          PACK      ADBLINE,DIM70,SP30,SP30,SP10
.
          PACK      KEY10,ADBNUMB,ADBCLNO    * Key for new comments record
          CALL      WRDETB1                  * Write the new record
          GOTO      MODCOMM6                 * Get the next comment record
.
.         Finished posting comments
.
MODCOMM9  RETURN
.
.         Subroutine to print AAE text lines file. Used by IBAAAE01.
.
PRTAAECM  CALL      HASAAECM
          BRANCH    OVRCD OF PRTAAEC3
.
          PRINT     *1,"---------- Comments ----------"
          ADD       ONE,CLNO
.
          PACK      KEY10 WITH ADANUMB,SP10
          CALL      RDSDETB1
PRTAAEC1  CALL      RDKDETB1
          BRANCH    OVRCD OF PRTAAEC4
.
          MATCH     ADBNUMB,ADANUMB
          GOTO      PRTAAEC4 IF NOT EQUAL
.
          COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
.
PRTAAEC2  PRINT     *1,ADBLINE
          ADD       ONE,CLNO
          GOTO      PRTAAEC1
.
PRTAAEC3  PRINT     *N,*5,"** No Comments **"
          ADD       ONE,CLNO
PRTAAEC4  RETURN
.
.         Subroutine to determine if this A&E attendance has any text lines
.
HASAAECM  PACK      KEY10 WITH ADANUMB,SP10
          CALL      RDSDETB1
          CALL      RDKDETB1
          BRANCH    OVRCD OF OVERCOND
.
          MATCH     ADANUMB,ADBNUMB
          GOTO      OVERCOND IF NOT EQUAL
.       
          RETURN
.
.         Subroutine to display AAE current text lines on AAE screen
.
AAETEXT   DISPLAY   *P1:CVERT,*EF;
.
          MOVE      ZERO,WTEXT
.
          PACK      KEY10 WITH ADANUMB,SP10
          CALL      RDSDETB1
          CALL      RDKDETB1
          BRANCH    OVRCD OF AAETEXT3
          GOTO      AAETEXT2
.
AAETEXT1  CALL      RDKDETB1
          BRANCH    OVRCD OF AAETEXT3
.
AAETEXT2  MATCH     ADBNUMB,ADANUMB
          GOTO      AAETEXT3 IF NOT EQUAL
.
          DISPLAY   *P1:CVERT,ADBCLNO,"> ",*V2LON,ADBLINE
          ADD       ONE,WTEXT
          ADD       ONE,CVERT
          GOTO      AAETEXT1
.
AAETEXT3  RETURN
.............................................................................
