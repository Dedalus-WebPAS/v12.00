.******************************************************************************
.* System         :  THEATRE                                                  *
.* Include Name   :  OPRDEA01.PBL                                             *
.* Description    :  Collection of functions concerning the oprdetaf          *
.******************************************************************************
.* Date           :  11/10/1990                                               *
.* Author         :  Bill Spew                                                *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 19/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V11.03.01 07/03/2023  Ebon Clements     TSK 0909393                 *
.*                  Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes *
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.08.01 18/07/2016  Jill Parkinson  TSK 082130                    *
.*                  Added CURSTAT to CANDEA00 so preadmitted bookings get canc*
.*        V10.02.02 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as            *
.*                  part of PATGSRFD keys                                     *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast       *
.*                  messages                                                  *
.******************************************************************************
.*        V9.10.01  03/09/2008  Davin         CAR 147323                      *
.*                  Send hl7 message for new bookings (added call DGCLIS12)   *
.*                  Send hl7 message for upd bookings (added call DGCLIS14)   *
.*                  Send hl7 message for del bookings (added call DGCLIS15)   *
.*        V9.09.01  20/12/2007  Jill Habasque CAR 126282                      *
.*                  Added write of created date/time for oprdetaf             *
.*        V9.03.01  23/02/2004  Jill Habasque CAR 47741                       *
.*                  Added open of patvisa1 before genbokno                    *
.*        V9.02.06  02.02.2002  Ebon Clements                                 *
.*                  Set WATHI002 cgi for waiting list history file            *
.*        V9.02.05  24.01.2002  Ebon Clements                                 *
.*                  Set flag ADDWLFLG for waiting list history file           *
.*        V9.02.04  29.11.2001  B.G.Ackland                                   *
.*                  Remove pi's from Program                                  *
.*        V9.02.03  14/09/2001  Mike Laming    RCH/SVHM                       *
.*                  Activate Datagate API (see below V9.02.01)                *
.*        V9.02.02  09/09/2001  Sandra Barcham RCH/SVHM                       *
.*                  Add DOB & SEX to patgsrnf                                 *
.*        V9.02.01  29/08/2001  Mike Laming    RCH/SVHM                       *
.*                  Add Datagate API DGCLICCB (Cancel Booking)                *
.*        V5.10.B01 29/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.*        V5.09.B02 01/02/2001  Greg Horvat                                   *
.*                  Replaced Z variables with Z70                             *
.*        V5.09.B01 10/01/2001  Steve Downing  SRF 647505                     *
.*                  Open patgsrnf before each read, to fix I*C in IBAHOS31    *
.******************************************************************************
.*        V5.08.02  27/06/2000  Greg Horvat                                   *
.*                  Recompiled for PATICOKY - Validating ICD10 version 2 codes*
.*        V5.08.01  20/06/2000  Jill Habasque                                 *
.*                  Added update of WTWMBSCD when WMSTAT is changed           *
.*        V5.08.B02 05/04/2000  Jill Habasque SRF 646452                      *
.*                  Added ICD10 Keyword search                                *
.*        V5.08.B01 30/03/2000  Greg Horvat      SRF 646507                   *
.*                  Changed to open the bokrc1af file before reading it       *
.*        V5.07.02  28/10/1999  Jill Habasque                                 *
.*                  Changed the key to be packed with "999" instead of "zzz"  *
.*                  when transferring bookings in OPMVSBOK                    *
.*        V5.07.01  25/02/1999  Jill Habasque                                 *
.*                  Added test for inactive codes                             *
.*        V5.05.01  23/02/1998  Steve Armstrong                               *
.*                  Mods to delete a linked event if a booking is deleted     *
.******************************************************************************
.*        V5.04.02  17/04/1997  howellsy   RHA                                *
.*                  Write to W/L History                                      *
.*        V5.04.01  03/10/1996  Howellsy          EOC & ACC                   *
.*                  Generate Booking No when placed on the Waiting List       *
.*        V5.03.01  20/11/1995    Justin Coates  (SUHT mods)                  *
.*                  re-allocate wmbook when cancel, dont set to zero          *
.******************************************************************************
.  Modifications  :  V5.01  03/01/91 Graeme Williams
.                           Fixed OPMVSBOK so that if moving a case to itself
.                           it will still recalculate start times (needed for
.                           transferring bookings)
.                    V5.02  16/01/91 Graeme Williams
.                           Fixed OPMVSBOK to check for the end of the session
.                           when moving bookings around
.
.         V5.01.01  21/01/1991 'The Phantom'
.                   Converted to Release 5.01
.                   15/11/1991  Justin Coates
.                   Changed Delete to Cancel and cancel bokrec record if needed
.                   12/05/92    ROD
.                   If Cancellation Code's 1st Ind. = D, delete booking detail
.                   records from system
.                   04/06/1992  Justin Coates 
.                   Added delete audits when cancel booking
.         V5.01.02  09/10/1992  i chung          SRF 117407
.                   Update corresponding W/L record if Admission Booking is
.                   cancelled (for OPCANBOK - sub-routine CANREC00)
.         V5.01.03  30/11/92  J.Tan         SRF 118743
.                   Fixed the admission cancellation code
.         V5.01.04  15/12/1992  i chung          SRF 119401
.                   When cancelling a zero U/R booking also delete the record
.                   from the Surname file
.         V5.01.05  19/01/93 J.Tan   SRF 120121
.                   Mods not to open surname file as it's open in main programs
.         30/07/93  ROD                
.                   Delete Medical Wanings and Donor details for Booking No.
.         12/10/93  Whirlpool  srf 440401
.                   fixed bug in CANREC routine - trying to update an invalid
.                   record
.         07/12/93  Glenn Berry      Hawkes Bay Changes     
.                   Added Special Equipment Tracking  (OPCNSPET=1)       
.         29/12/93  Rob Leonard   SRF 440581
.                   Disallow cancellation of admission booking when patient
.                   currently admitted
.         22/08/94  Rob Leonard
.                   Fixed bug in CANREC routine - would set adm booking status
.                   to cancelled even when answering no to Canc Adm Book quest.
.         12/10/94  Rob Leonard
.                   Set removal date when removing pat from W/L
.
.*******************************************************************************
.  
.    FUNCTION ROSTER  DESCRIPTION
.    ---------------  ------------------------------------------------------
.    OPMVSBOK         Moves a patients operation record From one case number
.                     To another.
.  
.    OPCALCTM         Recalculates the start times for each operation record
.                     after a cancellation or case number adjustment.
.
.    OPTRNOPR         Transfer Patient to a new theatre and update it to the
.                     oprdetaf.
.
.    OPTOPALL         Transfer All subsequent bookings to another theatre.
.
.    OPINCTIM         Increment start times in a given session starting and
.                     case number.
.
.    OPCANBOK         Cancel a operation booking.
.
.    OPPSTDEA         Post a OPRDEAFD record.
.
.    OPDSPOPR         Display patients operation description on lines 18-22
.*******************************************************************************
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, OPRDEAFD, OPROPRFD
.
.    I/O's : OPRDEAIO, OPROPRIO
.
.    PBL's : STDOPR01, PATITMDS, OPROPRDS,OPRSES01
.
.*******************************************************************************
.                                 OPMVSBOK
.
.    The function Moves a patients operation record to a selected case number.
.
.    Function  : Move the first case number to the selected position. All other
.                bookings are moved to eliminate the empty space left by moving
.                the case number.
.  
.    Parameters:
.    CASETO   - New case  number location
.    CASEFROM - Current case  number location
.    KEY22    - Must be set to session date, time and clinic/doctor
.               (Not CURSESS as in OPTRNBOK have to move bookings for the new
.                session so this will stuff up the value of CURSESS)
.
.    Note : When a patient operation is cancelled to update the case numbers
.           Simply Specify the case number cancelled in CASETO and CASEFROM
.           eg Case 4 cancelled   CASETO=CASEFROM=4
.
.    Return Parameters:
.    EXIT = 0 - No problems, a successful booking transfer
.    EXIT = 1 - Unsuccessful transfer eg invalid parameters, record cases not
.               on file
.*******************************************************************************
OPMVSBOK  MATCH     CASEFROM,CASETO    * test whether to shuffle up or down
          GOTO      MVSBOK10 IF EQUAL  * Do cancellation or just recalc times
          GOTO      MVSBOK50 IF LESS   * moving case backwards
          GOTO      MVSBOK30           * moving case forwards
.
.    Remove a Case record by shuffling the case numbers down and re-calculate
.    start times also check the that an empty space does exist.
.
MVSBOK10  PACK      KEY26,KEY22,ZERO,CASEFROM
          CALL      RAOPDEA1           * No record thus empty
          COMPARE   ONE,OVRCD          * test for record not exist
          GOTO      MVSBOK80 IF NOT EQUAL   * just recalculate start times
.
.         will come here only if the read fails  ie record is cancelled
.
          MOVE      ZERO,FORM3         * use form3 signal case number changes
          CALL      RSOPDEA1           * Re-position fptr 
.
MVSBOK20  CALL      RKOPDEA1
          BRANCH    OVRCD,MVSBOK21     * Go to recalculate start times
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
.
          MATCH     KEY22,KEY26        * test same session
          GOTO      MVSBOK21 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT      * test for booked records
          GOTO      MVSBOK21 IF NOT EQUAL
.
          MOVE      TWO,AUDIFLAG       * before change audit
          CALL      AUDEA000           * write audit
.
          MOVE      OPDACASE,FORM3     * move dim field to a form 
          SUB       ONE,FORM3          * decrement case as we read forwards
          MOVE      FORM3,OPDACASE     * copy the new case number
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1           * update with new case number
          MOVE      THREE,AUDIFLAG     * after change audit
          CALL      AUDEA000           * write audit
          MOVE      ONE,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14           * send update booking message
          GOTO      MVSBOK20
.
MVSBOK21  COMPARE   ZERO,FORM3         * test if any case numbers were changed
          GOTO      MVSBOK81 IF EQUAL
          GOTO      MVSBOK80           * recalculate start times
.
.------------------------------------------------------------------------------
.         Moving a case forwards (CASETO > CASEFROM)
.         Shuffle case numbers Down.
.
.         move the record to be moved into case number 999 (temp storage)
.
MVSBOK30  PACK      KEY26,KEY22,ZERO,CASEFROM,SP30
          CALL      RDOPDEA1                * get the case record & move to end
          BRANCH    OVRCD,MVSBOK90          * invalid data
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          PACK      OPDACASE,NINE,NINE,NINE * set up case number
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update with case 999 = end place
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TWO,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
.         shuffle the case numbers down
.         (positioning at the case_from position)
.
          CALL      RSOPDEA1                * Re-position fptr after update
.
MVSBOK40  CALL      RKOPDEA1                * next read
          BRANCH    OVRCD,MVSBOK70          * no more data
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
          MATCH     KEY22,KEY26             * test same session
          GOTO      MVSBOK70 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT
          GOTO      MVSBOK70 IF NOT EQUAL   * finished all booked records
.
          MATCH     OPDACASE,CASETO         * test reached desired case
          GOTO      MVSBOK70 IF LESS        * have finished the swapping
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      OPDACASE,FORM3          * set up current case number
          SUB       ONE,FORM3               * decrement case as we read forwards
          MOVE      FORM3,OPDACASE          * reset the case number
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update the case
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      THREE,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          GOTO      MVSBOK40                * get next item
.
.------------------------------------------------------------------------------
.         Moving a case backwards (CASETO < CASEFROM)
.         Shuffle case numbers Up.
.
.         move the record to be moved into case number zzz (temp storage)
.
MVSBOK50  PACK      KEY26,KEY22,ZERO,CASEFROM
          CALL      RDOPDEA1                * get the case record & move to end
          BRANCH    OVRCD,MVSBOK90          * data is stuffed
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          PACK      OPDACASE,NINE,NINE,NINE   * set case to '999'
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update with case 999 = end place
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      FOUR,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
.         shuffle the case numbers up
.         (positioning at the case_from position)
.
          CALL      RSOPDEA1                * Re-position fptr after update
.
MVSBOK60  CALL      RPOPDEA1                * Read back from desired rec
          BRANCH    OVRCD,MVSBOK70          * no more data
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
          MATCH     KEY22,KEY26             * test same session
          GOTO      MVSBOK70 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT
          GOTO      MVSBOK70 IF NOT EQUAL   * finished all booked records
.
          MATCH     CASETO,OPDACASE         * test reached desired case
          GOTO      MVSBOK70 IF LESS        * finish
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      OPDACASE,FORM3          * set up form number
          ADD       ONE,FORM3               * increment case as we read forwards
          MOVE      FORM3,OPDACASE          * reset the case number
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      FIVE,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          GOTO      MVSBOK60                * get next case
.
.------------------------------------------------------------------------------
.         all the swapping is done
.         set up the case which was chosen to be moved with its new case number
.
MVSBOK70  PACK      KEY26,KEY22,ZERO,NINE,NINE,NINE
          CALL      RDOPDEA1                * get the last record (CASEFROM)
          BRANCH    OVRCD,MVSBOK90          * data is stuffed
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      CASETO,OPDACASE         * set new case number
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      SIX,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
MVSBOK80  CALL      OPCALCTM                * recalculate start times
.
.         delete the temporary '999' record written
.
MVSBOK81  PACK      KEY26,KEY22,ZERO,NINE,NINE,NINE
          CALL      RDOPDEA1
          BRANCH    OVRCD,MVSBOK82          * zzz record doesnt exist
          CALL      DEOPDEA1                * delete the temporary record
.
          MOVE      FOUR,AUDIFLAG           * delete audit
          CALL      AUDEA000                * write audit
.
.>>>>     MOVE      SEVEN,HL7TRGID
.>>>>     MOVE      "OPRDEA01",HL7INCLD
.>>>>     PROC      DGCLIS15                * send delete booking message
.
.         reset to the first case displayed on the screen
.
MVSBOK82  MOVE      ONE,CURPOS
          LOAD      CURCASE,CURPOS,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                                   CASE07,CASE08,CASE09,CASE10,CASE11,CASE12:
                                   CASE13,CASE14
          PACK      KEY26,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,ZERO,CURCASE
          CALL      RDOPDEA1                * get the selected case
. 
          MOVE      ZERO,EXIT
          GOTO      MVSBOK99
.
MVSBOK90  MOVE      ONE,EXIT                * unsuccessful transfers
.
MVSBOK99  RETURN
+
.*******************************************************************************
.                                 OPCALCTM
.
.    This program recalculates the start times for each operation record
.    after a cancellation or case number adjustment.
.  
.    Function  : Loop through all bookings from the first booking to the end
.                of the session, and will recalculate the start times of each
.                booking. The OPRDEAFD records will be updated as appropriate.

.    Parameters:
.    KEY22    - Must be set to session date, time and clinic/doctor
.
.    Return Parameters:
.    None
.*******************************************************************************
OPCALCTM  CALL      RDOPSES1                * get session patient prep time -
.                                             OPSEPREP
          UNPACK    KEY22,KEY3,KEY8,EXPSTART 
          PACK      KEY26,KEY22,ZERO,SP30
          CALL      RSOPDEA1                * position fptr oprdetaf
.
CALCTM10  CALL      RKOPDEA1
          BRANCH    OVRCD,CALCTM99
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
          MATCH     KEY22,KEY26             * test record is same session
          GOTO      CALCTM99 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT           * test for booked patients only
          GOTO      CALCTM99 IF NOT EQUAL
.
          MOVE      OPDAEXPD,EXPDURA        * save the expected duration
          MATCH     EXPSTART,OPDAEXPS       * test if start times are correct
          GOTO      CALCTM20 IF EQUAL
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      EXPSTART,OPDAEXPS       * set the new start time
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update operat'n rec with new start
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      EIGHT,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
.    initial start time in EXPSTART and duration EXPDURA
.
CALCTM20  ADD       OPSEPREP,EXPDURA        * add patient prep time to duration
          COMPARE   ZERO,EXPDURA            * test a duration
          GOTO      CALCTM30 IF NOT EQUAL
          MOVE      ONE,EXPDURA             * if no duration then make it one
.
CALCTM30  CALL      OPADDTIM              * Calculate new start time in EXPSTART
          GOTO      CALCTM10
.
CALCTM99  RETURN
+
.*******************************************************************************
.                                 OPINCTIM
.    Increment start times in a given session starting and case number.
.
.    Function : Loop through all bookings from the current booking to end of
.               the session, and add the increment amount to start times of
.               each booking.
.  
.    Parameters:
.    KEY22    - Must be set to session date, time and clinic/doctor
.    CASEFROM - Starting case number
.    EXPDURA  - Duration to be added
.
.    Return Parameters:
.    None
.*******************************************************************************
OPINCTIM  MOVE      CASEFROM,FORM3          * must decrement case because full
          SUB       ONE,FORM3                 key
          PACK      KEY26,KEY22,ZERO,FORM3,SP30
          CALL      RSOPDEA1                * position fptr oprdetaf
.
INCTIM10  CALL      RKOPDEA1
          BRANCH    OVRCD,INCTIM99
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
          MATCH     KEY22,KEY26           * test record is same session
          GOTO      INCTIM99 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT         * test for booked patients only
          GOTO      INCTIM10 IF EQUAL
.
          MOVE      TWO,AUDIFLAG          * before change audit
          CALL      AUDEA000              * write audit
          MOVE      OPDAEXPS,EXPSTART     * set the start time, Duration is set
          CALL      OPADDTIM              * Calculate new start time in EXPSTART
          MOVE      EXPSTART,OPDAEXPS     * move new startime to operation rec.
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1              * update operation rec. with new start
          MOVE      THREE,AUDIFLAG        * after change audit
          CALL      AUDEA000              * write audit
          MOVE      NINE,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14              * send update booking message
          GOTO      INCTIM10
.
INCTIM99  RETURN
+
.*******************************************************************************
.                                 OPTRNOPR
.    Transfer Patient to a new theatre and update it to the oprdetaf.
.
.    Function  : Aks for the theatre the patient is to be transfered to. It
.                will then update the OPRDEAFD record with the new theatre code.
.
.    Parameters:
.    CURSESS   - Current session in process
.    CURCASE   - Current case number of booking
.
.    Return Parameters:
.    Exit = 0 - Theatre transfer complete
.    Exit = 1 - No Transfer done
.*******************************************************************************
OPTRNOPR  DISPLAY   *PSUBOPT2:2,*V2LON," (Change ",*+,OPCNODSC,") ";
.
          PACK      KEY22,CURSESS,SP20
          CALL      RDOPSES1                * get default theatre
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get patient operation details
          BRANCH    OVRCD,TRNOPR90          * missing record (should not occur)
.
          MOVE      ONE,EXIT                * Set flag to say single patient
          MOVE      ONE,SCRNFLAG            * set for no redisplay
          CALL      TRTHE000                * get new theatre
          BRANCH    EXIT,TRNOPR99
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MATCH     OPSETHET,XBITEM         * keyin theatre = default theatre ?
          GOTO      TRNOPR10 IF NOT EQUAL   * no
          MOVE      SP6,XBITEM              * update oprdetaf with blank theatre
.
TRNOPR10  MOVE      XBITEM,OPDATHET         * change theatre code
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update theatre code
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          MOVE      ZERO,EXIT
          GOTO      TRNOPR99
.
TRNOPR90  MOVE      ONE,EXIT
.
TRNOPR99  RETURN
+
.*******************************************************************************
.                                 OPTOPALL
.    Transfer All subsequent bookings to another theatre.
.
.    Function : Ask for theatre the patient is to be transfered to.  It will
.               Update oprdetaf for all bookings from the current case number
.               to the end of session session with the new theatre code.
.
.    Parameters:
.    CURSESS   - Current session in process
.    CURCASE   - Current case number of booking
.
.    Return Parameters:
.    Exit = 0 - Theatre transfer complete
.    Exit = 1 - No Transfer done
.*******************************************************************************
OPTOPALL  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Transfer Session) ";
          MOVE      ONE,SCRNFLAG            * set redisplay flag
.
          PACK      KEY22,CURSESS,SP20
          CALL      RDOPSES1                * get default theatre
.
          MOVE      SP6,OPDATHET            * Clear theatre for booking
          MOVE      TWO,EXIT                * Set flag to say all patients
          MOVE      ONE,SCRNFLAG            * set for no redisplay
          CALL      TRTHE000                * get new theatre.Returned in XBITEM
          BRANCH    EXIT,TOPALL99
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * read the first case
          BRANCH    OVRCD,TOPALL10
          GOTO      TOPALL20
.
TOPALL10  CALL      RKOPDEA1                * read next operation record
          BRANCH    OVRCD,TOPALL90
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     CURSESS,KEY22
          GOTO      TOPALL90 IF NOT EQUAL
.
TOPALL20  MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      XBITEM,OPDATHET         * alter theatre code
.
          MATCH     OPSETHET,OPDATHET       * keyin theatre = default theatre ?
          GOTO      TOPALL30 IF NOT EQUAL   * no
          MOVE      SP6,OPDATHET            * update oprdetaf with blank theatre
. 
TOPALL30  CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update theatre code
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN1,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          GOTO      TOPALL10
.
TOPALL90  MOVE      ZERO,EXIT
.
TOPALL99  RETURN
+
.*******************************************************************************
.                                 THASK000
.    Ask the appropriate question based on what is required
.*******************************************************************************
THASK000  MOVE      UNDLN6,XBITEM           * Default prompt
.
          MATCH     SP6,OPDATHET
          GOTO      THASK100 IF EQUAL
          MOVE      OPDATHET,XBITEM         * Display theatre if known
          GOTO      THASK200
.
THASK100  MATCH     SP6,OPSETHET
          GOTO      THASK200 IF EQUAL
          MOVE      OPSETHET,XBITEM         * Use sessions theatre
.
THASK200  BRANCH    EXIT,THASK300,THASK400
.
THASK300  DISPLAY   *P1:24,*EL,"Please input the new ",*+,OPCNODSC," for this ":
                    "patient : ",XBITEM;
          MOVELPTR  OPCNODSC,CCOL
          ADD       "42",CCOL
          GOTO      THASK999
.
THASK400  DISPLAY   *P1:24,*EL,"Please input the new ",*+,OPCNODSC," for the ":
                    "patients : ",XBITEM;
          MOVELPTR  OPCNODSC,CCOL
          ADD       "42",CCOL
.
THASK999  RETURN
+
.*******************************************************************************
.                   TRTHE000
.         Get transfer theatre code from opropraf
.*******************************************************************************
TRTHE000  CALL      THASK000                * Ask the question
          KEYIN     *PCCOL:24,*V2LON,XBITEM;
.
          ENDSET    XBITEM
          APPEND    SP6,XBITEM
          RESET     XBITEM
.
          MATCH     SP6,XBITEM
          GOTO      TRTHE900 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,XBITEM
          GOTO      TRTHE800 IF EQUAL       * exitchar entered
.
          MATCH     "?",XBITEM
          GOTO      TRTHE100 IF NOT EQUAL   * validate code entered
.
          CALL      OPROPRDS                * call theatre room ? routine
          MOVE      ZERO,SCRNFLAG           * signal for redisplay
          GOTO      TRTHE000                * go back and get code
.
.         validate what was entered
.
TRTHE100  MOVE      XBITEM,KEY6
          CALL      RDOPOPR1                * validate code & get description
          BRANCH    OVRCD,TRTHE200
.
          COMPARE   ONE,OPRMSTAT
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code ",*HOFF;
            CALL      HITENTER
            GOTO      TRTHE000
          ENDIF
.
          BRANCH    SCRNFLAG,TRTHE110       * no redisplay needed
          CALL      OPDSPSHD                * redisplay top of screen
.
TRTHE110  MOVE      FALSE,EXIT
          GOTO      TRTHE999
.
TRTHE200  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Invalid Operating Room code **    ";
          CALL      HITENTER
          GOTO      TRTHE000
.
.         exitchar entered
.
TRTHE800  MOVE      OPSETHET,XBITEM         * default to original theatre
.
.         nothing entered
.
TRTHE900  BRANCH    SCRNFLAG,TRTHE950       * no redisplay needed
          CALL      OPDSPSHD                * redisplay top of screen
.
TRTHE950  MOVE      ONE,EXIT                * set exit flag
.
TRTHE999  RETURN
+
.*******************************************************************************
.                                 OPCANBOK
.    Cancel a operation booking 
.
.    Function : First get confirmation to cancel the booking and then get 
.               cancellation reason code, (validated against the codes file,
.               category "SC").  To cancel the booking, update the OPRDEAFD
.               record with cancellation code, and status of "3".
.               The case number will be set to the next available case number
.               (based on how many bookings have been cancelled so far.
.               The first one will have case number 1).
.
.    Parameters:
.    CURSESS  - Current session in process.  Date Time Clinic/Doctor
.    CURCASE  - Current case number of booking
.
.    Return Parameters:
.    Exit = 0 - Booking Cancelled
.    Exit = 1 - Booking Not Cancelled
.    SCRNFLAG = 0 - Original screen must be redisplayed
.    SCRNFLAG = 1 - No redisplayed required
.******************************************************************************
OPCANBOK  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Cancel Booking) ";
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1
          BRANCH    OVRCD,CANBOK95
          MOVE      OPDAADMN,SAVADMN        * save the admission number
.
          DISPLAY   *P1:24,*EL,"Ok to cancel this Booking (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? _";
.
          KEYIN     *P35:24,*V2LON,ANS;
          REPLACE   UPPLOW,ANS
.
          MATCH     ANSY,ANS
          GOTO      CANBOK10 IF EQUAL       * delete
          MATCH     ANSN,ANS
          GOTO      CANBOK95 IF EQUAL       * do not delete
          BEEP
          GOTO      OPCANBOK
.
CANBOK10  MOVE      ONE,SCRNFLAG
.
CANBOK20  DISPLAY   *P1:24,*EL,"Please enter Cancellation code : ___";
.
CANBOK25  MOVE      "24",EVERT              * row
          MOVE      "34",ECOL               * column
          MOVE      SP3,CKYIDEF3            * no default
          MOVE      "0",CKYIMAND            * not mandatory
          PACK      CODE,ANSS,ANSC
          MOVE      ONE,CNEWDS              * new ? mark
.
          CALL      PATCODKY
          BRANCH    EXIT,CANBOK25:          * nothing entered
                         CANBOK90           * exitchar entered
.
          MOVE      ACODE,REPLY
          MOVE      REPLY,DIM3
.
.         If this event is a linked event for an EOC, then we need to delete
.         this record also
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*120,PTCNUEOC
          IF        PTCNUEOC = 1
            MOVE      SAVADMN,PVIBILL
            PROC      EOCDELEV
          ENDIF
.
          CALL      DEAREC00           * delete this case from all files ?
          BRANCH    EXIT,CANBOK35      * all traces of booking have been deleted
.
          CALL      CANREC00           * cancel bokrecfd and update W/L rec ?
          BRANCH    EXIT,CANBOK95      * exitchar entered
.
          MOVE      DIM3,REPLY
          CALL      CANDEA00           * cancel operation booking
          MOVE      REPLY,SAOPCHGD 
          MOVE      THREE,OPHITCHG
          CALL      WOPHIS00           * write to history
          GOTO      CANBOK99
.
CANBOK35  CALL      CANREC00           * cancel bokrecfd and update W/L rec ?
          GOTO      CANBOK80
.
CANBOK40  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Cancellation code **    ";
          CALL      HITENTER
          GOTO      CANBOK20
.
CANBOK50  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Cancellation code is mandatory **    ";
          CALL      HITENTER
          GOTO      CANBOK20
.
CANBOK80  MOVE      ZERO,EXIT               * valid entry
          GOTO      CANBOK99
.
.         EXITCHAR entered
.
CANBOK90  BRANCH    SCRNFLAG,CANBOK95       * no redisplay needed
          CALL      OPDSPSHD                * redisplay header
          CALL      OPDSPCUR                * redisplay screen
.
CANBOK95  MOVE      ONE,EXIT                * no changes made
.
CANBOK99  RETURN
+
.**********************************************************************
.*  DEAREC00  :  Delete booking from all files?                       *
.**********************************************************************
DEAREC00  MOVE      ZERO,EXIT
.
          MATCH     ANSD,TCINDC1            * 1st indicator 'D' ?
          GOTO      DEAREC99 IF NOT EQUAL
.
. delete all traces of this booking
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get existing data
          BRANCH    OVRCD,DEAREC99
          CALL      DEOPDEA1                * cancel operation record
.
          MOVE      FOUR,AUDIFLAG           * delete audit
          CALL      AUDEA000                * write audit
.
          MOVE      TEN2,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS15                * send delete booking message
.
. delete from oprdebaf
.
          PACK      KEY11,OPDAUNIQ,SP20
          CALL      RSOPDEB1
DEAREC10  CALL      RKOPDEB1
          BRANCH    OVRCD,DEAREC20
.
          MATCH     OPDAUNIQ,OPDBUNIQ
          GOTO      DEAREC20 IF NOT EQUAL
.
          PACK      KEY11,OPDBUNIQ,OPDBTEAM
          CALL      DEOPDEB1
          MOVE      FOUR,AUDIFLAG           * delete audit
          CALL      AUDEB000                * write audit
          PACK      KEY11,OPDBUNIQ,OPDBTEAM
          CALL      RSOPDEB1
          GOTO      DEAREC10
.
. delete from oprdecaf
.
DEAREC20  PACK      KEY11,OPDAUNIQ,SP20
          CALL      RSOPDEC1
DEAREC25  CALL      RKOPDEC1
          BRANCH    OVRCD,DEAREC30
.
          MATCH     OPDAUNIQ,OPDCUNIQ
          GOTO      DEAREC30 IF NOT EQUAL
.
          PACK      KEY11,OPDCUNIQ,OPDCTEAM
          CALL      DEOPDEC1
          MOVE      FOUR,AUDIFLAG           * delete audit
          CALL      AUDEC000                * write audit
          PACK      KEY11,OPDCUNIQ,OPDCTEAM
          CALL      RSOPDEC1
          GOTO      DEAREC25
.
.    After cancelling a booking the session durations and start times must
.    be updated.  Also release suspended sessions.
.
DEAREC30  PACK      KEY22,CURSESS,SP20
          CALL      RDOPSES1                * get session details
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUSES000                * write audit
          SUB       OPDAEXPD,OPSETIMB       * subtract patient operation durat'n
          SUB       OPSEPREP,OPSETIMB       * subtract patient prep time
          CALL      UPOPSES1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUSES000                * write audit
.
          COMPARE   ONE,OPSESUSP            * test if session was suspended
          GOTO      DEAREC40 IF NOT EQUAL
          CALL      OPCHKFUL                * check if session is still full
          COMPARE   ONE,EXIT                * exit=1 if session not full
          GOTO      DEAREC40 IF NOT EQUAL
          CALL      OPUNSPND                * un-suspend session
.
.    Adjust the case numbers and start times by calling OPMVSBOK if a cases
.    exist after the cancelled case.
.
DEAREC40  MOVE      CURCASE,FORM3
          ADD       ONE,FORM3
          PACK      KEY26,CURSESS,ZERO,FORM3
          CALL      RDOPDEA1                * get session details
          BRANCH    OVRCD,DEAREC90
.
          PACK      KEY22,CURSESS,SP20
          MOVE      CURCASE,CASETO          * set parameters for OPMVSBOK to
          MOVE      CURCASE,CASEFROM          cancel
          CALL      OPMVSBOK                * read just casenum and start times
.
          CALL      DELHIS00                 * delete history
.
DEAREC90  MOVE      ONE,EXIT
.
DEAREC99  RETURN
. *****************************************************************************
. * DELHIS0000 : Delete a Record                                                
. *****************************************************************************
DELHIS00  
.
. ------- delete record from the history file ---------------
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
.
          COMPARE   ONE,PTCNURHA
          GOTO      DELHIS99 IF NOT EQUAL
.
          OPEN      OPRHISA1,"oprhisaf" 
          PACK      KEY26,PURNO,OPDAADMN,OPDAUNIQ,SP20
.
DELHIS50  PACK      KEY38,KEY26,SP20,SP20
          CALL      RSOPHIS1
          CALL      RKOPHIS1
          BRANCH    OVRCD,DELHIS99
.
          PACK      KEY27,OPHIURNO,OPHIEVNT,OPHIUNIQ,SP10
          MATCH     KEY26,KEY27
          GOTO      DELHIS99 IF NOT EQUAL
.
          PACK      KEY38,OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT,OPHIUCNT,SP10
          CALL      DEOPHIS1
.
          GOTO      DELHIS50
.
DELHIS99  RETURN
+
.*********************************************************************
.*                  CANREC00                    Called by : CANBOK30 *
.*        When cancel a booking, see if want to cancel admission bk. *
.*        Para's  : SAVADMN       the admission number               *
.*********************************************************************
CANREC00  OPEN      BOKRC1A1,"bokrc1af"
          MOVE      SAVADMN,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,CANREC82          * no booking record
.
CANREC05  DISPLAY   *P1:24,"Do you want to cancel the Admission Booking (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*EL;
.
CANREC10  KEYIN     *P53:24,*DV,UNDLN1,*P53:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      CANREC82 IF EQUAL       * don't do anything
          MATCH     ANSY,ANS
          GOTO      CANREC15 IF EQUAL       * cancel the booking
          BEEP
          GOTO      CANREC10
.
...  Check if booking Admitted or On Leave, dont allow these to be cancelled 
.
CANREC15  MOVE      SAVADMN,KEY8            * read admission file to
          CALL      RDMISS1                 * get admission status
          BRANCH    OVRCD,CANREC20
.
          MOVE      DASTAT,FORM1
          IF        FORM1=TWO | FORM1=FOUR
            DISPLAY   *P1:24,"Cannot cancel Admission Booking, booking admitted. ",*B;
            CALL      HITENTER
            GOTO      CANREC80
          ENDIF
          CLEAR     FORM1
.
CANREC20  DISPLAY   *P1:24,*EL,"Enter the Booking Cancellation Code : ";
.
CANREC25  MOVE      "24",EVERT              * row
          MOVE      "39",ECOL               * column
          MOVE      SP3,CKYIDEF3            * no default
          MOVE      "0",CKYIMAND            * not mandatory
          PACK      CODE,ANSB,ANSC           * category
          MOVE      ONE,CNEWDS              * new ? mark
.
          CALL      PATCODKY
          BRANCH    EXIT,CANREC25:          * nothing entered
                         CANREC90           * exitchar entered
.
          MOVE      ACODE,REPLY
.
.         Now check if there is a corresponding Waiting List record
.         that also needs to be updated
.
CANREC60  COMPARE   ONE,HBWAIT              * using Waiting List ?
          GOTO      CANREC80 IF NOT EQUAL   * no
.
          OPEN      WATTR1A1,"wattr1af"
          PACK      KEY19,BKREURNO,SP20
          CALL      RDSTREA1
CANREC65  CALL      RDKTREA1                * read wattr1af file
          BRANCH    OVRCD,CANREC80
.
          MATCH     WMURNO,BKREURNO         * same U/R ?
          GOTO      CANREC80 IF NOT EQUAL   * no
.
          MATCH     WMBOOK,BKREBOOK         * same Booking Number ?
          GOTO      CANREC65 IF NOT EQUAL   * no, get next wattr1af record
.
          IF        !(WMSTAT>1 & WMSTAT<5)
            GOTO      CANREC65 
          ENDIF
.
........  COMPARE   TWO,WMSTAT              * is Status = Scheduled ?
........  GOTO      CANREC65 IF NOT EQUAL   * no, get next wattr1af record
.
. check if put patient back on the waiting list
.
          COMPARE   ONE,WTCNRPWL
          GOTO      CANREC70 IF NOT EQUAL
.
          DISPLAY   *P1:24,"Do you want to put the patient back on the Waiting":
                    " List (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*EL;
.
CANREC67  KEYIN     *P65:24,*DV,UNDLN1,*P65:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      CANREC75 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      CANREC70 IF EQUAL
          BEEP
          GOTO      CANREC67
.
CANREC70  CALL      KDATEL00                * keyin new date on list 
          MOVE      ONE,WMSTAT              * set Status to Unscheduled
          CALL      SAVHIS00
          MOVE      "05",WTWMBSCD
          MOVE      ONE,NBRFLAG
          GOTO      CANREC78
.
CANREC75  MOVE      SIX,WMSTAT              * set Status to Removed
          CALL      SAVHIS00
          MOVE      "20",WTWMBSCD
          MOVE      ONE,NBRFLAG
          PACK      WMDATE4,CCC,CYY,CMM,CDD
          REP       " 0",WMDATE4
.
. now input the removal reason for W/L Cancellation
.
          DISPLAY   *P1:24,"Enter the Removal reason : ",*EL;
          MOVE      ONE,CKYIMAND            * keyin is mandatory
          MOVE      SP3,CKYIDEF3
          MOVE      "WR",CODE
          MOVE      "24",EVERT
          MOVE      "28",ECOL
.
          CALL      PATCODKY
          BRANCH    EXIT,CANREC99,CANREC92
.
.        now ACODE contains our removal reason
.
          MOVE      ACODE,WMREASON          * cancellation reason
.
. now update waiting list treatments file
.
CANREC78  MOVE      WMDATE3,SAVESCHA
          MOVE      SP8,WMDATE3             * blank off Scheduled Adm. Date
          OPEN      PATVISA1,"patvisaf"
          CALL      GENBOKNO           * generate booking number
          CALL      UPTREA1                 * update wattr1af record
.
          CALL      GETRSC00                     * get reason  
          MOVE      ZERO,ADDWLFLG
          MOVE      Z70,WATHI002
          CALL      WRTHIS00                     * write to history
.
. now update booking file with cancellation reason
.
CANREC80  MOVE      REPLY,BKRECANC          * set cancel code
          MOVE      TWO,BKRESTAT            * set status
          CALL      UPBKREC1                * update booking record
.
          MATCH     "DGSRVADT",PRGID
          IF        !@EQUAL
            MOVE      TEN3,HL7TRGID
            MOVE      "OPRDEA01",HL7INCLD
            PROC      DGCLICCB              * Pass Cancel to Datagate  *I-90201
          ENDIF
.
. now update/write to watcataf 
.
. ------ write a record to the Category Change file ------
.
          PACK      WTCAEDAT,CCC,CYY,CMM,CDD * Effective Date -Cancellation Date
          REP       " 0",WTCAEDAT
          MOVE      BKREURNO,WTCAURNO       * UR Number
          MOVE      BKREPROC,WTCAPROC       * Procedure Code
          MOVE      BKRECNT,WTCAPCNT        * Count
          PACK      WTCACATF,ANSB,ANSC      * category
          MOVE      SP3,WTCACODF            * Code Changed From
          MOVE      BKRECANC,WTCACODT       * Code Changed To
.
          PACK      KEY29,WTCAEDAT,WTCACATF,WTCAURNO,WTCAPROC,WTCAPCNT
          OPEN      WATCATA1,"watcataf"     * open watcataf file
          CALL      RAWTCAT1                * check if record already exist
          IF        OVRCD=1
            CALL      WRWTCAT1                * write category change record
          ELSE
            CALL      UPWTCAT1
          ENDIF
.
.
.
. If zero U/R booking, delete record from the Surname file
.
          MATCH     ZEROUR,PURNO            * zero U/R ?
          GOTO      CANREC82 IF NOT EQUAL   * no
          CALL      DESURN00                * yes, delete record
.
CANREC82  MOVE      ZERO,EXIT               * valid entry
          GOTO      CANREC99
.
. error messages
.
CANREC85  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Cancellation code **    ";
          CALL      HITENTER
          GOTO      CANREC20
.
CANREC86  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Cancellation Code is mandatory **    ";
          CALL      HITENTER
          GOTO      CANREC20
.
.         EXITCHAR entered
.
CANREC90  BRANCH    SCRNFLAG,CANREC95       * no redisplay needed
CANREC92  CALL      OPDSPSHD                * redisplay header
          CALL      OPDSPCUR                * redisplay screen
.
CANREC95  MOVE      ONE,EXIT
.
CANREC99  RETURN
+
.**************************************************************************
.*  KDATEL0000  :   Keyin Date on List                                      *
.**************************************************************************
KDATEL00  DISPLAY   *P1:24,*EL,*EL,"New Date on List             :" 
.
KDATEL05  UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          MOVE      "32",CCOL
          MOVE      TWENTY4,CVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CCANLDTE
.
KDATEL10  CALL      KEYDATE
          BRANCH    CQUEST,KDATEL05
          BRANCH    OVRCD,KDATEL05                * anything input
.
          PACK      WMDATE1,CCENT,CYEAR,CMON,CDAY
          REP       " 0",WMDATE1
.
          MOVE      ZERO,EXIT
          GOTO      KDATEL99
.
KDATEL90  MOVE      ONE,EXIT
.
KDATEL99  RETURN
+
.*********************************************************************
.*  CANWLF00  :  Cancel waiting list related files                   *
.*********************************************************************
CANWLF00  
.
CANWLF99  RETURN
+
.******************************************************************************
.*                           DESURN00                    Called by : CANBOK30 *
.*            DElete record from SURName file                                 *
.******************************************************************************
.
DESURN00  OPEN      PATGSRN1,"patgsrnf"
          MOVE      PTMASNAM,GSRSNAM
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          PACK      KEY115,PURNO,SAVADMN,GSRSNAM,GSRGNAM,PTGSGNM2,PBDATE,PSEX:
                           SP70,SP70
          CALL      RAPTGSR1
          BRANCH    OVRCD,DESURN99
          CALL      DEPTGSR1
.
DESURN99  RETURN
+
.******************************************************************************
.                                 CANDEA00
.    Cancel a operation booking.
.    CURSESS : current session date time clinic/doctor
.    CURCASE : cancel case number
.    CURSTAT : current status
.******************************************************************************
.    Get last cancelled case number
.
CANDEA00  MOVE      ONE,FORM3               * set cancelled casenum to first
          PACK      KEY26,CURSESS,THREE,Z70
          CALL      RSOPDEA1                * position form pointer
          CALL      RPOPDEA1                * get last cancelled case no. in
          BRANCH    OVRCD,CANDEA10            session
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY22,CURSESS           * test same session 
          GOTO      CANDEA10 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT          * test for cancelled status
          GOTO      CANDEA10 IF NOT EQUAL
          MOVE      OPDACASE,FORM3
          ADD       ONE,FORM3               * set next cancelled casenum
.
CANDEA10  PACK      KEY26,CURSESS,CURSTAT,CURCASE
          CALL      RDOPDEA1                * get existing data
          BRANCH    OVRCD,CANDEA90
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
          MOVE      FORM3,OPDACASE          * set cancelled casenum
          MOVE      REPLY,OPDACANC          * set cancellation code
          MOVE      THREE,OPDASTAT          * set cancelled status
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * cancel operation record
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN4,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS15                * send cancel booking message
.
.    Cancel (Delete) Special Item Usage records for this Session
          IF        OPCNSPET=1
            OPEN      OPRSIUA1,"oprsiuaf"   * Open special item usage file
            OPEN      OPRSIUA2,"oprsiuaf"  
            CALL      CANSIU00              * cancel oprsiuaf records
            CLOSE     OPRSIUA1              * close files
            CLOSE     OPRSIUA2
          ENDIF
.
.    After cancelling a booking the session durations and start times must
.    be updated.  Also release suspended sessions.
.
          PACK      KEY22,CURSESS,SP20
          CALL      RDOPSES1                * get session details
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUSES000                * write audit
          SUB       OPDAEXPD,OPSETIMB       * subtract patient operation durat'n
          SUB       OPSEPREP,OPSETIMB       * subtract patient prep time
          CALL      UPOPSES1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUSES000                * write audit
.
          COMPARE   ONE,OPSESUSP            * test if session was suspended
          GOTO      CANDEA20 IF NOT EQUAL
          CALL      OPCHKFUL                * check if session is still full
          COMPARE   ONE,EXIT                * exit=1 if session not full
          GOTO      CANDEA20 IF NOT EQUAL
          CALL      OPUNSPND                * un-suspend session
.
.    Adjust the case numbers and start times by calling OPMVSBOK if a cases
.    exist after the cancelled case.
.
CANDEA20  MOVE      CURCASE,FORM3
          ADD       ONE,FORM3
          PACK      KEY26,CURSESS,ZERO,FORM3
          CALL      RDOPDEA1                * get session details
          BRANCH    OVRCD,CANDEA80
.
          PACK      KEY22,CURSESS,SP20      * parameter to OPMVSBOK
          MOVE      CURCASE,CASETO          * set parameters for OPMVSBOK to
          MOVE      CURCASE,CASEFROM          cancel
          CALL      OPMVSBOK                * read just case no. & start times
.
CANDEA80  MOVE      ZERO,EXIT
          GOTO      CANDEA99
.
CANDEA90  MOVE      ONE,EXIT
.
CANDEA99  RETURN
+
.*******************************************************************************
.                                 OPPSTDEA
.    Post a OPRDEAFD record.
.    Function : Post a booking details to OPRDEAFD and update session 
.               suspension status
.    Parameters:
.    CURSESS  - Current session in process.  Date Time Clinic/Doctor
.    CURCASE  - Current case number of booking
.
.    Return Parameters:
.    Exit = 0 - Booking posted, No problems
.    Exit = 1 - Booking posted session is full
.*******************************************************************************
OPPSTDEA  MOVE      ONE,FORM3               * set cancelled case no. to first
          PACK      KEY26,CURSESS,ZERO,Z70
          CALL      RSOPDEA1                * position fptr
          CALL      RPOPDEA1                * get last case no. in session
          BRANCH    OVRCD,PSTDEA10
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY22,CURSESS           * test same session 
          GOTO      PSTDEA10 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT           * test for booked status
          GOTO      PSTDEA10 IF NOT EQUAL
          MOVE      OPDACASE,FORM3
          ADD       ONE,FORM3               * set next case no.
PSTDEA10
          CALL      DEAI0000                * restore saved oprdeafd variables
.
          MOVE      "42",FORM2              * get previous unique record
          MOVE      " 42",PRXCODE   * System Lock Sector 42
          CALL      GETSLK00        * Get System Lock of Sector 42
          READ      CONTROLF,FORM2;*32,OPCNUNIQ
          ADD       ONE,OPCNUNIQ            * set it to next unique number
          WRITAB    CONTROLF,FORM2;*32,OPCNUNIQ
          CALL      RELSLK00        * Release System Lock of Sector 42
          MOVE      OPCNUNIQ,OPDAUNIQ
.
          UNPACK    CURSESS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MOVE      ZERO,OPDASTAT
          MOVE      FORM3,OPDACASE
          MOVE      BKREBOOK,OPDAADMN       * restore Admission Number
          CALL      IBACLOCK
          PACK      OPDACRDT,CCC,CYY,CMM,CDD
          REP       " 0",OPDACRDT
.
          CLOCK     TIME,OPDACTIM
          MOVE      WBSEUID,OPDAWEBC
.
          PACK      KEY26,CURSESS,ZERO,FORM3
          CALL      WROPDEA1                * Operation details 
.
          MOVE      ONE,AUDIFLAG            * add audit
          CALL      AUDEA000                * write audit
.
          MOVE      TEN5,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS12                * send new booking message
.
          PACK      KEY22,CURSESS,SP20
          CALL      RDOPSES1                * get session book time
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUSES000                * write audit
          ADD       OPDAEXPD,OPSETIMB       * Add duration to session book time
          ADD       OPSEPREP,OPSETIMB       * Add patient prep time
          CALL      UPOPSES1                * update book time
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUSES000                * write audit
.
          PACK      KEY22,CURSESS,SP20
          CALL      OPCHKFUL                * check if session is full
          BRANCH    EXIT,PSTDEA80           * exit=1 session is not full
.
          CALL      OPSUSPND                * suspend session
          GOTO      PSTDEA90
PSTDEA80
          CALL      OPUNSPND                * unsuspend session
          MOVE      ZERO,EXIT               * Signal rec. posted OK no Problems
          GOTO      PSTDEA99
PSTDEA90
          MOVE      ONE,EXIT                * Signal rec.posted session now full
PSTDEA99  RETURN
+
.*******************************************************************************
.                                 OPDSPOPR
.    Display patients operation description on lines 18-22
.    and allow alteration if desired (added 13/12/90 JC)
.
.    Function   : Display operation details of current booking and allow the 
.                 user to alter them
.
.    Parameters :
.      CURCASE  - Current case number of booking
.      CURSESS  - Current session in process.  Date Time Clinic/Doctor
.
.    Return Parameters :
.      Redisplay the screen
.*******************************************************************************
OPDSPOPR  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Change Operation Details) ";
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get operation details
          BRANCH    OVRCD,DSPOPR99
.
.         display all the descriptions
.
          CALL      DPOPR000
.
DSPOPR20  DISPLAY   *P1:24,"Select Item : ",*EL;
.
DSPOPR30  KEYIN     *P15:24,*DV,UNDLN1,*P15:24,*V2LON,FORM1;
.
          COMPARE   ZERO,FORM1
          GOTO      DSPOPR99 IF EQUAL       * exit selected
.
.         change the operation description
.
          MOVE      "18",PRVYPOS            * starting row
          ADD       FORM1,PRVYPOS           * line to enter on
.
          BRANCH    FORM1,DSPOPR40,DSPOPR50,DSPOPR60
          BEEP
          GOTO      DSPOPR30
.
DSPOPR40  CALL      DPOPRPV1                * enter first description
          GOTO      DSPOPR20
.
DSPOPR50  CALL      DPOPRPV2                * enter second description
          GOTO      DSPOPR20
.
DSPOPR60  CALL      DPOPRPV3                * enter third description
          GOTO      DSPOPR20
.
DSPOPR99  RETURN
.
.*****************************************************************************
.* DPOPR000 : Display the operation details             Called by OPDSPOPR   *
.*****************************************************************************
DPOPR000  MOVE      FIVE,HLEF
          MOVE      TEN8,HTOP
          MOVE      SEVENTY3,HRIG
          MOVE      TWENTY2,HBOT
          DISPLAY   *P1:17,*EF,*P5:18,"*-------------------------------------":
                                      "------------------------------*",*V2LON:
                    *P3:19,"1",*HOFF," | ",OPDAPROV," ",OPDADES1," |",*V2LON:
                    *P3:20,"2",*HOFF," | ",OPDAPRV2," ",OPDADES2," |",*V2LON:
                    *P3:21,"3",*HOFF," | ",OPDAPRV3," ",OPDADES3," |":
                    *P5:22,"*----------------------------------------------":
                           "---------------------*"
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
DPOPR999  RETURN
+
.*****************************************************************************
.
.         The following are copies of the includes in OPCHGPRV.PBL 
.         with a slight modification to DPITM000 to allow for redisplaying
.         the 'window' after a ? mark and then not entering anything
+
.*******************************************************************************
.                                 DPOPRPV1
.    The function changes provisional item 1 of a selected case record.
.
.    Function  : This routine will start by checking if they are using the
.                1st provisional item. If they are, the user will be prompted
.                to keyin the 1st provisional item, defaulting to the current
.                value. This keyin is set at the position the provisional item
.                is being displayed. The program will then check if the user is
.                defaulting the operation description to item description. If
.                they are, the program will ask to enter the new description,
.                defaulting to the item description (old description if the item
.                was not changed, or if the new item code is blank). The program
.                will then update the OPRDEAFD file with the new data. In all
.                cases, the final item code and description is displayed for
.                the user.
.
.    Parameters:
.    CURSESS   - Current session in process
.    CURCASE   - Current case number in use
.    PRVYPOS   - The line for which the keyin is to take place
.
.    Return Parameters:
.    None
.*******************************************************************************
DPOPRPV1  PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get operation data
          BRANCH    OVRCD,DPDPV199
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
.
          BRANCH    OPCNNPRV,DPDPV110,DPDPV110,DPDPV110
.
.         not using provisional items
.
          MOVE      OPDADES1,DESC55         * set default desc
          CALL      DPDES000                * update description
          MOVE      DESC55,OPDADES1         * set up desc
          GOTO      DPDPV120                * update file
.
.         change first provisional item
.
DPDPV110  MOVE      OPDAPROV,PROVITEM       * set default prov. item for DPITM
          MOVE      OPDADES1,DESC55         * default to first operation desc
.
          CALL      DPITM000                * get provisional item
          BRANCH    EXIT,DPDPV199           * finished
          MOVE      PROVITEM,OPDAPROV
          MOVE      DESC55,OPDADES1
 
DPDPV120  CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN6,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
DPDPV199  RETURN
+
.*******************************************************************************
.                                 DPOPRPV2
.    The function changes provisional item 2 of a selected case record.
.
.    Function : See DPOPRPV1
.
.    Parameters:
.    CURSESS   - Current session in process
.    CURCASE   - Current case number in use
.    PRVYPOS   - The line for which the keyin is to take place
.
.    Return Parameters:
.    None
.*******************************************************************************
DPOPRPV2  PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get operation data
          BRANCH    OVRCD,DPDPV299
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
.
          BRANCH    OPCNNPRV,DPDPV210,DPDPV220,DPDPV220
.
.         not using provisional items
.
DPDPV210  MOVE      OPDADES2,DESC55         * set default desc
          CALL      DPDES000                * update description
          MOVE      DESC55,OPDADES2
          GOTO      DPDPV230
.
.         change second provisional item
.
DPDPV220  MOVE      OPDAPRV2,PROVITEM       * set default prov. item for DPITM
          MOVE      OPDADES2,DESC55         * default to second operation desc
.
          CALL      DPITM000                * get provisional item
          BRANCH    EXIT,DPDPV299
          MOVE      PROVITEM,OPDAPRV2
          MOVE      DESC55,OPDADES2
.
DPDPV230  CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN7,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
DPDPV299  RETURN
+
.*******************************************************************************
.                                 DPOPRPV3
.    The function changes provisional item 3 of a selected case record.
.
.    Function : See DPOPRPV1
.
.    Parameters:
.    CURSESS   - Current session in process
.    CURCASE   - Current case number in use
.    PRVYPOS   - The line for which the keyin is to take place
.
.    Return Parameters:
.    None
.*******************************************************************************
DPOPRPV3  PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get operation data
          BRANCH    OVRCD,DPDPV399
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit
.
          BRANCH    OPCNNPRV,DPDPV310,DPDPV310,DPDPV320
.
.         not using provisional items
.
DPDPV310  MOVE      OPDADES3,DESC55         * set default desc
          CALL      DPDES000                * update description
          MOVE      DESC55,OPDADES3
          GOTO      DPDPV330
.
.         change third provisional item
.
DPDPV320  MOVE      OPDAPRV3,PROVITEM       * set default prov. item for DPITM
          MOVE      OPDADES3,DESC55         * default to third operation desc
.
          CALL      DPITM000                * get provisional item
          BRANCH    EXIT,DPDPV399
          MOVE      PROVITEM,OPDAPRV3
          MOVE      DESC55,OPDADES3
.
DPDPV330  CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      AUDEA000                * write audit
          MOVE      TEN8,HL7TRGID
          MOVE      "OPRDEA01",HL7INCLD
          PROC      DGCLIS14                * send update booking message
.
DPDPV399  RETURN
+
.*******************************************************************************
.                                 DPITM000
.    Get provisional item or MBS
.    This routine is the guts of the 3 OPDPDPV it responsible for getting code
.    from operator and validating the code enetered.  Also calculate where
.    the description should come from.
.*******************************************************************************
DPITM000  MOVE      PROVITEM,SAVED10        * save original provisional item
          MOVE      DESC55,PROVDESC         * copy operation description
.
DPITM100  MOVE      ONE,SCRNFLAG            * set redisplay flag off
          MOVE      SAVED10,PROVITEM        * Restore original provisional item
          DISPLAY   *P7:PRVYPOS,PROVITEM,*P7:PRVYPOS;
.
DPITM110  IF        OPCNCODE=0
           MOVE      SEVEN,ECOL
           MOVE      PRVYPOS,EVERT
           MOVE      ZERO,CKYIMAND
           MOVE      PROVITEM,CKYIDEF9
           CALL      PATICOKY
           MOVE      OPCODE,PROVITEM
           MOVE      ODESC,PROVDESC
          ELSE
            KEYIN     *V2LON,*RV,PROVITEM;
          ENDIF
          DISPLAY   *P7:PRVYPOS,PROVITEM,*P7:PRVYPOS;
.
          ENDSET    PROVITEM
          APPEND    SP9,PROVITEM
          RESET     PROVITEM
.
          MATCH     SP9,PROVITEM
          GOTO      DPITM400 IF EQUAL       * <Return> hit
.
          MATCH     EXITCHAR,PROVITEM
          GOTO      DPITM900 IF EQUAL       * exitchar entered
.
          CMATCH    "?",PROVITEM
          GOTO      DPITM300 IF NOT EQUAL   * validate item entered
.
          BRANCH    OPCNCODE,DPITM120
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** ? option is not available for ICD9 codes **    ";
          CALL      HITENTER
          GOTO      DPITM100
.
DPITM120  UNPACK    PROVITEM,ANS,DIM1A      * DIM1A = search letter
          CALL      PATITMDS                * MBS ? routine
          MOVE      ZERO,SCRNFLAG           * set redisplay flag
          MOVE      SP10,PROVITEM           * clear the item
.
DPITM200  MOVE      SAVED10,PROVITEM        * Restore original provisional item
.
          DISPLAY   *P1:24,*EL,"Provisional Item code :",*P25:24,PROVITEM:
                    *P25:24;
          GOTO      DPITM110
.
.         validate the code
.
DPITM300  BRANCH    OPCNCODE,DPITM310       * check using ICD9 or MBS
.
          PACK      KEY9,PROVITEM
          CALL      RDICO1                  * Validate against ICD9s file
          BRANCH    OVRCD,DPITM600
          MOVE      ODESC,PROVDESC          * set provisional item description
          GOTO      DPITM400
.
DPITM310  PACK      KEY9,PROVITEM
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD                * Validate against MBS codes file
          BRANCH    OVRCD,DPITM600
          MOVE      IDESC,PROVDESC          * set provisional item description
.
.         have a valid code
.
DPITM400  BRANCH    SCRNFLAG,DPITM500
.
.         redisplay the screen and get back original record
.
          CALL      OPDSPSHD                * redisplay session header
          CALL      OPDSPCUR                * display session
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * get back details
.
          CALL      DPOPR000                * display operation details
.
          MOVE      ONE,SCRNFLAG            * signal screen is OK
.
.         check on how to change the description
.
DPITM500  DISPLAY   *P7:PRVYPOS,*V2LON,PROVITEM;
.
          COMPARE   ONE,OPCNUDSC            * test if using operation desc
          GOTO      DPITM520 IF NOT EQUAL   * cant change operation desc
.
          COMPARE   ONE,OPCNIDFL            * test if default operation desc
          GOTO      DPITM510 IF NOT EQUAL   * default to first op desc
          MOVE      PROVDESC,DESC55         * default to item description
.
DPITM510  CALL      DPDES000                * change description
          GOTO      DPITM530
.
DPITM520  MOVE      PROVDESC,DESC55         * set up desc to display
.
DPITM530  DISPLAY   *P7:PRVYPOS,PROVITEM,*P17:PRVYPOS,DESC55;
          GOTO      DPITM800
.
.         invalid item
.
DPITM600  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Invalid Provisional Item code **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,DPITM100
          GOTO      DPITM200
.
DPITM800  MOVE      ZERO,EXIT               * valid item
          GOTO      DPITM999
.
.         exitchar entered
.
DPITM900  DISPLAY   *P7:PRVYPOS,SAVED10;
          MOVE      SAVED10,PROVITEM
          MOVE      ONE,EXIT
.
DPITM999  RETURN
+
.*******************************************************************************
.                                 DPDES000
.         Enter an operation description
.         Para's  : DESC55        the description
.         Returns : DESC55        the description
.*******************************************************************************
DPDES000  DISPLAY   *P17:PRVYPOS,DESC55;
          KEYIN     *P17:PRVYPOS,*V2LON,*RV,DESC55;
.
          ENDSET    DESC55
          APPEND    SP30,DESC55
          APPEND    SP30,DESC55
          RESET     DESC55
          DISPLAY   *P17:PRVYPOS,DESC55;
DPDES999  RETURN
+
.
.*************************************************************************
. CANSIU00   : cancel (delete) oprsiuaf records for this session
. parameters : OPRDEA record
.*************************************************************************
CANSIU00  PACK      KEY24,OPDAUNIQ,OPDADATE,SP20   
          CALL      RSOPSIU2              * positional read
          CALL      RKOPSIU2              * next read
          BRANCH    OVRCD,CANSIU99
.
          MATCH     OPDAUNIQ,OPSIUNIQ     * same uniq?
          GOTO      CANSIU99 IF NOT EQUAL
          MATCH     OPDADATE,OPSIDATE     * same date?
          GOTO      CANSIU99 IF NOT EQUAL
.
          PACK      KEY24,OPSIUNIQ,OPSIDATE,OPSIITEM
          CALL      DEOPSIU2              * delete Special Item usage
          GOTO      CANSIU00              * get next
.
CANSIU99  RETURN
+
.
          INC       WLHISSUB/PBL
          INC       OPHISSUB/PBL
