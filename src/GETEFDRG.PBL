.------------------------------------------------------------------------------
.
.        Include Name  : GETEFDRG.PBL
.                                       
.        Function      : Utility routines to get DRG version using
.                        Effective DRG table (pmsedgaf)
.
.        Modifications :
.           V10.14.02    22/02/2019  J.Tan          TSK 0829856
.                        Mod to use Admission date for 'As effective date'
.           V10.14.01    18/02/2019  Ebon Clements  TSK 0829856
.                        Added contract effective from date functionality
.           V10.13.01    03/01/2019  J.Tan          TSK 0868413
.                        Mod position read on health fund/claim
.           V10.08.02    27/09/2016  Don Nguyen   TSK 0312570
.                        Modified GHFDRG00 to exit when match found for hospital
.           V10.08.01    20/09/2016  Don Nguyen   TSK 0312570
.                        Modified GSTDRG00 & GTEDRG40 to also check for blank   
.                        Hospital State for Default DRG (PTHSHSST). 
.           V10.08.00    26/08/2016  Don Nguyen   TSK 0312570
.                        Created include
.------------------------------------------------------------------------------
.
.******************************************************************************
.                               GHFDRG00                                       
.         Get Effective DRG Version for patient Health Fund                    
.                                                                              
.         Parameters:                                                          
.           AFUNDH   - Admission Health Fund Code
.           PMVXMHOS - Visit Hospital (for Hospital State; PTHSHSST)
.           DDATE    - Discharge Date
.           ADATE    - Admission Date
.                                                                              
.         Return:                                                              
.           KEY3 - DRG Version                                                 
.******************************************************************************
GHFDRG00  MOVE      SP3,KEY3
          OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
.
          MATCH     SP6,AFUNDH
          GOTO      GHFDRG91 IF EQUAL       * Blank Health Fund
.
          OPEN      PATFN1A1,"patfn1af"
          PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                 * Read a Health Fund file record
          BRANCH    OVRCD,GHFDRG91
.
          OPEN      PATFX1A1,"patfx1af"
          PACK      KEY6,AFUNDH,SP70
          CALL      RDPTFX11                * Read a Health Fund Extn file 
          IF        OVRCD=1  
            MOVE     SP70,PTFXCEFR          * Clear Contracts Effective From
          ENDIF
.
          MOVE      DDATE,D8                * Use Discharge date
.
          MATCH     "0",PTFXCEFR            * As at the effective date
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "1",PTFXCEFR            * For Admissions From
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "2",PTFXCEFR            * For Discharges From
          IF        @EQUAL
            MOVE      DDATE,D8              * Discharge date 
          ENDIF
.
          MATCH     SP3,PMVXMHOS            * No Hospital
          GOTO      GHFDRG20 IF EQUAL
.
.         Check Health Fund (using visit Hospital)
.
          PACK      KEY42,FOUR,PMVXMHOS,HCODE,Z70
          CALL      RSPMEDG2
GHFDRG11  CALL      RPPMEDG2
          BRANCH    OVRCD,GHFDRG18
.
          MATCH     "4",PMEGRTYP       * Record Type = Hospital / Health Fund ?
          GOTO      GHFDRG18 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY6          * Extract Hospital ID & HF Code
.
          MATCH     D3,PMVXMHOS             * Same Hospital?
          GOTO      GHFDRG18 IF NOT EQUAL
.
          MATCH     KEY6,HCODE                * Same Health Fund Code?
          GOTO      GHFDRG18 IF NOT EQUAL
.
          MATCH     SP8,D8             * No discharge date?
          GOTO      GHFDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     PMEGFDAT,D8      
          GOTO      GHFDRG11 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GHFDRG90 IF EQUAL
.
          MATCH     D8,PMEGTDAT
          GOTO      GHFDRG11 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GHFDRG90           * Got DRG we want
.
GHFDRG18  MATCH     SP70,PMVXMHOS
          GOTO      GHFDRG91 IF EQUAL
.
.         Get Health Fund DRG Version (NO Hospital)
.
GHFDRG20  PACK      KEY42,ONE,SP3,HCODE,Z70
          CALL      RSPMEDG2
GHFDRG21  CALL      RPPMEDG2
          BRANCH    OVRCD,GHFDRG91
.
          MATCH     "1",PMEGRTYP            * Record Type = Health Fund ?
          GOTO      GHFDRG91 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY6        * Extract Hosptial (blank) & HF Code
.
          MATCH     SP70,D3
          GOTO      GHFDRG91 IF NOT EQUAL
.
          MATCH     KEY6,HCODE
          GOTO      GHFDRG91 IF NOT EQUAL
.
          MATCH     SP8,D8             * No discharge date?
          GOTO      GHFDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     PMEGFDAT,D8    
          GOTO      GHFDRG21 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GHFDRG90 IF EQUAL
.
          MATCH     D8,PMEGTDAT
          GOTO      GHFDRG21 IF LESS   * Discharged AFTER Effective To Date
.
GHFDRG90  MOVE      PMEGDRGV,KEY3      * DRG Version
          MOVE      ZERO,EXIT
          GOTO      GHFDRG99
.
GHFDRG91  MOVE      ONE,EXIT
GHFDRG99  RETURN
+
.******************************************************************************
.         Get Effective DRG Version for patient Claim Code                     
.                                                                              
.         Parameters:                                                          
.           ACLAIM - Admission Health Fund Code                                
.           DDATE  - Discharge Date                                            
.           ADATE  - Admission Date
.                                                                              
.         Return:                                                              
.           KEY3 - DRG Version                                                 
.******************************************************************************
GCLDRG00  MOVE      SP3,KEY3
          OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
          OPEN      PATCODE1,"patcodes"
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GCLDRG91
.
          OPEN      PATCFAA1,"patcfaaf"
          PACK      KEY3,ACLAIM,SP70
          CALL      RDPTCFA1                * Read a Health Fund Extn file
          IF        OVRCD=1 
            MOVE      SP70,PTCFCEFR         * Contracts Effective From
          ENDIF
.
          MATCH     "3",PTCFCEFR            * Not in use
          GOTO      GCLDRG91 IF EQUAL
.
          MOVE      DDATE,D8                * Use Discharge date
          MATCH     "0",PTCFCEFR            * As at the effective date
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "1",PTCFCEFR            * For Admissions From
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "2",PTCFCEFR            * For Discharges From
          IF        @EQUAL
            MOVE      DDATE,D8              * Discharge date
          ENDIF
.
          PACK      KEY42,TWO,SP3,ACLAIM,Z70
          CALL      RSPMEDG2
GCLDRG11  CALL      RPPMEDG2
          BRANCH    OVRCD,GCLDRG91
.
          MATCH     "2",PMEGRTYP            * Record Type = Claim Code ?
          GOTO      GCLDRG91 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY3    * Extract Hospital (blank) & Claim Code
.
          MATCH     SP70,D3
          GOTO      GCLDRG91 IF NOT EQUAL
.
          MATCH     KEY3,ACODE              * Same Claim Code?
          GOTO      GCLDRG91 IF NOT EQUAL
.
          MATCH     SP8,D8             * No discharge date?
          GOTO      GCLDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     PMEGFDAT,D8     
          GOTO      GCLDRG11 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GCLDRG90 IF EQUAL
.
          MATCH     D8,PMEGTDAT
          GOTO      GCLDRG11 IF LESS   * Discharged AFTER Effective To Date
.
GCLDRG90  MOVE      PMEGDRGV,KEY3      * DRG Version
          MOVE      ZERO,EXIT
          GOTO      GCLDRG99
.
GCLDRG91  MOVE      ONE,EXIT
GCLDRG99  RETURN
+
.******************************************************************************
.         Get System State Default (using Hospital state)
.
.         Parameters:                                                          
.           PMVXMHOS - Visit Hospital (for Hospital State; PTHSHSST)
.           DDATE    - Discharge Date
.           ADATE    - Admission Date
.                                                                              
.         Return:                                                              
.           KEY3 - DRG Version                                                 
.******************************************************************************
GSTDRG00  MOVE      SP3,KEY3
          OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
          IF        IBCNMHOS=0
            MOVE      SP1,PTHSHSST     * Default state blank for non-multi hosp
            GOTO      GSTDRG10
          ENDIF
.
          MATCH     SP3,PMVXMHOS       * No hospital for visit
          IF        @EQUAL
            MOVE      SP1,PTHSHSST     * Default state blank
            GOTO      GSTDRG10
          ENDIF
.
.         Read Hospital file to get PTHSHSST (State for Default DRG)
.
          PACK      KEY3,PMVXMHOS,SP70
          CALL      RDPTHSP1
.
GSTDRG10  MOVE      SP70,D3                 * blank hospital
          PACK      KEY42,THREE,D3,PTHSHSST,Z70
          CALL      RSPMEDG2
GSTDRG11  CALL      RPPMEDG2
          BRANCH    OVRCD,GSTDRG20
.
          MATCH     "3",PMEGRTYP            * Record Type = State ?
          GOTO      GSTDRG20 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,D1     * Extract Hospital (blank) & State Code
.
          MATCH     SP70,D3
          GOTO      GSTDRG20 IF NOT EQUAL
.
          MATCH     D1,PTHSHSST        * Same State Code?
          GOTO      GSTDRG20 IF NOT EQUAL
.
          MATCH     SP8,DDATE          * No discharge date?
          GOTO      GSTDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     PMEGFDAT,DDATE
          GOTO      GSTDRG11 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GSTDRG90 IF EQUAL
.
          MATCH     DDATE,PMEGTDAT
          GOTO      GSTDRG11 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GSTDRG90           * Got DRG we want
.
GSTDRG20  MATCH     SP70,PTHSHSST
          GOTO      GSTDRG91 IF EQUAL  * Exit
.
          MOVE      SP70,PTHSHSST
          GOTO      GSTDRG10
.
GSTDRG90  MOVE      PMEGDRGV,KEY3      * DRG Version
          MOVE      ZERO,EXIT
          GOTO      GSTDRG99
.
GSTDRG91  MOVE      ONE,EXIT
GSTDRG99  RETURN
+
.******************************************************************************
.*                  GTEDRG00                           Called by: GDRG0000    *
.*        Get Effective DRG Version with regards to Discharge Date (DDATE).   *
.*        Retrieve using the following order:                                 *
.*          Health Fund by Hospital                                           *
.*          Health Fund                                                       *
.*          Claim Code                                                        *
.*          System State Default                                              *
.*          System Default                                                    *
.*                                                                            *
.*        Parameters:                                                         *
.*          DDATE - Discharge Date                                            *
.*          ADATE - Admission Date                                            *
.*                                                                            *
.*          AFUNDH / ACLAIM / PMVXMHOS (PTHSHSST)                             *
.*                                                                            *
.*        Return:                                                             *
.*          KEY3     - DRG Version                                            *
.*          CFPAYCOD - 3M Codefinder PAY Code                                 *
.******************************************************************************
GTEDRG00  MOVE      SP3,KEY3
          MOVE      SP3,CFPAYCOD       * Default blank PAY Code
          OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
.
          MATCH     SP6,AFUNDH
          GOTO      GTEDRG30 IF EQUAL     * Blank health fund; check Claim Code
.
          OPEN      PATFN1A1,"patfn1af"
          PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                 * Read a health fund file record
          BRANCH    OVRCD,GTEDRG30
.
          OPEN      PATFX1A1,"patfx1af"
          PACK      KEY6,AFUNDH,SP70
          CALL      RDPTFX11                * Read a Health Fund Extn file
          IF        OVRCD=1
            MOVE     SP70,PTFXCEFR          * Clear Contracts Effective From
          ENDIF
.
          MOVE      DDATE,D8                * Use Discharge date
.
          MATCH     " ",PTFXCEFR            * No Contract Effective date set
          IF        @EQUAL
            MATCH     SP8,DDATE             * No discharge date?
            GOTO      GTEDRG40 IF EQUAL     * Check System State Default
          ENDIF
.
          MATCH     "0",PTFXCEFR            * As at the effective date
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "1",PTFXCEFR            * For Admissions From
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "2",PTFXCEFR            * For Discharges From
          IF        @EQUAL
            MOVE      DDATE,D8              * Discharge date
.
            MATCH     SP8,DDATE             * No discharge date?
            GOTO      GTEDRG40 IF EQUAL     * Check System State Default
          ENDIF
.
          MATCH     SP3,PMVXMHOS           * No Hospital
          GOTO      GTEDRG20 IF EQUAL
.
.         Check Health Fund
.
          PACK      KEY42,FOUR,PMVXMHOS,HCODE,Z70
          CALL      RSPMEDG2
GTEDRG11  CALL      RPPMEDG2
          BRANCH    OVRCD,GTEDRG18
.
          MATCH     "4",PMEGRTYP       * Record Type = Hospital / Health Fund ?
          GOTO      GTEDRG18 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY6        * Extract Hospital ID & HF Code
.
          MATCH     D3,PMVXMHOS             * Same Hospital?
          GOTO      GTEDRG18 IF NOT EQUAL
.
          MATCH     KEY6,HCODE         * Same Health Fund Code?
          GOTO      GTEDRG18 IF NOT EQUAL
.
          MATCH     PMEGFDAT,D8          
          GOTO      GTEDRG11 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GTEDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     D8,PMEGTDAT
          GOTO      GTEDRG11 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GTEDRG90           * Got DRG we want
.
GTEDRG18  MATCH     SP70,PMVXMHOS
          GOTO      GTEDRG30 IF EQUAL  * try Claim code
.
.         Check Health Fund with NO Hospital
.
GTEDRG20  MOVE      SP70,D3
          PACK      KEY42,ONE,D3,HCODE,Z70
          CALL      RSPMEDG2
GTEDRG21  CALL      RPPMEDG2
          BRANCH    OVRCD,GTEDRG30
.
          MATCH     "1",PMEGRTYP            * Record Type = Health Fund ?
          GOTO      GTEDRG30 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY6        * Extract Hosptial (blank) & HF Code
.
          MATCH     SP70,D3
          GOTO      GTEDRG30 IF NOT EQUAL
.
          MATCH     KEY6,HCODE         * Same Health Fund Code?
          GOTO      GTEDRG30 IF NOT EQUAL
.
          MATCH     PMEGFDAT,D8          
          GOTO      GTEDRG21 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GTEDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     D8,PMEGTDAT
          GOTO      GTEDRG21 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GTEDRG90           * Got DRG we want
.
.         Check Claim Code - No Hospital
.
GTEDRG30  OPEN      PATCODE1,"patcodes"
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GTEDRG40
.
          OPEN      PATCFAA1,"patcfaaf"
          PACK      KEY3,ACLAIM,SP70
          CALL      RDPTCFA1                * Read a Health Fund Extn file
          IF        OVRCD=1
            MOVE      SP70,PTCFCEFR         * Contracts Effective From
          ENDIF
.
          MATCH     "3",PTCFCEFR            * Not in use
          GOTO      GTEDRG40 IF EQUAL
.
          MOVE      DDATE,D8                * Use Discharge date
.
          MATCH     "0",PTCFCEFR            * As at the effective date
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "1",PTCFCEFR            * For Admissions From
          IF        @EQUAL
            MOVE      ADATE,D8              * Admission date
          ENDIF
.
          MATCH     "2",PTCFCEFR            * For Discharges From
          IF        @EQUAL
            MOVE      DDATE,D8              * Discharge date
          ENDIF
.
          MOVE      SP70,D3
          PACK      KEY42,TWO,D3,ACLAIM,Z70
          CALL      RSPMEDG2
GTEDRG31  CALL      RPPMEDG2
          BRANCH    OVRCD,GTEDRG40
.
          MATCH     "2",PMEGRTYP            * Record Type = Claim Code ?
          GOTO      GTEDRG40 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,KEY3    * Extract Hospital (blank) & Claim Code
.
          MATCH     SP70,D3
          GOTO      GTEDRG40 IF NOT EQUAL
.
          MATCH     KEY3,ACODE              * Same Claim Code?
          GOTO      GTEDRG40 IF NOT EQUAL
.
          MATCH     PMEGFDAT,D8          
          GOTO      GTEDRG31 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GTEDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     D8,PMEGTDAT
          GOTO      GTEDRG31 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GTEDRG90           * Got DRG we want
.
.         Check System State Default (using Hospital state if available)
.
GTEDRG40  IF        IBCNMHOS=0
            MOVE      SP1,PTHSHSST     * Default state blank for non-multi hosp
            GOTO      GTEDRG50
          ENDIF
.
          MATCH     SP3,PMVXMHOS       * No hospital for visit
          IF        @EQUAL
            MOVE      SP1,PTHSHSST     * Default state blank
            GOTO      GTEDRG50
          ENDIF
.
.         Read Hospital file to get PTHSHSST (State for Default DRG)
.
          PACK      KEY3,PMVXMHOS,SP70
          CALL      RDPTHSP1
.
GTEDRG50  MOVE      SP70,D3           * blank hospital
          PACK      KEY42,THREE,D3,PTHSHSST,Z70
          CALL      RSPMEDG2
GTEDRG51  CALL      RPPMEDG2
          BRANCH    OVRCD,GTEDRG60
.
          MATCH     "3",PMEGRTYP       * Record Type = State ?
          GOTO      GTEDRG60 IF NOT EQUAL
.
          UNPACK    PMEGKEYV,D3,D1     * Extract Hospital (blank) & State Code
.
          MATCH     SP70,D3
          GOTO      GTEDRG60 IF NOT EQUAL
.
          MATCH     D1,PTHSHSST        * Same State Code?
          GOTO      GTEDRG60 IF NOT EQUAL
.
          MATCH     SP8,DDATE          * No discharge date?
          GOTO      GTEDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     PMEGFDAT,DDATE
          GOTO      GTEDRG51 IF LESS   * Discharged BEFORE Effective From Date
.
          MATCH     SP70,PMEGTDAT      * Blank Effective To Date?
          GOTO      GTEDRG90 IF EQUAL  * Got DRG we want
.
          MATCH     DDATE,PMEGTDAT
          GOTO      GTEDRG51 IF LESS   * Discharged AFTER Effective To Date
.
          GOTO      GTEDRG90           * Got DRG we want
.
GTEDRG60  MATCH     SP70,PTHSHSST
          GOTO      GTEDRG91 IF EQUAL  * exit
.
          MOVE      SP70,PTHSHSST
          GOTO      GTEDRG50           * Try with blank State
.
GTEDRG90  MOVE      PMEGDRGV,KEY3      * DRG Version
          MOVE      PMEGPYCD,CFPAYCOD  * Codefinder PAY Code
          MOVE      ZERO,EXIT
          GOTO      GTEDRG99
.
GTEDRG91  MOVE      ONE,EXIT
GTEDRG99  RETURN
+
