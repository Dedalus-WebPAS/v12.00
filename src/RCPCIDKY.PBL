.******************************************************************************
.*  Include Name  : RCPCIDKY.PBL                                              *
.*  Description   : Keyin of Cashier codes                                    *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : RCPCASH    - Keyed in code                                *
.*                  RCPSUR    - Description                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications : 01/06/94  ROD    Changed to use CKYIDEF6 not CKYIDEF3     *
.*                                                                            *
.******************************************************************************
RCPCIDKY
RPCDKY00  MATCH     SP3,CKYIDEF6                  * using a default ?
          GOTO      RPCDKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,RCPCASH:
                    *PECOL:EVERT,*DV,RCPCASH
          GOTO      RPCDKY20
.
RPCDKY10  MOVE      CKYIDEF6,RCPCASH                 * default
          KEYIN     *PECOL:EVERT,*DV,RCPCASH:     * default used
                    *PECOL:EVERT,*V2LON,*RV,RCPCASH:
                    *PECOL:EVERT,*DV,RCPCASH
.
.         check what was entered
.
RPCDKY20  CALL      CIDCK000
          BRANCH    EXIT,RPCDKY25,RPCDKY31,RPCDKY41,RPCDKY95
.                        "?"      valid    nothing  exitchar
          GOTO      RPCDKY00
.
.         a "?" was input
.
RPCDKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
RPCDKY26  CALL      RCPCIDDS
          BRANCH    CNEWDS,RPCDKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      RPCDKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
RPCDKY27  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      RPCDKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Cashier Code : ":
                    *P16:24,*DV,UNDLN3:      * no default used
                    *P16:24,*V2LON,RCPCASH:
                    *P16:24,*DV,RCPCASH
          GOTO      RPCDKY29
.
RPCDKY28  MOVE      CKYIDEF6,RCPCASH
          KEYIN     *P1:24,*EL,*DV,"Cashier Code : ":
                    *P16:24,*DV,RCPCASH:     * default used
                    *P16:24,*V2LON,*RV,RCPCASH:
                    *P16:24,*DV,RCPCASH
.
.         repeat the validation of the code
.         check what was entered
.
RPCDKY29  CALL      CIDCK000
          BRANCH    EXIT,RPCDKY26,RPCDKY30,RPCDKY40,RPCDKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      RPCDKY27
.
.         a valid code was entered 
. 
RPCDKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,RCPCASH
.
RPCDKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      RPCDKY99 
.
.         nothing was input
.
RPCDKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,RCPCASH
.
RPCDKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      RPCDKY99 
.
.         EXITCHAR input
.
RPCDKY90  CALL      FRESCR00                * free the screen saved
.
RPCDKY95  MOVE      TWO,EXIT                * EXITCHAR
.
RPCDKY99  RETURN
+
.*********************************************************************
.*                  CIDCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
CIDCK000  ENDSET    RCPCASH
          APPEND    SP6,RCPCASH
          RESET     RCPCASH
.
          MATCH     EXITCHAR,RCPCASH
          GOTO      CIDCK600 IF EQUAL       * exit char
.
          MATCH     SP6,RCPCASH
          GOTO      CIDCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,RCPCASH
          GOTO      CIDCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
CIDCK500  PACK      KEY6,RCPCASH,SP6
          CALL      RDCIDA1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,CIDCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      CIDCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      CIDCK999
.
.         exitchar entered
.
CIDCK600  MOVE      FOUR,EXIT
          MOVE      SP6,RCPCASH
          MOVE      SP20,RCPSUR              * clear desc field
          GOTO      CIDCK999
.
.         nothing entered
.
CIDCK700  BRANCH    CKYIMAND,CIDCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP6,RCPCASH
          MOVE      SP20,RCPSUR              * clear desc field
          GOTO      CIDCK999
.
.         "?" entered
.
CIDCK800  MOVE      ONE,EXIT
          GOTO      CIDCK999
.
.         invalid code entered
.
CIDCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Cashier Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CIDCK999
.
.         field is mandatory
.
CIDCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
CIDCK999  RETURN
.
          INCLUDE   RCPCIDDS
+
