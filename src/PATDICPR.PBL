.------------------------------------------------------------
. File          :  PATDICPR.PBL
.
. Function      :  Update Keyword Index File for ICD9 Disease Code
.
. Parameters    :  DPCODE
.
. Modifications :
.
. V9.09.01  25/07/2007  Mike Laming   CAR 145525
.           Changed "RPPTDI1 RKPTDI1" in CLEAR00 to "RSPTDI1 RKPTDI1"
. V9.02.01  08/08/2002  Pat Dirito
.           Changed from RDICD1 to RD10T9D1 as RDICD1 uses ICD10 parameter
.-----------------------------------------------------------------------------
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDIICD    DIM       xx    * Code Field Segment 1
.  PTDIICD    DIM       xx    * Code Field Segment n
.  PTDIKWD     DIM       15    * Key Word
.  PTDISPA     DIM       10    * Spare
.
.  Index 1   PTDIICD, PTDIICD, PTDIKWD
.  Index 2   PTDIKWD, PTDIICD, PTDIICD
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change DIC to the File   ID eg :%s/DIC/DKI/g
.    Global Change PTDI to the IO Call ID eg :%s/PTDI/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY9 to the Key for the Master File Table eg KEY6 
.    Global Change KEY24 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATDICUP
.
          INC       PATDICFD/INC
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATDICA1,"patdicaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     DPCODE,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     DPCODE,KEY9        * Validate Record on File
          CALL     RD10T9D1           * Read ICD9 Disease File
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     DPCODE,KEYINDEX
          MOVE     DPCODE,KEYWORDS
          CALL     BWORD000
.
          MOVE     DDESC,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    PATDICA1
.
GENR9999  GOTO     PATDICEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RSPTDI1                                            *C-145525
          CALL     RKPTDI1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTDIICD
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,PTDIICD,PTDIKWD,SP70
          CALL     DEPTDI1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,PTDIICD,PTDIKWD
          CALL      RDPTDI1
          IF        OVRCD=1
            CALL      WRPTDI1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATDICIO/INC
          INC       IBAOVRIO/INC
.
PATDICEP  ENDPROC                          * End of Procedure
.
