.******************************************************************************
.*  File          :  PAT105MN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.5 Coding Maintenance Routines for PATWEB88      *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.         V11.02.01 19/07/2022  Bella Turco    TSK 0921957                    *
.                   Added NOMORE00 - Display "No more records" at end of list *
.                   Added PTCNNRTD - Displays number of rows set by parameter *
.                   value                                                     *
.         V10.03.01 22/11/2011  Mike Laming    CAR 253821                     *
.                   Added ICD10EDN variable to pass Edition no. to PATWEB88   *
.         V9.09.00  20/03/2008 Mike Laming     CAR 163918                     *
.                   Created module to cleanup PATWEB88                        *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 5 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD5DI00  RESET     PTICD001
          MOVE      "5",ICD10EDN
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD5DI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD5DI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD5DI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD5DI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VAL5CD00
            GOTO      ICD5DI99
          ENDIF
.
          GOTO      ICD5DI93
.
ICD5DI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD5DI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT15D1
          COMPARE   ZERO,OVRCD
          GOTO      ICD5DI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT15D1      * Writes away the data
          PROC      PATK5DUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD5DI99
.
ICD5DI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT15D1
          BRANCH    OVRCD,ICD5DI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICD5DI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT15D1      * Writes away the data
          PROC      PATK5DUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD5DI99
.
ICD5DI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT15D1
          BRANCH    OVRCD,ICD5DI91
          CALL      DEPT15D1      * Deletes the data
          PROC      PATK5DUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD5DI99
.
ICD5DI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT15D1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 5 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD5DI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD5DI99
.
ICD5DI91  MOVE      "ICD10 Edition 5 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5DI99
.
ICD5DI92  MOVE      "ICD10 Edition 5 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5DI99
.
ICD5DI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5DI99
.
ICD5DI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 5 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD5OP00  RESET     PTICO001
          MOVE      "5",ICD10EDN
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD5OP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD5OP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD5OP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD5OP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VAL5CD00
            GOTO      ICD5OP99
          ENDIF
.
          GOTO      ICD5OP93
.
ICD5OP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD5OP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT15O1
          COMPARE   ZERO,OVRCD
          GOTO      ICD5OP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT15O1      * Writes away the data
          PROC      PATK5OUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICD5OP99
.
ICD5OP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT15O1
          BRANCH    OVRCD,ICD5OP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICD5OP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT15O1      * Writes away the data
          PROC      PATK5OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD5OP99
.
ICD5OP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT15O1
          BRANCH    OVRCD,ICD5OP91
          CALL      DEPT15O1      * Deletes the data
          PROC      PATK5OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD5OP99
.
ICD5OP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT15O1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 5 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD5OP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD5OP99
.
ICD5OP91  MOVE      "ICD10 Edition 5 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5OP99
.
ICD5OP92  MOVE      "ICD10 Edition 5 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5OP99
.
ICD5OP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD5OP99
.
ICD5OP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 5 Code Validation
.------------------------------------------------------------------------------
VAL5CD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VAL5CD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VAL5CD10,VAL5CD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VAL5CD90
.
VAL5CD10  MOVE      VALDCODE,KEY9
          CALL      RDPT15O1
          IF        OVRCD=0
            MOVE      ODESC,VALDNAME
          ENDIF
          GOTO      VAL5CD90
.
VAL5CD20  MOVE      VALDCODE,KEY9
          CALL      RDPT15D1
          IF        OVRCD=0
            MOVE      DDESC,VALDNAME
          ENDIF
          GOTO      VAL5CD90
.
VAL5CD90  CALL      OPENHTML
          BRANCH    EXIT,VAL5CD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VAL5CD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 5 Procedure Code: Search Table
.-----------------------------------------------------------
SPT5OA00  WRITE     HTMLFILE;"<tr align=center>":
                            "<td class=HeadingCell colspan=2>":
                            "<b>You Searched for ":
                             KEYWORDS,"</td>":
                            "</tr>":
                            "<tr>":
                            "<td class=HeadingCell width=15%><b>Code</b></td> ":
                            "<td class=HeadingCell><b>Description</b></td>":
                            "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD   * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT5O2
.
SPT5OA10  CALL      RKPT5O2
          BRANCH    OVRCD,SPT5OA90
.
          CALL      SPT5OB00
          BRANCH    OVRCD,SPT5OA10,SPT5OA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT15O1
          BRANCH    OVRCD,SPT5OA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT5OA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT5OA20,SPT5OA30
          GOTO      SPT5OA99
.
SPT5OA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",ODESC,"</td>":
                             "</tr>"
.
          GOTO      SPT5OA80
.
SPT5OA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",ODESC:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",ODESC,"</td>":
                             "</tr>"
.
SPT5OA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT5OA10 IF NOT EQUAL
          GOTO      SPT5OA99
.
SPT5OA90  CALL      NOMORE00
.
SPT5OA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 5 Procedure Record
.------------------------------------------------------------
SPT5OB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPT5OB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPT5OC00
            BRANCH    EXIT,SPT5OB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT5O2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT5OB91 IF EQUAL
.
          GOTO      SPT5OB90
.
SPT5OB90  MOVE      ZERO,OVRCD
          GOTO      SPT5OB99
SPT5OB91  MOVE      ONE,OVRCD
          GOTO      SPT5OB99
SPT5OB92  MOVE      TWO,OVRCD
SPT5OB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 5 Procedure Words
.------------------------------------------------------------
SPT5OC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPT5OC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT5OC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPT5O1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT5OC10 IF EQUAL
.
          CALL      RKPT5O1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT5OC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT5OC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT5OC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT5OC10              * Check Next Search Word
.
SPT5OC90  MOVE      ONE,EXIT
SPT5OC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 5 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPT5DA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD     * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT5D2
.
SPT5DA10  CALL      RKPT5D2
          BRANCH    OVRCD,SPT5DA90
.
          CALL      SPT5DB00
          BRANCH    OVRCD,SPT5DA10,SPT5DA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT15D1
          BRANCH    OVRCD,SPT5DA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT5DA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT5DA20,SPT5DA30
          GOTO      SPT5DA99
.
SPT5DA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",DDESC,"</td>":
                             "</tr>"
.
          GOTO      SPT5DA80
.
SPT5DA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",DDESC:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",DDESC,"</td>":
                             "</tr>"
.
SPT5DA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT5DA10 IF NOT EQUAL
          GOTO      SPT5DA99
.
SPT5DA90  CALL      NOMORE00              * Display No More Records Message
.
SPT5DA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 5 Diagnosis Record
.------------------------------------------------------------
SPT5DB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPT5DB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPT5DC00
            BRANCH    EXIT,SPT5DB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT5D2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT5DB91 IF EQUAL
.
          GOTO      SPT5DB90
.
SPT5DB90  MOVE      ZERO,OVRCD
          GOTO      SPT5DB99
SPT5DB91  MOVE      ONE,OVRCD
          GOTO      SPT5DB99
SPT5DB92  MOVE      TWO,OVRCD
SPT5DB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 5 Diagnosis Words
.------------------------------------------------------------
SPT5DC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPT5DC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT5DC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPT5D1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT5DC10 IF EQUAL
.
          CALL      RKPT5D1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT5DC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT5DC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT5DC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT5DC10              * Check Next Search Word
.
SPT5DC90  MOVE      ONE,EXIT
SPT5DC99  RETURN
