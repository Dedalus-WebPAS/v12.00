.------------------------------------------------------------------------------
. Include       : EMRTHSCD
.
. Function      : Emergency Telehealth Services (EMRTHSFD) routines
.
. Modifications :
.------------------------------------------------------------------------------
. V10.14.01  16/04/2019  Don Nguyen    TSK 0821139
.            Modified code to cater for new Status value '4' - Exited ETS.
. V10.14.00  19/02/2019  Don Nguyen    TSK 0821139
.            Created new
.------------------------------------------------------------------------------
.         AUTS0000  Add/Update Telehealth Services record
.------------------------------------------------------------------------------
AUTS0000  MATCH     SP70,DEMVIADM
          GOTO      AUTS9100 IF EQUAL
.
          MATCH     SP70,SAVELOCN           * Saved location exists?
          GOTO      AUTS1000 IF EQUAL       * Blank; add new Telehealth Service
.
.
.         Existing ED visit...
.
          MATCH     SAVELOCN,NEWLOCAT       * No change in Location code
          GOTO      AUTS9000 IF EQUAL
.
          PACK      KEY3,SAVELOCN,SP70      * Get CURRENT Location
          CALL      RDEMLOC1
          BRANCH    OVRCD,AUTS9100
.
          MATCH     ANSE,EMLOTYPE           * Type Telehealth Service?
          IF        @EQUAL
.
.           CURRENT Location is of type Telehealth Service
.
.
.           Get the last Telehealth Service record for this admission...
            PACK      KEY11,DEMVIADM,Z70
            CALL      RSEMTHS1
            CALL      RPEMTHS1
            BRANCH    OVRCD,AUTS9100
.
            MATCH     EMTSVISN,DEMVIADM     * Matching Visit Number?
            GOTO      AUTS9100 IF NOT EQUAL
.
            PACK      KEY8,CCC,CYY,CMM,CDD
            REP       " 0",KEY8
            MOVE      KEY8,EMTSUDAT         * Updated date
            CLOCK     TIME,EMTSUTIM         * Updated time
            MOVE      USERID,EMTSUUID       * Updated WEB User ID
.
.
.           Check if NEW Location is of type Telehealth Service...
.
            PACK      KEY3,NEWLOCAT,SP70
            CALL      RDEMLOC1
            BRANCH    OVRCD,AUTS9100
.
.
.           Set current Status to 'Exited ETS' for both Telehealth and
.           Non-Telehealth Service.
.
            MOVE      "4",EMTSSTAT   * Set Status to 'Exited ETS'
            CALL      UPEMTHS1
.
            MATCH     ANSE,EMLOTYPE      * New Location is Telehealth Service
            GOTO      AUTS1000 IF EQUAL  * Add NEW Telehealth record
          ELSE
.
.           CURRENT Location is NOT type Telehealth Service
.
            GOTO      AUTS1000              * Add new Telehealth Service record
          ENDIF
.
          GOTO      AUTS9000                * Finished Update
.
.
.         New ED visit...
.
AUTS1000  PACK      KEY3,EMVILOCN,SP70      * Get NEW location
          CALL      RDEMLOC1
          BRANCH    OVRCD,AUTS9100
.
.         Check if NEW location is Telehealth Service...
.
          MATCH     ANSE,EMLOTYPE           * New Location Telehealth Service?
          GOTO      AUTS9000 IF NOT EQUAL   * No; bail
.
          PACK      KEY11,DEMVIADM,Z70
          CALL      RSEMTHS1
          CALL      RPEMTHS1
          BRANCH    OVRCD,AUTS2000
. 
          MATCH     EMTSVISN,DEMVIADM
          GOTO      AUTS2000 IF NOT EQUAL
. 
.         Record with same visit number already exists; get the next counter
          MOVE      ZERO,F3
          MOVE      EMTSCNTR,F3
          COMPARE   ZERO,F3
          GOTO      AUTS2000 IF EQUAL
.
          CALL      CLEMRETS           * clear the file vars
.
          ADD       ONE,F3
          MOVE      F3,EMTSCNTR        * assign new counter
          GOTO      AUTS8000           * now assign other values
.
.         Start counter at one...
AUTS2000  CALL      CLEMRETS           * clear the file vars
          MOVE      ONE,EMTSCNTR
.
.         Use the next assigned counter...
AUTS8000  MOVE      DEMVIADM,EMTSVISN
          MOVE      EMVISITE,EMTSSITE
          MOVE      PMVXMHOS,EMTSHOSP
          MOVE      EMVILOCN,EMTSLOCN
.
          MOVE      ONE,EMTSSTAT       * Current Emergency Patient
          IF        EMVISTAT = 2 | EMVISTAT = 3
            MOVE      TWO,EMTSSTAT     * Discharged
          ELSE
            IF        EMVISTAT = 4
              MOVE      THREE,EMTSSTAT * Cancelled
            ENDIF
          ENDIF
.
          RJUSTIFY  EMTSCNTR           * right justify counter
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,EMTSCDAT      * Created date
          CLOCK     TIME,EMTSCTIM      * Created time
          MOVE      USERID,EMTSCUID    * Created WEB User ID
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WREMTHS1
          TRAPCLR   IO
          BRANCH    OVRCD,AUTS9100   
.
AUTS9000  MOVE      ZERO,EXIT          * Finish success
          GOTO      AUTS9999
.
AUTS9100  MOVE      ONE,EXIT           * Error
AUTS9999  RETURN
+
