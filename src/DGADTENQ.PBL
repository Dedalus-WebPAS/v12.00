.**************************************************************************
.*  DGADTENQ  :  ADT Enquiry for a Datagate request                       *
.*               ~~~~~~~~~~~                                              *
.*                                                                        *
.*  Author    :  ROD   (30/01/96)                                         *
.*                                                                        *
.*  Mods      :                                                           *
.*            V12.00.01 15/05/2025 Nikitha B     TSK 0955096              *
.*                     Alphanumeric changes                               *
.*            V9.02.02 14/11/2002  Davin                                  *
.*                     Removed reference to $$DBASE WEB compile option    *
.*            V9.02.01 26/09/2001 (22/06/2001)  Mike Laming               *
.*                     Add fields from Visit Extn.1 - PMSVX1 Data fields  *
.*                     PMVXVLOC -> PMVXUDT6 (for RCH & SVHM) using        *
.*                     $$DBASE compiler optn for WEB based vers only      *
.*            V5.07.02 29/11/1999  Davin                                  *
.*                     Changed for 4 character drgs                       *
.*            V5.07.01 22/12/1998  Davin                                  *
.*                     Y2K mods                                           *
.*            V5.07.00 21/10/1998  Davin                                  *
.*                     Mods for 507 changes                               *
.*            V5.04.01 17/04/1997  Steve Armstrong                        *
.*                     Mods to use inbound & outbound comms client        *
.*                     (DGATEIN & DGATEOUT).                              *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTENQ
 IFGE     $$VER,7
.
          INC       PATDADFD/INC
          INC       PATAXEFD/INC
+
.**************************************************************************
.*  DGENQ000  :  process a datagate Visit Enquiry message                 *
.**************************************************************************
DGENQ000  CALL      VENIN000                      * initialisation
.
          MOVE      "VEN",RPLYHEAD
          READ      DGATEOUT;AADMNO
.
          MATCH     ZEROVISN,AADMNO               * do we have a U/R Number?
          GOTO      DGENQ850 IF EQUAL             * error
.
. now get visit details for this patient
.
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,DGENQ800                * missing admission details
          CALL      RDPTVIS1                * Read Visit master using Admiss.No
          BRANCH    OVRCD,DGENQ805
          CALL      RDPMVX11                * Read Visit Extn.1 using Admiss.No
          BRANCH    OVRCD,DGENQ805
.
. if discharged get discharge details
.
          IF        ASTAT=3
            MOVE      AADMNO,KEY8
            CALL      RDDSCH1
            BRANCH    OVRCD,DGENQ810              * missing discharge details
          ENDIF
.
. get admission/discharge transfer source
.
          UNPACK    SP10,PTDAADTS,PTDADCTS        * clear admit/disch trans srce
          MOVE      AADMNO,KEY8
          CALL      RDPTDAD1
.
. write successful reply message
.
          MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
.
. read the rest of the incoming message (admission details)
.
         PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
         WRITE     DGATEIN;CCENCODE:
                          RPLYHEAD,RPLYCODE,RPLYDESC:
                          DGMISC1,DGMISC2,DGMISC3:
                          AADMNO,AURNO,ADATE,ATIME,AWARD,ABED,ADOCTR:
                          ADOCTA,ADOCTL,ASRCE,ATYPE,ACLSS,ACARE,AFUNDH,AFNDTB:
                          AFNDMM,AMBS,ADIAG1,ADIAG2,ADOCTT,ACLSSFT,ACLAIM,ADIET:
                          APLOCCR,ASTAY,ADYHOSP,AMEMB,AMEMBNO,AUSR1,AUSR2:
                          AUSR3,AUSR4,AUSR5,AUSR6,AUSR7,ARDRNAM,AFNDYY,AFNDCG:
                          PTYPE,PTMXCARD,PTMXCEXP,PTMXCXMP,PTMXFMLY,PTMIPVIS:
                          ABWGHT,PMVXRHC1,PMVXRH1G,PTMSFHAN,PTMSECRA,PTMSMGIN:
                          PTAXGUTY,PTAXGUIN,PTAXGUIT,PTAXGUNM,PTAXGUA1,PTAXGUA2:
                          PTAXGUA3,PTAXGUA4,PTAXGUPC,PTAXGUPH,PTAXGUOC,PTAXGUEC:
                          PTAXGURL,PTMIDOFR,PMVXRHC1,PTMIREFD,PTMIDFCN,PTMIPHPU:
                          PTMIAGNC,PTMIESSF,PTMIUSR8,PTMIUSR9,PTMIUSR0,PTMIPDRG:
                          PTDAADTS:
. write discharge details
                          DDATE,DTIME,DSTAT,DDEST,DDIAG:
                          DDIAG2,DUSD1,DUSD2,DUSD3,DUSD4,DUSD5,DFMBS,PTDSDMDC:
                          PTDSDDRG,DWLREASN,PTDSSIDX,PTDSVOGU:
                          PTDSOPER,PTDADCTS:
                          PMVXVLOC,PMVXCFSG,PMVXINGP,PMVXCPTH,PMVXHCP1:
                          PMVXHCP2,PMVXHCP3,PMVXHCP4,PMVXRHC1,PMVXRHC2:
                          PMVXRHC3,PMVXRHC4,PMVXCADM,PMVXIRAD,PMVXIDUS:
                          PMVXCRAV,PMVXRCCT,PMVXAPWD,PMVXAPBD,PMVXPOWD:
                          PMVXPOBD,PMVXPSTY,PMVXVALW,PMVXPALW,PMVXINDC:
                          PMVXMDAV,PMVXFRMS,PMVXEDC1,PMVXEDC2,PMVXEDC3:
                          PMVXUDF1,PMVXUDF2,PMVXUDF3,PMVXUDF4,PMVXUDT1:
                          PMVXUDT2,PMVXUDT3,PMVXUDT4,PMVXUDT5,PMVXUDT6	
.
          ADD       ONE,SUCCPROC               * increment # successful messages
          GOTO      DGENQ999
.
. now set up reply error messages
.
DGENQ800  MOVE      "02100",RPLYCODE
          MOVE      "Missing admission record",RPLYDESC
          GOTO      DGENQ900
.
. ----------------------------------------- Addition for Visit Extn.2      I900
DGENQ805  MOVE      "02100",RPLYCODE                                     * I900
          MOVE      "Missing visit record",RPLYDESC                      * I900
          GOTO      DGENQ900                                             * I900
.
DGENQ810  MOVE      "02105",RPLYCODE
          MOVE      "Missing discharge record",RPLYDESC
          GOTO      DGENQ900
.
DGENQ850  MOVE      "02122",RPLYCODE
          MOVE      "Missing visit no. in message",RPLYDESC
          GOTO      DGENQ900
.
. write reply error message
.
DGENQ900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3
          ADD       ONE,ERRRPROC               * increment # error messages
.
DGENQ999  GOTO      ENDPROC1
+
.************************************************************************
.*  VENIN000  :  Initialisation                                         *
.************************************************************************
VENIN000  CALL      CLPATMIS                      * clear admission details
          CALL      CLPATDSC                      * clear discharge details
.
          OPEN      PATDADA1,"patdadaf"
          IF        PTCNAXEU=1
            OPEN      PATAXEA1,"pataxeaf"
          ENDIF
.
VENIN999  RETURN
.
.
OVERCOND  MOVE      ONE,OVRCD
          RETURN
.
          INC       CLPATMIS
          INC       CLPATDSC
          INC       PATDADIO/INC
          INC       PATAXEIO/INC
.
 XIF
ENDPROC1  ENDPROC
