.
.*************************************************************************
.* PATHEAD.PBL   : Include to display the standard 3 line header for a   *
.*                 given admission record.                               *
.*                 This include is calls PMIHEAD to print the standard   *
.*                 2 line PMI heading.                                   *
.* DATA FILES    : PATMA1FD    PMI master file                           *
.*                 PATMI1FD    Admission file                            *
.*                 PATDSCFD    Discharges file (for discharge date)      *
.*                 PATDO1FD    Patient doctors file                      *
.*                 PATNOBFD    No Bed file                               *
.*                 PATWR1FD    Ward/Bed file                             *
.*                 PATDHEAD    Special variables for this include        *
.* PARAMETERS    : CCNOTES     Case Notes Indicator (System Parameter)   *
.*                 A PATMA1FD record                                     *
.*                 A PATMI1FD record                                     *
.*                 DDATE       Discharge date if discharged              *
.* OPEN FILES    : PATDO1A1    Patient doctors file                      *
.*                 PATNOBE1    No Bed file                               *
.*                 PATWR1A4    Ward/Bed file                             *
.*************************************************************************
. MODS:
.-------------------------------------------------------------------------
. V12.00.01 14/05/2025 Thanh T         TSK 0955096
.           Changed for alphanumeric visit number
.-------------------------------------------------------------------------
.           08/10/1998  Glenn Berry      5.07 changes
.-------------------------------------------------------------------------
.
. -----   Initialise display variables  -----
.
PATHEAD   MOVE      SP30,PATHDADM
          MOVE      SP30,PATHDDOC
.
. -----  format doctors name -----
.
PATHD200  MOVE      ADOCTA,KEY6                    
          CALL      RDDOCT1
          BRANCH    OVRCD,PATHD700
.
          MOVE      DSNAME,PATHDDOC                                      
          ENDSET    PATHDDOC
          CALL      PATHD250                       * get rid of spaces
          LENSET    PATHDDOC
          APPEND    ", ",PATHDDOC
          APPEND    DGNAME,PATHDDOC
          RESET     PATHDDOC
          GOTO      PATHD700
.
PATHD250  CMATCH    SP1,PATHDDOC                    * get rid of spaces
          RETURN    IF NOT EQUAL                   * in doctors desc
          BUMP      PATHDDOC BY -1
          RETURN    IF EOS
          GOTO      PATHD250
.
. ----- get ward description -----
.
PATHD700  COMPARE   TWO,ASTAT
          GOTO      PATHD790 IF NOT EQUAL
.
PATHD710  MATCH     SP3,AWARD
          GOTO      PATHD730 IF NOT EQUAL    
.
          PACK      KEY14,AADMNO,SP6                
          CALL      RDSWARD4
          CALL      RDKWARD4
          BRANCH    OVRCD,PATHD790
.
          MATCH     AADMNO,WSTBY
          GOTO      PATHD790 IF NOT EQUAL
.
          MOVE      WWARD,PATHDWRD
          MOVE      WBED,PATHDBED
          GOTO      PATHD795
.
PATHD730  MATCH     SP3,ABED
          GOTO      PATHD750 IF NOT EQUAL    
.
          MOVE      APLUR,NBPLUR
          PACK      KEY13,AWARD,AADMNO,NBPLUR
          CALL      RDNOBE1
          BRANCH    OVRCD,PATHD790
.
          MOVE      NBWARD,PATHDWRD
          MOVE      NBROOM,PATHDBED
          GOTO      PATHD795
.
PATHD750  MOVE      AWARD,PATHDWRD
          MOVE      ABED,PATHDBED
          GOTO      PATHD795
.
PATHD790  MOVE      SP7,PATHDWB
          GOTO      PATHD800
.
PATHD795  MATCH     SP3,PATHDBED
          GOTO      PATHD797 IF EQUAL
.
          PACK      PATHDWB,PATHDWRD,SLASH,PATHDBED
          GOTO      PATHD800
.
PATHD797  PACK      PATHDWB,PATHDWRD,SP4
.
. ----- format admission status -----
.
PATHD800  BRANCH    ASTAT,PATHD810,PATHD820,PATHD830,PATHD840,PATHD850
.
PATHD810  MOVE      "Patient is Pre-admitted",PATHDADM
          GOTO      PATHD900
.
PATHD820  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CLEAR     PATHDADM
          APPEND    "Admitted on ",PATHDADM
          APPEND    CPCDATE,PATHDADM
          RESET     PATHDADM
          GOTO      PATHD900
.
PATHD830  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CLEAR     PATHDADM
          APPEND    "Discharged on ",PATHDADM
          APPEND    CPCDATE,PATHDADM
          RESET     PATHDADM
          GOTO      PATHD900
.
PATHD835  MOVE      "Patient is Discharged",PATHDADM
          GOTO      PATHD900
.
PATHD840  MOVE      "Patient is On-leave",PATHDADM
          GOTO      PATHD900
.
PATHD850  MOVE      "Patient is Cancelled",PATHDADM
.
PATHD900  CALL      PMIHEAD                  * Display PMI details
          DISPLAY   *P1:5,"Admission No : ",*V2LON,AADMNO:
                    *P1:6,PATHDADM,*HOFF:
                    *P26:6,"Ward : ",*V2LON,PATHDWB,*HOFF:
                    *P42:6,"Doct : ",*V2LON,PATHDDOC
.
PATHD999  RETURN
