+
.         OUTINVS.PBL
.         -----------
.
.         This include controls the printing of invoices for outpatients.
.         All invoice layouts are controlled through this include, the
.         code that is invoice layout dependent being located in the
.         include PATINVTn, where "n" if the invoice layout.
.
.         MODIFICATIONS:
.
.------------------------------------------------------------------------------
. V12.00.01 26/05/2025  J.Tan          TSK 0955096
.           Added Alphanumeric visit number changes
. V11.05.02 21/02/2025  J.Tan   TSK 0955356
.           Added FININVN,FINADMN,FINURNO
. V11.05.01  29/11/2024  Thanh T       TSK 0939466
.            Added Distance in kms field
.------------------------------------------------------------------------------
.         V11.02.01 03/03/2022  J.Tan           TSK 0902652
.                   Mod for Patient Invoice new functionality (PTCNPPIN=1)
.         V11.01.01 24/03/2021  Ebon Clements     TSK 0904373
.                   Fixed close bold tag SCRDISP             
.         V11.00.02 04/06/2020  J.Tan             TSK 0886178
.                   Mod not to apply deposit to ABF Invoice
.         V11.00.01 02/04/2020  J.Tan             TSK 0868813
.                   Changed PKNAME to DIM80
.         V10.15.01 09/10/2019  J.Tan          TSK 0857392
.                   Mod for ABF checking for Indic1=A for Tier code
.         V10.14.02 26/09/2019  J.Tan          TSK 0857392
.                   Mod for ABF Outpatient invoice
.         V10.14.01 21/03/2019  J.Tan            TSK 0857392
.                   Changed RCP Transaction count from DIM3 to DIM5
.         V10.12.01 27/04/2018  J.Tan          Task 0845996
.                   Mod to update CMDEUDAT,CMDEUTIM and CMDEUUID
.         V10.11.01 25/08/2017  Thanh T.    TSK 0821710
.                   HASCMNTS changed.
.         V10.07.02 21/12/2015  J.Tan       CAR 0303942
.                   Mods to setup Payment code
.         V10.07.01 29/10/2015  Ebon Clements  CAR 268970
.                   Change to use OUTCLIFD index 1 instead of index 2
.         V10.04.02 01/05/2014  J.Tan        CAR 261630
.                   Removed patauc Cash audit file
.         V10.04.01 27/02/2014 J.Tan         CAR 275810
.                   Mods to check Defence force claim
.         V10.03.01 21/03/2012 J.Tan         CAR 262115
.                   Mods to display/print Quantity
.         V10.02.01 21/04/2011 J.Tan           CAR 237245
.                   Mods for Johns Hopkins Insurance Providers
.         V10.01.03 23/03/2011 Sandra Barcham  CAR 239851
.                   Fix visit date in O/P Original invoice Header
.         V10.01.02 13/01/2011  Mike Laming    CAR 233084
.                   Added new include CLPATINV (Added PTINCNID) in ZEROINV
.         V10.01.01 06/01/2011  Jill Parkinson CAR 233088
.                   Added use of PMVXFNDM
.         V10.00.01 19/04/2010  J.Tan        CAR 219670
.                   Mods to set date/time updated to rcpdteaf file
.         V9.12.01  05/10/2009  J.Tan        CAR 191624
.                   Mods to set the Deposit Applied date to Current date
.         V9.10.02  23/05/2008 J.Tan      CAR 169149
.                   Added Date and Time invoice created/updated
.         V9.10.01  05/05/2008 J.Tan      CAR 162313
.                   Added Deposit status to comdepaf file
.         V9.08.08  02/05/2007  J.Tan          CAR 132276
.                   Mods for SX Outpatient invoice
.         V9.08.07  16/03/2007  J.Tan          CAR 135853
.                   Mods different Name sequence for JH 
.         V9.08.06  22/02/2007 Mike Laming   CAR 119436
.                   Add PROC FLAGDEBT (PMSOSDTM) for Outstanding Debt Alert ind.
.         V9.08.05  11/01/2007  Mike Laming   CAR 103368
.                   Add PTCNUCCP and routine CCPS0000 for CCPS EDI
.         V9.08.04  11/12/2006  J.Tan          CAR 127646
.                   Added Reference column to JH Details invoice
.         V9.08.03  05/12/2006  J.Tan         CAR 127092
.                   Mods SELF PRFA for JH Invoices
.         V9.08.02  01/12/2006  J.Tan         CAR 126763
.                   Added Quantity column for JH Invoice
.         V9.08.01  14/11/2006 Peter Vela CAR 124547
.                   Added functionality for PRFA of Deceased Patient
.         V9.07.01  31/07/2006 J.Tan      CAR 103365
.                   Mods for John hopkins invoice
.         V9.04.06  25/10/2005 Sandra Barcham  81362
.                   Fixed incorrect packing of fincode for deposits
.         V9.04.05  30/12/2004 J.Tan  CAR 55977
.                   Mods initialise adate/ddate 
.         V9.04.04  06/12/2004  Lina Vo CAR 54561
.                   Opened controlf before read of PTCNMHOS to fix I*C 
.         V9.04.03  06/10/2004  J.Tan   CAR 53798
.                   Removed PATMSCDS
.         V9.04.02  23/09/2004 Lina Vo CAR 53660
.                   Changed for Multi Hospital to use PTHSTFEE instead 
.                   of PTCNTFEE
.         V9.04.01  20/08/2004 J.Tan  CAR 52835
.                   Setup hospital ID for patfinsf
.         V9.03.12  12/08/2004 J.Tan  CAR 52504
.                   Fixed pending cash
.         V9.03.11  30/06/2004 J.Tan  CAR 51112
.                   Mods to checked for nonbanked deposit when printing invoice
.         V9.03.10  11/06/2004 J.Tan  CAR 50035
.                   Mods to set mechanical vent var from pmsmtiaf
.         V9.03.09  01/06/2004 J.Tan  CAR 50273
.                   Fixed invoice date for rcpdteaf file
.                   Exclude cancelled non-bank deposit
.         V9.03.08  27/05/2004 J.Tan  CAR 50108
.                   Fixed deposit date
.         V9.03.07  13/05/2004 J.Tan  CAR 49827
.                   Mods to get non-banked deposit
.         V9.03.06  09/01/2004 J.Tan
.                   Fixed setting invoice number for rcpdteaf file
.         V9.03.05  11/12/2003  J.Tan
.                   Replace patcash with comdepaf file
.         V9.03.04  20/10/2003  J.Tan    CAR 44172
.                   Mods to read misc.theatre item file (pmsmitaf)
.         V9.03.03  15/09/2003 Lina Vo   CAR 40497
.                   Changed index and read of patmchgf to include effective date
.         V9.03.02  01/08/2003 J.Tan
.                   Changed the colour of the invoice heading
.         V9.03.01  27/06/2003 J.Tan 
.                   Mods for WEB to check if any invoice to be printed
.         V9.02.10  08/04/2003 J.Tan 
.                   Mods for WEB Billing
.         V9.02.09  27/11/2002  Dean Cramer  
.                   Change CR to CODECR due to same chamge in PATDINVS.
.         V9.02.08  18/10/2002  Dean Cramer
.                   Make so picks up outpatient clinic descpription.
.         V9.02.07  12/10/2002  Dean Cramer
.                   Make so picks up fund fields.             
.         V9.02.06  25/06/2002  Dean Cramer
.                   Added GETOUTX - extra outpatient data   
.         V9.02.05  07/04/2002  J.Tan
.                   Added parameter to use theatre fees     
.         V9.02.04  01/03/2002  J.Tan         
.                   Mods to commented out theatre fees
.         V9.02.03  10/02/2002  J.Tan         
.                   Mods not to use theatre fee for St.vincent
.         V9.02.02  29.11.2001  B.G.Ackland   
.                   Remove pi's from Program
.         V9.02.01  17/10/2001  Dean Cramer 
.                   Include tests for web reporting (PGM = "IBARSH")        
.         V5.09.B01 05/01/2001  Greg Horvat
.                   Replaced certain variables with KEY?? variables cause
.                   OUT66 couldnt compile
.         V5.08.04  19/07/2000  Dean Cramer   SRF 6710
.                   Alter key size and pack variables for code size
.                   increase in patmchgf.
.         V5.08.03  05/07/2000  Jill Habasque                                 
.                   Fixed call to GSTB0000 after reading patmchgf             
.         V5.08.02  27/06/2000  J.TaN
.                   Recompiled for PATCALF - to set FGSTFLAG 
.         V5.08.01  06/06/2000  Jill Habasque                                 
.                   Changed from key10 to key14 for the health fund file      
.         V5.08.B03 07/04/2000  Davin
.                   Mods for templating - call templating from layouts
.         V5.08.B02 21/03/2000  Davin
.                   Mods for templating - use PATINVHL for headers
.                                       - use PATINVTL for tails (from layouts)
.         V5.08.B01 22/02/2000  Steve Downing
.                   Mods to initialise GST variables
.         V5.07.01  19/01/1999  Nicole Harrington  507 rebound 096 
.                   Mods to display century on service date
.         V5.07.00  15/10/1998  Jim Stathopoulos
.                   507 Changes
.------------------------------------------------------------------------------
.         V5.04.01  15/07/1996  Greg Horvat       SRF 616143
.                   Added new invoice layout for MBF - layout 17
.
.         V5.01.01  25/08/1989  Graeme Williams
.                   Do not allow posting of GST item by user
.         V5.01.02  20/09/1989  Graeme Williams  SRF 101341, 101345
.                   Put cash into current month when printing invoices
.         V5.01.03  14/02/1990  J.Tan            SRF 102921
.                   Fixed outpatients do not have a daycase indicator
.         V5.01.04  16/05/1990  J.Tan            SRF 104959
.                   Mods to use invoice date for deposit of financial summary
.         V5.01.05  22/05/1990  J.Tan            SRF 104901
.                   Added 8 new theatre classifications
.         V5.01.06  15/01/1991  J.Tan            SRF 107488
.                   Fixed patient and rebate portion when posting cash
.         V5.01.07  12/06/1992  Graeme Williams  SRF 114865
.                   Changes for 9 character miscellaneous items
.         V5.01.08  25/06/1992  i chung          SRF 115282
.                   Initialized PINVLOCK, PINVREB, PINVCANC, PINVPCSH,
.                               PINVCADM, PINVSPAR in routine ZEROINV
.         V5.01.09  05/11/1992  Graeme Williams
.                   Set the OTSERVS variable when inputting misc. items.
.         V5.01.10  23/11/1992    Justin Coates  (HONG KONG changes)
.                   added new layout for HK
.         V5.01.11  02/12/1992  Justin Coates   (HONG KONG)
.                   Added OTCNPZIN for printing zero invoices
.         V5.01.12  08/12/1992  Justin Coates   SRF 119318 
.                   Fix the setting up of AMDATE from OBADATE 
.         V5.01.13  18/12/92 J.Tan   SRF 11119597
.                   Fixed to save the receipt number for printing invoices
.         V5.01.14  07/01/93 Whirlpool              
.                   Dudley Address changes                                 
.         V5.01.15  15/02/93 J.Tan   SRF 120630
.                   Mods to use visit site for invoice range key
.         V5.01.16  10/05/93 J.Tan   SRF 122451
.                   Mods to setup OTSERVS for cash
.         V5.01.17  28/05/93 J.Tan   SRF 120832
.                   Allows multiple items for non-fixed items
.         V5.01.18  18/06/93 Robert Sumsion  Quote 7659  SRF122783 
.                   Allows multiple items for non-fixed items
.         V5.01.19  29/06/93 Robert Sumsion  Quote 7659  SRF122783 
.                   Modified for new format of PATCRA & updating changes
.         V5.01.20  06/07/93 DIG                       SRF  123629 + 123541    
.                   Removed open of PATRE1A1 as it is now assumed it is open. 
.         V5.01.21  19/08/1993  Glenn Berry   Bulletin 27  SRF 117999         
.                   Added 'Parent of' for P.R.A.                             
.         V5.01.22  22/11/1993  Glenn Berry      Hawkes Bay Changes
.                   Added Invoice Layout 13 - Hawkes Bay
.         V5.01.23  28/01/1994  Glenn Berry      Hawkes Bay Changes
.                   Fix invoice number not being printed :                    
.                       MOVE      CNEXTINV,SAVINVNO                          
.         V5.01.24  05/04/1994  Steve Armstrong
.                   Mods for new WP for U/R based comments
.         V5.01.25  26/07/1994  Rob Leonard   Quote 8064
.                   Added Invoice Layout 15 - Taranaki
.         V5.02.01  20/02/1995  Greg Horvat      SRF 134879
.                   Recompiled for PATAUMFD & PATIOAUM - added new variable,
.                   changed to write to this new variable & changed to write
.                   the correct date to the miscell item date
.         V5.02.02  14/03/1995  Greg Horvat      SRF 134573  Quote 8300
.                   Recompiled for PATBNKFD & PATIOBNK - Added PTBFRDTE &
.                   changed to write to this variable
.         V5.03.B0  24/05/1995  Paul Howells     SRF 134644
.                   Check whether using invoice pending file before writing
.
.******************************************************************************
.
+
.        This is the start of the code that controls the printing of the invoice.
INVPRT    MOVE      ONE,HEADTYPE                  * Print "Invoice" mode
          UNPACK    PBDATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,B8DATE
          MOVE      CPCDATE,B10DATE
.
          UNPACK    OBADATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,A8DATE
          MOVE      CPCDATE,A10DATE
          MOVE      OBADATE,ADATE
.
          MOVE      SP8,D8DATE
          MOVE      SP8,DDATE
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PMSVX1A1,"pmsvx1af"
            MOVE      SP70,PMVXMHOS
            PACK      KEY8,OTNUMB
            CALL      RDPMVX11
            OPEN      PATHSPA1,"pathspaf"
            MOVE      SP10,PTHSTFEE
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
          ENDIF
.
          CALL      DISPINV
          RETURN
+
.       This subroutine displays the invoice, and then asks if the invoice
.       is to be printed, have a cash receipt done, have miscellaneous items
.       added, comments added, or change the bed fee.
.
DISPINV   MOVE      TWO,PROGFLAG
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,ABFFLAG
.
          LOAD      IPASS,NUMINVS,ZERO,ONE   * set the current pass number
.
          CALL      INITIVAR          * Initialize any special variables
          CALL      GETOUTX
.
          CALL      SCRDISP           * Display heading for invoice
          CALL      GETCASH           * Get any cash figures from discharge prog.
.         Check if we are generating one or two invoices
.
          MOVE      TWO,PROGFLAG      * set flag to say display invoice
          BRANCH    NUMINVS,DLINE110,DLINE120
.
.         One invoice being generated
.
DLINE110  CALL      OUTTBI            * Display invoice
          GOTO      DLINE800
.
.         Two invoices being generated
.
DLINE120  BRANCH    IPASS,DLINE130,DLINE140
.
DLINE130  DISPLAY   *P1:6,*EL,*P24:6,"P a t i e n t   I n v o i c e";
          CALL      OUTTBI
          GOTO      DLINE800
.
DLINE140  DISPLAY   *P1:6,*EL,*P24:6,"I n s u r a n c e   I n v o i c e";
          CALL      OUTTBI
.
.         Check if the patient has comments
.
DLINE800  OPEN      VISMDTA1,"vismdtaf" 
          PROC      HASCMNTS
.
DLINE999  RETURN
+
.******************************************************************************
.       Subroutine to display screen heading
.******************************************************************************
SCRDISP   CALL      GETPNAM
          LOAD      CVERT,NUMINVS,SIX,SEVEN
.
          BRANCH    PROGFLAG,ENDDISP
          IF        INVTYPE=2
            WRITE      HTMLFILE;"<tr bgcolor=#99FFFF>":
                              "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>":
                               "<td><b> Reference </b></td>":
                               "<td><b> Item </b></td>":
                               "<td><b> Qty</b></td>":
                               "<td align=right><b> Unit $</b></td>":
                               "<td align=right><b> GST Excluded </b></td>":
                               "<td align=right><b> GST Amount </b></td>":
                               "<td align=right><b> Unit Total </b></td>":
                               "<td align=right><b> Amount</b></td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr bgcolor=#99FFFF>":
                               "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>":
                               "<td><b> Item </b></td>":
                               "<td><b> Reference </b></td>":
                               "<td><b> Qty </b></td>":
                               "<td align=right><b> GST Excluded </b></td>":
                               "<td align=right><b> GST Amount </b></td>":
                               "<td align=right><b> Total </b></td>":
                               "</tr>";
          ENDIF
ENDDISP   RETURN
+
.       Get the name of the patient (Title Given-names Surname)
.       (for JH - Title Surname Given-names)
.
GETPNAM   CLEAR     PNAME
          REP       ", ",PTITL
          REP       ", ",PGNAME
          REP       ", ",PSNAME
          REP       ", ",PTMASNAM
          RESET     PGNAME
          RESET     PSNAME
          RESET     PTMASNAM
.
          PACK      DIM6 WITH PTITL,SP2
.
.       Get the first non-blank character of the title
.
FRSTCH    CMATCH    SP1,DIM6
          GOTO      APGNAM IF NOT EQUAL
          BUMP      DIM6
          GOTO      FRSTCH IF NOT EOS
.
.       No title, name will be Given-names Surname
.
          IF        CFEETYP <> 9
            APPEND    PGNAME,PNAME     * original
          ELSE
            APPEND    PTMASNAM,PNAME     * Singapore
          ENDIF
          GOTO      APS1
.
.       Check for last non-blank in title, and append Given names there
.
APGNAM    APPEND    DIM6,PNAME
APG1      CMATCH    SP1,PNAME
          GOTO      APG2 IF NOT EQUAL
          BUMP      PNAME,-1
          GOTO      APG1 IF NOT EOS
.
.       Append given name at this point or Surname for JH
.
APG2      APPEND    SP1,PNAME
          IF        CFEETYP <> 9
            APPEND    PGNAME,PNAME     * original
          ELSE
            APPEND    PTMASNAM,PNAME     * Singapore
          ENDIF
.
.       Search for last non-blank in given names to append surname there
.
APS1      CMATCH    SP1,PNAME
          GOTO      APS2 IF NOT EQUAL
          BUMP      PNAME,-1
          GOTO      APS1 IF NOT EOS
.
.       No given name or title. Name will be just Surname
.
          CLEAR     PNAME
          IF        CFEETYP <> 9
            APPEND    PTMASNAM,PNAME     * original
          ELSE
            APPEND    PGNAME,PNAME     * Singapore
          ENDIF
          GOTO      APSPS
.
.       Append surname at this point or given name for JH
.
APS2      APPEND    SP1,PNAME
          IF        CFEETYP <> 9
            APPEND    PTMASNAM,PNAME     * original
          ELSE
            APPEND    PGNAME,PNAME     * Singapore
          ENDIF
.
.       Append spaces to make sure there are no characters leftover from
.       previous names.
.
APSPS     APPEND    SP30,PNAME
          APPEND    SP30,PNAME
          RESET     PNAME
          RETURN
+
.******************************************************************************
.        This subroutine does the actual printing of an invoice.
.        We are only interested in invoices that have a non-zero
.        gross total.
.******************************************************************************
PRNTINV  CALL      SINVH000                 * check for template header
         BRANCH    EXIT,ENDDISP
         CALL      SINVT000                 * check for template tail
         BRANCH    EXIT,ENDDISP
.
         IF        OTCNPZIN <> ONE
           COMPARE   ZERO,GROSSTOT
           GOTO      DISPINVE IF EQUAL
         ENDIF
.
.        Print the invoice. This is the entry point that is used for the
.        batches of invoices from IBAOUT20.
.
PRNTINV0  MOVE      THREE,PROGFLAG
          MOVE      ZERO,INVPRTD
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,TOTGST
.
          LOAD      IPASS,NUMINVS,ZERO,ONE   * set the pass indicator
          LOAD      PINVTYP,NUMINVS,ZERO,TWO * 1st invoice will be patient
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PMSVX1A1,"pmsvx1af"
            MOVE      SP70,PMVXMHOS
            PACK      KEY8,OTNUMB
            CALL      RDPMVX11
            OPEN      PATHSPA1,"pathspaf"
            MOVE      SP10,PTHSTFEE
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
          ENDIF
.
.        Get the patients name
.
PRNTOSD   CALL      GETPNAM
.
.        Get any pending cash receipts
.
          CALL      GETCASH
.
         MATCH      "1",PTCNPPIN
         GOTO       NXTINV1 IF EQUAL          * Using new Patient Invoice
.
.        We now check for a special case. If we are printing split invoices
.        and there is no cash, and no patient portion, then only print the
.        second invoice.
.
          IF        CSAMT = 0.00 & IPASS = 1
            MOVE      ONE,PROGFLAG           * calculate invoice flag
            CALL      OUTTBI
            IF        GROSSTOT = 0.00
              MOVE      TWO,IPASS            * start with pass 2 (rebate port.)
              MOVE      ONE,PINVTYP          * insurance invoice
            ENDIF
            MOVE      THREE,PROGFLAG         * print invoice flag
          ENDIF
.
          MOVE      OBAOUTNO,KEY8            * Make sure we read the visit file
          CALL      RDVISA1
.
.        Get the next invoice number and write a zero invoice record
.
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
NXTINV1   OPEN      PATIRNG1,"patirnge"
          OPEN      PATINVR1,"patinvrf"
.
NXTINV2   MOVE      OSTIRNG,KEY3
          CALL      TRIRNG1
          BRANCH    OVRCD,NOIRNGE,RNGEFULL
.
          MOVE      IRNEXT,CNEXTINV
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          COMPARE   ZERO,OVRCD
          GOTO      NXTINV2 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** Generating Invoice ",CNEXTINV," ***";
.
          CALL      ZEROINV
.
          UNPACK    OBADATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,A8DATE
          MOVE      CPCDATE,A10DATE
.
          MOVE      SP8,D8DATE
          CALL      SETUPIV       * Set up any special varaibles
.
.        Set the claim type indicator if this is a claim
.
          MOVE      ZERO,CLAIM
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDSIV
.
.        Check the indicators for a W, M, or V (these are the claims)
.
          MOVE      ZERO,FORM2
SETIVLP   ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      ENDSIV IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     ANSW,ANS
          GOTO      SVISCLM IF EQUAL
          MATCH     ANSM,ANS
          GOTO      SVISCLM IF EQUAL
          MATCH     ANSV,ANS
          GOTO      SVISCLM IF EQUAL
          MATCH     ANSD,ANS
          GOTO      SVISCLM IF EQUAL
          GOTO      SETIVLP
.
.        We have a claim
.
SVISCLM   REP       "W1M2V3D4",ANS
          MOVE      ANS,CLAIM
.
.        Set up the person responsible for the account
.
ENDSIV    RESET     PGNAME
          MOVE      PGNAME,ANS
          PACK      DIM8 WITH SP1,PTITL,SP1,ANS,DOT
.
FRSTCH2   CMATCH    SP1,DIM8
          GOTO      PCKNAM2 IF NOT EQUAL
          BUMP      DIM8
          GOTO      FRSTCH2 IF NOT EOS
.
PCKNAM2   PACK      PFNAME WITH DIM8,PTMASNAM
          REP       ", ",PFNAME
          MOVE      PFNAME,PKNAME
          MOVE      PSUBRB,PKSUBR
          MOVE      PADD1,PKADD1
          MOVE      PADD2,PKADD2
          MOVE      PADD4,PKADD4
          MOVE      PPOST,PKPOST
          MOVE      SP10,PKRELP
.
          COMPARE   "22",PTCNINVL
          GOTO      GPRESP40 IF NOT EQUAL       * Not Johns Hopkins invoice
.
          CALL      JHPRO000                    * Set default PRFA name
          MOVE      PFNAME,PKNAME
.
          MATCH     "PRFA",EXPPAYER
          GOTO      GPRESP40 IF EQUAL
          MATCH     SP70,EXPPAYER
          GOTO      GPRESP40 IF EQUAL
.
          PACK      KEY14,EXPPAYER,ZERO8,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            PACK      PKNAME,HFNAME,SP70
            PACK      PKADD1,HFADD1,SP70
            PACK      PKADD2,HFADD2,SP70
            PACK      PKSUBR,HFADD3,SP70
            PACK      PKADD4,HFADD4,SP70
            PACK      PKPOST,HFPCODE,SP70
            GOTO      GPRESP90
          ENDIF
.
GPRESP40  MOVE      OBAOUTNO,AADMNO
          MOVE      AADMNO,KEY8
.
          CALL      RDRESP1
          BRANCH    OVRCD OF GPRESP95
.
          COMPARE   "22",PTCNINVL
          GOTO      GPRESP50 IF NOT EQUAL
          MATCH     "SELF",PKRELP
          GOTO      GPRESP95 IF EQUAL
.
.---- P.R.A. Record exists, so use it.
.
GPRESP50  MATCH     "1",PTCNDEES             * using deceased PRFA indicator
          GOTO      GPRESP85 IF NOT EQUAL    * no
.
          CMATCH    "$",PKNAME
          IF        @EQUAL
            STRIP     PTCNDDES
            MOVE      PTCNDDES,PFNAME
            UNPACK    PKNAME,DIM2,KEY78      * ignore "$ "
            PACK      PFNAME,PFNAME,SP1,KEY78,SP30
            GOTO      GPRESP95
          ENDIF
.
GPRESP85  COMPARE   ONE,PTCNPAOF             * using 'parent of'?
          GOTO      GPRESP90 IF NOT EQUAL    * no
.
.---- If first character of PKNAME is "@" then replace with "Parent of"
.
          CMATCH    "@",PKNAME
          IF        @EQUAL 
            MOVE      "Parent of ",PFNAME
            UNPACK    PKNAME,DIM2,KEY78      * ignore "@ "
            PACK      PFNAME,PFNAME,KEY78,SP30
            GOTO      GPRESP95
          ENDIF
.
GPRESP90  PACK      PFNAME,PKNAME,SP30
.
GPRESP95  CALL      PRTIHDR
          BRANCH    EXIT,PRNTOSD             * Print the second invoice
.
GPRESP99  RETURN
.
.******************************************************************************
.        Print the invoice heading section
.******************************************************************************
PRTIHDR   MOVE      OBAOUTNO,PINVADM
          MOVE      OBAURNO,PINVUR
          MOVE      TWO,PINVSYS
          MOVE      OBASITE,PINVSITE
          MOVE      CNEXTINV,SAVINVNO
.
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          MOVE      PFNDMM,AFNDMM
          MOVE      PFNDMM,PMVXFNDM
.
          MOVE      OBADATE,ADATE
.
          CALL      HEAD
.
.        Generate the body and tail of the invoice
.
          CALL      OUTTBI
.
.        Update the invoice record with the appropriate details
.
          MOVE      SAVINVNO,KEY8
          CALL      RDINV1
.
          MOVE      OBAOUTNO,PINVADM
          MOVE      OBAURNO,PINVUR
          MOVE      PTMASNAM,PINALPHA
          MOVE      GROSSTOT,PINVAMT
          ADD       TOTGST,PINVAMT
          MOVE      TOTGST,PTINGSTA       * GST Amount
          MOVE      TOTDISC,PTINDISC
          MOVE      ROUNDAMT,PTINROUN
          MOVE      HFMISC,PINVREB
          MOVE      TWO,PINVSYS
          MOVE      OBASITE,PINVSITE
.
.         with the New Patient Invoice functionality, running Rebate Invoice 
.         might possible include Items with Patient portion 
.
          IF         PREBFLAG=1
            IF         PINVREB=GROSSTOT
              MOVE       ONE,PINVTYP          * Insurance Invoice
            ENDIF
          ELSE
            IF         IPATFLAG=2
              MOVE       TWO,PINVTYP          * Patient invoice
            ENDIF
          ENDIF
.
          IF        PRINTABF=1
            MOVE      THREE,PTINCMIX          * ABF invoice
          ENDIF
.
          PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          MOVE      OBACOMP,PINVCLM
          CALL      UPINV1
.
          IF        PRINTABF=1
            CALL      UOAB0000                * Update Inv. no to ABF file
          ENDIF
.
          PROC      FLAGDEBT                * set outstanding debt indicator
.
          IF        CFEETYP=9
            CALL      UIPR0000              * Update Insurance Provider for JH
          ENDIF
.
.         CCPS EDI Extract                                            *I-103368
          CALL      CCPS0000                * extract Invoice data for CCPS
.
.       Do Cash updates, and then remove the patient from the list
.       of patient who will still have invoices pending. Then print invoice.
.
          CALL      POST0000
.
          DISPLAY   *P1:24,*EL:
                    "*** Invoice Generation Completed ***",*W,*P1:24,*EL;
.         CALL      CLSPRINT
.
          MATCH     "1",PTCNPPIN
          GOTO      PRTIH160 IF NOT EQUAL
.
.         If we are printing Patient invoice, do not remove Pending record as
.         we still have Health fund invoice to print
.
          COMPARE   TWO,IPATFLAG
          GOTO      PRTIH900 IF EQUAL
.
          CALL      DELIPEND
          GOTO      PRTIH900           * print New Patient Invoice functionality
.
.         If we are generating multiple invoices, check if we need to generate
.         another invoice.
.
PRTIH160  CALL      DELIPEND
.
          BRANCH    IPASS,PRTIH200,PRTIH900
          GOTO      PRTIH900
.
.         We have generated the first invoice. Now time to generate the second
.
PRTIH200  MOVE      TWO,IPASS                * second pass
          MOVE      ONE,PINVTYP              * insurance invoice
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,TOTGST
.
.         Check if we need to print a form feed between invoices
.
          BRANCH    CINVFF,PRTIH400
          GOTO      PRTIH800
.
PRTIH400  PRINT     *F;                      * formfeed between invoices
PRTIH800  MOVE      ONE,EXIT
          GOTO      PRTIH999
.
PRTIH900  MOVE      ZERO,EXIT
PRTIH999  RETURN
+
.******************************************************************************
.        Invoice is for zero amount, and for zero bed days
.******************************************************************************
DISPINVE  MATCH     "IBARSH",PGM
          GOTO      ENDDISP IF EQUAL
.
          MATCH     "OUTWEB07",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B:
                      "No Invoice Details to be Printed.  ";
            CALL      HITENTER
          ELSE
            MOVE       "No Invoice details to be printed. ",WEBTITLE
            CALL       WEBERR00
            MOVE       ONE,EXIT
          ENDIF
          GOTO      ENDDISP
.
.         Invalid invoice range in the control file
.
NOIRNGE   MATCH     "OUTWEB07",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Invalid Invoice Range in Control file.  ";
            CALL      HITENTER
          ELSE
            MOVE       "Invalid Invoice Range in Controlf file. ",WEBTITLE
            CALL       WEBERR00
            MOVE       ONE,EXIT
          ENDIF
          GOTO      ENDDISP
.
.         No more invoices available in invoice range
.
RNGEFULL  MATCH     "OUTWEB07",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B:
                      "Next Invoice Number Outside of Valid Range.  ";
            CALL      HITENTER
          ELSE
            MOVE       "Next Invoice Number Outside of Valid Range. ",WEBTITLE
            CALL       WEBERR00
            MOVE       ONE,EXIT
          ENDIF
          GOTO      ENDDISP
.
.         Subroutine to get the next transaction number
.
NXTTRAN1  MOVE      OBAOUTNO,TADMNO
          MOVE      OBAOUTNO,KEY8
          CALL      TRVISA1
          MOVE      PVITRAN,OTTRANS
          RETURN
.
.*************************************************************************** 
.* GETOUTX     Subroutine to get Outpatient Extra Information              *
.*************************************************************************** 
GETOUTX   MOVE       SP30,OCADESC
.
          PACK       CFNAME,OSTFILE,FILCLIA1
          OPEN       OUTCLIA1,CFNAME
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     PVIDOCT,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.         CLOSE     OUTCLIA1
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            GOTO      GETOUTX9
          ENDIF
.
GETOUTX9  DISPLAY    *P34:6,*V2LON,OBASITE,*P50:6,OCADESC
          RETURN
.*************************************************************************** 
.* GETCASH     Subroutine to get the total cash amount currently entered   *
.*************************************************************************** 
GETCASH   BRANCH    PRABFINV,CASHLP99           * no deposit for ABF Invoice
.
          OPEN      COMDEPA2,"comdepaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPDTEA6,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          MOVE      ZERO,CSAMT
          MOVE      ZERO,CASHFLAG
          MOVE      OBAOUTNO,AADMNO
          MOVE      AADMNO,DOBAOUTN
          MOVE      AADMNO,DAADMNO
          MOVE      "0",CMDESTAT
          PACK      KEY26,AADMNO,CMDESTAT,SP70
          CALL      RSCMDEP2
CASHLP20  CALL      RKCMDEP2
          BRANCH    OVRCD,CASHLP48
.
          MATCH     DOBAOUTN,CMDEADMN
          GOTO      CASHLP48 IF NOT EQUAL
          MATCH     "0",CMDESTAT
          GOTO      CASHLP48 IF NOT EQUAL
.
          PACK      KEY17,CMDERECN,CMDETCNT
          CALL      RDRCDTE1
          BRANCH    OVRCD,CASHLP20
.
          ADD       RCDTAMNT,CSAMT
          UNPACK    CMDETDAT,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,CSDAY
          MOVE      XMON,CSMON
          MOVE      XYEAR,CSYEAR
          MOVE      XCENT,CSCENT
          MOVE      CMDERECN,CSREPNO
          GOTO      CASHLP20
.
CASHLP48  COMPARE   ZERO,CSAMT
          GOTO      CASHLP50 IF EQUAL
          MOVE      ONE,CASHFLAG
.
.         Check for any non-banked deposits
.
CASHLP50  MOVE      ZERO,NBNKAMNT
          PACK      KEY25,AADMNO,SP70
          CALL      RSRCDTE6
CASHLP60  CALL      RKRCDTE6
          BRANCH    OVRCD,CASHLP99
.
          MATCH     DAADMNO,RCDTVIST
          GOTO      CASHLP99 IF NOT EQUAL
.
          MATCH     SP10,RCDTINVN          * Invoice number must be blank
          GOTO      CASHLP60 IF NOT EQUAL
          MATCH     SP10,RCDTRELD
          GOTO      CASHLP60 IF NOT EQUAL   * has been released
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,CASHLP60 
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      CASHLP60 IF EQUAL
.
          ADD       RCDTAMNT,NBNKAMNT
          GOTO      CASHLP60
.
CASHLP99  RETURN
+
.******************************************************************************
.      Invoice was printed, so post any cash receipts to DTRAN and INVR files
.******************************************************************************
POST0000  BRANCH    PRABFINV,POST9999           * no deposit for ABF Invoice
.
          OPEN      COMDEPA2,"comdepaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPBDTA1,"rcpbdtaf"
          MOVE      OBAOUTNO,AADMNO
          MOVE      AADMNO,DOBAOUTN
.
POST1000  MOVE      "0",CMDESTAT
          PACK      KEY26,DOBAOUTN,CMDESTAT,SP70
          CALL      RSCMDEP2
          CALL      RKCMDEP2
          BRANCH    OVRCD,POST3000
.
          MATCH     DOBAOUTN,CMDEADMN
          GOTO      POST3000 IF NOT EQUAL
.
          MATCH     "0",CMDESTAT              * Check for deposit status
          GOTO      POST3000 IF NOT EQUAL
.
          MOVE      ZERO,RCDTAMNT
          PACK      KEY17,CMDERECN,CMDETCNT
          CALL      RDRCDTE1
          IF        OVRCD=0
            PACK      RCDTINVN,SP4,PINVNO
            PACK      RCDTUDAT,CCC,CYY,CMM,CDD
            REP       " 0",RCDTUDAT           * date updated
            CLOCK     TIME,RCDTUTIM           * time updated
            MOVE      USERID,RCDTUUID         * web user id
            CALL      UPRCDTE1
          ENDIF
.
          MOVE      RCDTAMNT,OTPATAMT
          MOVE      CMDERECN TO OTRECEPT
.
          MOVE      PINVADM,OTNUMB
          MOVE      PINVNO,OTINVNO
.
          CALL      NXTTRAN1                * Get the next transaction number
.
          MOVE      TWO,OTRECTYP            *** RECORD TYPE CASH
          MOVE      ONE,OTINVPRT            *** SET AS ALREADY PRINTED
          MOVE      CMDETDAT,OTDDATE
          MOVE      "PY",OTTYPE
          MOVE      SP30,OTITEMNO
          MOVE      SP3,OTMISGRP
          MOVE      ZERO,OTDTGSTA           * GST Applicable
          MOVE      ZERO,OTDTGSTM           * GST Amount
          MOVE      SP20,OTDTGSTC           * GST Code
          MOVE      "1",OTPAYTYP       ***** set up cash Payment 
.
          MOVE      SP30,OTDDESC
          MOVE      "PATIENT",OTDDESC
          MULT      SEQ,OTPATAMT            **** MAKE IT NEGATIVE
          MOVE      OTPATAMT,OTPATPOR       * Patient portion
          MOVE      ZERO,OTREBPOR           * Rebate portion
          PACK      OTDTEFFD,PINVDTE
          REP       " 0",OTDTEFFD
.
.         Read the first record of Bank details file to get the payment type
.         ie. Cash, Cheque
.
          MOVE      SP70,OTDTPCOD
          OPEN      RCPBDTA1,"rcpbdtaf"
          PACK      KEY15,RCDTRECN,SP70
          CALL      RSRCBDT1
          CALL      RKRCBDT1
          IF        OVRCD=0
            MATCH     RCDTRECN,RCBDRECN      * Same receipt number?
            IF        @EQUAL
              MOVE      RCBDPCOD,OTDTPCOD
            ENDIF
          ENDIF
.
.         Post this cash
.
          MOVE      ZERO,OTSERVS
          MOVE      SP70,OTDTDSKM
          MOVE      SP70,OTSPARE 
.
          PACK      KEY22 WITH OTNUMB,OTINVNO,OTTRANS
          CALL      WRDTRO1
.
          MOVE      "1",CMDESTAT            * set Deposit status to 'Applied'
          PACK      CMDEAINV,SP4,OTINVNO,SP70
.         PACK      CMDEREFD,PINVDTE
          PACK      CMDEREFD,CCC,CYY,CMM,CDD,SP70    * Current date           
          REP       " 0",CMDEREFD           * Date deposit applied            
          CALL      IBACLOCK
          MOVE      CTIMEIS,CMDEREFT        * Time invoice is written to
.
          PACK      CMDEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",CMDEUDAT           * date updated
          CLOCK     TIME,CMDEUTIM           * time updated
          MOVE      USERID,CMDEUUID         * web user id
          CALL      UPCMDEP2
.
          OPEN      PATINVR1,"patinvrf"
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          ADD       OTPATAMT,PINVPATA
.
.         Check if the invoice now balances
.
          MOVE      PINVAMT,TOTAMT       * Invoice amount +
          ADD       PINVPATA,TOTAMT      * Patient payments +
          ADD       PINVHFDA,TOTAMT      * H.F payments +
          ADD       PINVOTHA,TOTAMT      * Other payments +
          ADD       PINVJOUR,TOTAMT      * Journals = amount outstanding
.
          MOVE      "99999999",PINVBLCD  * Assume it doesn't balance
.
          COMPARE   ZERO,TOTAMT          * Does it balance ?
          GOTO      POST2000 IF NOT EQUAL
.
.         Invoice is now balanced. Record the date that this occured
.
          PACK      PINVBLCD,CCC,CYY,CMM,CDD
          REP       " 0",PINVBLCD
.
POST2000  PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
          PROC      FLAGDEBT                * set outstanding debt indicator
.
          OPEN      PMSVX1A1,"pmsvx1af"
          MOVE      SP70,PMVXMHOS
          PACK      KEY8,OTNUMB
          CALL      RDPMVX11
.
          MOVE      PINVNO,FININVN
          MOVE      PINVADM,FINADMN
          MOVE      PINVUR,FINURNO
.
          MOVE      ANSB,FINTYPE
          PACK      FINCODE WITH ANSA,SP20
.         PACK      FINDATE,CCC,CYY,CMM,CDD
          MOVE      PINVDTE,FINDATE
          REP       " 0",FINDATE
          MOVE      RCDTAMNT,FINAMT
          MOVE      TWO,FINSYS
          MOVE      OBASITE,FINSITE
          MOVE      ZERO,FGSTFLAG
          MOVE      PMVXMHOS,FINHOSP
          CALL      PATCALF
.
          GOTO      POST1000
.
.         Check for any non banked deposits (unreleased cash)
.
POST3000  MOVE      ZERO,NBNKAMNT
          PACK      KEY25,DOBAOUTN,SP70
          CALL      RSRCDTE6
POST3200  CALL      RKRCDTE6
          BRANCH    OVRCD,POST4000
.
          MATCH     DOBAOUTN,RCDTVIST
          GOTO      POST4000 IF NOT EQUAL
.
          MATCH     SP10,RCDTINVN          * Invoice number must be blank
          GOTO      POST3200 IF NOT EQUAL
          MATCH     SP10,RCDTRELD
          GOTO      POST3200 IF NOT EQUAL   * has been released
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,POST3200
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      POST3200 IF EQUAL
.
          ADD       RCDTAMNT,NBNKAMNT
          PACK      RCDTINVN,SP4,CNEXTINV   * store the invoice number
          PACK      RCDTUDAT,CCC,CYY,CMM,CDD
          REP       " 0",RCDTUDAT           * date updated
          CLOCK     TIME,RCDTUTIM           * time updated
          MOVE      USERID,RCDTUUID         * web user id
          CALL      UPRCDTE6
          GOTO      POST3200
.
POST4000  OPEN      PATINVR1,"patinvrf"
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,POST9999
          MOVE      NBNKAMNT,PINVPCSH       * pending cash
          MULT      "-1",PINVPCSH
.
          PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
          PROC      FLAGDEBT                * set outstanding debt indicator
POST9999  RETURN
+
.***************************************************************************
.         Subroutine to remove an admisssion number from the file of
.         patients who still have invoices pending.
.***************************************************************************
DELIPEND  COMPARE   ZERO,PTCNIPEN
          GOTO      DELIP999 IF NOT EQUAL
.
.         Check for ABF
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      DELIP400 IF NOT EQUAL   * Not using ABF
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,DELIP400
          MATCH     "A",TCINDC2
          GOTO      DELIP400 IF NOT EQUAL
.
          PACK      KEY5,ANSN,ANSC,OTBBTCOD  * Tier 2 code
          CALL      RDCODE1
          BRANCH    OVRCD,DELIP400
.
          MATCH     "A",TCINDC1
          GOTO      DELIP400 IF NOT EQUAL
.
.         ABF invoice - Check if still Item tobe invoiced
.
          PACK      KEY14,OBAOUTNO,SP20
          CALL      RSPMMTI1
          CALL      RKPMMTI1
          BRANCH    OVRCD,DELIP200
.
          MATCH     PMMIVISN,DOBAOUTN
          GOTO      DELIP999 IF EQUAL       * found item to be invoiced
.
.         No item to be invoiced, check ABF charge
.
DELIP200  CALL      ABFV0000                * Check if ABF Invoice raised
          BRANCH    EXIT,DELIP999           * ABF invoice has not been raised
.
DELIP400  OPEN      PATIPEN1,"patipenf"
          MOVE      OBAOUTNO,IPADMNO
.
          MOVE      IPADMNO,KEY8
          CALL      RDIPEN1
.
          COMPARE   ZERO,OVRCD
          CALL      DEIPEN1 IF EQUAL
.
DELIP999  RETURN
+
.**********************************************************************
.         Update Invoice number to ABF file
.**********************************************************************
UOAB0000  READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      UOAB9999 IF NOT EQUAL   * Not using ABF
.
          OPEN      PMSABFA1,"pmsabfaf"
.
UOAB1000  PACK      KEY19,OBAOUTNO,SP70
          CALL      RSPMABF1
          CALL      RKPMABF1
          BRANCH    OVRCD,UOAB9999
.
          MATCH     DOBAOUTN,PMABADMN       * Same admission no ?
          GOTO      UOAB9999 IF NOT EQUAL
.
          MATCH     SP70,PMABINVN
          GOTO      UOAB9999 IF NOT EQUAL  * Invoice must be blank
.
          MOVE      CNEXTINV,PMABINVN
          CALL      UPPMABF1
          GOTO      UOAB1000
.
UOAB9999  RETURN
+
.***************************************************************************
.         Check if ABF invoice has been raised
.***************************************************************************
ABFV0000  OPEN      PATINVR3,"patinvrf"
          MOVE      PINVADM,OBAOUTNO
          PACK      KEY16,OBAOUTNO,SP70
          CALL      RSPTINV3
ABFV1000  CALL      RKPTINV3
          BRANCH    OVRCD,ABFV8100
.
          MATCH     PINVADM,OBAOUTNO
          GOTO      ABFV8100 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      ABFV1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX         * check for ABF invoice
          GOTO      ABFV8000 IF EQUAL
          GOTO      ABFV1000
.
ABFV8000  MOVE      ZERO,EXIT              * found an ABF Invoice
          GOTO      ABFV9000
.
ABFV8100  MOVE      ONE,EXIT               * ABF Invoice has not been raised
.
ABFV9000  MOVE      CNEXTINV,KEY8
          CALL      RDINV1                 * reposition to the original invoice
ABFV9999  RETURN
+
.***************************************************************************
.         Subroutine to write a zero record to the invoice file
.***************************************************************************
.
ZEROINV   CALL      CLPATINV                * clear Invoice variables
.
          MOVE      CNEXTINV,KEY8
          MOVE      CNEXTINV,PINVNO
.
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
...       MOVE      ZERO,PINVUR
          MOVE      TWO,PINVSYS
.
          MOVE      OBASITE,PINVSITE
.
...       MOVE      "99999999",PINVBLCD
...       MOVE      "0",PTINCRST
.
          CALL      RDINV1
          COMPARE   ZERO,OVRCD
          GOTO      ZERORET IF EQUAL
.
          PACK      PTINCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINCDAT             * date created
          CLOCK     TIME,PTINCTIM             * time created
          MOVE      USERID,PTINCUID           * web user id
          CALL      WRINV1
.
ZERORET   RETURN
+
DISPMORE  DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
          LOAD      CVERT,NUMINVS,SEVEN,EIGHT
          DISPLAY   *P1:CVERT,*EF;
          RETURN
.
.****************************************************************************
.  SINVH000 - Set up invoice header for stationery templating
.****************************************************************************
SINVH000  OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATDET1,"ibatmdaf"
.
          MOVE      OTCNOTIH,KEY6
          CALL      RDIBTH1                   * read header detail file
          BRANCH    OVRCD,SINVH900
.
          PACK      KEY12,OTCNOTIH,SP6        * position on details file
          CALL      RSIBDET1
          CALL      RKIBDET1
          BRANCH    OVRCD,SINVH900
.
          MATCH     OTCNOTIH,IBDTSCOD         * match stationery codes
          GOTO      SINVH950 IF EQUAL
.
.------ stationery code not on file ------
.
SINVH900  MOVE      "User Defined Stationery Code not on File. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
          MOVE      ONE,EXIT
          GOTO      SINVH999
.
SINVH950  MOVE      ZERO,EXIT
SINVH999  RETURN
+
.****************************************************************************
.  SINVT000 - Set up invoice tail for stationery templating
.****************************************************************************
SINVT000  OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATDET1,"ibatmdaf"
.
          MOVE      OTCNOTIT,KEY6
          CALL      RDIBTH1                   * read header detail file
          BRANCH    OVRCD,SINVT900
.
          PACK      KEY12,OTCNOTIT,SP6        * position on details file
          CALL      RSIBDET1
          CALL      RKIBDET1
          BRANCH    OVRCD,SINVT900
.
          MATCH     OTCNOTIT,IBDTSCOD         * match stationery codes
          GOTO      SINVT950 IF EQUAL
.
.------ stationery code not on file ------
.
SINVT900  MOVE       "User Defined Stationery Code not on File. ",WEBTITLE
          CALL       WEBERR00
          MOVE       ONE,EXIT
          MOVE      ONE,EXIT
          GOTO      SINVT999
.
SINVT950  MOVE      ZERO,EXIT
SINVT999  RETURN
+
CVERTDTE  RESET     DIM6
          MOVE      SP10,DIM10
          MATCH     SP8,DIM8
          RETURN    IF EQUAL
.
          UNPACK    DIM8 WITH XCENT,XYEAR,XMON,XDAY
          PACK      DIM10 WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          RETURN
.
.       PNAME IS TOO LONG FOR INPUT RECEIPT DETAILS
.
GETPNAMS  REP       ", ",PTITL
          REP       ", ",PGNAME
          REP       ", ",PSNAME
          REP       ", ",PTMASNAM
.
          MOVE      PGNAME,ANS
          CLEAR     PNAME
          APPEND    ANS,PNAME
          APPEND    DOT,PNAME
          APPEND    PTMASNAM,PNAME
          RESET     PNAME,20
          APPEND    SP30,PNAME
          RESET     PNAME
          RETURN
.
.***************************************************************************
.*                                 GSTA0000            Called by: TADD0000 *
.*                  Keyin GST for an item                                  *
.***************************************************************************
GSTA0000  COMPARE   TWO,PTITGSTA
          IF        @EQUAL
            CALL      KGST0000
          ENDIF
          MOVE      PTITGSTA,GSTAPP  
          MOVE      PTITGSTC,GSTCODE
.
GSTA9999  RETURN
+
.***************************************************************************
.*                                 GSTB0000            Called by: TADD0000 *
.*              Keyin GST for a miscellaneous charge                       *
.***************************************************************************
GSTB0000  COMPARE   TWO,PTMCGSTA
          IF        @EQUAL
            CALL      MGST0000
          ENDIF
          MOVE      PTMCGSTA,GSTAPP
          MOVE      PTMCGSTC,GSTCODE
.
GSTB9999  RETURN
+
.*****************************************************************************
.*                           MVMT0000                                        *
.* Move fields from outdtrof to pmsmtiaf                                     *
.*****************************************************************************
MVMT0000  MOVE      OTNUMB,PMMIVISN
          MOVE      OTTRANS,PMMITRAN
          MOVE      OBAOUTNO,PMMIURNO
          MOVE      " 2",PMMISYST
          MOVE      OTITEMNO,PMMIITEM
          MOVE      OTDDESC,PMMIDESC
          PACK      PMMIDES2,SP70
          MOVE      OTDDATE,PMMITDAT
          MOVE      OTRECTYP,PMMIRTYP
          MOVE      OTPATAMT,PMMIAMTT
          MOVE      OTPATPOR,PMMIAMTP
          MOVE      OTREBPOR,PMMIRBAT
          MOVE      ZERO,PMMIAMTG
          MOVE      OTPATAMT,PMMIAMTH
          UNPACK    SP70,PMMITDOC,PMMIODOC,PMMISESS
          MOVE      OTSERVS,PMMISERV
          MOVE      OTMISGRP,PMMIMGRP
          MOVE      OTBATCHN,PMMIBTCH
          MOVE      OTDTGSTA,PMMIGSTA
          MOVE      OTDTGSTC,PMMIGSTC
          MOVE      OTDTGSTM,PMMIGSTM
          MOVE      PASSCODE,PMMIWUSR
MVMT9999  RETURN
+
.------------------------------------------------------------
.                            CCPS0000
.                     write Invoice data for CCPS Extract
.------------------------------------------------------------
CCPS0000  MOVE      ZERO,EXIT
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND03;*218,PTCNUCCP
.
          MATCH     "1",PTCNUCCP            * "1" is "Using CCPS EDI extract"
          GOTO      CCPS9999 IF NOT EQUAL
.
          OPEN      PATEDTA1,"patedtaf"
....      OPEN      PATVISA1,"patvisaf"
....      OPEN      PATFX1A1,"patfx1af"
....      OPEN      VISPAYA1,"vispayaf"
          PACK      KEY8,OBAOUTNO,SP8
.
.                                           * initialise "patedtaf"
CCPS1000  MOVE      ZERO,PTETFLAG           * 0 = EDI not sent
          MOVE      PTETFLAG,DPTETFLG
          MOVE      "CCPS  ",PTETPAYC
          MOVE      OBAOUTNO,PTETADMN
          MOVE      PINVNO,PTETINVN
          UNPACK    SP30,PTETBATN,PTETPBAT,PTETNBAT
          MOVE      PINVUR,PTETURNO
          MOVE      PINVDTE,PTETINDT
          MOVE      "0",PTETINTY            * 0 = Medical only, 1 = Claim
          MOVE      "0",PTETEXCL            * 0 = not excluded
          MOVE      " 0",PTETCCST           * 0 = not sent
          MOVE      PINVAMT,PTETINAM
          MOVE      PINVSYS,PTETISYS
          UNPACK    SP70,PTETRCAL,PTETSPAR
.
          PACK      KEY14,OBAOUTNO,SP10
          CALL      RSVSPAY1
CCPS2000  CALL      RKVSPAY1
          BRANCH    OVRCD,CCPS9999
.
          MATCH     VSPAVISN,KEY8
          GOTO      CCPS9999 IF NOT EQUAL
.
          PACK      KEY6,VSPAPAYC
          CALL      RDPTFX11
          BRANCH    OVRCD,CCPS2000
.
          COMPARE   ZERO,PTFXEXTR           * is Expected Payor HF for EDI ?
          GOTO      CCPS2000 IF EQUAL       * no, get next one
.
          MOVE      "1",PTETINTY            * set Invoice Type to "Claim"
          GOTO      CCPS5000
.
.         Write CCPS EDI Summary record to "patedtaf"
.
CCPS5000  PACK      KEY26,DPTETFLG,PTETADMN,PTETINVN,SP30
          CALL      RAPTEDT1
          IF        OVRCD = 1
            CALL      WRPTEDT1
          ENDIF
.
CCPS9999  RETURN
.
.............................................................................
.------------------------------------------------------------
.         Set up JH PRFA name
.------------------------------------------------------------
JHPRO000  STRIP     PTITL
          STRIP     PGNAME
.
          MOVE      SP70,PFNAME
          CLEAR     PFNAME
          APPEND    PTITL,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PGNAME,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PTMASNAM,PFNAME
          APPEND    SP70,PFNAME
          RESET     PFNAME
JHPRO999  RETURN
.
.------------------------------------------------------------
.         Update Insurance Provider for JH
.------------------------------------------------------------
UIPR0000  OPEN      PATIPRA1,"patipraf"
          MOVE      PINVADM,KEY8
          PACK      KEY19,KEY8,SP70
          CALL      RSPTIPA1
UIPR1000  CALL      RKPTIPA1
          BRANCH    OVRCD,UIPR9999
.
          MATCH     KEY8,PTIRVISN
          GOTO      UIPR9999 IF NOT EQUAL
.
          MATCH     SP70,PTIRINVN
          GOTO      UIPR9999 IF NOT EQUAL
.
.         set invoice number to insurance providers
.
          MOVE      PINVNO,PTIRINVN
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      RAPTIPA1
          COMPARE   ZERO,OVRCD
          GOTO      UIPR1000 IF EQUAL
.
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      WRPTIPA1
.
          MOVE      SP70,PTIRINVN
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      RDPTIPA1                        * reposition
.
          GOTO      UIPR1000
.
UIPR9999  RETURN
+
