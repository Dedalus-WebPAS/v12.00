.***************************************************************************
.*           PATSTRYR.PBL                                                  *
.*                                                                         *
.* Requires: PATDRGFD.INC & PATDRGA2 open                                  *
.*           PATSTCUR  DIM     4   (save variable for current year (ccyy)  *
.*           CSTRTYR   DIM     8   (save variable for start of year        *
.*                                   date (ccyymmdd))                      *
.* Returns:  EXIT  0 = valid start of year date found                      *
.*                 1 = start of year date not found                        *
.*           CSTRTYR   start of year date (ccyymmdd)                       *
.*                     (space filled if date not found)                    *
.***************************************************************************
.
PATSTRYR  MOVE      SP8,CSTRTYR 
          CALL      IBACLOCK                     * get current date
.
.         Position in the patdrgaf file using the current date
.
          PACK      KEY14,CCC,CYY,CMM,CDD,SP20
          REP       " 0",KEY14
          CALL      RDSDRGA2                     * position in file
          CALL      RDKDRGA2                     * read next record
          BRANCH    OVRCD,PATSYR95               * eof - error
.
.         A record has been found for the current period, so save the current
.         financial year
.
          MOVE      DRGYR,PATSTCUR               * save current financial year
.
.         Keep saving the start date for the current period until the
.         financial year changes
.
PATSYR50  MOVE      DRGFDTE,CSTRTYR              * save start date for period
          CALL      RDPDRGA2                     * read previous record
          BRANCH    OVRCD,PATSYR90               * eof - check CSTRTYR
.
          MATCH     PATSTCUR,DRGYR               * same financial year still ?
          GOTO      PATSYR90 IF NOT EQUAL        * no - finished
.
          GOTO      PATSYR50                     * get next record
.
.         Make sure a start date has been found
.
PATSYR90  MATCH     SP8,CSTRTYR                  * saved year blank ?
          GOTO      PATSYR95 IF EQUAL            * yes - error
.
          MOVE      ZERO,EXIT
          GOTO      PATSYR99
.
.         No valid record was found in patdrgaf
.
PATSYR95  DISPLAY   *P1:24,*EL,*B,"Period Date Range File is incomplete.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PATSYR99  RETURN
+
