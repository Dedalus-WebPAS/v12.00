.----------------------------------------------------------------------
.    File          : PRIPPINV.PBL
.    
.    Function      : Include to Private Practice Invoices for WEB
.
.    Modifications :
.                    
.        V11.04.02 11/04/2024  J.Tan         TSK 0941662
.                    Mod PRDTGSTM - GST of Total amt(Item amt x Quantity)
.        V10.14.01   22/03/2019  J.Tan            TSK 0857392
.                    Changed RCP Transaction count from DIM3 to DIM5
.        V10.13.01   31/07/2018  J.Tan            TSK 0848506
.                    Changed PP Doctor from DIM6 to DIM10
.                    V10.11.01 30/08/2017 Peter Vela TSK 0387619
.                    Added validation for IMC Details PPRINV50
.                    V10.08.01 03/08/2016 Peter Vela CAR 0294177
.                    Added FROM GOVERNMENT at CHKP0000
.                    V10.07.01 21/12/2015 J.Tan      CAR 0303942
.                    Fixed displaying receipts
.                    V10.06.01 17/05/2015 J.Tan      CAR 308951
.                    Mods to display (*) for unreleased cash
.                    V10.04.03 16/06/2014 Peter Vela CAR 302147
.                    Added journal validation to PRCNPJCI
.                    V10.04.02 31/03/2014 Peter Vela CAR 285260
.                    Added PRCNPJCI functionality
.                    V10.04.01 16/12/2013 J.Tan      CAR 294414
.                    Fixed checking register type for cash 
.                    V10.00.01 22/09/2010 J.Tan      CAR 230162
.                    Mods to display Insurance code for payment               
.                    V9.12.02  01/10/2009 Peter Vela CAR 205008
.                    Added Print NCF button to Private Practice Invoice
.                    V9.12.01  18/06/2009 J.Tan      CAR 198435
.                    Mods to display full description of item
.                    V9.11.01  06/03/2009 J.Tan      CAR 188205
.                    Mods to print 'Non-bank deposit' as 'Payment recieved'    
.                    V9.09.01 26/09/2007 J.Tan   
.                    Mods not to add GST amount to Cash
.                    V9.03.08 06/07/2004 J.Tan   CAR 51112
.                    Mods for non-banked deposit
.                    V9.03.07 21/04/2004 J.Tan   
.                    Fixed non-banked amount for two same invoice in one receipt
.                    V9.03.06 19/04/2004 J.Tan   
.                    Fixed non-banked amount
.                    V9.03.05 01/04/2004 J.Tan   CAR 44116
.                    Mods for credit note
.                    V9.03.04 18/03/2004 J.Tan   CAR 44109
.                    Fixed displaying patient indicator
.                    V9.03.03 25/02/2004 J.Tan   CAR 44109
.                    Mods to display patient indicator 
.                    V9.03.02 03/02/2004 J.Tan   CAR 45047
.                    Mods to display Non-banked Cash total
.
.----------------------------------------------------------------------
PPRINV00  MOVE      URNUMBER,VDEBTOR
          MOVE      ONE,VSCANPMI
          BRANCH    F1,PPRINV40
.
          MOVE      SP30,DIM27
          MOVE      SP3,PRHRBULK
          PACK      KEY27,PRINUNIQ,PRINPRAC,PRINDOCT,SP30
          CALL      RSPRHR1
          CALL      RKPRHR1
          BRANCH    OVRCD,PPRINV10
.
          PACK      DIM27,PRHRUNIQ,PRHRPRAC,PRHRDOCT,SP30
          MATCH     KEY27,DIM27          
          IF        !@EQUAL
            MOVE      SP3,PRHRBULK
            GOTO      PPRINV10
          ENDIF
.
          CALL      CBUL0000
.
PPRINV10  MOVE      ZERO,EXIT
          MOVE      SP2,DIM2
          MOVE      SP70,KEY20
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
          CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PPRINV20               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
          MOVE      PRDTCODE,PRHRPIND
.
PPRINV20  PACK      KEY5,CODEGI,PRHRPIND
          CALL      RDCODE1
          BRANCH    OVRCD,PPRINV25
          MOVE      TDESC,KEY20                  * Patient indicator
.
          MATCH     ANSI,TCINDC1
          IF        @EQUAL
             MOVE      "IP",DIM2
          ELSE
             MOVE      "OP",DIM2
          ENDIF
.
PPRINV25  MOVE      "Unknown code",TDESC         * get claim code description
          PACK      KEY5,CODECL,PRINCLAM
          CALL      RDCODE1
.
          MOVE      "Unknown practice",PRPRDESC  * get medical practice name
          PACK      KEY6,PRINPRAC
          CALL      RDPRPR1
.
          MOVE      "Unknown doctor",DOCNAME     * get serv. doctor name
          PACK      KEY10,PRINDOCT,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,PPRINV30               * not on file
.
          MOVE      PMHCTITL,PACTITLE               * format doctor name
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      ONE,PACFRMT
.
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
PPRINV30  UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      ZERO,RECDISP                 * set rec/journ display flag
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr><td width=18%>":
                               "Person Responsible for Account</td>":
                               "<td>&nbsp;</td>":
                               "<td>Claim Code</td>":
                               "<td><b>",TDESC,"( ",DIM2,")</b></td></tr>":
.
                               "<tr><td>Name</td>":
                               "<td><b>",PRINNAMR,"</b></td>":
                               "<td>Medical Practice</td>":
                               "<td><b>",PRPRDESC,"</b></td></tr>":
.
                               "<tr><td>Address</td>":
                               "<td><b>&nbsp;",PRINPRA1,"</b></td>":
                               "<td>Service Doctor</td>":
                               "<td><b>&nbsp;",DOCNAME,"</b></td></tr>":
.
                               "<tr><td>&nbsp;</td>":
                               "<td><b>&nbsp;",PRINPRA2,"</b></td>":
                               "<td>Patient Indicator</td>":
                               "<td><b>&nbsp;",KEY20,"</b></td></tr>":

                               "<tr><td>&nbsp;</td>":
                               "<td><b>&nbsp;",PRINPRA3,"</b>":
                               "<b>&nbsp;",PRINPOST,"</b></td>":
                               "<td><b>&nbsp;</b></td>":
    "<td align=right><b><input type=button value='Print NCF'":
    "onClick=PMenuLinkTo('priweb02.pbl|11|001|1|430|160');":
    " class='button'></b></td></tr>"
          GOTO      PPRINV99
.
.         Display all the service items first, then the receipts and journals
.
PPRINV40 WRITE     HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                             "<td><b>Date</b></td>":
                             "<td><b>Item</b></td>":
                             "<td><b>Description (":
                             "Number of Services)</b></td>":
                             "<td align=right><b>GST Exc.</b></td>":
                            "<td align=right><b>GST Amount</b></td>":
                            "<td align=right><b>Amount</b></td>":
                            "</tr>"
.
. ------- Get the service item transactions for this invoice ---------------
.
          MOVE      ZERO,TOTINV                  * initialise totals
          MOVE      ZERO,TOTGST
          MOVE      ZERO,TOTPAID
          MOVE      ZERO,TOTOUT
.
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
PPRINV50  CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PPRINV70               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          COMPARE   ONE,PRDTRTYP                 * item entry ?
          GOTO      PPRINV50 IF NOT EQUAL        * no
.
          COMPARE   TWO,PRDTIFLG
          GOTO      PPRINV50 IF EQUAL
.
.         Valid service item transaction found, so display it
.
          UNPACK    PRDTSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
.
          BRANCH    PRDTIFLG,PPRINV60                     * AMA item
.
          MOVE      "-",ANS
          PACK      KEY11,ANSM,ANS,PRDTITMN
          WRITE     HTMLFILE;"<td>&nbsp;",KEY11;
.
          MATCH     SP3,PRDTSUBN                 * subitem number ?
          GOTO      PPRINV62 IF EQUAL            * no
.
          MOVE      "(",ANS
          MOVE      ")",KEY1
          PACK      KEY5,ANS,PRDTSUBN,KEY1
          WRITE     HTMLFILE;"&nbsp;",KEY5;
          GOTO      PPRINV62
.
PPRINV60  MOVE      "-",ANS
          PACK      KEY11,ANSA,ANS,PRDTITMN
          WRITE     HTMLFILE;"<td>&nbsp;",KEY11,"</td>";
          GOTO      PPRINV64
.
PPRINV62  WRITE     HTMLFILE;"</td>";
.
PPRINV64  MOVE      PRDTDESC,SHDESC
          WRITE     HTMLFILE;"<td>&nbsp;",PRDTDESC;
.
          WRITE     HTMLFILE;"(",PRDTSERV,"x",PRDTIAMT,")</td>";
.
          MOVE      ZERO,FORM8P2
          COMPARE   PRDTAMNT,ZERO      
          IF        !@EQUAL
            MOVE      PRDTGSTM,FORM8P2
            ADD       FORM8P2,TOTGST
            WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td>":
                               "<td align=right>",FORM8P2,"</td>";
            ADD       PRDTAMNT,FORM8P2
            WRITE     HTMLFILE;"<td align=right>",FORM8P2,"</td></tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td>":
                               "<td align=right>",FORM8P2,"</td>":
                               "<td align=right>",FORM8P2,"</td></tr>"
          ENDIF
.
          ADD       PRDTAMNT,TOTINV              * update running total
          GOTO      PPRINV50                     * get next transaction
.
. ------- No more service items to display so display the total ----------- 
.  
PPRINV70  MOVE      TOTINV,FORM8P2
          ADD       TOTGST,FORM8P2
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Invoice Total</b></td>":
                             "<td align=right><b>",TOTINV,"</b></td>":
                             "<td align=right><b>",TOTGST,"</b></td>":
                             "<td align=right><b>",FORM8P2,"</b></td>":
                             "</tr>"
          CALL      TCRD0000                     * Print Credit Note
          CALL      PRJR0000                     * Print Receipt/Journal
.
.         End of transactions display, so display totals
.
          COMPARE   ZERO,RECDISP                 * any recpts/journ. displayed ?
          GOTO      PPRINV80 IF EQUAL            * no
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Payment Total</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",TOTPAID,"</b></td>":
                             "</tr>"
.
PPRINV80  MOVE      TOTINV,TOTOUT                * display outstanding debt
          ADD       TOTGST,TOTOUT
          ADD       TOTPAID,TOTOUT
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Outstanding</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
.
          IF        TOTOUT<0
            WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                               TOTOUT,"</font></b></td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right><font color=#0000FF><b>":
                               TOTOUT,"</font></b></td>":
                               "</tr>"
          ENDIF
.
.         Check if this invoice is for a compensable claim.  If so,
.         see if the user wishes to view the claim details
.
          PACK      KEY5,CODECL,PRINCLAM
          CALL      RDCODE1                      * read claim code
          BRANCH    OVRCD,PPRINV99               * not on file
.         
          MATCH     SP1,TCINDC1                  * compensation code ?
          GOTO      PPRINV99 IF EQUAL            * no
.
          REP       COMCHAR,TCINDC1
          MOVE      TCINDC1,COMTYPE
.
.         CALL      CLAM0000
.
PPRINV99  RETURN
+
.****************************************************************************
.*                                 PRCR0000                                 *
.*            display header line for credit notes                          *
.****************************************************************************
TCRD0000  MOVE      ZERO,FORM82
          MOVE      ZERO,FORM72
          MOVE      ZERO,CREDTOT
          OPEN      PRICNOA3,"pricnoaf"
          PACK      KEY16,PRINNUMB,SP70
          CALL      RSPRCNO3
TCRD1000  CALL      RKPRCNO3
          BRANCH    OVRCD,TCRD2000
          MATCH     PRINNUMB,PRCOINVN
          GOTO      TCRD2000 IF NOT EQUAL
.
          ADD       PRCOAMNT,FORM82     * Total credit
          SUB       PRCOGSTM,FORM82     * exclude GST
          ADD       PRCOGSTM,FORM72     * Total GST
          GOTO      TCRD1000
.
TCRD2000  MOVE      FORM82,CREDTOT
          ADD       FORM72,CREDTOT
          COMPARE   ZERO,CREDTOT
          GOTO      TCRD9999 IF EQUAL   * No credit note
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Credit Note Total</b></td>":
                             "<td align=right><b>",FORM82,"</td>":
                             "<td align=right><b>",FORM72,"</td>":
                             "<td align=right><b>":
                               CREDTOT,"</b></td></tr>"
          ADD       CREDTOT,TOTINV              * update running total
.
TCRD9999  RETURN
+
.****************************************************************************
.*                                 PRJR0000                                 *
.*            display header line for receipts/journals     DINV0000        *
.****************************************************************************
PRJR0000  MOVE      ZERO,JOURFLAG
. ------- Now get any receipt/journal transactions and display them --------
.
PRJR1000  PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
PRJR3500  CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PRJR8000               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          COMPARE   ONE,PRDTRTYP                 * receipt/journal entry ?
          GOTO      PRJR3500 IF EQUAL            * no
.
          COMPARE   TWO,IBCNUGST
          GOTO      PRJR3600 IF NOT EQUAL        * not using Aust.GST
.
.         For Aust.GST - display the Journal before Cash
.
          IF        JOURFLAG=0
            COMPARE   THREE,PRDTRTYP             * check for journal record
            GOTO      PRJR3500 IF NOT EQUAL
          ELSE
            COMPARE   TWO,PRDTRTYP               * check for cash record
            GOTO      PRJR3500 IF NOT EQUAL
          ENDIF
.
.         Valid receipt/journal transaction found
.
PRJR3600  BRANCH    RECDISP,PRJR4000             * receipt(s) found
.
          IF        IBCNUGST=2 & JOURFLAG=0
            MOVE      ONE,RECDISP                * set journal found flag
            GOTO      PRJR4000
          ELSE 
            CALL      THED0000                   * display receipt header line
          ENDIF
.
.                                                * format transaction date
PRJR4000  UNPACK    PRDTSDAT,CCENT,CYEAR,CMON,CDAY 
          CALL      PACDATE
.
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          IF        PRDTRTYP=2
            WRITE     HTMLFILE;"<tr><td>Payment - ":
                               CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr>":
                               "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
          ENDIF
.
          COMPARE    THREE,PRDTRTYP              * journal ?
          IF         @EQUAL
            MATCH     "1",PRCNPJCI
            IF        @EQUAL
              WRITE     HTMLFILE;"<td>JNL ",PRDTTTYP,"&nbsp;</td>":
                                 "<td>",PRDTDESC,"</td>";
            ELSE
              WRITE     HTMLFILE;"<td>&nbsp;</td>":
                                 "<td>",PRDTDESC,"</td>";
            ENDIF
            GOTO       PRJR5000
          ELSE
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td>",PRDTDESC,"</td>";
          ENDIF
.
.         Cash
.
          PACK      HOWPAID,SP10,SP3             * receipt
          LOAD      HOWPAID,PRDTMETH,RCASH,RCHEQUE,RCCARD,RMCARD,RMORDER
          WRITE     HTMLFILE;"<td align=right>",PRDTRECN,"</td>":
                             "<td>&nbsp;</td>";
.         ADD       PRDTGSTM,PRDTAMNT
          GOTO      PRJR6200
.
.         Journals
.
PRJR5000  MOVE      ZERO,FORM8P2
          MOVE      ZERO,FORM82
          IF        PRDTAMNT=0
            MOVE      PRDTGSTM,FORM8P2              * GST Journal
          ELSE
            MOVE      PRDTAMNT,FORM82               * Normal Journal
          ENDIF
          MOVE      "Journal      ",HOWPAID 
          WRITE     HTMLFILE;"<td align=right>",FORM82,"</td>":
                             "<td align=right>",FORM8P2,"</td>";
          ADD       PRDTGSTM,PRDTAMNT
.
PRJR6200  WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td></tr>"
          ADD       PRDTAMNT,TOTPAID
          GOTO      PRJR3500                     * get next transaction
.
PRJR8000  COMPARE   TWO,IBCNUGST
          GOTO      PRJR9000 IF NOT EQUAL
.
          BRANCH    JOURFLAG,PRJR9000             * done Journal record, exit.
          MOVE      ONE,JOURFLAG
          GOTO      PRJR1000
.
PRJR9000  PACK      KEY12,SP4,PRINNUMB,SP70 * set up invoice number
          CALL      CHKP0000             * Get cash pending for the invoice
PRJR9999  RETURN
+
.****************************************************************************
.*                                 THED0000      Called by: FULL0000        *
.*            display header line for receipts/journals     DINV0000        *
.****************************************************************************
.
THED0000  WRITE     HTMLFILE;"<tr bgcolor=#99FFFF>":
                         "<td><b>Date</b></td>":
                         "<td><b>&nbsp;</b></td>":
                         "<td><b>Description</b></td>":
                         "<td align=right><b>Receipt No.</b></td>":
                         "<td align=right><b>&nbsp;</b></td>":
                         "<td align=right><b>Amount</b></td>":
                         "</tr>"
.
THED4000  MOVE      ONE,RECDISP                  * set receipt found flag
.
THED9999  RETURN
+
.****************************************************************************
.*                              CBUL0000                                    *
.*                       Check if bulk billing                              *
.****************************************************************************
.
CBUL0000  MOVE      ZERO,EXIT
          MOVE      SP30,BBDESC
          PACK      PRHRBULK,PRHRBULK,SP10
          MATCH     SP3,PRHRBULK
          GOTO      CBUL9000 IF EQUAL
.
          PACK      KEY5,CATGB,PRHRBULK
          CALL      RDCODE1
          BRANCH    OVRCD,CBUL9000
.
          MOVE      ONE,FORM1
.
          WHILE     FORM1 < SIX
             LOAD   ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
             TYPE   ANS                     * see if we have a number
             GOTO   CBUL1000 IF EQUAL
.
             REP    "C1W2V3H4S5M6",ANS      
.
             TYPE   ANS                     * see if we have a number
             GOTO   CBUL1000 IF NOT EQUAL
.
             MOVE   THCSCOD,ANS
.
             TYPE   ANS
             GOTO   CBUL9000 IF EQUAL
.
             REP    "H1I2G3",ANS
.
             TYPE    ANS
             GOTO    CBUL9000 IF NOT EQUAL
.
             MOVE    ZERO,EXIT 
             MOVE    TDESC,BBDESC
.
             GOTO   CBUL9999
.
.------ increment the counter and continue loop ------
.
CBUL1000     ADD    ONE,FORM1
.
          DO
.
CBUL9000  MOVE      ONE,EXIT      
.
CBUL9999  RETURN
+
.*******************************************************************************
.         Display Cash pending total
.         FORM=0 - Generate only, 1 - Print, 2 - Display
.*******************************************************************************
CHKP0000  MOVE      ZERO,CASHTOT
          MOVE      KEY12,SAVKEY12     * saved variable for invoice number
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPREGA1,"rcpregaf"
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
CHKP1000  CALL      RKRCDTE3
          BRANCH    OVRCD,CHKP8000
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      CHKP8000 IF NOT EQUAL
.
          MATCH     VDEBTOR,RCDTURNO        * same debtor ?
          GOTO      CHKP1000 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,CHKP1000
.
          MOVE      SP70,DIM4
          MATCH     "0",RCBNSTAT        * Cash Not banked?
          GOTO      CHKP1000 IF NOT EQUAL
.
          PACK      KEY3,RCDTREGI,SP70
          CALL      RDREGA1
          BRANCH    OVRCD,CHKP1000
.
          MOVE      " (*)",DIM4             * flag for non-banked deposit
          COMPARE   "2",REGIBACD
          GOTO      CHKP1200 IF EQUAL       * Private Practice
          COMPARE   "96",REGIBACD
          GOTO      CHKP1000 IF NOT EQUAL   * NOT PRI Deposit
.
CHKP1200  MULT      "-1",RCDTAMNT
          ADD       RCDTAMNT,CASHTOT
.
          REP       " 0",RCDTTDAT
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          MOVE      SP70,KEY30
          CLEAR     KEY30
.
          MATCH     SP70,RCDTDPTY
          GOTO      CHKP2000 IF NOT EQUAL
.
          MOVE      "P",KEY1
          MOVE      RCBNTTYP,F1
          LOAD      KEY1,F1,ANSH,ANSI,ANSG,ANSH
          MATCH     "H",KEY1
          IF        @EQUAL
            APPEND    "FROM HEALTH FUND ",KEY30
          ELSE
            MATCH     "I",KEY1
            IF        @EQUAL
              APPEND    "FROM INSURANCE  ",KEY30
            ELSE
              MATCH     "G",KEY1
              IF        @EQUAL
                APPEND    "FROM GOVERMENT  ",KEY30
              ELSE
                APPEND    "FROM PATIENT    ",KEY30
              ENDIF
            ENDIF
          ENDIF
          APPEND    RCBNRCOD,KEY30
          GOTO      CHKP3000
.
CHKP2000  PACK      KEY5,ANSD,ANSP,RCDTDPTY,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            APPEND    "DEPOSIT - ",KEY30
            APPEND    TDESC,KEY30
          ENDIF
.
CHKP3000  APPEND    SP70,KEY30
          RESET     KEY30
.
          MOVE      "999,999,999.99",DIM14
          FEDIT     RCDTAMNT,DIM14
          WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                             "</td>":
                             "<td>&nbsp;</td>":
                             "<td>",KEY30:
                             "Receipt ##",RCDTRECN,DIM4,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",RCDTAMNT,"</td></tr>"
          GOTO      CHKP1000
.
CHKP8000  ADD       CASHTOT,TOTPAID
CHKP9999  RETURN
+
