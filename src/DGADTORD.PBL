.******************************************************************************
.*  DGADTORD  :  Order/Bill Patient from a DataGate request                   *
.*               ~~~~~~~~~~~~~~~~~~                                           *
.*  Author    :  Greg Horvat  (02/05/1998)                                    *
.*                                                                            *
.*  Mods      :                                                               *
.*        V12.00.01 14/05/2025  Peter Vela     TSK 0955096                    *
.*                  Alphnumeric Visit Number changes                          *
.*        V11.04.01 29/01/2024  J.Tan          TSK 0919335                    *
.*                  Mod to set TSRVDATE for HF History                        *
.*        V11.01.01 18/02/2021  J.Tan          TSK 0888639                    *
.*                  Added PTDTTMNO - Theatre Team no                          *
.*                  Increased record counter for patmmb                        *
.*        V11.00.01 06/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.13.01 21/08/2017  J.Tan        TSK 0859742                      *
.*                  Added ECLIPSE new fields from pmsmtiaf and aaedtr         *
.*        V10.07.01 21/12/2015  J.Tan          CAR 0303942                    *
.*                  Mods to setup Payment code                                *
.*        V10.04.03 16/06/2014  Steve Armstrong  CAR 301639                   *
.*                  Moved call to TFILENAM to DGINI000 in DGADTORD from       *
.*                  CRET0000.                                                 *
.*                  Also added call to KILL0000 in CRET0000.                  *
.*        V10.04.02 30/04/2014 Steve Armstrong  CAR 261630                    *
.*                  Mods for non-port based tempfile use.                     *
.*                  Also removed reference to PATAUMFD (No longer used).      *
.*        V10.04.01 20/01/2014  J.Tan        CAR 249828                       *
.*                  Added PTDTPMBS - Patient MBS Team and Counter             *
.******************************************************************************
.*        V10.03.01 05/09/2012  J.Tan  CAR 267784                             *
.*                  Checking for TCINDC19 of Claim for Contract Eff.=Disc.from*
.******************************************************************************
.*        V10.01.01 15/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to Misc.Charges PATMCHFD - incl. new routine PATMCHRD*
.*                  13/01/2011  J.Tan         CAR 233048                      *
.*                  Mods to Theatre fees PATTFEFD to use HF Table type        *
.******************************************************************************
.*        V10.00.02 18/08/2010 J.Tan            CAR 222031                    *
.*                  Mods to set PMMIDUPD and PMMITUPD                         *
.*        V10.00.01 27/03/2008 J.Tan      CAR 161684                          *
.*                  Mods to initialise ATSPARE1 - used for indicator to       *
.*                  'Print Service Date/Time for EMR Procedures               *
.******************************************************************************
.*        V9.09.01  09/05/2007  Peter Vela     CAT SJOG ED Billing            *
.*                  Initialised new aaedtref variables                        *
.*        V9.08.02  07/05/2007 J.Tan   CAR 140282                             *
.*                  Mods to initialise batch no for NZ Priv.extract           *
.*        V9.08.01  04/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2      *
.*                  Mods to PATDGWFD - Add field PTDWDRGV (add to Keys)       *
.*        V9.07.01  19/09/2006  J.Tan          CAR 119336                     *
.*                  Added claim code to suggested classification              *
.*        V9.04.03  12/08/2005  Jeni Tan       CAR 62750                      *
.*                  Removed redefined variables                               *
.*        V9.04.02  30/11/2004  J.Tan    CAR 55293                            *
.*                  Added unique id to dtr file                               *
.*        V9.04.01  06/10/2004  J.Tan    CAR 53798                            *
.*                  Mods default claim code for multi hospital                *
.*        V9.03.03  17/03/2004  Steve Armstrong   13395                       *
.*                  Mods to ORD message for Merlin Interface (MHAC)           *
.*        V9.03.02  21/10/2003  J.Tan      CAR 44172                          *
.*                  Mods to read misc.theatre item file (pmsmtiaf)            *
.*        V9.03.01  19/09/2003  Lina Vo    CAR 40497                          *
.*                  Changed index and read of patmchgf to include effective   *
.*                  date.                                                     *
.******************************************************************************
.*        V9.02.01  11/09/2002  Davin  SRF 13395                              *
.*                  Ported from 6.10                                          *
.******************************************************************************
.*        V6.09.03  14.01.2002  Glenn Saunders - Mayne AR Data Extraction proj*
.*                  Set new 'dtr' field SAP indicator to 'N' for new records  *
.*        V6.09.02  28/11/2001  Tonii  SRF13489                               *
.*                  Changed length of tempfile                                *
.*        V6.09.01  21/12/2000  Jill Habasque                                 *
.*                  Removed reference to PTCTEPSD (609 Casemix mods)          *
.*        V6.07.02  15/03/2000  Steve Downing                                 *
.*                  Mods to initialise GST variables                          *
.*        V6.07.01  22/02/2000  Steve Armstrong                               *
.*                  Mods to use PATSTRYR to get start of year date (CHNADMTP) *
.******************************************************************************
.*        V6.06.02  14/09/1999  Davin                                         *
.*                  Changed for 4 character DRGs                              *
.*        V6.06.01  15/07/1999  Greg Horvat      99/00 PRS2 Mods              *
.*                  Recompiled for CHNADMTP - Changed to keyin contract vars  *
.*               V6.05.01 22/12/98 J.Tan                                      *
.*               Added session to input items                                 *
.*               V6.05.02 10/02/99 J.Tan                                      *
.*               Mods to check session date correctly                         *
.*                                                                            *
.******************************************************************************
          DEFPROC   DGADTORD
 IFGE     $$VER,7
.
          INC       AAEDE1FD/INC
          INC       AAEDI1FD/INC
          INC       AAEDTRFD/INC
          INC       AAEMCHFD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       PATMCHFD/INC
          INC       PATMMBFD/INC
          INC       PATSGCFD/INC
          INC       PMSMTIFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
.
. ----- Procedure Constants & Variables -----
.
URNUMBER  DIM       8                       * Message input variables
ADMNUMBR  DIM       8
CHRGDATE  DIM       8
CHRGTIME  DIM       8
SURNAME   DIM       8
BIRTHDTE  DIM       8
MERREFNO  DIM       8
MCHGAMNT  DIM       12
STATFLAG  DIM       1
.
CHGCODE   DIM       9
CHGDTE8   DIM       8
CHGTME    DIM       8
CURDTE8   DIM       8
CURRDATE  DIM       8
CMDLINE   DIM       70
DELFLAG   DIM       1
SESSION   DIM       2
FORM6P2   FORM      6.2
FORM12P2  FORM      12.2
KEY21A    DIM       21
KEY26A    DIM       26
MBSFLAG   FORM      1
MBSLOP    FORM      2
REFER     DIM       9
STYPE     FORM      1
USEDEF    FORM      "0"
XAMT      FORM      4.2
XMBS      FORM      5
XUNIQUE   FORM      8
HFHIFUND  FORM      2
.
TEMPFILE   IFILE     SQL, FIXED=59, KEY="u1-8,9-15,16-20,21-28"
. Name     Type    Size    Physical   Start
. ----     ----    ----    --------   -----
XSDATE     DIM       8         8        1
DXAMT      DIM       7         7        9
DXMBS      DIM       5         5       16
DXUNIQ     DIM       8         8       21
.DTTRANSN1 DIM       6         6       29
.TPATAMTT  FORM      12.2       8       35
.TPATAMTP  FORM      12.2       8       43
.TREBATE   FORM      12.2       8       51
.EOR                                   59
+
.******************************************************************************
.*                                  DGORD000                                  *
.*                 Cancel A Discharge From A Datagate Request                 *
.******************************************************************************
DGORD000  OPEN      AAEDE1A1,"aaede1af"
          OPEN      AAEDI1A1,"aaedi1af"
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      AAEMCHG1,"aaemchgf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDTRA3,"patdtraf"
          OPEN      PATDTRA4,"patdtraf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATMCHG1,"patmchgf"
          OPEN      PATMMBS1,"patmmbsf"
          OPEN      PATMMBS2,"patmmbsf"
          OPEN      PATTFEE1,"pattfeef"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND05;*130,PTCNAUAT,*131,PTCNWSPC,*190,PTSGCOPT
          READ      CONTROLF,HUND09;*183,PTCNDMCC,*192,PTCNDMJC
.
          PACK      SECUSER,SP30,SP10
          PACK      KEY4,PASSCODE
          CALL      RDPASS1                 * Read a passcode file record
.
          CALL      IBACLOCK                * Get current date
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
.
          MOVE      "ORD",RPLYHEAD
.>>>>     READ      DGATEOUT;KEY8,CHGCODE,CHGDTE8,CHGTME,REFER,DELFLAG,SESSION
          READ      DGATEOUT;URNUMBER,ADMNUMBR,CHRGDATE,CHRGTIME,SURNAME:
                             BIRTHDTE,MERREFNO,MCHGAMNT,STATFLAG
.
          MOVE      SP2,SESSION
          RJUSTIFY  ADMNUMBR
          MOVE      ADMNUMBR,AADMNO
          MOVE      CHRGDATE,CHGDTE8
          MOVE      CHRGTIME,CHGTME
          MOVE      SP9,REFER
.
          CALL      VLPAT000                * Validate patient
          BRANCH    EXIT,DGORD800
.
          MATCH     ANSD,STATFLAG
          GOTO      DGORD100 IF EQUAL       * Delete charge code
.
          MATCH     ANSU,STATFLAG
          GOTO      DGORD200 IF EQUAL       * Update charge code
.
          CALL      VLCOD000                * Validate charge code
          BRANCH    EXIT,DGORD800
.
. ----- Insert a charge code -----
.
          CALL      VLDAT000                * Validate charge date & time
          BRANCH    EXIT,DGORD800
.
          CALL      VLREF000                * Validate reference
          BRANCH    EXIT,DGORD800
.
          CALL      UPMMB000                * Update patmmbsf
          PERFORM   PVITYPE,ADAAE000,ADOUT000,ADINP000 * Add to ???dtr?f
.                           A&E      Outpat   Inpat
          BRANCH    EXIT,DGORD800
.
          IF        MBSFLAG<>3
            CALL      THEA0000              * Regenerate theatre items charges
          ENDIF
          CALL      UUPTMIS1                * Unlock an admin file record
          GOTO      DGORD500
.
. ----- Delete charge code -----
.
DGORD100  PERFORM   PVITYPE,DEAAE000,DEOUT000,DEINP000 * Delete charge
.                           A&E      Outpat   Inpat
          BRANCH    EXIT,DGORD800
.
          CALL      UUPTMIS1                * Unlock an admin file record
          GOTO      DGORD500
.
. ----- Update charge code -----
.
DGORD200  PERFORM   PVITYPE,UPAAE000,UPOUT000,UPINP000 * Update ???dtr?f
.                           A&E      Outpat   Inpat
          BRANCH    EXIT,DGORD800
.
          CALL      UUPTMIS1                * Unlock an admin file record
.
. send reply message as successful
.     NB.  SP1 at end of reply has been added because the interpreter does
.     not place a "|" after the last field, and Datagate requires this.  By
.     putting an extra field on the end (which is blank), a "|" will then
.     be added after the last field.
.
DGORD500  MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE,RPLYHEAD:
                            DGMISC1,DGMISC2,DGMISC3:
                            RPLYCODE,RPLYDESC,SP1
.
          ADD       ONE,SUCCPROC                  * increm # success transactns
          GOTO      DGORD999
.
DGORD800  CALL      UUPTMIS1                * Unlock an admin file record
          CALL      KILL0000                * Erase temporary file
.
. --- send back an Error response ---
.
DGORD900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE,RPLYHEAD:
                            DGMISC1,DGMISC2,DGMISC3:
                            RPLYCODE,RPLYDESC,SP1
.
          ADD       ONE,ERRRPROC                  * increm # error transactions
.
DGORD999  GOTO      ENDPROC1
+
.******************************************************************************
.*                                  VLPAT000              Called by: DGORD000 *
.*                              Validate Patient                              *
.******************************************************************************
VLPAT000  MOVE      ZERO,CMIXFLAG
          MOVE      AADMNO,PVIBILL
          PACK      KEY8,PVIBILL
          CALL      RLPTVIS1                * Read a visit file record
          BRANCH    OVRCD,VLPAT800,VLPAT810
.
          COMPARE   FOUR,PVITYPE
          GOTO      VLPAT800 IF EQUAL       * Invalid admin
.
          MOVE      "0  ",ACLSS
          MOVE      "0  ",ACLAIM
          BRANCH    PVITYPE,VLPAT100,VLPAT200,VLPAT300
.                           A&E      Outpat   Inpat
          GOTO      VLPAT300
.
. ----- Validate A&E patient -----
.
VLPAT100  PACK      KEY8,PVIBILL
          CALL      RDDETA1                 * Read an A&E detail file record
          BRANCH    OVRCD,VLPAT110
.
          MOVE      ADACLASS,ACLSS
          MOVE      ADACOMP,ACLAIM
          MOVE      ADADATE,ADATE
          MOVE      TWO,ASTAT
.
VLPAT110  PACK      KEY8,PVIBILL
          CALL      RDDISA1                 * Read an A&E disch file record
          BRANCH    OVRCD,VLPAT400
.
          MOVE      ADSDATE,DDATE
          MOVE      THREE,ASTAT
          GOTO      VLPAT400
.
. ----- Validate outpatient -----
.
VLPAT200  MOVE      "out",OSTFILE
          PACK      KEY6,PVISITE
          CALL      RDSITA1                 * Read a site file record
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          MOVE      PVIDATE,ADATE
          MOVE      PVIDATE,DDATE
          MOVE      THREE,ASTAT
          GOTO      VLPAT400
.
. ----- Validate inpatient -----
.
VLPAT300  MOVE      AADMNO,KEY8
          CALL      RDMISS1                 * Read an admin file record
          BRANCH    OVRCD,VLPAT820,VLPAT830
.
          MATCH     AURNO,URNUMBER
          GOTO      VLPAT825 IF NOT EQUAL   * Admno. not valid for this U/R
.
          COMPARE   THREE,ASTAT
          GOTO      VLPAT310 IF NOT EQUAL   * Not disch'd
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1                 * Read a disch file record
          BRANCH    OVRCD,VLPAT840
.
VLPAT310  PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2                * Position on & read a transfer
          CALL      RDKTRAN2                  file record
          BRANCH    OVRCD,VLPAT850
.
          MATCH     TADMN,AADMNO
          GOTO      VLPAT850 IF NOT EQUAL   * Different admin no
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            MOVE      ONE,CMIXFLAG          * Casemix patient
          ENDIF
.
VLPAT400  BRANCH    ASTAT,VLPAT860,VLPAT500,VLPAT500,VLPAT500,VLPAT870
.
VLPAT500  MOVE      PVIURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,VLPAT880
          CALL      RDPMPX21
.
          MATCH     PURNO,URNUMBER          * check U/R from Merlin
          GOTO      VLPAT880 IF NOT EQUAL
.
          REP       UPPLOW,SURNAME
          MATCH     SURNAME,PSNAME
          GOTO      VLPAT885 IF NOT EQUAL   * Surname not valid for this U/R
.
          MATCH     BIRTHDTE,PBDATE
          GOTO      VLPAT886 IF NOT EQUAL   * D.O.B. not valid for this U/R
.
          COMPARE   ONE,PVITYPE
          GOTO      VLPAT900 IF NOT EQUAL   * Not an A&E patient
.
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          GOTO      VLPAT900
.
VLPAT800  MOVE      "Missing visit file record",RPLYDESC
          MOVE      "02101",RPLYCODE
          GOTO      VLPAT950
.
VLPAT810  MOVE      "Visit record locked on another port",RPLYDESC
          MOVE      "02160",RPLYCODE
          GOTO      VLPAT950
.
VLPAT820  MOVE      "Missing admission file record",RPLYDESC
          MOVE      "02100",RPLYCODE
          GOTO      VLPAT950
.
VLPAT825  MOVE      "Admission number not valid for this U/R",RPLYDESC
          MOVE      "02200",RPLYCODE
          GOTO      VLPAT950
.
VLPAT830  MOVE      "Admission record Locked on another port",RPLYDESC
          MOVE      "02161",RPLYCODE
          GOTO      VLPAT950
.
VLPAT840  MOVE      "Missing discharge file record",RPLYDESC
          MOVE      "02105",RPLYCODE
          GOTO      VLPAT950
.
VLPAT850  MOVE      "Missing transfer record",RPLYDESC
          MOVE      "02102",RPLYCODE
          GOTO      VLPAT950
.
VLPAT860  MOVE      "Patient is not currently admitted",RPLYDESC
          MOVE      "02113",RPLYCODE
          GOTO      VLPAT950
.
VLPAT870  MOVE      "Pre-admission has been cancelled",RPLYDESC
          MOVE      "02115",RPLYCODE
          GOTO      VLPAT950
.
VLPAT880  MOVE      "Missing master file record",RPLYDESC
          MOVE      "01100",RPLYCODE
          GOTO      VLPAT950
.
VLPAT885  MOVE      "Surname not valid for this U/R",RPLYDESC
          MOVE      "02201",RPLYCODE
          GOTO      VLPAT950
.
VLPAT886  MOVE      "Date of Birth not valid for this U/R",RPLYDESC
          MOVE      "02202",RPLYCODE
          GOTO      VLPAT950
.
VLPAT900  MOVE      ZERO,EXIT
          GOTO      VLPAT999
.
VLPAT950  MOVE      ONE,EXIT
VLPAT999  RETURN
+
.******************************************************************************
.*                                  VLCOD000              Called by: DGORD000 *
.*                            Validate Charge Code                            *
.******************************************************************************
VLCOD000  MOVE      PTCNDMCC,CHGCODE        * get default charge code
          MATCH     SP9,CHGCODE
          GOTO      VLCOD800 IF EQUAL       * Blank charge code
.
          CALL      CDTH0000                * Check for existing theatre items
          CALL      DCLM0000                * Get default claim for multi hosp.
.
          CALL      CLPATDTR                * Clear the DTR file variables
          MOVE      ONE,TNINVS
.
. ----- Validate charge code against item file -----
.
          MOVE      ONE,MBSFLAG             * Charge type - MBS
          PACK      KEY17,CHGCODE,ADATE,SP10
          CALL      PATITMRD                * Read an item file record
          BRANCH    OVRCD,VLCOD200
.
          MATCH     SP1,PTITACTV
          GOTO      VLCOD300 IF EQUAL       * Active code
.
.         found an inactive record
.
VLCOD100  MATCH     SP9,PTITLINK
          GOTO      VLCOD200 IF EQUAL       * Blank link item code
.
          PACK      KEY17,PTITLINK,ADATE,SP70
          CALL      PATITMRD                * Read an item file record
          BRANCH    OVRCD,VLCOD200
.
.         The linked MBS code exists, check if active
.
          MATCH     SP1,PTITACTV
          GOTO      VLCOD100 IF NOT EQUAL   * not active, check for a linked MBS
          GOTO      VLCOD300
.
.         Found an invalid MBS, check if it's valid DRG or misc.charge
.
VLCOD200  IF        CMIXFLAG = 1
            UNPACK    SP10,KEY4,KEY5
            UNPACK    CHGCODE,KEY4,KEY5
            MATCH     SP5,KEY5
            GOTO      VLCOD250 IF NOT EQUAL * Check for A valid misc. charge
            OPEN      PATDGWA1,"patdgwaf"
            READ      CONTROLF,HUND05;*158,PTCNDGPV                   *I-137125
            PACK      KEY7,KEY4,PTCNDGPV,SP10                         *I-137125
            CALL      RDPTDGW1
            BRANCH    OVRCD,VLCOD250        * invalid DRG code, try misc.charge
            MOVE      TWO,MBSFLAG           * flag for valid DRG code
            MOVE      PTDWDESC,MDESC
            MOVE      SP10,MMSCGRP
            GOTO      VLCOD400
          ENDIF
.
.         Check for a valid misc.charge
.
VLCOD250  CALL      GTINM000              * Get inpatient misc charge
          BRANCH    OVRCD,VLCOD800        * invalid misc.charge
.
          COMPARE   THREE,MITMTYP
          GOTO      VLCOD260 IF NOT EQUAL
.
          MOVE      MPATPOR,FORM62      * check if this H.F. Adjustment code
          ADD       MHFPOR,FORM62
          COMPARE   ZERO,FORM62
          GOTO      VLCOD260 IF NOT EQUAL
          COMPARE   ZERO,MPATPOR
          GOTO      VLCOD800 IF NOT EQUAL * not allow for HF adjustment code
.
          MOVE      MPATPOR,TPATAMTP
          MOVE      ZERO,TREBATE
          GOTO      VLCOD270
.
VLCOD260  MOVE      MPATPOR,TPATAMTP
          MOVE      MHFPOR,TREBATE
.
VLCOD270  MOVE      MCHGAMNT,KEY9
          MOVE      KEY9,TPATAMTP           * Merlin invoice amount
          MOVE      TPATAMTP,TPATAMTT
          ADD       TREBATE,TPATAMTT
          MOVE      THREE,MBSFLAG
          GOTO      VLCOD500
.
.         Found a valid MBS item code
          
VLCOD300  MOVE      IDESC,MDESC
          MOVE      IMISGRP,MMSCGRP
          MOVE      ITEMNO,CHGCODE          * Linked item code new item code
.
          IF        PVITYPE <> 3
            CALL      GTAEM000              * Get A&E misc charge
            BRANCH    EXIT,VLCOD800         * No charge setup
            GOTO      VLCOD500
          ENDIF
.
          MATCH     ADATE,DDATE             * daycase patient ?
          IF        !@EQUAL
            CALL      AUAT0000              * Auto update of the admin type
          ENDIF
.
VLCOD400  MOVE      CHRGDATE,TSRVDATE       * Service date
          IF        CMIXFLAG = 1
            CALL      GTDRG000              * Get casemix theatre fees
          ELSE
            MOVE      CHRGDATE,CEFFDATE
            CALL      GTTHF000              * Get normal theatre fees
          ENDIF
          BRANCH    EXIT,VLCOD800           * no charge setup
.
. ----- Valid item found -----
.
VLCOD500  MOVE      PVIURNO,TDCODE
          MOVE      TPATAMTP,TPATAMTT
          ADD       TREBATE,TPATAMTT
          MOVE      "PY",TTYPE
          MOVE      CHGCODE,TITEMNO
          MOVE      "00000000",TREF
          MOVE      SESSION,TSESSION
.
          MOVE      "109",PRXCODE                 * System Lock Sector
          CALL      GETSLK00                      * Get System Lock-Sector
          READ      CONTROLF,HUND09;*161,PTCNBNUM
          MOVE      PTCNBNUM,TBATCHN
          ADD       ONE,PTCNBNUM
          WRITAB    CONTROLF,HUND09;*161,PTCNBNUM
          CALL      RELSLK00                      * Release System Lock-Sector
          GOTO      VLCOD900
.
VLCOD800  MOVE      "Invalid charge code",RPLYDESC
          MOVE      "02500",RPLYCODE
          GOTO      VLCOD950
.
VLCOD900  MOVE      ZERO,EXIT
          GOTO      VLCOD999
.
VLCOD950  MOVE      ONE,EXIT
VLCOD999  RETURN
+
.******************************************************************************
.*                                  CDTH0000              Called by: VLCOD000 *
.*                 Check for DTR theatre items for this patient               *
.******************************************************************************
CDTH0000  MOVE      ZERO,ADMTYPFG           * Auto admission type flag - dont do
          COMPARE   THREE,PVITYPE
          GOTO      CDTH9000 IF NOT EQUAL   * Not an inpatient, exit
.
          COMPARE   ONE,PTCNAUAT            * using auto admin type update ?
          GOTO      CDTH9000 IF NOT EQUAL
.
          BRANCH    CFEETYP,CDTH9000        * Using the rates file, exit
.
          MOVE      ONE,ADMTYPFG            * Auto admission type flag - yes
          PACK      KEY22,AADMNO,SP20
          CALL      RDSDTRN1                * Position on & read the DTR file
CDTH1000  CALL      RDKDTRN1
          BRANCH    OVRCD,CDTH2000
.
          MATCH     AADMNO,TADMNO
          GOTO      CDTH2000 IF NOT EQUAL   * Different admission number
.
          COMPARE   TWO,TRECTYPE
          GOTO      CDTH1000 IF NOT EQUAL   * Not a theatre item
.
          PACK      KEY17,TITEMNO,ADATE,SP70
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,CDTH1000
.
          MOVE      TWO,ADMTYPFG          * Auto admission type flag - no
          GOTO      CDTH1000
.
CDTH2000  PACK      KEY14,AADMNO,SP20
          CALL      RSPMMTI1
CDTH2200  CALL      RKPMMTI1
          BRANCH    OVRCD,CDTH9000
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      CDTH9000 IF NOT EQUAL   * Different admission number
.
          MATCH     "2",PMMIRTYP
          GOTO      CDTH2200 IF NOT EQUAL   * Not a theatre item
.
          PACK      KEY17,PMMIITEM,ADATE,SP70
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,CDTH2200
          MOVE      TWO,ADMTYPFG          * Auto admission type flag - no
.
CDTH9000  MOVE      ZERO,EXIT
CDTH9999  RETURN
+
.******************************************************************************
.*                                  THEA0000              Called by: VLCOD000 *
.*      Check for any existing theatre items for this patient                 *
.******************************************************************************
THEA0000  CALL      CRET0000                * Create temporary file
          MOVE      ZERO,XUNIQUE
          CALL      DCLM0000                * Get default claim for multi hosp.
.
          PACK      KEY14,AADMNO,SP20
          CALL      RSPMMTI1                * Position on & read misc.item file
THEA1000  CALL      RKPMMTI1
          BRANCH    OVRCD,THEA2000
.
          MATCH     PMMIVISN,DAADMNO
          GOTO      THEA2000 IF NOT EQUAL   * Different admission number
.
          MATCH     "2",PMMIRTYP
          GOTO      THEA1000 IF NOT EQUAL   * Not a theatre item
.
          CALL      MTDT0000                * move fields pmsmtiaf to dtr
          PACK      XDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",XDATE
          MATCH     XDATE,CHGDTE8
          GOTO      THEA1000 IF NOT EQUAL   * skip different date
.
          MATCH     TSESSION,SESSION        * same session
          GOTO      THEA1000 IF NOT EQUAL
.
          MOVE      ONE,MBSLOP
          MOVE      TITEMNO,MCHARGE
          MOVE      XDATE,TSRVDATE          * Service date
          CALL      GRAT0000                * get the first highest
          BRANCH    OVRCD,THEA1000
.
          MOVE      ZERO,XAMT               * reverse the amount order
          MOVE      "9999.99",XAMT          * to make key decreasing order
          SUB       TPATAMTT,XAMT
          MOVE      XAMT,DXAMT
.
          MOVE      ZERO,XMBS
          MOVE      "99999",XMBS
          SUB       IAMT,XMBS
          MOVE      XMBS,DXMBS
.
          ADD       ONE,XUNIQUE
          MOVE      XUNIQUE,DXUNIQ           * unique number
.
          PACK      KEY28,XDATE,DXAMT,DXMBS,DXUNIQ
          CALL      WRTEMP1
          GOTO      THEA1000
.
.         Loop through the tempfile to get the highest amount
.
THEA2000  MOVE      ZERO,MBSLOP
          PACK      KEY28,SP20,SP10
          CALL      RSTEMP1
THEA3000  CALL      RKTEMP1
          BRANCH    OVRCD,THEA9000
.
          PACK      KEY14,AADMNO,TTRANSN1
          CALL      RDPMMTI1
          BRANCH    OVRCD,THEA3000
          MOVE      PMMIITEM,TITEMNO
.
          ADD       ONE,MBSLOP
          MOVE      TITEMNO,MCHARGE
          MOVE      PMMITDAT,TSRVDATE          * Service date
          CALL      GRAT0000
          BRANCH    OVRCD,THEA3000
          MOVE      TPATAMTT,PMMIAMTT
          MOVE      TPATAMTP,PMMIAMTP
          MOVE      TREBATE,PMMIRBAT
.
          PACK      PMMIDUPD,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDUPD
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITUPD
          MOVE      PASSCODE,PMMIWUSR
          CALL      UPPMMTI1
          GOTO      THEA3000
.
THEA9000  CALL      KILL0000
THEA9999  RETURN
+
.******************************************************************************
.*                                  GRAT0000                                  *
.*                    Generate item rates                                     *
.******************************************************************************
GRAT0000  MOVE      ZERO,TPATAMTT
          MOVE      ZERO,TPATAMTP
          MOVE      ZERO,TREBATE
.
          IF        CMIXFLAG=1
            MOVE      MBSLOP,PTCTITMS     * Sequence
            CALL      CMGETTHR
            IF        OVRCD=1
              MOVE      ZERO,PTCTDPAT
              MOVE      ZERO,PTCTDREB
              MOVE      ZERO,OVRCD
            ENDIF
            MOVE      PTCTDPAT,TPATAMTP
            MOVE      PTCTDREB,TREBATE
          ELSE
            PACK      KEY17,TITEMNO,ADATE,SP70
            CALL      PATITMRD             * Read the item file for desc.
.
            MOVE      PMMITDAT,CEFFDATE
            MOVE      PMMITDAT,TSRVDATE    * Service date
            CALL      GTTHF000             * Get normal theatre charge
            IF        EXIT=0
              LOAD     TPATAMTP,MBSLOP,TFPAT1,TFPAT2,TFPAT3,TFPAT4,TFPAT5,TFPAT6
              LOAD      TREBATE,MBSLOP,TFHF1,TFHF2,TFHF3,TFHF4,TFHF5,TFHF6
            ENDIF
          ENDIF
          MOVE       TPATAMTP,TPATAMTT
          ADD        TREBATE,TPATAMTT
GRAT9999  RETURN
+
.******************************************************************************
.*                                  AUAT0000              Called by: VLCOD000 *
.*                    Automatic Update Of The Admission Type                  *
.******************************************************************************
AUAT0000  COMPARE   ZERO,ADMTYPFG
          GOTO      AUAT9000 IF EQUAL       * No admission type update
.
          PACK      ADMTYP,SP5              * Admission type
.
          BRANCH    ADMTYPFG,AUAT1000       * Auto admission type update
          COMPARE   ONE,PTCNWSPC
          GOTO      AUAT9000 IF NOT EQUAL   * Dont want manual admin type update
.
AUAT1000  MOVE      ICLSS,ADMTYP            * Admission type
.
          COMPARE   ONE,PTSGCOPT
          GOTO      AUAT2000 IF NOT EQUAL
.
          OPEN      PATSGCA1,"patsgcaf"
.
          PACK      KEY18,ACLAIM,AFUNDH,ITEMNO,SP70
          CALL      RDPTSGC1              * Read the suggested class file
          COMPARE   ZERO,OVRCD
          GOTO      AUAT1400 IF EQUAL
.
          MATCH     SP70,AFUNDH
          GOTO      AUAT1200 IF EQUAL
.
          MOVE      SP70,PTSGCLMN           * blank claim
          PACK      KEY18,PTSGCLMN,AFUNDH,TITEMNO,SP20  * key of fund and item
          CALL      RDPTSGC1                * read exceptions file
          COMPARE   ZERO,OVRCD
          GOTO      AUAT1400 IF EQUAL
.
AUAT1200  MOVE      SP70,PTSGFUND           * blank health fund
          PACK      KEY18,ACLAIM,PTSGFUND,TITEMNO,SP20  * key of fund and item
          CALL      RDPTSGC1                * read exceptions file
          BRANCH    OVRCD,AUAT2000           * no record so use ICLSS
.
AUAT1400  MOVE      PTSGCLSS,ADMTYP     * Admission type
.
AUAT2000  MATCH     ATYPE,ADMTYP
          GOTO      AUAT9000 IF EQUAL       * Same admission type, no change
.
          MATCH     SP3,ADMTYP
          IF        @EQUAL
.           DISPLAY   *P1:24,*EL,*B,"No Suggested Class for MBS ":
.                     *V2LON,ITEMNO,*HOFF,".  Adm Type not Updated.  ";
.           CALL      HITENTER
            GOTO      AUAT9000
          ENDIF
.
          PACK      KEY5,ANSA,SP1,ADMTYP
          CALL      RDCODE1                 * Read the codes file
          IF        OVRCD=1
.           DISPLAY   *P1:24,*EL,*B,"Suggested Adm Type ",*V2LON,ADMTYP,*HOFF:
.                     " not found for MBS code ",*V2LON,ITEMNO,*HOFF,".  ";
.           CALL      HITENTER
            GOTO      AUAT9000
          ENDIF
.
          BRANCH    ADMTYPFG,AUAT3000       * Auto admission type update
          GOTO      AUAT4000
.
AUAT3000  MOVE      AADMNO,EFTADMIN         * Admission number
          MOVE      ADMTYP,EFTATYPE         * Admission type
          CALL      CPAD0000                * Calc primary admin type eft date
          GOTO      AUAT5000
.
AUAT4000  MOVE      AADMNO,EFTADMIN         * Admission number
          MOVE      ADMTYP,EFTATYPE         * Admission type
          CALL      KSAD0000                * sub admin type eft date
.
AUAT5000  PROC      CHNADMTP                * Change the admission type
          BRANCH    NOATYPUP,AUAT9000
.
          PACK      KEY8,EFTADMIN
          CALL      RDMISS1                 * Read the admission file
          MOVE      TWO,ADMTYPFG            * Auto admission type flag - no
.
AUAT9000  MOVE      ZERO,EXIT
AUAT9999  RETURN
+
.******************************************************************************
.*                                  CPAD0000              Called by: AUAT0000 *
.*               Calculate Primary Admission Type Effective Date              *
.******************************************************************************
CPAD0000  UNPACK    CHGDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      EFTDATE,CCENT,CYEAR,CMON,CDAY * Effective date = MBS date
.
          PACK      DIM8,CCC,CYY,CMM,CDD        * setup current date
          REP       " 0",DIM8
.
          MATCH     EFTDATE,DIM8            * Effective date = current date ?
          GOTO      CPAD0200 IF NOT EQUAL   * no.
          CLOCK     TIME,EFTTIME            * yes, use current time
          REP       " 0",EFTTIME
          GOTO      CPAD9000
.
CPAD0200  UNPACK    ATIME,CHOUR,DIM1,CMIN,DIM1,DIM2
CPAD1000  MOVE      CHOUR,IHOUR
          MOVE      CMIN,IMIN
          IF        IMIN=59
            IF        IHOUR=23
              UNPACK    EFTDATE,CCENT,CYEAR,CMON,CDAY
              MOVE      CCENT,CC
              MOVE      CYEAR,YY
              MOVE      CMON,MM
              MOVE      CDAY,DD
.
              CALL      DMYCON              * Convert to julian format
.
              ADD       ONE,JULDAY          * Add 1 day
              MOVE      JULDAY,JWKDAY
              MOVE      JULYR,JWKYER
.
              CALL      JULCON              * Convert back to DDMMYY format
.
              PACK      EFTDATE,CC,YY,MM,DD    * Effective date + 1 day
.
              MOVE      ZERO,IMIN
              MOVE      ZERO,IHOUR          * Start of next day
            ELSE
              ADD       ONE,IHOUR           * Increment the hour
              MOVE      ZERO,IMIN
            ENDIF
          ELSE
            ADD       ONE,IMIN              * Increment the minute
          ENDIF
          PACK      EFTTIME,IHOUR,COLON,IMIN,COLON,ZERO,ZERO    * Effective time
.
          REP       " 0",EFTDATE
          REP       " 0",EFTTIME
.
. ----- Check if transfer already exists at this date & time.  If so add -----
. ----- 1 minute to the effective time so no duplicates are written. -----
.
          PACK      KEY30,EFTADMIN,EFTDATE,EFTTIME,SP20
          CALL      RDSTRAN2                * Position on & read the transfer
          CALL      RDKTRAN2                  file
          BRANCH    OVRCD,CPAD9000
.
          MATCH     EFTADMIN,TADMN
          GOTO      CPAD9000 IF NOT EQUAL   * Different admission number
.
          MATCH     EFTDATE,TDATE
          GOTO      CPAD9000 IF NOT EQUAL   * Different date
.
          MATCH     EFTTIME,TTIME
          GOTO      CPAD9000 IF NOT EQUAL   * Different time
.
          UNPACK    EFTTIME,CHOUR,DIM1,CMIN,DIM1,DIM2
          GOTO      CPAD1000                * Add 1 minute to the effective time
.
CPAD9000  MOVE      ZERO,EXIT
CPAD9999  RETURN
+
.******************************************************************************
.*                                  KSAD0000              Called by: AUAT0000 *
.*           Date for the Subsequent Admission Type Effective Date            *
.******************************************************************************
KSAD0000  MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
.
          MATCH     SP8,CHGDTE8
          IF        !@EQUAL
            UNPACK    CHGDTE8,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          PACK      EFTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",EFTDATE            * Effective date
.
          MOVE      "00:01:00",EFTTIME      * Default the effective time
.
. ----- Check if transfer already exists at this date & time.  If so add -----
. ----- 1 minute to the effective time so no duplicates are written. -----
.
KSAD1000  PACK      KEY30,EFTADMIN,EFTDATE,EFTTIME,SP20
          CALL      RDSTRAN2                * Position on & read the transfer
          CALL      RDKTRAN2                  file
          BRANCH    OVRCD,KSAD9000
.
          MATCH     EFTADMIN,TADMN
          GOTO      KSAD9000 IF NOT EQUAL   * Different admission number
.
          MATCH     EFTDATE,TDATE
          GOTO      KSAD9000 IF NOT EQUAL   * Different date
.
          MATCH     EFTTIME,TTIME
          GOTO      KSAD9000 IF NOT EQUAL   * Different time
.
          UNPACK    EFTTIME,CHOUR,DIM1,CMIN,DIM1,DIM2
          MOVE      CHOUR,IHOUR
          MOVE      CMIN,IMIN
          IF        IMIN=59
            ADD       ONE,IHOUR
            MOVE      ZERO,IMIN
          ELSE
            ADD       ONE,IMIN
          ENDIF
          PACK      EFTTIME,IHOUR,COLON,IMIN,COLON,ZERO,ZERO    * Effective time
          REP       " 0",EFTTIME
          GOTO      KSAD1000
.
KSAD9000  MOVE      ZERO,EXIT
KSAD9999  RETURN
+
.******************************************************************************
.*                                  GTTHF000              Called by: VLCOD000 *
.*                              Get Theatre Fees                              *
.******************************************************************************
GTTHF000  MOVE      ZERO,TPATAMTT
          MOVE      ZERO,TPATAMTP
          MOVE      ZERO,TREBATE
          MOVE      SP1,DIM1
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ANSD,DIM1
          ENDIF
.
. ----- Get default theatre fee banding no -----
.
GTTHF100  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF                * Get Contract Effective details
          BRANCH    EXIT,GTTHF999
.
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,CODECL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY18 WITH ACLAIM,AFUNDH,AFNDTB,DIM1A,SP70
          CALL      GETTFEES                * Get theatre amount
          BRANCH    EXIT,GTTHF999
.
          MOVE      TFPAT1,TPATAMTP
          MOVE      TFHF1,TREBATE
          PACK      TDDESC,IDESC,SP30
          MOVE      TWO,TRECTYPE
          MOVE      TFNINV,TNINVS
          MOVE      ZERO,EXIT
GTTHF999  RETURN
+
.******************************************************************************
.*                                  GTAEM000              Called by: VLCOD000 *
.*                             Get A&E Misc Charge                            *
.******************************************************************************
GTAEM000  PACK      KEY29,ACLAIM,AFUNDH,AFNDTB,ACLSS,CHGCODE
          CALL      RDAEMCH1                * Read an A&E misc charges file rec
          COMPARE   ONE,OVRCD
          GOTO      GTAEM100 IF NOT EQUAL   * Validate record found
.
          PACK      KEY29,ACLAIM,SP6,SP8,ACLSS,CHGCODE
          CALL      RDAEMCH1                * Read an A&E misc charges file rec
          COMPARE   ONE,OVRCD
          GOTO      GTAEM100 IF NOT EQUAL   * Validate record found
.
          PACK      KEY29,ZERO,SP2,SP6,SP8,ACLSS,CHGCODE
          CALL      RDAEMCH1                * Read an A&E misc charges file rec
          BRANCH    OVRCD,GTAEM950
.
GTAEM100  MOVE      AEMCPAT,TPATAMTP
          MOVE      AEMCREB,TREBATE
          MOVE      THREE,TRECTYPE
          MOVE      ZERO,EXIT
          GOTO      GTAEM999
.
GTAEM950  MOVE      ONE,EXIT
GTAEM999  RETURN
+
.******************************************************************************
.*                                  GTDRG000              Called by: VLCOD000 *
.*                                Get DRG Code                                *
.******************************************************************************
GTDRG000  MOVE      ONE,PTCTITMS
          PACK      MCHARGE,CHGCODE,SP10
          CALL      CMGETTHR                * Get theatre casemix funding
          BRANCH    OVRCD,GTDRG950
.
          MOVE      PTCTDPAT,TPATAMTP
          MOVE      PTCTDREB,TREBATE
          PACK      TDDESC,PTDWDESC,SP30
          MOVE      TWO,TRECTYPE
          MOVE      ZERO,EXIT
          GOTO      GTDRG999
.
GTDRG950  MOVE      ONE,EXIT
GTDRG999  RETURN
+
.******************************************************************************
.*                                  GTINM000              Called by: VLCOD000 *
.*                          Get Inpatient Misc Charge                         *
.******************************************************************************
GTINM000  MOVE      ZERO,EXIT
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          UNPACK    SP20,PTHFBCAT           * change "H/F Table" to "Table Type"
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1                 * "Table Type code" now in PTHFBCAT
.
          PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,CHGCODE,CURRDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          IF        EXIT = 1 
.                                           * try a blank Health Fund
            PACK      KEY29,ACLAIM,SP8,SP3,CHGCODE,CURRDATE,SP10
            CALL      PATMCHRD              * find Miscellaneous Charge Item
            IF        EXIT = 1
              MOVE      "0  ",MCLMCOD
              PACK      KEY29,MCLMCOD,SP8,SP3,CHGCODE,CURRDATE,SP10
              CALL      PATMCHRD            * find Miscellaneous Charge Item
              BRANCH    EXIT,GTINM950       * not found or invalid
            ENDIF
          ENDIF
.
GTINM100  MOVE      MPATPOR,TPATAMTP
          MOVE      MHFPOR,TREBATE
          MOVE      MDESC,KEY21
          PACK      TDDESC,KEY21,SP1,MERREFNO    * Merlin reference number
          MOVE      MITMTYP,TRECTYPE
          MOVE      MMSCGRP,TMISGRP
          MOVE      MNINV,TNINVS
.
GTINM900  MOVE      ZERO,EXIT
          GOTO      GTINM999
.
GTINM950  MOVE      ONE,EXIT
.
GTINM999  RETURN
+
.******************************************************************************
.*                                  VLDAT000              Called by: DGORD000 *
.*                         Validate Charge Date & Time                        *
.******************************************************************************
VLDAT000  MATCH     ADATE,CHGDTE8
          GOTO      VLDAT800 IF LESS        * Charge date < admin date
          GOTO      VLDAT100 IF NOT EQUAL   * Charge date <> admin date
.
          MATCH     ATIME,CHGTME
          GOTO      VLDAT810 IF LESS        * Charge time < admin time
.
VLDAT100  MATCH     CHGDTE8,CURDTE8
          GOTO      VLDAT820 IF LESS        * Current date < charge date
          GOTO      VLDAT200 IF NOT EQUAL   * Current date <> charge date
.
          CLOCK     TIME,KEY8
          MATCH     CHGTME,KEY8
          GOTO      VLDAT830 IF LESS        * Current time < charge time
.
VLDAT200  COMPARE   THREE,ASTAT
          GOTO      VLDAT900 IF NOT EQUAL   * Not inpat disched, exit
.
          MATCH     CHGDTE8,DDATE
          GOTO      VLDAT840 IF LESS        * Current date < disch date
          GOTO      VLDAT300 IF NOT EQUAL   * Current date <> disch date
.
          MATCH     CHGTME,DTIME
          GOTO      VLDAT850 IF LESS        * Current time < disch time
.
VLDAT300  UNPACK    CHGDTE8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          GOTO      VLDAT900
.
VLDAT800  MOVE      "Charge code date must be after admission date",RPLYDESC
          MOVE      "02501",RPLYCODE
          GOTO      VLDAT950
.
VLDAT810  MOVE      "Charge code time must be after admission time",RPLYDESC
          MOVE      "02502",RPLYCODE
          GOTO      VLDAT950
.
VLDAT820  MOVE      "Charge code date must be before current date",RPLYDESC
          MOVE      "02503",RPLYCODE
          GOTO      VLDAT950
.
VLDAT830  MOVE      "Charge code time must be before current time",RPLYDESC
          MOVE      "02504",RPLYCODE
          GOTO      VLDAT950
.
VLDAT840  MOVE      "Charge code date must be before discharge date",RPLYDESC
          MOVE      "02505",RPLYCODE
          GOTO      VLDAT950
.
VLDAT850  MOVE      "Charge code time must be before discharge time",RPLYDESC
          MOVE      "02506",RPLYCODE
          GOTO      VLDAT950
.
VLDAT900  MOVE      ZERO,EXIT
          GOTO      VLDAT999
.
VLDAT950  MOVE      ONE,EXIT
VLDAT999  RETURN
+
.******************************************************************************
.*                                  VLREF000              Called by: DGORD000 *
.*                             Validate Reference                             *
.******************************************************************************
VLREF000  COMPARE   TWO,MITMTYP
          GOTO      VLREF900 IF NOT EQUAL   * Not a theatre item, exit
.
          PACK      KEY17,REFER,CHGDTE8,SP70
          CALL      PATITMRD                * Read an item file record
          BRANCH    OVRCD,VLREF800
.
          MATCH     SP1,PTITACTV
          GOTO      VLREF900 IF EQUAL       * Active code
.
VLREF100  MATCH     SP9,PTITLINK
          GOTO      VLREF800 IF EQUAL       * Blank link item code
.
          PACK      KEY17,PTITLINK,CHGDTE8,SP70
          CALL      PATITMRD                * Read an item file record
          BRANCH    OVRCD,VLREF800
.
          MATCH     SP9,PTITLINK
          GOTO      VLREF100 IF NOT EQUAL   * Item code linked
.
          MOVE      ITEMNO,REFER            * Linked item code new item code
          GOTO      VLREF900
.
VLREF800  MOVE      "Invalid reference code",RPLYDESC
          MOVE      "02507",RPLYCODE
          GOTO      VLCOD950
.
VLREF900  MOVE      ZERO,EXIT
          GOTO      VLREF999
.
VLREF950  MOVE      ONE,EXIT
VLREF999  RETURN
+
.
.******************************************************************************
.*                                  UPMMB000              Called by: DGORD000 *
.*                               Update patmmbsf                              *
.******************************************************************************
UPMMB000  COMPARE   ONE,MBSFLAG
          GOTO      UPMMB900 IF NOT EQUAL   * Not an MBS item, exit
.
          COMPARE   THREE,TRECTYPE          * miscellaneous item ?
          GOTO      UPMMB900 IF EQUAL       * yes _ ignore
.
          BRANCH    CUSEMBS,UPMMB100,UPMMB100
          GOTO      UPMMB900
.
UPMMB100  MOVE      ZERO,FORM3
          PACK      KEY11,AADMNO,SP10
          CALL      RDSMMBS1                * Positon on & read a patient
          CALL      RDKMMBS1                  MBS file record
          BRANCH    OVRCD,UPMMB200
.
          MATCH     AADMNO,MMADMN
          GOTO      UPMMB200 IF NOT EQUAL   * Different admin no
.
          MOVE      MMRECN,FORM3
.
UPMMB200  ADD       ONE,FORM3
          MOVE      CHGCODE,MMCODE
          MOVE      AADMNO,MMADMN
          MOVE      FORM3,MMRECN
          UNPACK    CHGDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      MMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",MMDATE
          PACK      MMSTIM,ZERO,ZERO,COLON,ZERO,ZERO
          PACK      MMETIM,ZERO,ZERO,COLON,ZERO,ZERO
          PACK      KEY11,MMADMN,MMRECN
          CALL      RDAMMBS1                * Pos read a patient MBS file rec
          COMPARE   ONE,OVRCD
          GOTO      UPMMB200 IF NOT EQUAL   * Record exists, dont write
.
          CALL      WRMMBS1                 * Write a patient MBS file record
.
UPMMB900  MOVE      ZERO,EXIT
UPMMB999  RETURN
+
.******************************************************************************
.*                                  ADAAE000              Called by: DGORD000 *
.*                           Add record to aaedtref                           *
.******************************************************************************
ADAAE000  CALL      GTVST000                * Get next patvisaf transaction no
          MOVE      AADMNO,ATNUMB
          MOVE      TREF,ATINVNO
          MOVE      PVITRAN,ATTRANS
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      RDADTRE1                * Pos read an A&E DTR file record
          COMPARE   ONE,OVRCD
          GOTO      ADAAE000 IF NOT EQUAL   * Record exists
.
          PACK      ATDDESC,TDDESC,SP30
          MOVE      TPATAMTT,ATPATAMT
          PACK      ATDDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",ATDDATE
          MOVE      TITEMNO,ATITEMNO
          MOVE      TTYPE,ATTYPE
          MOVE      TPAYTYPE,ATPAYTYP
          MOVE      TRECEIPT,ATRECEPT
          MOVE      TINVPRT,ATINVPRT
          MOVE      ONE,ATRECTYP
          MOVE      TMISGRP,ATMISGRP
          MOVE      TDEPTYP,ATDEPTYP
          MOVE      TBATCHN,ATBATCHN
          MOVE      TPATAMTP,ATPATPOR
          MOVE      TREBATE,ATREBPOR
.>>>>     MOVE      PTDTDEPS,ATDEPFLG
          MOVE      PTDTEFFD,ATDTEFFD
.>>>>     MOVE      ANSN,AEDTSSAP           * SAP Flag             v6.09.03
          PACK      ATSPARE,SP30,SP10
          MOVE      ZERO,ATSPARE1
          MOVE      ZERO,ATSPARE2
          MOVE      SP70,ATDTBTCH
          MOVE      SP70,ATDTSUBN
          MOVE      SP70,ATDTEDAD
          MOVE      SP70,ATDTSVTM
          MOVE      SP70,ATDTPCOD
          PACK      ATDTACOI,SP70       * After Care Override Indicator
          PACK      ATDTDSOV,SP70       * Duplicate Service Override
          PACK      ATDTSTXT,SP70       * Service Text
          PACK      ATDTLSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      ATDTMPOV,SP70       * Multi Procedure Override
          PACK      ATDTNMPT,SP70       * Number of Patients Seen
          PACK      ATDTSDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      ATDTTDUR,SP70       * Time Duration (Mins)
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      WRDTRE1                 * Write an A&E DTR file record
.
ADAAE900  MOVE      ZERO,EXIT
ADAAE999  RETURN
+
.******************************************************************************
.*                                  ADOUT000              Called by: DGORD000 *
.*                          Add record to outdtrof                            *
.******************************************************************************
ADOUT000  CALL      GTVST000                * Get next patvisaf transaction no
          MOVE      AADMNO,OTNUMB
          MOVE      TREF,OTINVNO
          MOVE      PVITRAN,OTTRANS
          PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
          CALL      RDADTRO1                * Pos read an outpat DTR file record
          COMPARE   ONE,OVRCD
          GOTO      ADOUT000 IF NOT EQUAL   * Record exists
.
          PACK      OTDDESC,TDDESC,SP30
          MOVE      TPATAMTT,OTPATAMT
          PACK      OTDDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",OTDDATE
          MOVE      TITEMNO,OTITEMNO
          MOVE      TTYPE,OTTYPE
          MOVE      TPAYTYPE,OTPAYTYP
          MOVE      TRECEIPT,OTRECEPT
          MOVE      TINVPRT,OTINVPRT
          MOVE      ONE,OTRECTYP
          MOVE      TMISGRP,OTMISGRP
          MOVE      TDEPTYP,OTDEPTYP
          MOVE      TBATCHN,OTBATCHN
          MOVE      TPATAMTP,OTPATPOR
          MOVE      TREBATE,OTREBPOR
.>>>>     MOVE      PTDTDEPS,OTDEPFLG
          MOVE      PTDTEFFD,OTDTEFFD
          MOVE      ZERO,OTDTGSTA           * GST Applicable
          MOVE      SP6,OTDTGSTC            * GST Code
          MOVE      ZERO,OTDTGSTM           * GST Amount
.>>>>     MOVE      ANSN,OTDTSSAP           * SAP Flag            v6.09.03
          MOVE      SP70,OTDTBTCH
          MOVE      SP70,OTDTPCOD
.
          PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
          CALL      WRDTRO1                 * Write an outpat DTR file record
.
ADOUT900  MOVE      ZERO,EXIT
ADOUT999  RETURN
+
.******************************************************************************
.*                                  ADINP000              Called by: DGORD000 *
.*                            Add record to patdtraf                          *
.******************************************************************************
ADINP000  MATCH     ANSA,STATFLAG
          GOTO      ADINP900 IF NOT EQUAL   * Don't add new patdtraf record
.
          CALL      GTVST000                * Get next patvisaf transaction no
          MOVE      PVITRAN,TTRANSN1
          UNPACK    CHGDTE8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      CHGTME,PTDTTIME
          MOVE      ANSP,TTYPEDEB
          PACK      TTYPE,ANSD,ANSB
          MOVE      ZERO,TPAYTYPE
          MOVE      AADMNO,TADMNO
.>>>>     MOVE      ANSN,PTDTSSAP           * SAP Flag              v6.09.03
          MOVE      SP70,PTDTTMNO
          MOVE      SP70,PTDTPMBS
          MOVE      SP70,PTDTPCOD
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1                * Pos read an inpat DTR file record
          COMPARE   ONE,OVRCD
          GOTO      ADINP000 IF NOT EQUAL   * Record exists
.
. All variables set up previously in procedure
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      WRDTRN1                 * Write an inpat DTR file record
.
ADINP900  MOVE      ZERO,EXIT
ADINP999  RETURN
+
.******************************************************************************
.*                                  GTVST000              Called by: ADAAE000 *
.*                       Get Next patvisaf Transaction No ADOUT000 & ADINP000 *
.******************************************************************************
GTVST000  ADD       ONE,PVITRAN
          CALL      UPVISA1                 * Update a visit file record
.
          SUB       ONE,PVITRAN
GTVST900  MOVE      ZERO,EXIT
GTVST999  RETURN
+
.******************************************************************************
.*                                  DEAAE000              Called by: DGORD000 *
.*                              Delete A&E Charge                             *
.******************************************************************************
DEAAE000  MOVE      AADMNO,TADMNO
          MOVE      TADMNO,ATNUMB
.
          PACK      KEY31,ATNUMB,SP30
          CALL      RDSDTRE3
DEAAE020  CALL      RDKDTRE3
          BRANCH    OVRCD,DEAAE100
          MATCH     AADMNO,ATNUMB
          GOTO      DEAAE100 IF NOT EQUAL   * Different admin no
          MATCH     "00000000",ATINVNO
          GOTO      DEAAE020 IF EQUAL       * not invoiced
          BRANCH    ATRECTYP,DEAAE800       * found an invoiced misc.
          GOTO      DEAAE020
.
DEAAE100  MOVE      "00000000",KEY8
          PACK      KEY31,AADMNO,KEY8,ONE,SP30,SP1
          CALL      RDSDTRE3                * Position on & read an A&E DTR
          CALL      RDKDTRE3                  file record
          BRANCH    OVRCD,DEAAE900
.
          MATCH     AADMNO,ATNUMB
          GOTO      DEAAE900 IF NOT EQUAL   * Different admin no
.
          MATCH     "00000000",ATINVNO
          GOTO      DEAAE800 IF NOT EQUAL   * Different invoice no
.
          COMPARE   ONE,ATRECTYP
          GOTO      DEAAE900 IF NOT EQUAL   * Not a misc item
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      DEDTRE1                 * Delete an A&E DTR file record
.
          BRANCH    CUSEMBS,DEAAE200,DEAAE200
          GOTO      DEAAE100
.
DEAAE200  PACK      KEY20,ATITEMNO,AADMNO,SP20
          CALL      RDSMMBS2                * Position on & read a patient MBS
          CALL      RDKMMBS2                  file record
          BRANCH    OVRCD,DEAAE100
.
          MATCH     ATITEMNO,MMCODE
          GOTO      DEAAE100 IF NOT EQUAL   * Different MBS code
.
          MATCH     AADMNO,MMADMN
          GOTO      DEAAE100 IF NOT EQUAL   * Different admin no
.
          PACK      KEY11,MMADMN,MMRECN
          CALL      DEMMBS1                 * Delete a patient MBS file record
          GOTO      DEAAE100
.
DEAAE800  MOVE      "Cannot delete charge code",RPLYDESC
          MOVE      "02508",RPLYCODE
          GOTO      DEAAE950
.
DEAAE900  MOVE      ZERO,EXIT
          GOTO      VLREF999
.
DEAAE950  MOVE      ONE,EXIT
DEAAE999  RETURN
+
.******************************************************************************
.*                                  DEOUT000              Called by: DGORD000 *
.*                            Delete Outpat Charge                             *
.******************************************************************************
DEOUT000  MOVE      AADMNO,OTNUMB
          PACK      KEY31,OTNUMB,KEY8,ONE,SP30
          CALL      RDSDTRO3                * Position on & read an outpat DTR
DEOUT020  CALL      RDKDTRO3                  file record
          BRANCH    OVRCD,DEOUT100
.
          MATCH     AADMNO,OTNUMB 
          GOTO      DEOUT100 IF NOT EQUAL   * Different admin no 
          MATCH     "00000000",OTINVNO
          GOTO      DEOUT020 IF EQUAL       * not invoiced
          BRANCH    OTRECTYP,DEOUT800       * found an invoiced item
          GOTO      DEOUT020
.
DEOUT100  MOVE      "00000000",KEY8
          PACK      KEY31,AADMNO,KEY8,ONE,SP30
          CALL      RDSDTRO3                * Position on & read an outpat DTR
          CALL      RDKDTRO3                  file record
          BRANCH    OVRCD,DEOUT900
.
          MATCH     AADMNO,OTNUMB
          GOTO      DEOUT900 IF NOT EQUAL   * Different admin no
.
          MATCH     "00000000",OTINVNO
          GOTO      DEOUT800 IF NOT EQUAL   * Different invoice no
.
          COMPARE   ONE,OTRECTYP
          GOTO      DEOUT900 IF NOT EQUAL   * Not a misc item
.
          PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
          CALL      DEDTRO1                 * Delete an outpat DTR file record
.
          BRANCH    CUSEMBS,DEOUT200,DEOUT200
          GOTO      DEOUT100
.
DEOUT200  PACK      KEY20,OTITEMNO,AADMNO,SP20
          CALL      RDSMMBS2                * Position on & read a patient MBS
          CALL      RDKMMBS2                  file record
          BRANCH    OVRCD,DEOUT100
.
          MATCH     OTITEMNO,MMCODE
          GOTO      DEOUT100 IF NOT EQUAL   * Different MBS code
.
          MATCH     AADMNO,MMADMN
          GOTO      DEOUT100 IF NOT EQUAL   * Different admin no
.
          PACK      KEY11,MMADMN,MMRECN
          CALL      DEMMBS1                 * Delete a patient MBS file record
          GOTO      DEOUT100
.
DEOUT800  MOVE      "Cannot delete charge code",RPLYDESC
          MOVE      "02508",RPLYCODE
          GOTO      DEOUT950
.
DEOUT900  MOVE      ZERO,EXIT
          GOTO      VLREF999
.
DEOUT950  MOVE      ONE,EXIT
DEOUT999  RETURN
+
.******************************************************************************
.*                                  DEINP000              Called by: DGORD000 *
.*                           Delete Inpatient Charge                          *
.******************************************************************************
DEINP000  MOVE      "00000000",KEY8
          PACK      KEY23,AADMNO,KEY8,THREE,SP70
          CALL      RDSDTRN4                * Position on & read an inpat DTR
DEINP100  CALL      RDKDTRN4                  file record
          BRANCH    OVRCD,DEINP800
.
          MATCH     AADMNO,TADMNO
          GOTO      DEINP800 IF NOT EQUAL   * Different admin no
.
          MATCH     "00000000",TREF
          GOTO      DEINP800 IF NOT EQUAL   * Different invoice no
.
          COMPARE   THREE,TRECTYPE
          GOTO      DEINP800 IF NOT EQUAL   * Not a misc. charge
.
          UNPACK    TDDESC,KEY22,KEY8
          MATCH     KEY8,MERREFNO
          GOTO      DEINP100 IF NOT EQUAL   * different merlin reference no.
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      DEDTRN1                 * Delete an inpat DTR file record
          GOTO      DEINP900
.
DEINP800  MOVE      "Cannot delete charge code",RPLYDESC
          MOVE      "02508",RPLYCODE
          GOTO      DEINP950
.
DEINP900  MOVE      ZERO,EXIT
          GOTO      DEINP999
.
DEINP950  MOVE      ONE,EXIT
DEINP999  RETURN
+
.******************************************************************************
.*                                  UPINP000              Called by: DGORD000 *
.*                           Update Inpatient Charge                          *
.******************************************************************************
UPINP000  PACK      KEY18,AADMNO,THREE,SP70
          CALL      RDSDTRN3                * Position on & read an inpat DTR
UPINP100  CALL      RDKDTRN3                  file record
          BRANCH    OVRCD,UPINP800
.
          MATCH     AADMNO,TADMNO
          GOTO      UPINP800 IF NOT EQUAL   * Different admin no
.
          COMPARE   THREE,TRECTYPE
          GOTO      UPINP800 IF NOT EQUAL   * Not a misc. charge
.
          PACK      KEY8,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",KEY8
          MATCH     KEY8,CHGDTE8
          GOTO      UPINP100 IF NOT EQUAL   * different charge date
.
          MATCH     PTDTTIME,CHGTME
          GOTO      UPINP100 IF NOT EQUAL   * different charge time
.
.  ----   found record, now see if we have to update   ----
.
          UNPACK    TDDESC,KEY22,KEY8
          MATCH     KEY8,MERREFNO
          IF        !@EQUAL
            PACK      TDDESC,KEY22,MERREFNO * update merlin reference no.
          ENDIF
.
          MOVE      MCHGAMNT,KEY9           * check if amount changed
          MOVE      KEY9,FORM6P2
          COMPARE   TPATAMTT,FORM6P2
          IF        !@EQUAL
            MATCH     "00000000",TREF       * yes, so check if already invoiced
            IF        !@EQUAL
              CALL      ADJRN000            * need to add journal record
              BRANCH    EXIT,UPINP800
              GOTO      UPINP999
            ELSE
              MOVE      FORM6P2,TPATAMTP      * update patient amount
              MOVE      FORM6P2,TPATAMTT      * update total amount
            ENDIF
          ENDIF
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1                * Pos read an inpat DTR file record
          COMPARE   ONE,OVRCD
          GOTO      UPINP800 IF EQUAL       * Record does not exist
.
. All variables set up previously in procedure
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      UPDTRN1                 * Update an inpat DTR file record
          GOTO      UPINP900
.
UPINP800  MOVE      "Cannot update charge code",RPLYDESC
          MOVE      "02509",RPLYCODE
          GOTO      UPINP950
.
UPINP900  MOVE      ZERO,EXIT
          GOTO      UPINP999
.
UPINP950  MOVE      ONE,EXIT
UPINP999  RETURN
+
.******************************************************************************
.*                                  ADJRN000                                  *
.*                        Add journal record to patdtraf                      *
.******************************************************************************
ADJRN000  MATCH     SP70,PTCNDMJC
          GOTO      ADJRN950 IF EQUAL
.
          CALL      GTVST000                * Get next patvisaf transaction no
          MOVE      PVITRAN,TTRANSN1
          MOVE      SIX,TRECTYPE
          MOVE      PTCNDMJC,TTYPE          * default journal code
          PACK      KEY5,ANSJ,SP1,PTCNDMJC
          CALL      RDCODE1
          PACK      TDDESC,TDESC,SP70
          UNPACK    CURDTE8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          PACK      PTDTTIME,SP70
          PACK      TITEMNO,SP70
          MOVE      SP70,PTDTPCOD
.
          SUB       TPATAMTP,FORM6P2        * journal amount is difference
          MOVE      FORM6P2,TPATAMTP        * update patient amount
          MOVE      FORM6P2,TPATAMTT        * update total amount
.
          MOVE      "109",PRXCODE                 * System Lock Sector
          CALL      GETSLK00                      * Get System Lock-Sector
          READ      CONTROLF,HUND09;*161,PTCNBNUM
          MOVE      PTCNBNUM,TBATCHN
          ADD       ONE,PTCNBNUM
          WRITAB    CONTROLF,HUND09;*161,PTCNBNUM
          CALL      RELSLK00                      * Release System Lock-Sector
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1                * Pos read an inpat DTR file record
          COMPARE   ONE,OVRCD
          GOTO      ADJRN000 IF NOT EQUAL   * Record exists
.
. All variables set up previously in procedure
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      WRDTRN1                 * Write an inpat DTR file record
.
ADJRN900  MOVE      ZERO,EXIT
          GOTO      ADJRN999
.
ADJRN950  MOVE      ONE,EXIT
ADJRN999  RETURN
+
.******************************************************************************
.*                                  UPAAE000              Called by: DGORD000 *
.*                              Update AAE Charge                             *
.******************************************************************************
UPAAE000
UPAAE999  RETURN
+
.******************************************************************************
.*                                  UPOUT000              Called by: DGORD000 *
.*                           Update Outpatient Charge                         *
.******************************************************************************
UPOUT000
UPOUT999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 59 U1-8,9-15,16-20,21-28",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          CLOSE     TEMPFILE
          OPEN      TEMPFILE,FNAME                        * open 2nd index
.
CRET9999  RETURN
+
.*******************************************************************************
.         Move pmsmtiaf to patdtraf fields
.*******************************************************************************
MTDT0000  CALL      CLPATDTR
          MOVE      PMMIVISN,TADMNO
          MOVE      PMMITRAN,TTRANSN1
          MOVE      PMMIURNO,TDCODE
          MOVE      PMMIITEM,TITEMNO
          MOVE      PMMIDESC,TDDESC
          MOVE      PMMIDES2,PTDTDES2
          UNPACK    PMMITDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      "DB",TTYPE
          MOVE      PMMIREFN,TRECEIPT
          MOVE      PMMIRTYP,TRECTYPE
          MOVE      PMMIAMTT,TPATAMTT
          MOVE      PMMIAMTG,TPATAMTG
          MOVE      PMMIAMTH,TPATAMTH
          MOVE      PMMIAMTP,TPATAMTP
          MOVE      PMMIRBAT,TREBATE
          MOVE      PMMITDOC,TDOCTA
          MOVE      PMMIODOC,TDOCTO
          MOVE      PMMISERV,TSERVS
          MOVE      PMMISESS,TSESSION
          MOVE      PMMIMGRP,TMISGRP
          MOVE      PMMIGSTA,PTDTGSTA
          MOVE      PMMIGSTC,PTDTGSTC
          MOVE      PMMIGSTM,PTDTGSTM
          MOVE      PMMIUNIQ,PTDTUNIQ
MTDT9999  RETURN
+
.*************************************************************************
.         DCLM0000 - Get default claim code for multi hospital
.*************************************************************************
DCLM0000  IF        IBCNMHOS=1
            MOVE      SP10,PMVXMHOS
            MOVE      SP10,PTHSDCLM
            PACK      KEY8,AADMNO,SP10
            CALL      RDPMVX11   
            IF        OVRCD=0
              PACK      KEY3,PMVXMHOS,SP10
              CALL      RDPTHSP1               * get Hospital Using Theatre Fee
            ENDIF
          ENDIF
DCLM9999  RETURN
.
.*************************************************************************
.*  Temp I/O's                                                           *
.*************************************************************************
RSTEMP1   RESET     KEY28
          READ      TEMPFILE,KEY28;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;SDATE,DXAMT,DXMBS,DXUNIQ,TTRANSN1:
                             TPATAMTT,TPATAMTP,TREBATE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY28
          WRITE     TEMPFILE,KEY28;SDATE,DXAMT,DXMBS,DXUNIQ,TTRANSN1:
                             TPATAMTT,TPATAMTP,TREBATE
          RETURN
+
.==============================================================================
.
          INC       IBAOVRIO/INC            * Over flags
          INC       CMGETTHR
          INC       PATMCHRD                * Miscellaneous Charges read routine
.
          INC       AAEDE1IO/INC
          INC       AAEDI1IO/INC
          INC       AAEDTRIO/INC
          INC       AAEMCHIO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       PATMCHIO/INC
          INC       PATMMBIO/INC
          INC       PATSGCIO/INC
          INC       PMSMTIIO/INC
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
+
.
 XIF
ENDPROC1  ENDPROC
