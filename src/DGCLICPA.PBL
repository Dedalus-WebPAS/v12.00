.************************************************************************
.*  Procedure: DGCLICPA  -  Send a Cancel Pre-Admission                 *
.*                                                                      *
.*  Author   : Mike Laming  (06/07/2001)                                *
.*                                                                      *
.*  Requires : PATMI1FD (Booking) record                                *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V11.00.01 03/12/2020  Davin             TSK 0890602                 *
.*            Allow broadcast of A27 from HL7RECVR when ptcngosm=1      *
.************************************************************************
.*  V10.03.02 06/06/2013  Steve Armstrong   CAR 268961                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.05.01  12/12/2005  Steve Armstrong  CAR 88438                    *
.*            Mods to load PMI details into pmsqptaf                    *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.*            Mods to write directly to holding tables instead of       *
.*            hl7bmtaf.                                                 *
.************************************************************************
.*  V9.02.02   02/10/2001  Davin                                        *
.*             Mods to use message holding file                         *
.*  V9.02.01 : 04/09/2001  Mike Laming                                  *
.*             Correct to use only PATMI1FD (instead of BOKRC1FD)       *
.*                                                                      *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICPA
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND05;*107,PTCNDGPA
          READ      CONTROLF,HUND10;*111,PTCNA27M
          READ      CONTROLF,HUND28;*207,PTCNGOSM
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA27M               * yes - sending A27 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGPA <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI500 IF NOT EQUAL        * no - continue
.
          MATCH     "1",PTCNGOSM                 * allow broadcast from SIU ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
DGCLI500  MOVE      ZERO,IBAMFLAG
          IF        PTCNDGPA = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          MOVE      SP3,MHLRCODE
          PACK      PMQVSPAR,SP30,SP30,SP1
          CALL      WRPMQVI1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A27 message?
          IF        @EQUAL
            MOVE      "A27",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGPA                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CPA",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
.
DGCLIEND  ENDPROC
+
