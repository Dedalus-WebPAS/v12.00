.******************************************************************************
.* System         : Patient and Maternity                                     *
.*                                                                            *
.* Include name   : PTMTBABY.PBL                                              *
.*                                                                            *
.* Name           : Common code for admitting a Baby                          *
.*                                                                            *
.* Date           : 02/05/1995                                                *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Function       : To contain the common code and IO routines used to        *
.*                  admit a Baby                                              *
.*                  This is used by IBAHOS73 and IBAMAT02.                    *
.*                  This should be used to contain as much common code as     *
.*                  possible between the two programs.                        *
.*                  There is an include PTMTBABY.INC which contains vars/FD's.*
.*                                                                            *
.* Procedures     : LINKBAB                 procedure to link baby to mum     *
.*                  MBRNS000                procedure to update new borns file*
.*                                                                            *
.* Routines       : ADMITBAB                admit the baby                    *
.*                                                                            *
.* Returns        : NOFEE = 1               no bed fee set up - baby not admit*
.*                  CEXIT = 1               ward not entered - baby not admit *
.*                                                                            *
.* Modifications  :                                                           *
.*       V9.03.02  13/05/2004  Steve Armstrong      CAR 49679                 *
.*                 PAS-CIS Product Integration                                *
.*                 Removed unused routines (ADMITBAB & ACTBSAV)               *
.*       V9.02.01  29.11.2001  B.G.Ackland                                    *
.*                 Remove pi's from Program                                   *
.*                 17/02/2000  Tonii Tang                                     *
.*                 Changed exit variable from CEXIT to EXIT                   *
.*                 08/10/1998  Glenn Berry      5.07 changes                  *
.******************************************************************************
.*                  24/02/1998  Nick                SRF 642895                *
.*                              Mods to avoid I*D from HOS73                  *
.*                  12/01/1998  Steve Armstrong     SRF 643034                *
.*                              Mods to ACTBSAV to load new audit admission   *
.*                              variables                                     *
.*                  29/05/1996  Steve Armstrong                               *
.*                              Mods for Screen Painting of HOS73             *
.*                  04/07/95   Whirlpool                                      *
.*                             added new admission variables                  *
.******************************************************************************
.
.*********************************************************************
.         write to the UR link file to link the mother and baby
.         Para's  : MURNO         mothers UR number
.                   BURNO         babies  UR number 
.                   KEY8          babies birth date ccyymmdd
.*********************************************************************
          DEFPROC   LINKBAB
.
          INC       PATLINFD/INC
.
DOBBAB    DIM       8
FROMLINK  FORM      1   * 1 means FROM rec. doesnt exist
TOLINK    FORM      1   * 1 means TO rec. doesn't exist
.
MLNK0000  OPEN      PATLINK1,"patlinkf"
          READ      CONTROLF,SEVENTY7;*195,PTCNDLKR
          MOVE      KEY8,DOBBAB             * save DOB
          MOVE      MURNO,LINKFUR
          MOVE      BURNO,LINKTUR
          PACK      LINKSPR,SP20,SP20
          PACK      KEY16,LINKFUR,LINKTUR
          MOVE      DOBBAB,LINKDAT
          REP       " 0",LINKDAT
          MOVE      PTCNDLKR,LINKREA
          MOVE      ZERO,FROMLINK
          MOVE      ZERO,TOLINK
          CALL      RDLINK1
          IF        OVRCD=1
             MOVE      ONE,FROMLINK 
          ENDIF
.
          PACK      KEY16,LINKTUR,LINKFUR
          CALL      RDLINK1
          IF        OVRCD=1
             MOVE      ONE,TOLINK 
          ENDIF
.
          IF        FROMLINK=0
              IF        TOLINK=0
                  GOTO      MLNK9999     * FROM rec. & TO rec. exists
              ELSE
                  CALL      DELINK1      * FROM rec. exists & TO rec. not exist
                  CALL      WRLINK1
              ENDIF
          ELSE
              IF        TOLINK=1
                  CALL      WRLINK1      * FROM rec. & TO rec. not exist
              ELSE
                  CALL      DELINK1      * FROM rec. not exist & TO rec. exists
                  CALL      WRLINK1
              ENDIF
          ENDIF
.
          GOTO      MLNK9999
.
          INC       PATLINIO/INC
          INC       IBAOVRIO/INC
.
MLNK9999  ENDPROC
+
.*********************************************************************
.         maintain the new borns file
.         Para's  : KEY8               baby date of birth
.*********************************************************************
          DEFPROC   MBRNS000
.
          INC       PATBRNFD/INC
.
          MOVE      KEY8,CPTDATE            * set date as babies date of birth
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,MBRNS999
.
          OPEN      PATBRNS1,"patbrnsf"
          PACK      BRDATE,DRGYR,DRGNUM     * use the current date
          MOVE      BRDATE,KEY6
          MOVE      ZERO,BRADMN
          MOVE      SP20,BRSPARE
          CALL      RDBRNS1
.
          ADD       ONE,BRADMN
          IF        OVRCD = ZERO
            CALL      UPBRNS1
          ELSE
            CALL      WRBRNS1                 * a new record
          ENDIF
          GOTO      MBRNS999
.
          INC       PATBRNIO/INC
          INC       IBAOVRIO/INC
.
MBRNS999  ENDPROC
+
