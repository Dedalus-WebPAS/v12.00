.******************************************************************************
.* Include Name   : LEGGTDRG.PBL                                              *
.* Description    : Gets a DRG & MDC code for a LEGACY patient                *
.* Author         : Greg Horvat  03/06/2000                                   *
.* Parameters Req : A valid admission record                                  *
.* Returns        : DDRGCODE - DRG code                                       *
.*                  DMDCCODE - MDC code                                       *
.*                  DRGVER   - DRG version                                    *
.* Modifications  :                                                           *
.*        V9.03.01  31/06/2004  Ebon Clements   CAR 51239                     *
.*                  Removed open of PATFN1A1                                  *
.*        V9.03.00  05/05/2004  Sylvek Litewka  CAR 49209                     *
.*                  Created from GETDRG.PBL (release 6.11)                    *
.******************************************************************************
LEGGTDRG
LGDRG000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY5;*171,PTCNDGPV
.
          UNPACK    SP20,DDRGCODE,DMDCCODE,DRGVER
          OPEN      LEGDSCH1,"legdschf"
          PACK      KEY8,LAADMNO,SP8
          CALL      RDLGDSC1                * Read a discharge file record
          BRANCH    OVRCD,LGDRG900
.
          MOVE      LPDSDDRG,DDRGCODE
          MOVE      LPDSDMDC,DMDCCODE
          MOVE      LPDSVOGU,DRGVER
.
          MATCH     SP6,LAFUNDH
          GOTO      LGDRG100 IF EQUAL       * Blank health fund
.
          PACK      KEY14,LAFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                 * Read a health fund file record
          BRANCH    OVRCD,LGDRG100
.
          MATCH     SP3,PTHFGRPV  
          GOTO      LGDRG100 IF EQUAL       * Blank DRG version no
.
          MOVE      PTHFGRPV,KEY3
          GOTO      LGDRG300
.
LGDRG100  OPEN      LEGCODE1,"legcodes"
          PACK      KEY5,ANSC,ANSL,LACLAIM
          CALL      RDLGCOD1                * Read a codes file record
          BRANCH    OVRCD,LGDRG200
.
          MATCH     SP3,LPCDGRPV  
          GOTO      LGDRG200 IF EQUAL       * Blank DRG version no
.
          MOVE      LPCDGRPV,KEY3
          GOTO      LGDRG300
.
LGDRG200  MOVE      PTCNDGPV,KEY3
.
LGDRG300  OPEN      LEGPGRA1,"legpgraf"
          PACK      KEY13,LAADMNO,SP1,ONE,KEY3
          CALL      RDLGPGR1                * Read a patient grouper file record
          BRANCH    OVRCD,LGDRG900
.
          MOVE      LPTPGNDR,DDRGCODE
          MOVE      LPTPGNMD,DMDCCODE
          MOVE      LPTPGGPV,DRGVER
.
LGDRG900  MOVE      ZERO,EXIT
LGDRG999  RETURN
