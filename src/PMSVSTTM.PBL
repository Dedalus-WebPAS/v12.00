.-----------------------------------------------------------------------
.                                                                     
.     Include Name : PMSVSTTM.INC                                   
.                                                                    
.     Function     : File Definition include for Patient System      
.                    Clinical Coding Value Set ID                    
.                                                                    
.     Modifications:                                                 
.     V11.02.01  30/08/2022  Bella Turco    TSK 0921957              
.                Added PTCNNRTD to maintenance lists                 
.     V10.11.00  14/11/2017  Don Nguyen     TSK 0816814              
.                Created Include                                     
.
.------------------------------------------------------------
.   Clear Parameters for PMSVSTTD
.------------------------------------------------------------
CPVST000  MOVE      SP70,PTVST001
          MOVE      SP70,PTVST002
          MOVE      SP70,PTVST003
          MOVE      SP70,PTVST004
          MOVE      SP70,PTVST005
          MOVE      SP70,PTVST006
          MOVE      SP70,PTVST007
.
CPVST999  RETURN
.
.-----------------------------------------------------------
. Initialize's file data (Value Set ID Maintenace)
.-----------------------------------------------------------
CLRVST00  MOVE      SP70,PMVTVSID
          MOVE      SP70,PMVTVSNM
          MOVE      SP70,PMVTVURL
          MOVE      SP70,PMVTFURL
          MOVE      SP70,PMVTHDPE
.
CLRVST99  RETURN
.-----------------------------------------------------------------
. moves cgi variables into file variables (Value Set ID maintenance)
.-----------------------------------------------------------------
VSTSAV00  MOVE      PTVST001,PMVTVSID
          MOVE      PTVST002,PMVTVSNM
          MOVE      PTVST003,PMVTVURL
          MOVE      PTVST004,PMVTFURL
          MOVE      PTVST005,PMVTHDPE
.
VSTSAV99  RETURN
.-----------------------------------------------------------
. * Checks mandatory fields (Value Set ID Maintenance)
.------------------------------------------------------------
CHKVST00  RESET     PTVST001
          MATCH     SP70,PTVST001
          GOTO      CHKVST90 IF EQUAL
.
          RESET     PTVST002
          MATCH     SP70,PTVST002
          GOTO      CHKVST91 IF EQUAL
.
          GOTO      CHKVST98
.
CHKVST90  MOVE      "Value Set ID is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVST99
.
CHKVST91  MOVE      "Value Set Name is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVST99
.
CHKVST98  MOVE      ZERO,EXIT
CHKVST99  RETURN
.-------------------------------------------------------------------------------
.  Set Parameters for Value Set ID Maintenance
.-------------------------------------------------------------------------------
STVST000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STVST001,STVST002,STVST003,STVST004,STVST005,STVST006:
                       STVST007
.
          GOTO      STVST999
.
STVST001  MOVE      QRYDATA,PTVST001
          GOTO      STVST999
.
STVST002  MOVE      QRYDATA,PTVST002
          GOTO      STVST999
.
STVST003  MOVE      QRYDATA,PTVST003
          GOTO      STVST999
.
STVST004  MOVE      QRYDATA,PTVST004
          GOTO      STVST999
.
STVST005  MOVE      QRYDATA,PTVST005
          GOTO      STVST999
.
STVST006  MOVE      QRYDATA,PTVST006
          GOTO      STVST999
.
STVST007  MOVE      QRYDATA,PTVST007
          GOTO      STVST999
.
STVST999  RETURN
.
.------------------------------------------------------------
. Value Set ID Maintenance
.------------------------------------------------------------
VSTD0000  BRANCH    FLDITMNO,VSTD0100,VSTD0200,VSTD0300,VSTD0400,VSTD0500:
                             VSTD0600,VSTD0700,VSTD0800
          GOTO      VSTD9999
.
VSTD0100  WRITE     HTMLFILE;PMVTVSID;   * Value Set ID
          GOTO      VSTD9999
.
VSTD0200  WRITE     HTMLFILE;PMVTVSNM;   * Value Set ID
          GOTO      VSTD9999
.
VSTD0300  WRITE     HTMLFILE;PMVTVURL;   * Value Set URL
          GOTO      VSTD9999
.
VSTD0400  WRITE     HTMLFILE;PMVTFURL;   * FHIR Local URL
          GOTO      VSTD9999
.
VSTD0500  WRITE     HTMLFILE;PMVTHDPE;   * HDP Equivalent Value
          GOTO      VSTD9999
.
VSTD0600  CALL      VSTAB000             * Table of Value Set ID
          GOTO      VSTD9999
.
VSTD0700  CALL      NXTVST00             * Link to Next Group
          GOTO      VSTD9999
.
VSTD0800  CALL      PREVST00             * Link to Last Group
          GOTO      VSTD9999
.
VSTD9999  RETURN
.
.-----------------------------------------------------------
. Value Set ID Maintenance
.-----------------------------------------------------------
VSTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      VSHED000   * Output Table Heading
.
          IF        NORECORD=0
           MOVE      PTCNNRTD,NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,PTVST001,SP70
          CALL      RDPMVST1
          IF        OVRCD=0
            CALL      RPPMVST1
          ENDIF
.
VSTAB100  CALL      RKPMVST1
          BRANCH    OVRCD,VSTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      VSTAB100
          ENDIF
.
          CALL      VSROWA00      *  Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      VSTAB100 IF NOT EQUAL
          GOTO      VSTAB999
.
VSTAB900  CALL      NOMORE00      * Display No More Records Message
.
VSTAB999  RETURN
+
.-------------------------------------------------------------------------------
. Link to next set of Code Value Set ID
.-------------------------------------------------------------------------------
NXTVST00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",PTVST001
.
          WRITE     HTMLFILE;"patweb87.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&ptvst001=",PTVST001;
.
          REP       "+ ",PTVST001
.
NXTVST99  RETURN
+
.-------------------------------------------------------------------------------
. Link to previous set of Code Value Set ID
.-------------------------------------------------------------------------------
PREVST00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE    ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",PTVST001
.
          WRITE     HTMLFILE;"patweb87.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&ptvst001=",PTVST001;
.
          REP       "+ ",PTVST001
.
PREVST99  RETURN
+
.------------------------------------------------------------
. Value Set ID Maintenance (TABLE HEADING)
.------------------------------------------------------------
VSHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "<b>Value Set ID</b></td>":
                               "<td class=HeadingCell>":
                               "<b>Value Set Name</b></td>":
                               "<td class=HeadingCell>":
                               "<b>HDP Equivalent Value</b></td>":
                             "</tr>"
.
VSHED999  RETURN
.------------------------------------------------------------
. Value Set ID Maintenance (TABLE ROW)
.------------------------------------------------------------
VSROWA00  MATCH     SP70,PMVTVSNM
          IF        @EQUAL
            MOVE      "&nbsp;",PMVTVSNM
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail48":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptvst001=",PMVTVSID,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             PMVTVSID,"</td>":
                             "<td>",PMVTVSNM,"</td>":
                             "<td>",PMVTHDPE,"</td>":
                             "</tr>"
          GOTO      VSROWA99
.
VSROWA99  RETURN

