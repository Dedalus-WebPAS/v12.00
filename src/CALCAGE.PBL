.*********************************************************************** 
.*                            CALCAGE                                  *
.*               Calculate Patient's Age at a given date               *
.*                                                                     *
.*   Includes   : PATMA1FD  Master file                                *
.*                PATCALAG  Data variables for this include            *
.*                PATDHEAD  For PSAGETYP and PSAGECON                  *
.*                                                                     *
.*   Parameters : PBDATE    Birth date (from master file CCYYMMDD)     *
.*                AGEDATE   Date age to be calculated as (CCYYMMDD)    *
.*                                                                     * 
.*   Returns    : PSAGEYRS  Age in years                               *
.*                PSAGEMNT  Age in months                              *
.*                PSAGEDAY  Age in days                                *
.*                                                                     *
.* Modifications:                                                      *
.*                  06/10/1998  Glenn Berry      5.07 changes          *
.***********************************************************************
.*                30/08/90 Graeme Williams                             *
.*                         Include written                             *
.*                07/01/91 Graeme Williams                             *
.*                         Always calculate age in months and days     *
.*                17/05/94 Ian Rutt                                    *
.*                         Added routine to calculate patient age.     *
.*                         If parameter (PTCNDATE) is on this include  * 
.*                         will return age in days,weeks,months or yrs *
.*                         depending on certain parameters.  CALCAGE   *
.*                         will return PSAGECON (converted age) and    *
.*                         PSAGETYP (age in days,weeks,months or years)*
.*                08/02/95 Whirlpool                                   *
.*                         Added comment so we know that PSAGECON and  *
.*                         PSAGETYP are defined in PATDHEAD.INC !!!    *
.*                                                                     *
.*********************************************************************** 
.
CALCAGE   OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY;*229,PTCNDATE,*230,PTCNDAY:
                                    *233,PTCNWEEK,*236,PTCNMONT
 
          MOVE      ZERO,PSAGEYRS             * age in years
          MOVE      ZERO,PSAGEMNT             * age in months
          MOVE      ZERO,PSAGEDAY             * age in days
.
.         Set up the birth date to calculate the julian birth day  
.
          REP       " 0",PBDATE
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
.
.         Save the birth date as form fields
.
          MOVE      XDAY,IDAY
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
.         Convert the birth date to julian
.
          MOVE      IDAY,DD
          MOVE      IMON,MM
          MOVE      IYEAR,YY
          MOVE      ICENT,CC 
          CALL      DMYCON                    * julian routine
.
          MOVE      JULDAY,BJDAY              * save julian birth day
          MOVE      LEAPYR,BRTHLYR            * save birthdate leap year flag
.
.         Set up the to date to calculate to julian day  
.
          REP       " 0",AGEDATE
          UNPACK    AGEDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON                    * julian routine
.
          MOVE      JULDAY,CJDAY              * save admission julian day
.
.
.         Calculate the patients age in years
.
CCAGE100  UNPACK    AGEDATE,CCENT,CYEAR,CMON,CDAY
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY     * patients birth date
          MOVE      CCENT,FORM2               * Convert to a form field
          MOVE      FORM2,PSAGEYRS
          MOVE      XCENT,FORM2               * Convert to a form field
          SUB       FORM2,PSAGEYRS
          MULT      "100",PSAGEYRS
.
          MOVE      CYEAR,FORM2               * Convert to a form field
          ADD       FORM2,PSAGEYRS
          SUB       IYEAR,PSAGEYRS
.
.         Save this calculation for later use
.
          MOVE      PSAGEYRS,SAVAGEYR
.
          COMPARE   ZERO,PSAGEYRS
          GOTO      CCAGE300 IF EQUAL
.
.         Check if the to month is < birth month
.
          REP       " 0",CMON
          MATCH     XMON,CMON                 
          GOTO      CCAGE200 IF EQUAL
          GOTO      CCAGE300 IF NOT LESS
          SUB       ONE,PSAGEYRS                 * subtract one if it is less
          GOTO      CCAGE300
.
.         Check if the to day is < birth day
.
CCAGE200  MATCH     XDAY,CDAY
          GOTO      CCAGE300 IF NOT LESS
          SUB       ONE,PSAGEYRS                 * subtract one if it is less
.
.         Calculate the patients age in months
.
CCAGE300  MOVE      SAVAGEYR,PSAGEMNT
          MULT      TEN2,PSAGEMNT
          MOVE      CMON,FORM2                * Convert to month to a form
          ADD       FORM2,PSAGEMNT
          SUB       IMON,PSAGEMNT
.
.         Check if the to day is < birth day
.
          MATCH     XDAY,CDAY
          GOTO      CCAGE500 IF NOT LESS
          SUB       ONE,PSAGEMNT                 * subtract one if it is less
.
.         Calculate the patients age in days
.
CCAGE500  MOVE      SAVAGEYR,PSAGEDAY
          MULT      "365",PSAGEDAY
          ADD       CJDAY,PSAGEDAY
          SUB       BJDAY,PSAGEDAY
.
          COMPARE   ZERO,SAVAGEYR            * Check if born in current year
          GOTO      CCAGE600 IF EQUAL
.
          ADD       BRTHLYR,PSAGEDAY         * Adjust for leap years
.
.  If PTCNDATE = 1 we are using the Age conversion, ie. converting to days
.  if age is less than days parameter (PTCNDAY), convert to weeks if age (in
.  weeks) is less than the week parameter (PTCNWEEK) or convert to Months if
.  Age in months is less than month parameter (PTCNMONT).  The age will display
.  in years if none of these are true.  The age will be in PSAGECON and the
.  type (day,week,month or year) in PASEGTYP.
.  
CCAGE600  COMPARE   ONE,PTCNDATE
          GOTO      CCAGE999 IF NOT EQUAL 
.
          IF        PSAGEDAY <= PTCNDAY
            MOVE      PSAGEDAY,KEY5            * require age in days. 
            RESET     KEY5 
            UNPACK    KEY5,DIM1,DIM1,PSAGECON  * move age in PSAGECON
            MOVE      "d",PSAGETYP           * Move dys to PSAGETYP to say
            GOTO      CCAGE999                 * what age is recored as
          ENDIF
.
          MOVE      PSAGEDAY,PSAGEWKS
          DIV       "7",PSAGEWKS               * get number of weeks
          IF        PSAGEWKS <= PTCNWEEK
            MOVE      PSAGEWKS,KEY5            * require age in weeks
            RESET     KEY5 
            UNPACK    KEY5,DIM1,DIM1,PSAGECON  * move wks to PSADETYPE to
            MOVE      "w",PSAGETYP           * signify that age is recorded
            GOTO      CCAGE999                 * as weeks  
          ENDIF
.
          IF        PSAGEMNT <= PTCNMONT
            MOVE      PSAGEMNT,KEY4            * require age in months
            RESET     KEY4
            UNPACK    KEY4,DIM1,PSAGECON       * move mths to signify that the
            MOVE      "m",PSAGETYP          * age is recorded as months
          ELSE
            MOVE      PSAGEYRS,PSAGECON        * age remains as years
            MOVE      "y",PSAGETYP
          ENDIF 
.
CCAGE999   RETURN
