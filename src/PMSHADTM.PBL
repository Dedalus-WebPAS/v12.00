. *----------------------------------------------------------------------
. * Include Name  :   PMSHADTM.PBL
. *
. * Function      :   CGI variables for PMSHADFD - pmshadaf.dat
. *
. * Includes Needed : PMSHADFD - ABF HAC Adjustments table
. *                   PMSHADIO - IO for ABF HAC Adjustments table
. * Modifications :
. *
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for ABF HAC Adjustments table        
.------------------------------------------------------------------------
.
CLRHAD00  MOVE      SP70,PMHAD001
          MOVE      SP70,PMHAD002
          MOVE      SP70,PMHAD003
          MOVE      SP70,PMHAD004
          MOVE      ZERO,PMHAD005
          RETURN
.
.------------------------------------------------------------------------
. ABF HAC Adjustments table       
.------------------------------------------------------------------------
PMHD0000  BRANCH    FLDITMNO,PMHD0100,PMHD0200,PMHD0300,PMHD0400,PMHD0500
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PMHD9999
.
PMHD0100  UNPACK    DIM127,D1,KEY1,DIM127        * Contract ID
          MOVE      KEY1,F1
          BRANCH    F1,PMHD0110
.
          WRITE     HTMLFILE;PMHDCNTR;
          GOTO      PMHD9999
.
PMHD0110  PACK      KEY6,PMHDCNTR,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,PMHD9999
.
          WRITE     HTMLFILE;PMCIDESC;           * Contract ID Description
          GOTO      PMHD9999
.
PMHD0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,PMHD0210
          PACK      PTCDLDES,SP70
          PACK      KEY5,CATHa,PMHDHACG,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PMHD9999
          WRITE     HTMLFILE;PTCDLDES;
          GOTO      PMHD9999
.
PMHD0210  WRITE     HTMLFILE;PMHDHACG;           * HAC Group 01-16 Cat Ha
          GOTO      PMHD9999
.
PMHD0300  WRITE     HTMLFILE;PMHDVALF;           * Value From
          GOTO      PMHD9999
.
PMHD0400  WRITE     HTMLFILE;PMHDVALT;           * Value To
          GOTO      PMHD9999
.
PMHD0500  WRITE     HTMLFILE;PMHDADJV;           * Complexity Adjusment %
          GOTO      PMHD9999
.
PMHD9999  RETURN
.
.------------------------------------------------------------
. Set Parameters for ABF HAC Adjustments table         
.------------------------------------------------------------
SPPHD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPPHD001,SPPHD002,SPPHD003,SPPHD004,SPPHD005
.
          MOVE      ONE,EXIT
          GOTO      SPPHD999
.
SPPHD001  MOVE      QRYDATA,PMHAD001
          PACK      PMHAD001,PMHAD001,SP70
          GOTO      SPPHD999
.
SPPHD002  MOVE      QRYDATA,PMHAD002
          PACK      PMHAD002,PMHAD002,SP70
          GOTO      SPPHD999
.
SPPHD003  MOVE      QRYDATA,PMHAD003
          PACK      PMHAD003,PMHAD003,SP70
          GOTO      SPPHD999
.
SPPHD004  MOVE      QRYDATA,PMHAD004
          PACK      PMHAD004,PMHAD004,SP70
          GOTO      SPPHD999
.
SPPHD005  SQUEEZE   QRYDATA
          MOVE      QRYDATA,PMHAD005
          GOTO      SPPHD999
.
SPPHD999  RETURN
.
.------------------------------------------------------------------------
.         Move work variables to ABF HAC Adjustments table file variables
.------------------------------------------------------------------------
MVPMHD00  MATCH     PMHAD001,SP70
          IF        !@EQUAL
            MOVE      PMHAD001,PMHDCNTR
          ENDIF
.
          MATCH     PMHAD002,SP70
          IF        !@EQUAL
            MOVE      PMHAD002,PMHDHACG
          ENDIF
.
          MATCH     PMHAD003,SP70
          IF        !@EQUAL
            MOVE      PMHAD003,PMHDVALF
          ENDIF
.
          MATCH     PMHAD004,SP70
          IF        !@EQUAL
            MOVE      PMHAD004,PMHDVALT
          ENDIF
.
          COMPARE   PMHAD005,ZERO
          IF        !@EQUAL
            MOVE      PMHAD005,PMHDADJV
          ENDIF
.
MVPMHD99  RETURN
.------------------------------------------------------------------------
.         Move work variables to ABF HAC Audit file variables
.------------------------------------------------------------------------
MVAUA000  CALL      CLPMSAUA                    * Clear audit file variables
. 
          MATCH     "A",UPDTTYPE
          IF        @EQUAL
            MATCH     PMHDCDAT,SP70             * Audit Date
            IF        !@EQUAL
              MOVE      PMHDCDAT,PMAAADAT
            ENDIF
            MATCH     PMHDCTIM,SP70             * Audit Time
            IF        !@EQUAL
              MOVE      PMHDCTIM,PMAAATIM
            ENDIF
          ELSE
            MATCH     PMHDUDAT,SP70             * Audit Date
            IF        !@EQUAL
              MOVE      PMHDUDAT,PMAAADAT
            ENDIF
            MATCH     PMHDUTIM,SP70             * Audit Time
            IF        !@EQUAL
              MOVE      PMHDUTIM,PMAAATIM
            ENDIF      
          ENDIF
.
          MATCH     UPDTTYPE,SP70
          IF        !@EQUAL
            MOVE      UPDTTYPE,PMAAATYP     * Audit Type A-Add C-Change D-Delete
          ENDIF
.
          MATCH     PMHDCNTR,SP70
          IF        !@EQUAL
            MOVE      PMHDCNTR,PMAACNTR          * Contract ID
          ENDIF
.
          MATCH     PMHDHACG,SP70
          IF        !@EQUAL
            MOVE      PMHDHACG,PMAAHACG          * HAC Group 01-16 Cat Ha
          ENDIF
.
          MATCH     PMHDVALF,SP70
          IF        !@EQUAL
            MOVE      PMHDVALF,PMAAVALF          * Value From
          ENDIF
.
          MATCH     "A",UPDTTYPE
          IF        !@EQUAL
            MATCH     OLDPMVAL,SP70
            IF        !@EQUAL
              MOVE      OLDPMVAL,PMAAOVLT        * Old Value To
            ENDIF
.
            COMPARE   OLDPMADJ,ZERO
            IF        !@EQUAL
              MOVE      OLDPMADJ,PMAAOADJ        * Old Complexity Adjustment %
            ENDIF
          ENDIF
.
          MATCH     "D",UPDTTYPE
          IF        !@EQUAL
            MATCH     PMHDVALT,SP70
            IF        !@EQUAL
              MOVE      PMHDVALT,PMAANVLT        * New Value To
            ENDIF
.
            COMPARE   PMHDADJV,ZERO
            IF        !@EQUAL
              MOVE      PMHDADJV,PMAANADJ        * New Complexity Adjustment %
            ENDIF
          ENDIF
.
            MATCH     USERID,SP70                * User ID Created
            IF        !@EQUAL
              MOVE      USERID,PMAACUID
            ENDIF
.
MVAUA999  RETURN
