.------------------------------------------------------------
.  File          :  PATMKIPR.PBL
.
.  Function      :  Update Keyword Index File for Miscellaneous Charge Table
.
.  Parameters    :  PTMKCODE  - Key to Miscellaneous Charge Table
.
.  Modifications :
.
.     V11.05.01 25/02/2025  Don Nguyen    TSK 0952080
.               Corrected code to ensure that all keywords are added for
.               duplicate Miscellaneous Charge Code Key; where the same Code Key
.               exists in the Miscellaneous Charge File for a different
.               'Effective Date'.
.               We firstly clear out all key word records (patmkiaf) for the
.               matching Miscellaneous Charge Code Key, then re-add the keywords
.               using the 'Charge Code' & 'Description' values from the
.               Miscellaneous Charge File.
.     V11.02.01 14.09.2022  DA Sarkies    TSK 0909756
.               Changed size of description variable from 60 to 90.
.     V10.03.01 09/03/2012  J.Tan         CAR 259879
.               Added Active/Inactive and Effective todate to NZ Misc.
.     V10.01.01 29/11/2010  Mike Laming   CAR 233046
.               Mods for "H/F Table Type" (PTMCHFTT) - replaces H/F Table MHFTAB
.     V10.00.01 30/04/2010  J.Tan     CAR 220887
.               Mods checking for Active/Inactive of Misc.charge
.      V9.09.01 19/06/2007 Jill Habasque              
.               Mods to loop through all nzpmchaf records instead of defaults
.      V9.08.00 14/11/2006 Ebon Clements CAR 120007
.               Added using NZ private billing functionality. CFEETYP=5
.      V9.04.01 27/09/2004 Lina Vo CAR 53798  
.               Mods for Multi Hospital to use PTHSDCLM instead of PTCNDCLM.
.               Changed to only write blank heath fund misc charges.
.      V9.03.02 27/07/2004 J.Tan  CAR 52075  
.               Mods to have the miscellaneous code as keyword search
.      V9.03.01 11/09/2003 Lina Vo  CAR 40497
.               Changed procedure to only write the first misc charge record
.               disregarding effective date.  
.
.  This procedure is now called everytime patmchgf is added, updated or deleted.
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PMHKKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PMHKKKWD
.  Index 2   PMHKKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the HCGE0000 routine to call the MISBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    MISBW000
.       MOVE    DGNAME,KEYWORDS
.       CALL    MISBW000
.       MOVE    TDESC,KEYWORDS
.       CALL    MISBW000
.------------------------------------------------------------
          DEFPROC   PATMKIUP
.
          INC       PATHSPFD/INC
          INC       PATMKIFD/INC
.
KEYINDEX  DIM       26        * Code Index in Table
KEYWORDS  DIM       90        * String Containing the Key Words to be Indexed
NINTY     FORM      "90"
KEYWRDNO  FORM      2
KEYWRD00  DIM       90
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
HCGE0000  READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*212,CFEETYP
.
          IF        CFEETYP<>5
            MATCH     SP10,MHFUND            * Only write for blank health fund
            GOTO      HCGE9999 IF NOT EQUAL
.
            MATCH     SP10,PTMCHFTT
            GOTO      HCGE9999 IF NOT EQUAL
          ENDIF
.
          COMPARE   ONE,IBCNMHOS
          GOTO      HCGE0800 IF NOT EQUAL
          IF        CFEETYP=5
            GOTO      HCGE1000
          ENDIF
.
.         Check if Misc charge Claim code is a default claim for multi hospital 
.
          OPEN      PATHSPA1,"pathspaf"
          PACK      KEY3,SP10
          CALL      RSPTHSP1
HCGE0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HCGE9999
.
          IF        CFEETYP<>5
            MATCH     MCLMCOD,PTHSDCLM
            GOTO      HCGE1000 IF EQUAL
          ENDIF
.
          GOTO      HCGE0100
.
.         Check if Misc charge Claim code is a default claim  
.
HCGE0800  IF        CFEETYP<>5
            MATCH     MCLMCOD,PTCNDCLM
            GOTO      HCGE9999 IF NOT EQUAL
          ENDIF
.
HCGE1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATMKIA1,"patmkiaf"
          TRAPCLR   IO
          BRANCH    OVRCD,HCGE9999
.
          IF        CFEETYP=5
            PACK      KEYINDEX,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          ELSE
            PACK      KEYINDEX,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP70
          ENDIF
.
          CALL      MISCL000      * Clear Current Key Word Index records
.
.
.         Add all Kew Word records again (for active records only)
.
          IF        CFEETYP=5
            PACK      KEY37,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
            CALL      RSNZMCH1
HCGE1100    CALL      RKNZMCH1
            BRANCH    OVRCD,HCGE1900
.
            PACK      KEY26,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
            MATCH     KEYINDEX,KEY26             * check record still exist
            GOTO      HCGE1900 IF NOT EQUAL
.
            MATCH     "1",NZMCACFL             * Inactive ?
            GOTO      HCGE1100 IF EQUAL
.
            CALL      ADDKWD00
            GOTO      HCGE1100
          ELSE
            PACK      KEY29,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP70
            CALL      RDSMCHG1
HCGE1200    CALL      RDKMCHG1
            BRANCH    OVRCD,HCGE1900
.
            PACK      KEY26,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP70
            MATCH     KEYINDEX,KEY26
            GOTO      HCGE1900 IF NOT EQUAL
.
            MATCH     "1",PTMCACTV               * Inactive ?
            GOTO      HCGE1200 IF EQUAL          * yes
.
            CALL      ADDKWD00
            GOTO      HCGE1200
          ENDIF
.
HCGE1900  CLOSE     PATMKIA1
.
HCGE9999  GOTO      PTSMKIEP
+
.------------------------------------------------------------------------------
. Add Kew Word records for Miscellaneous Charge Code & Description
.------------------------------------------------------------------------------
ADDKWD00  IF        CFEETYP=5
            MOVE      NZMCDESC,KEYWORDS          * NZ Private billing
            CALL      MISBW000
.
            MOVE      NZMCMCHG,KEYWORDS
            CALL      MISBW000
          ELSE
            MOVE      MDESC,KEYWORDS
            CALL      MISBW000
.
            MOVE      MCHARGE,KEYWORDS
            CALL      MISBW000
          ENDIF
.
ADDKWD99  RETURN
+
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
MISCL000  PACK      KEY56,KEYINDEX,SP70
          CALL      RSPTMKI1
          CALL      RKPTMKI1
          BRANCH    OVRCD,MISCL999
          MATCH     KEYINDEX,PTMKCODE
          GOTO      MISCL999 IF NOT EQUAL
.
          PACK      KEY56,PTMKCODE,PTMKKKWD,SP70
          CALL      DEPTMKI1
          GOTO      MISCL000
.
MISCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
MISBW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      MISBW000 IF NOT EOS
            GOTO      MISBW999               * entire name if blank
          ENDIF
          PACK      KEY90,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY90,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      MISBW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
MISBW100  MOVE      KEYWORDS,KEY30
          PACK      KEY30,KEY30,SP70
          REP       UPPLOW,KEY30             * Always Upper Case
          PACK      KEY56,KEYINDEX,KEY30
          UNPACK    KEY56,PTMKCODE,PTMKKKWD
          UNPACK    PTMKCODE,PTMKDCLM,KEY23
          CALL      RDPTMKI1
          IF        OVRCD=1
            CALL      WRPTMKI1
          ENDIF
          GOTO      MISBW190
.
.         Check for any more words
.
MISBW190  SETLPTR   KEYWORDS,NINTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY90,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY90,KEYWORDS
          GOTO      MISBW000
.
MISBW999  RETURN
+
          INC       PATHSPIO/INC
          INC       PATMKIIO/INC
          INC       IBAOVRIO/INC
.
PTSMKIEP  ENDPROC                          * End of Procedure
+
