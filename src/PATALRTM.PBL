. *----------------------------------------------------------------------
. * Include Name  :   PATALRTM.PBL
. *
. * Function      :   CGI functions for PATALRFD - patalrtf.dat
. * 
. * Usage         :   Requires PATALRFD - Open on index 1
. *                            PATALRIO
. *                            PATALRTD
. *                            PATALRTM
. * 
. *                   CLRPAR00 CALL CPATAL00
. *
. *                   SETPAR00 
. *
. *                   MATCH     "ptalcatg",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SPACAT00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   MATCH     "ptalcode",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SPACOD00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   Add, Update or delete CALL MVPTAL00
. *
. *                   PROFLD tag CALL ALRITM00
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V10.10.00  08/05/2017  Davin             TSK 0321570
.             Created include
. *----------------------------------------------------------------------
.------------------------------------------------------------------------
. Clear cgi parameter array for patient alert fields
.------------------------------------------------------------------------
CPATAL00  MOVE      ONE,PTCATCNT
          WHILE     PTCATCNT <= 20
            PACK      PTCATARR[PTCATCNT],SP70
            ADD       ONE,PTCATCNT
          DO
          MOVE      ZERO,PTCATCNT
.
          MOVE      ONE,PTCODCNT
          WHILE     PTCODCNT <= 200
            PACK      PTCODARR[PTCODCNT],SP70,SP70,SP70
            ADD       ONE,PTCODCNT
          DO
          MOVE      ZERO,PTCODCNT
.
CPATAL99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for patient alerts category fields
.------------------------------------------------------------
SPACAT00  ADD       ONE,PTCATCNT
          IF        PTCATCNT>20
            GOTO      SPACAT99
          ENDIF
.
          PACK      PTCATARR[PTCATCNT],QRYDATA,SP70
.
SPACAT99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for patient alerts category and code fields
.------------------------------------------------------------
SPACOD00  ADD       ONE,PTCODCNT
          IF        PTCODCNT>200
            GOTO      SPACOD99
          ENDIF
.
          PACK      PTCODARR[PTCODCNT],QRYDATA,SP70,SP70,SP70
.
SPACOD99  RETURN
.
.------------------------------------------------------------
. Tags to output patient alert details
. Usage - XXXWEB01.XX.XXX.AA.B
.
. Tag Parameter 
.davvy     AA = Link Category
.          B  = 1 - Check box option list - Short Description
.               2 - Check box option list - Long Description
.               3 - Selecteion list       - Short Description
.               4 - Selecteion list       - Long Description
.------------------------------------------------------------
ALRITM00  
.davvy    UNPACK    DIM127,D1,PATALRCT,DIM127        * Link Category
          MOVE      ALERT001,PATALRCT
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          BRANCH    F1,ALRITM10,ALRITM20,ALRITM30,ALRITM40
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALRITM99
.
ALRITM10  CALL      ALRBOX00              
          GOTO      ALRITM99
.
ALRITM20  CALL      ALRBOX00
          GOTO      ALRITM99
.
ALRITM30  CALL      ALRSLS00
          GOTO      ALRITM99
.
ALRITM40  CALL      ALRSLS00
          GOTO      ALRITM99
.
ALRITM99  RETURN
.
.------------------------------------------------------------
. Add check box options
.------------------------------------------------------------
ALRBOX00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"ptalcatg#"":
                             " value=#"",PATALRCT,"#">"
.
          PACK      PTCDKEY2,PATALRCT,SP70
          CALL      RDSCODE2
ALRBOX10  CALL      RDKCODE2
          BRANCH    OVRCD,ALRBOX99
.
          MATCH     PATALRCT,TCODE
          GOTO      ALRBOX99 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      ALRBOX10 IF EQUAL
.
          PACK      KEY7,TCODE,ACODE,SP70
.
          MATCH     "I",PTCOACTV
          GOTO      ALRBOX10 IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH    "1",PTCDHOSS
            IF       @EQUAL
              PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
              CALL     RDMLCOD1
              BRANCH   OVRCD,ALRBOX10
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"ptalcode#" ":
                             "id=#"",KEY7,"#" ":
                             "onclick=#"AlrComToggle('",KEY7,"');#" ":
                             "value=#"",TCODE,ACODE,TCINDC9,"#">";
          IF        F1=1
            WRITE     HTMLFILE;TDESC;
          ENDIF
          IF        F1=2
            WRITE     HTMLFILE;PTCDLDES;
          ENDIF
          WRITE     HTMLFILE;"<br>"
.
          GOTO      ALRBOX10
.
ALRBOX99  RETURN
.
.------------------------------------------------------------
. Add Selection options to 
.------------------------------------------------------------
ALRSLS00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"ptalcatg#"":
                             " value=#"",PATALRCT,"#">"
.
          PACK      KEY4,TCODE,SP70
          WRITE     HTMLFILE;"<select name=#"ptalcode#" ":
                             "id=#"",ANSS,KEY4,"#">"
.
          WRITE     HTMLFILE;"<option ":
                             "value=#"",PATALRCT,SP3,"#">":
                             "</option>";
.
          PACK      PTCDKEY2,PATALRCT,SP70
          CALL      RDSCODE2
ALRSLS10  CALL      RDKCODE2
          BRANCH    OVRCD,ALRSLS90
.
          MATCH     PATALRCT,TCODE
          GOTO      ALRSLS90 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      ALRSLS10 IF EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      ALRSLS10 IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH    "1",PTCDHOSS
            IF       @EQUAL
              PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
              CALL     RDMLCOD1
              BRANCH   OVRCD,ALRSLS10
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<option ":
                             "value=#"",TCODE,ACODE,"#">";
.
          IF        F1=3
            WRITE     HTMLFILE;TDESC;
          ENDIF
          IF        F1=4
            WRITE     HTMLFILE;PTCDLDES;
          ENDIF
          WRITE     HTMLFILE;"</option>"
          GOTO      ALRSLS10
.
ALRSLS90  WRITE     HTMLFILE;"</select>"
.
ALRSLS99  RETURN
.

