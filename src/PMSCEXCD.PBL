.----------------------------------------------------------------------
. Program       : PMSCEXCD
.
. Function      : Get the extra contact details for a ur and specific 
.               : contact type from pmscesaf. (First active)
.
. Inputs        : PURNO    = Valid U/R Number
.               : CONTTYPE = Value to match with category tc indicator 1
.                            to get the contact type code
.
. Modifications :
.
. V10.15.01 06/02/2020  Ebon Clements  TSK 0887235
.           Default the postal address to the patiens address if no extra
.           contact postal address exists
. V10.08.01 07/06/2016  Jill Parkinson TSK 0820003
.           Added back in restriction that contact type = 'C' when using
.           patmx1af/patma1af contact details (xcon5000)
.           Also added in Postal Address and PRFA to default to pmi if no
.           contact is set up
. V10.06.01 21/09/2015  Davin             CAR 313906
.           Removed restriction that contact type = 'C' when using
.           patmx1af/patma1af contact details (xcon5000)
. V10.03.02 05/12/2011  Ebon Clements     CAR 248529
.           Fixed test for inactive contacts
. V10.03.01 08/11/2011  Ebon Clements     CAR 248529
.           Added contact type counter
. V10.02.02 20/09/2011  Davin             CAR 248749
.           Mods to not use 2nd contact (if indc3=2)
. V10.02.01 22/06/2011  Steve Armstrong   CAR 240722
.           Mods to handle changes to PMSCEXFD for extended names &
.           first and second given name.
. V10.02.00 07/06/2011  Ebon Clements     CAR 239530
.           Created include
.-------------------------------------------------------------------------------
XCON0000  MATCH     SP70,CONTTYPE                * Exit if no contact type
          GOTO      XCON5000 IF EQUAL
.
          PACK      KEY5,CATTC,SP70
          CALL      RDSCODE1
XCON1000  CALL      RDKCODE1
          BRANCH    OVRCD,XCON5000
.
          MATCH     CATTC,TCODE
          GOTO      XCON5000 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      XCON1000 IF EQUAL
.
          MATCH     "2",TCINDC3          * indc3=2 --> This is 2nd contact only
          GOTO      XCON1000 IF EQUAL
.
          MATCH     CONTTYPE,TCINDC1
          GOTO      XCON2000 IF EQUAL
.
          GOTO      XCON1000

XCON2000  PACK      KEY14,PURNO,ACODE,SP70
          CALL      RSPMCEX1 
XCON2100  CALL      RKPMCEX1 
          BRANCH    OVRCD,XCON5000
.
          MATCH     PMCEURNO,PURNO          * Same U/R
          GOTO      XCON5000 IF NOT EQUAL
.
          MATCH     PMCETYPE,ACODE          * Same Contact Type
          GOTO      XCON5000 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV            * Ignore if inactive
          GOTO      XCON2100 IF EQUAL
.
          GOTO      XCON9999
.
XCON5000  CALL      CLPMSCEX                * Clear extra contact file vars
.
          MATCH     "C",CONTTYPE            * Carer so default to postal or
          GOTO      XCON8000 IF EQUAL       * pmi address if no extra contact
.
          MATCH     "2",CONTTYPE            * Postal address - default to
          GOTO      XCON9000 IF EQUAL       * pmi address if no extra contact
.
          MATCH     "6",CONTTYPE            * PRFA so default to postal or
          GOTO      XCON8000 IF EQUAL       * pmi address if no extra contact
.
          GOTO      XCON9999
.
XCON8000  MATCH     SP70,PTMXADD1           * Use postal address
          GOTO      XCON8500 IF NOT EQUAL   * if populated
.
          GOTO      XCON9000
.
XCON8500  MOVE      PTITL,PMCETITL         * Using postal address and
          MOVE      PTMASNAM,PMCESNAM      * patient details
          MOVE      PMPXFNAM,PMCEGNAM
          MOVE      PMPXSNAM,PMCEGNM2
.
          MOVE      PTMXADD1,PMCEADD1
          MOVE      PTMXADD2,PMCEADD2
          MOVE      PTMXADD3,PMCEADD3
          MOVE      PTMXADD4,PMCEADD4
          MOVE      PTMXPOST,PMCEPOST
.
          MOVE      PTELEP,PMCETELP
          MOVE      PTELEB,PMCETELB
          MOVE      PTMXCELL,PMCETELM
          MOVE      SP70,PMCERELP
          MOVE      PMPXPEML,PMCEEMAI
          MOVE      SP70,PMCECONT
          GOTO      XCON9999
.
XCON9000  MOVE      PTITL,PMCETITL          * Use all patient details
          MOVE      PTMASNAM,PMCESNAM
          MOVE      PMPXFNAM,PMCEGNAM
          MOVE      PMPXSNAM,PMCEGNM2
.
          MOVE      PADD1,PMCEADD1
          MOVE      PADD2,PMCEADD2
          MOVE      PSUBRB,PMCEADD3
          MOVE      PADD4,PMCEADD4
          MOVE      PPOST,PMCEPOST
.
          MOVE      PTELEP,PMCETELP
          MOVE      PTELEB,PMCETELB
          MOVE      PTMXCELL,PMCETELM
          MOVE      SP70,PMCERELP
          MOVE      PMPXPEML,PMCEEMAI
          MOVE      PCONT,PMCECONT
          GOTO      XCON9999
.
XCON9999  RETURN

