.******************************************************************************
.* Include Name   : DISPITEM.PBL 
.* Routines       : DISPITEM, FORMATRD, SELECTED
.* Modifications  : 19/10/1992    Justin Coates
.*                  made sure dispitem didn't clear VAR after displaying it
.*                  so any print screen programs will work
.*                  22/12/1994    Whirlpool    
.*                  Var needed to be cleared so now sets up printvar for the
.*                  print screen programs 
.******************************************************************************
.*********************************************************************
.         Display an Item
.         Para's  : VAR      to contain the variable to display
.                   SCRPSTFD record
.                   PRINTVAR to contain variable to print
.*********************************************************************
DISPITEM  MOVE      SCPSROW,CVERT       * Setup Display Position
          MOVE      SCPSCOL,CCOL 
          MOVE      SCPSLEN,LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      DISITM99 IF EQUAL
          MATCH     SP70,SCPSFMT
          IF        !@EQUAL
            MATCH     SP70,VAR
            IF        !@EQUAL
              CALL      FORMATRD
            ENDIF
          ENDIF
          PACK      DIM2,SP1,SCPSATT
          PACK      ATT,SP70,SP70
          REP       DIM2,ATT
          SETLPTR   ATT,LLCNT
          SETLPTR   VAR,LLCNT
.
          SUB       ONE,CCOL
          RESET     LABELDAT[CVERT],CCOL
          RESET     LABELATT[CVERT],CCOL
          APPEND    VAR,LABELDAT[CVERT]
          APPEND    ATT,LABELATT[CVERT]
          SETLPTR   LABELDAT[CVERT]
          SETLPTR   LABELATT[CVERT]
          ENDSET    LABELDAT[CVERT]
          ENDSET    LABELATT[CVERT]
          RESET     LABELDAT[CVERT]
          RESET     LABELATT[CVERT]
....          PUTVAR    VAR,ATT,CCOL,CVERT
.
DISITM99  RETURN
.--------------------------------
. Format a Field - Round
.--------------------------------
FORMATRD  STRIP     VAR
          MOVELPTR  VAR,BEFDECP
          MOVELPTR  VAR,AFTDECP
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECP
            SUB       BEFDECP,AFTDECP
            SUB       ONE,BEFDECP
          ELSE  
            MOVE      ZERO,AFTDECP
          ENDIF
          NFORMAT   FORM21,BEFDECP,AFTDECP
          RESET     VAR
          STRIP     VAR
          MOVE      VAR,FORM21
.
          MOVE      SCPSFMT,VAR
          STRIP     VAR
          MOVELPTR  VAR,BEFDECPF
          MOVELPTR  VAR,AFTDECPF
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECPF
            SUB       BEFDECPF,AFTDECPF
            SUB       ONE,BEFDECPF
          ELSE  
            MOVE      ZERO,AFTDECPF
          ENDIF
          NFORMAT   FORMAT21,BEFDECPF,AFTDECPF
          MOVE      FORM21,FORMAT21
.
          ENDSET    VAR
          RESET     VAR
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            STRIP     VAR
            ENDSET    VAR
            APPEND    SP1,VAR
            RESET     VAR
          ELSE 
            STRIP     VAR
          ENDIF
          FEDIT     FORMAT21,VAR
.
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            RESET     VAR,LLCNT
            CMOVE     ")",VAR
            ENDSET    VAR
            RESET     VAR
          ENDIF
          RETURN
+
.--------------------------------------------
. Get Selected Item's screen position Details
.--------------------------------------------
SELECTED  PACK     KEY20,PRGID,SCRID,SCSLITM,SCSLROW,SCSLCOL
          MOVE     ZERO,EXIT
          CALL     RDSCPS2
          IF       OVRCD=1
            MOVE     ONE,EXIT
          ENDIF
          MOVE     SCPSCOL,CCOL
          MOVE     SCPSROW,CVERT
          RETURN
..........................................................................
