.******************************************************************************
.*  File          :  PAT109MN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.9 Coding Maintenance Routines for PATWEB88      *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.        V11.02.01  19/07/2022  Bella Turco    TSK 0921957                    *
.                   Added NOMORE00 - Display "No more records" at end of list *
.                   Added PTCNNRTD - Displays number of rows set by parameter *
.                   value                                                     *
.        V10.06.00  17/03/2015  Davin           CAR 311669   HDP 2015/2016    *
.                   Created - cloned from PAT108MN.PBL                        *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 9 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD9DI00  RESET     PTICD001
          MOVE      "9",ICD10EDN
.                                           * make sure files are open
          OPEN      PATI19D1,"pat10d9f"     * Open ICD10 Ed.9 Diseases
          OPEN      PATK9DA1,"patkcd9f"     * ICD10 Ed.9 Key Word Diag.
          OPEN      PATK9DA2,"patkcd9f"     * ICD10 Ed.9 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD9DI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD9DI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD9DI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD9DI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VAL9CD00
            GOTO      ICD9DI99
          ENDIF
.
          GOTO      ICD9DI93
.
ICD9DI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD9DI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT19D1
          COMPARE   ZERO,OVRCD
          GOTO      ICD9DI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT19D1      * Writes away the data
          PROC      PATK9DUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD9DI99
.
ICD9DI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT19D1
          BRANCH    OVRCD,ICD9DI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICD9DI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT19D1      * Writes away the data
          PROC      PATK9DUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD9DI99
.
ICD9DI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT19D1
          BRANCH    OVRCD,ICD9DI91
          CALL      DEPT19D1      * Deletes the data
          PROC      PATK9DUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD9DI99
.
ICD9DI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT19D1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 9 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD9DI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD9DI99
.
ICD9DI91  MOVE      "ICD10 Edition 9 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9DI99
.
ICD9DI92  MOVE      "ICD10 Edition 9 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9DI99
.
ICD9DI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9DI99
.
ICD9DI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 9 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD9OP00  RESET     PTICO001
          MOVE      "9",ICD10EDN
.
          OPEN      PATI19O1,"pat10o9f"     * Open ICD10 Ed.9 Operations
          OPEN      PATK9OA1,"patkco9f"     * ICD10 Ed.9 Key Word Proc.
          OPEN      PATK9OA2,"patkco9f"     * ICD10 Ed.9 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD9OP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD9OP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD9OP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD9OP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VAL9CD00
            GOTO      ICD9OP99
          ENDIF
.
          GOTO      ICD9OP93
.
ICD9OP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD9OP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT19O1
          COMPARE   ZERO,OVRCD
          GOTO      ICD9OP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT19O1      * Writes away the data
          PROC      PATK9OUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICD9OP99
.
ICD9OP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT19O1
          BRANCH    OVRCD,ICD9OP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICD9OP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT19O1      * Writes away the data
          PROC      PATK9OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD9OP99
.
ICD9OP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT19O1
          BRANCH    OVRCD,ICD9OP91
          CALL      DEPT19O1      * Deletes the data
          PROC      PATK9OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD9OP99
.
ICD9OP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT19O1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 9 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD9OP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD9OP99
.
ICD9OP91  MOVE      "ICD10 Edition 9 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9OP99
.
ICD9OP92  MOVE      "ICD10 Edition 9 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9OP99
.
ICD9OP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD9OP99
.
ICD9OP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 9 Code Validation
.------------------------------------------------------------------------------
VAL9CD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VAL9CD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VAL9CD10,VAL9CD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VAL9CD90
.
VAL9CD10  MOVE      VALDCODE,KEY9
          CALL      RDPT19O1
          IF        OVRCD=0
            MOVE      PT0ODSC2,VALDNAME
          ENDIF
          GOTO      VAL9CD90
.
VAL9CD20  MOVE      VALDCODE,KEY9
          CALL      RDPT19D1
          IF        OVRCD=0
            MOVE      PT0DDSC2,VALDNAME
          ENDIF
          GOTO      VAL9CD90
.
VAL9CD90  CALL      OPENHTML
          BRANCH    EXIT,VAL9CD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VAL9CD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 9 Procedure Code: Search Table
.-----------------------------------------------------------
SPT9OA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV9,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.9 in use yet?
....      GOTO      SPT9OA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT9O2
SPT9OA10  CALL      RKPT9O2
          BRANCH    OVRCD,SPT9OA90
.
          CALL      SPT9OB00
          BRANCH    OVRCD,SPT9OA10,SPT9OA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT19O1
          BRANCH    OVRCD,SPT9OA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT9OA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT9OA20,SPT9OA30
          GOTO      SPT9OA99
.
SPT9OA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
          GOTO      SPT9OA80
.
SPT9OA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODSC2:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
SPT9OA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT9OA10 IF NOT EQUAL
          GOTO      SPT9OA99
.
SPT9OA90  CALL      NOMORE00      * Display No More Records Message
.
SPT9OA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 9 Procedure Record
.------------------------------------------------------------
SPT9OB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPT9OB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPT9OC00
            BRANCH    EXIT,SPT9OB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT9O2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT9OB91 IF EQUAL
.
          GOTO      SPT9OB90
.
SPT9OB90  MOVE      ZERO,OVRCD
          GOTO      SPT9OB99
SPT9OB91  MOVE      ONE,OVRCD
          GOTO      SPT9OB99
SPT9OB92  MOVE      TWO,OVRCD
SPT9OB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 9 Procedure Words
.------------------------------------------------------------
SPT9OC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPT9OC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT9OC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPT9O1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT9OC10 IF EQUAL
.
          CALL      RKPT9O1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT9OC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT9OC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT9OC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT9OC10              * Check Next Search Word
.
SPT9OC90  MOVE      ONE,EXIT
SPT9OC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 9 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPT9DA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV9,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.9 in use yet?
....      GOTO      SPT9DA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT9D2
SPT9DA10  CALL      RKPT9D2
          BRANCH    OVRCD,SPT9DA90
.
          CALL      SPT9DB00
          BRANCH    OVRCD,SPT9DA10,SPT9DA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT19D1
          BRANCH    OVRCD,SPT9DA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT9DA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT9DA20,SPT9DA30
          GOTO      SPT9DA99
.
SPT9DA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
          GOTO      SPT9DA80
.
SPT9DA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDSC2:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
SPT9DA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT9DA10 IF NOT EQUAL
          GOTO      SPT9DA99
.
SPT9DA90  CALL      NOMORE00        * Display No More Records Message
.
SPT9DA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 9 Diagnosis Record
.------------------------------------------------------------
SPT9DB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPT9DB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPT9DC00
            BRANCH    EXIT,SPT9DB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT9D2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT9DB91 IF EQUAL
.
          GOTO      SPT9DB90
.
SPT9DB90  MOVE      ZERO,OVRCD
          GOTO      SPT9DB99
SPT9DB91  MOVE      ONE,OVRCD
          GOTO      SPT9DB99
SPT9DB92  MOVE      TWO,OVRCD
SPT9DB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 9 Diagnosis Words
.------------------------------------------------------------
SPT9DC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPT9DC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT9DC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPT9D1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT9DC10 IF EQUAL
.
          CALL      RKPT9D1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT9DC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT9DC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT9DC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT9DC10              * Check Next Search Word
.
SPT9DC90  MOVE      ONE,EXIT
SPT9DC99  RETURN
