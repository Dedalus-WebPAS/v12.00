.************************************************************************
.*  Include  : DGCLICCP  -  Datagate Patient Pre-Admission Change       *
.*                                                                      *
.*  Author   : Steve Armstrong  (27/05/2004)                            *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      * 
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V11.00.01 07/12/2020  Davin             TSK 0890602                 *
.*            Allow broadcast of A08 from HL7RECVR when ptcngosm=1      *
.************************************************************************
.*  V10.03.03 10/12/2013  Davin S          CAR 295110                   *
.*            Mods to only trigger hl7 update if 'create' preadmission  *
.*            parameter (ptcna14m) is also set to yes.                  *
.*  V10.03.02 14/11/2013  Steve Armstrong  CAR 294040                   *
.*            Mods to only write to watqueaf for proprietary messages.  *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.02.02 01/08/2011  Steve Armstrong   CAR 247602                  *
.*            Mods to not populate watqueaf for HL7 messages.           *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.************************************************************************
          DEFPROC   DGCLICCP
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       MEHLERFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
          INC       WATQUEFD/INC
          INC       WATTX1FD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND10;*239,PTCNDGCP
          READ      CONTROLF,HUND10;*104,PTCNA08M,*108,PTCNA14M
          READ      CONTROLF,HUND28;*207,PTCNGOSM
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA08M               * yes - sending A08 ?
            IF        @EQUAL
              COMPARE   ONE,PTCNA14M             * yes - sending A14 ?
              IF        @EQUAL
                MOVE      ONE,CISMFLAG           * yes- set flag for CIS message
              ENDIF
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGCP <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI500 IF NOT EQUAL        * no - continue
.
          MATCH     "1",PTCNGOSM                 * allow broadcast from SIU ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
DGCLI500  MOVE      ZERO,IBAMFLAG
          IF        PTCNDGCP = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
          OPEN      WATQUEA1,"watqueaf"
.
          CALL      DGMESSID                     * Get Message ID
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21                     * get pmspx2af details
          IF        OVRCD = 1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          MOVE      FORM1,CSALIAS
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
.
.         Save admission fields to transfer fields as no transfer record
.         exists for preadmissions.
.
          MOVE      PTMIXWRD,TWARD
          MOVE      PTMIEBED,TBED
          MOVE      ATYPE,TATYPE
          MOVE      ADOCTA,TADOCT
          MOVE      ACLSSFT,TDEPT
          MOVE      SP3,MHLRCODE
          PACK      PMQVSPAR,SP70
          CALL      WRPMQVI1
.
.         Read bokrc1af record using key
.
          MOVE      AADMNO,KEY8
          CALL      RDBKREC1                     * booking record found ?
          IF        OVRCD = 1
            CALL      CLWATTR1                   * no - don't get wait vars
          ELSE
.
.           Get wattr1af record
.
            PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
            CALL      RDTREA1                    * read wattr1af
            IF        OVRCD = 1
              CALL      CLWATTR1                 * no - clear variables
            ENDIF
          ENDIF
.
.         Write a record to watqueaf (holding W/L details) only
.         where proprietary messages are turned on.
.
          IF        IBAMFLAG = 1
            MOVE      MESSAGID,WTQUMESI
            PACK      WTQUSPAR,SP30,SP30
            CALL      WRWTQUE1
          ENDIF
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A08 message?
          IF        @EQUAL
            MOVE      "A08",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGCP                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CCP",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
          CLOSE     WATQUEA1
.
DGCLI999  GOTO      DGCLIEND
+
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
          INC       CLWATTR1
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
+
