.************************************************************************
.*  Procedure: DGCLICOB  -  Send Change in O/P Booking Details          *
.*                                                                      *
.*  Author   : Steve Armstrong  15/11/2001                              *
.*             (Copied from DGCLICON)                                   *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*             OUTBOAFD (Outpatients Clinic Session Booking)            *
.*             OUTBB1FD (Outpatients Clinic Session Booking Table 2)    *
.*                                                                      *
.*  Requirement removed (V9.02.01)                                      *
.*             PATCGPFD (Registered GP/Practice Changes)                *
.*             PATNIDFD (Patient National ID Number)                    *
.*             NHIMASFD (NZ Health Interface)                           *
.*             PMIGTNID.PBL (Get the National ID Number)                *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V12.00.01 14/05/2025  Ebon Clements    TSK 0955096                  *
.*            Alphanueric visit number changes                          *
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V11.00.01 06/11/2020  Davin             TSK 0890602                 *
.*            Allow broadcast of A08 from HL7RECVR when ptcngosm=1      *
.************************************************************************
.*  V10.15.01 13/11/2019  Davin             TSK 0861253                 *
.*            Added OUTTHIFD/CLOUTTHI/outthiaf for telehealth fields    *
.************************************************************************
.*  V10.04.01 24/03/2014  Steve Armstrong   CAR 298483                  *
.*            Mods to not send a COB API message where this is a        *
.*            Reschedule message from trigger 31.                       *
.************************************************************************
.*  V10.03.02 06/06/2013  Steve Armstrong   CAR 268961                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.05.01   10/01/2006  Steve Armstrong     CAR 88829                *
.*             Added HL7 messages                                       *
.************************************************************************
.*  V9.02.02 : 14/02/2002  Davin                                        *
.*             Save ur number & visit number                            *
.*  V9.02.01   29/11/2001  Davin                                        *
.*             Mods to use message holding file                         *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICOB
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       OUTQUEFD/INC
          INC       OUTTHIFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,THIRTY2;*107,OTCNDGOB
          READ      CONTROLF,THIRTY2;*119,OTCNA08M
          READ      CONTROLF,HUND28;*207,PTCNGOSM
.                                    
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,OTCNA08M               * yes - sending A08 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF   
          ENDIF
.   
.         Check if we are sending any messages.
.         
          IF        OTCNDGOB <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI500 IF NOT EQUAL        * no - continue
.
          MATCH     "1",PTCNGOSM                 * allow broadcast ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.         
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.         
DGCLI500  MOVE      ZERO,IBAMFLAG 
          IF        OTCNDGOB = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF     
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      OUTQUEA1,"outqueaf"
          OPEN      OUTTHIA1,"outthiaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to outqueaf (holding visit details)
.
          MATCH    ZEROVISN,OBAOUTNO
          IF       @EQUAL
            CALL      CLOUTTHI
          ELSE
            PACK      KEY8,OBAOUTNO,SP70         * Outpatient Telehealth Info
            CALL      RDOTTHI1
            IF        OVRCD=1
              CALL      CLOUTTHI
            ENDIF
          ENDIF
.
          MOVE      MESSAGID,OTQUMESG            
          MOVE      SP20,OTQUSPAR
          CALL      WROTQUE1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A08 message?
          IF        @EQUAL
            MOVE      "A08",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,OTCNDGOB                 * sending proprietary message?
          GOTO      DGCLI900 IF NOT EQUAL        * no
.
          MATCH     "OUTWEB02",PRGID             * OUTWEB02 triggering ?
          GOTO      DGCLI800 IF NOT EQUAL        * no
.
.         Check for specific trigger points for where we are sending
.         A05 messages instead of an A08
.
          MATCH     SP8,HL7INCLD
          GOTO      DGCLI800 IF NOT EQUAL
.
.         Check if this is a reschedule message trigger and if it is,
.         then we will be sending a COR message elsewhere (LOGR0000),
.         so we dont need to send a COB here
.
          COMPARE   THIRTY1,HL7TRGID
          GOTO      DGCLI999 IF EQUAL
.
DGCLI800  MOVE      "COB",H7INMETP
          CALL      WRINB000                     * write hl7inbaf record
.
DGCLI900  CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     OUTQUEA1
          CLOSE     OUTTHIA1
.
DGCLI999  GOTO      DGCLIEND                * end procedure
.
.      I/O's go in here
.
          INC       BRHLCOMN
          INC       CLOUTTHI
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       OUTQUEIO/INC
          INC       OUTTHIIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
