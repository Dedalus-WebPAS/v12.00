.**************************************************************************
.*                             PRNTENQR              Called by:           *
.*              Enquiry on all available printers                         *
.*                                                                        *
.**************************************************************************
.
PREQ0000  MOVE      ONE,CPAGENO
          MOVE      SEVEN,CVERT
          MOVE      FIVE,CCOL
          MOVE      ZERO,FIELD
.
          DISPLAY   *P40:4,*EF
          PACK      KEY3,SP3
          DISPLAY   *P5:6,"Code",SP2,"Printer"
          CALL      RDSPRTA1                    * positional read
PREQ1000  CALL      RDKPRTA1                    * read next record
          BRANCH    OVRCD,PREQ5000              * end of file ?
.
          ADD       ONE,CVERT
          COMPARE   TWENTY2,CVERT
          IF        @EQUAL
            COMPARE   FIFTY,CCOL                * page full ?
            IF        @EQUAL
              GOTO      PREQ4000
            ELSE
              MOVE      FIFTY,CCOL
              MOVE      SEVEN,CVERT
              DISPLAY   *P50:6,"Code",SP2,"Printer"
              GOTO      PREQ1000
            ENDIF
          ELSE
            GOTO      PREQ3000
          ENDIF
.
PREQ3000  ADD       ONE,FIELD
          DISPLAY   *PCCOL:CVERT,*V2LON,PRTCODE,SP3,*HOFF,PRTDESC
          PACK      TMPKE2[FIELD],PRTCODE
          GOTO      PREQ1000
.
.  new page needed
.
PREQ4000  BRANCH    CPAGENO,PREQ4500
.
.  middle pages  :  Next & First options
.
PREQ4100  MOVE      "1",CALLPOSN
          CALL      KEYC0000
          BRANCH    EXIT,PREQ9999:              * exit
                         PREQ9999:              * search again
                         PREQ7000:              * next
                         PREQ8000               * previous
          GOTO      KEYC0000
.
.  first page  :  Next option
.
PREQ4500  MOVE      "2",CALLPOSN
          CALL      KEYC0000
          BRANCH    EXIT,PREQ9999:              * exit
                         PREQ9999:              * search again
                         PREQ7000:              * next
                         PREQ5900               * previous
          GOTO      KEYC0000
.
.  end of data 
.
PREQ5000  BRANCH    CPAGENO,PREQ5500
.
.  middle pages  :  First option
.
PREQ5100  MOVE      "3",CALLPOSN
          CALL      KEYC0000
          BRANCH    EXIT,PREQ9999:              * exit
                         PREQ9999:              * search again
                         PREQ5900:              * next
                         PREQ8000               * previous
          GOTO      KEYC0000
.
.  first page  :  No options
.
PREQ5500  MOVE      "4",CALLPOSN
          CALL      KEYC0000
          BRANCH    EXIT,PREQ9999:              * exit
                         PREQ9999:              * search again
                         PREQ5900:              * next 
                         PREQ5900               * previous
          GOTO      KEYC0000
.
PREQ5900  BEEP
PREQ5910  BRANCH    CALLPOSN,PREQ4100,PREQ4500,PREQ5100,PREQ5500
.
.   number entered
.
PREQ7000  MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          ADD       ONE,CPAGENO
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD
          GOTO      PREQ3000
.
.   previous screen selected
.
PREQ8000  MOVE      TMPKE2[1],KEY3
          CALL      RDPRTA1                    * reread first record on screen
          MOVE      ZERO,FORM2                 * initialise count
          WHILE     FORM2 <= TWENTY8
            CALL      RDPPRTA1                 * read previous record
            ADD       ONE,FORM2                * increment count
          DO
.
          MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          SUB       ONE,CPAGENO
          MOVE      ZERO,FIELD
          DISPLAY   *P1:CVERT,*EF
          GOTO      PREQ3000
.
PREQ9999  RETURN



KEYC0000  DISPLAY   *P1:24,*EF,"e(",*V2LON,ANSX,*HOFF,")it, ":
                    "(",*V2LON,ANSS,*HOFF,")elect Printer";
          MOVE      TWENTY3,CCOL
          IF        CALLPOSN = 1|CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1|CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :  ";
          ADD       FIVE,CCOL
.
KEYC1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*JR,DIM1:
                    *PCCOL:24,*DV,DIM1
          UNPACK    DIM1,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS                     * exit selected ?
          GOTO      KEYC9100 IF EQUAL
.
          MATCH     ANSS,ANS                     * search again selected ?
          GOTO      KEYC9200 IF EQUAL
.
          MATCH     ANSN,ANS                     * next selected ?
          GOTO      KEYC9300 IF EQUAL
.
          MATCH     ANSR,ANS                     * previous selected ?
          GOTO      KEYC9400 IF EQUAL
.
KEYC1500  BEEP
          GOTO      KEYC1000
.
KEYC9100  MOVE      ONE,EXIT                     * exit
          GOTO      KEYC9999
.
KEYC9200  MOVE      TWO,EXIT                     * search again
          GOTO      KEYC9999
.
KEYC9300  MOVE      THREE,EXIT                   * next
          GOTO      KEYC9999
.
KEYC9400  MOVE      FOUR,EXIT                    * previous
          GOTO      KEYC9999
.
KEYC9999  RETURN
+













