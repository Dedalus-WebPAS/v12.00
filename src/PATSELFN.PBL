.
. PATSELFN.PBL
. ------------
.        V9.03.01 08/08/2003  Lina Vo          CAR 41196
.                 Changed proc to use Common codes rather than control fields
.                 and ibasmaaf for system report options.
.        V5.01.01 20/11/89 J.Tan
.                 Add other financial option.  SRF 102252
.        V5.01.02 11/04/1994  Steve Armstrong
.                 Changed PATCODDS to PATCODDS and standardised error
.                 messages
.        V5.03.01 05/06/1995  Paul Howells     SRF 136735
.                 Use SYS98 parameters & IBAUSEFD (not SECNUMB2).    
.
.        This include is used to select the financial option desired. It
.        only displays the options that are switched on at the system level.
.        If only one system is switched on AND it is not outpatients, then
.        this routine will return with a success, doing a full report. This
.        will mean that someone who is running only Inpatients will not be
.        aware that this include was executed at all.
.
.        DATA INCLUDES NEEDED  :   IBASECFD/INC     Program Security file
.                                  IHAPASS/INC      User Security file
.                                  OUTSITFD/INC     Outpatient Site file
.                                  PATCODFD/INC     Codes file
.                                  PATSELDT/INC     Variables used by this inc.
.
.        OPEN FILES NEEDED     :   OUTSITA1         1st index of site file
.                                  PATCODE1         1st index of codes file
.                                  CONTROLF         Control file
.
.        DATA RETURNED         :   FSTSYS           Starting system (0-9)
.                                  FEDSYS           Ending system (0-9)
.
.                                  FSTSITE          Starting site (SP6-ZZZZZZ)
.                                  FEDSITE          Ending site (SP6-ZZZZZZ)
.
.                                  FSTDEPT          Starting Out Dept (SP3-ZZZ)
.                                  FEDDEPT          Ending Out Dept (SP3-ZZZ)
.
.                                  EXIT             Exit flag
.                                                   0 = Okay to run report
.                                                   1 = No report selected
.
.        ======================================================================
.        PATSELFN : Main code for include
.        ======================================================================
.
PATSELFN PROC      SLFN0000
         RETURN
.
.        ======================================================================
.        Procedure SLFN0000                  
.        ======================================================================
         DEFPROC   SLFN0000
.
         INC       COMCODFD/INC
         INC       IBASMAFD/INC
         INC       IBAUSEFD/INC
.
CODEDATE DIM    8
STCLM    DIM    3
.
         OPEN      IBAUSEA1,"ibauseaf"
         OPEN      COMCODA1,"comcodaf"
.
.
SLFN1000 CALL      SFINI000                      * Initialise variables
.
.        Display and select the options
.
         CALL      DSPFN000                      * Display options
         BRANCH    EXIT,SLFN9999                 * No option selected
.
         COMPARE   TWO,FSTSYS                    * Check for outpatient
         GOTO      SLFN5000 IF EQUAL
         GOTO      SLFN8000                      * Go check security levels
.
.        Display and select the outpatient options
.
SLFN5000 CALL      GETOP000                      * Get and validate option
         BRANCH    EXIT,SLFN9999                 * Nothing selected
.
.        Option selected. Validate security codes
.
SLFN8000 CALL      VLSEC000                      * Validate security codes
         BRANCH    EXIT,SLFN1000                 * Security not sufficient
.
SLFN9000 MOVE      ZERO,EXIT                     * Valid selection
         GOTO      SLFN9999
.
SLFN9999 GOTO      ENDSLFN
.
.
.
.        ======================================================================
.        SFINI000 : Initialise the variables, and determine the number of
.                   systems that are available to be reported on.
.        ======================================================================
.
SFINI000 MOVE      ZERO,FSTSYS
         MOVE      NINE,FEDSYS                   * Default system range 0-9
.
         MOVE      SP6,FSTSITE                   * Default site range,
         MOVE      "ZZZZZZ",FEDSITE              * '      ' to 'ZZZZZZ'
.
         MOVE      SP3,FSTDEPT                   * Default dept range,
         MOVE      "ZZZ",FEDDEPT                 * '   ' to 'ZZZ'
.
         CALL      IBACLOCK
.
SFINI999 RETURN
.
.        ======================================================================
.        DSPFN000 : Display the available systems
.        ======================================================================
.
DSPFN000 MOVE     SP3,STCLM
         DISPLAY   *P1:4,*EF:                    * Clear screen
                   *P1:4,"Enter Report Type  : ",UNDLN3
.
         PACK     CMCDDESC,SP70
         MOVE     FOUR,EVERT
         MOVE     TWENTY2,ECOL
         MOVE     ZERO,CKYIMAND                 * mandatory
         MOVE     XCDATE,CODEDATE
         REP      " 0",CODEDATE
         MOVE     "RP",CODE
         CALL     COMCODKY
.
         MOVE     CMCDACOD,STCLM
.
         MATCH    SP3,STCLM
         GOTO     DSPFN999 IF EQUAL
.
         DISPLAY  *P40:4,*V2LON,CMCDDESC
.
         MOVE     ZERO,F1
         MOVE     CMCDSI01,F1
         IF       F1>0
           MOVE     F1,FSTSYS
           MOVE     F1,FEDSYS             
         ENDIF
.
DSPFN999 RETURN
.
.        ======================================================================
.        GETOP000 : Keyin the outpatient option desired by the user
.        ======================================================================
.
GETOP000 DISPLAY   *P1:6,*EF:
                   *P1:6,*V2LON,ZERO,*HOFF," = Exit":
                   *P1:7,*V2LON,ONE,*HOFF," = All Outpatient Depts":
                   *P1:8,*V2LON,TWO,*HOFF," = Single Outpatient Dept":
                   *P1:9,*V2LON,THREE,*HOFF," = Single Outpatient Site" 
         KEYIN     *P1:11,"Option : ",*EF,*DV,UNDLN1:
                   *P10:11,*V2LON,FORM1
.
         COMPARE   ZERO,FORM1                    * Check if anything selected
         GOTO      GETOP900 IF EQUAL             * No
.
         BRANCH    FORM1,GETOP800:               * All outpatient depts
                         GETOP100:               * Single dept
                         GETOP500                * Single site
.
         BEEP
         GOTO      GETOP000
.
.        Single outpatient department selected
.
GETOP100 
GETOP110 DISPLAY   *P1:13,*EF,"Enter Department Code : ",UNDLN3;
.
         MOVE      TEN3,EVERT
         MOVE      TWENTY5,ECOL
         MOVE      ZERO,CKYIMAND                 * mandatory
         MOVE      CODEO,CODE                    * Category for display
         CALL      PATCODKY                      * Display available codes
   
         ENDSET    ACODE
         APPEND    SP3,ACODE
         RESET     ACODE
.
.        Check if anything was entered
.
         MATCH     SP3,ACODE
         GOTO      GETOP000 IF EQUAL
.
         DISPLAY   *P40:13,*V2LON,TDESC       * Display the description
         MOVE      ACODE,FSTDEPT                 * Starting dept = ACODE
         MOVE      ACODE,FEDDEPT                 * Ending dept = ACODE
.
         GOTO      GETOP800
.
.        No code was entered
.
.        Single outpatient site selected
.
GETOP500 
GETOP510 KEYIN     *P1:13,*EF,"Enter Site Code : ",*DV,UNDLN6:
                   *P19:13,*V2LON,OSTSITE:
                   *P19:13,*DV,OSTSITE
.
         ENDSET    OSTSITE
         APPEND    SP3,OSTSITE
         RESET     OSTSITE
.
.        Check to see if anything was entered
.
         MATCH     SP6,OSTSITE
         GOTO      GETOP000 IF EQUAL
.
.        Validate code
.
         MOVE      OSTSITE,KEY6
         CALL      RDSITA1
         BRANCH    OVRCD,GETOP550                * Invalid code
.
         DISPLAY   *P40:13,*V2LON,OSTDESC     * Display the description
         MOVE      OSTSITE,FSTSITE               * Starting site = OSTSITE
         MOVE      OSTSITE,FEDSITE               * Ending site = OSTSITE
.
         MOVE      OSTSYST,FSTDEPT               * Starting dept = OSTSYST
         MOVE      OSTSYST,FEDDEPT               * Ending dept = OSTSYST
.
         GOTO      GETOP800
.
.        Invalid code
.
GETOP550 DISPLAY   *P1:24,*EL,*B,"Invalid Code.  ";
         CALL      HITENTER
         GOTO      GETOP510
.
.        A Valid option has been selected
.
GETOP800 MOVE      ZERO,EXIT
         GOTO      GETOP999
.
.        No option selected
.
GETOP900 MOVE      ONE,EXIT
.
GETOP999 RETURN
.
.        ======================================================================
.        VLSEC000 : Validate the security levels for the options selected
.        ======================================================================
.
VLSEC000 MOVE      PASSCODE,KEY4                 * Get security code
         CALL       OPPASS1           * Open the security file
         CALL      RDPASS1                       * Read the password file
         CALL       CLPASS1                      * Close the security file
         BRANCH    OVRCD,VLSEC900                * No record - assume not valid
.
.        Get the programs security record
.
         MOVE      PRGID,KEY8                    * Get the program name
         OPEN      IBASECU1,"ibasecuf"           * Open the security file
         CALL      RDSECU1                       * Read the password file
         CLOSE     IBASECU1                      * Close the security file
         BRANCH    OVRCD,VLSEC800                * No record - assume valid
.
         COMPARE   ZERO,PSECLEV                  * Zero security level means
         GOTO      VLSEC800 IF EQUAL             * everyone can run program
.
.        Check if the user has permission on patient billing menu
.
         COMPARE   ONE,FUBIL                     * Check patient billing is on
         GOTO      VLSEC100 IF NOT EQUAL         * No
.
         CALL      VLBIL000                      * Check patient billing menu
         BRANCH    EXIT,VLSEC100                 * Not valid
.
         GOTO      VLSEC800                      * Valid. Selection okay
.
.        Check what option was selected
.
VLSEC100 BRANCH    FSTSYS,VLSEC200,VLSEC400,VLSEC300,VLSEC800
.
.        A combined report was selected. Check each valid system
.
         CALL      VLAAE000                      * Check A + E
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLINP000                      * Check Inpatients
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLOUT000                      * Check General Outpatients
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLMAM000                      * Check Mammography
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLRAD000                      * Check Radiology
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
.        An A + E report selected
.
VLSEC200 CALL      VLAAE000                      * Check A + E
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
.        An Inpatient report selected
.
VLSEC300 CALL      VLINP000                      * Check Inpatient
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
.        An Outpatient report selected
.
VLSEC400 MATCH     SP3,FSTDEPT                   * Check if all depts
         GOTO      VLSEC500 IF NOT EQUAL         * No
.
.        An all outpatient report was selected
.
         CALL      VLOUT000                      * Check General Outpatients
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLMAM000                      * Check Mammography
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         CALL      VLRAD000                      * Check Radiology
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
.        A single outpatient department was selected
.
VLSEC500 PACK      KEY5,CODEO,FSTDEPT            * Get the code file record
         CALL      RDCODE1                       * Read in the record
         BRANCH    OVRCD,VLSEC900                * Assume not valid
.
         BRANCH    TCFORM6,VLSEC510:             * General outpatients
                           VLSEC520:             * Mammography
                           VLSEC530              * Radiology
.
VLSEC510 CALL      VLOUT000                      * Check General Outpatients
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
VLSEC520 CALL      VLMAM000                      * Check Mammography
         BRANCH    EXIT,VLSEC900                 * Not valid
.
         GOTO      VLSEC800                      * Selection valid
.
VLSEC530 CALL      VLRAD000                      * Check Radiology
         BRANCH    EXIT,VLSEC900                 * Not valid
.
.        Selection validated
.
VLSEC800 MOVE      ZERO,EXIT
         GOTO      VLSEC999
.
.        Selection not valid for this user
.
VLSEC900 MOVE      ONE,EXIT
         DISPLAY   *P1:24,*EL,*B,"Security Level Insufficient ":
                   "to Run this Selection.  ";
         CALL      HITENTER
.
VLSEC999 RETURN
.
.        ======================================================================
.        VLBIL000 : Check security for patient billing
.        ======================================================================
.
VLBIL000 BRANCH    FUBIL,VLBIL100                * Check if system switched on
         GOTO      VLBIL800                      * System not available
.
.        Check the security level against the program security level
.
VLBIL100 PACK      KEY7,SECODE,IBCNBLSN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLBIL900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLBIL900 IF LESS              * Security level insufficient
.
VLBIL800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLBIL999
.
VLBIL900 MOVE      ONE,EXIT                      * Not Valid
.
VLBIL999 RETURN
.
.        ======================================================================
.        VLAAE000 : Check security for patient billing
.        ======================================================================
.
VLAAE000 BRANCH    FUAAE,VLAAE100                * Check if system switched on
         GOTO      VLAAE800                      * System not available
.
.        Check the security level against the program security level
.
VLAAE100 PACK      KEY7,SECODE,IBCNAESN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLAAE900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLAAE900 IF LESS              * Security level insufficient
.
VLAAE800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLAAE999
.
VLAAE900 MOVE      ONE,EXIT                      * Not Valid
.
VLAAE999 RETURN
.
.        ======================================================================
.        VLINP000 : Check security for patient billing
.        ======================================================================
.
VLINP000 BRANCH    FUINP,VLINP100                * Check if system switched on
         GOTO      VLINP800                      * System not available
.
.        Check the security level against the program security level
.
VLINP100 PACK      KEY7,SECODE,IBCNINSN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLINP900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLINP900 IF LESS              * Security level insufficient
.
VLINP800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLINP999
.
VLINP900 MOVE      ONE,EXIT                      * Not Valid
.
VLINP999 RETURN
.
.        ======================================================================
.        VLOUT000 : Check security for patient billing
.        ======================================================================
.
VLOUT000 BRANCH    FUOUT,VLOUT100                * Check if system switched on
         GOTO      VLOUT800                      * System not available
.
.        Check the security level against the program security level
.
VLOUT100 PACK      KEY7,SECODE,IBCNOPSN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLOUT900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLOUT900 IF LESS              * Security level insufficient
.
VLOUT800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLOUT999
.
VLOUT900 MOVE      ONE,EXIT                      * Not Valid
.
VLOUT999 RETURN
.
.        ======================================================================
.        VLMAM000 : Check security for patient billing
.        ======================================================================
.
VLMAM000 BRANCH    FUMAM,VLMAM100                * Check if system switched on
         GOTO      VLMAM800                      * System not available
.
.        Check the security level against the program security level
.
VLMAM100 PACK      KEY7,SECODE,IBCNMASN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLMAM900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLMAM900 IF LESS              * Security level insufficient
.
VLMAM800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLMAM999
.
VLMAM900 MOVE      ONE,EXIT                      * Not Valid
.
VLMAM999 RETURN
.
.        ======================================================================
.        VLRAD000 : Check security for patient billing
.        ======================================================================
.
VLRAD000 BRANCH    FURAD,VLRAD100                * Check if system switched on
         GOTO      VLRAD800                      * System not available
.
.        Check the security level against the program security level
.
VLRAD100 PACK      KEY7,SECODE,IBCNRASN          * Get users security level
         CALL      RDIBUSE1
         BRANCH    OVRCD,VLRAD900
.
         MOVE      IBUSLEVL,ANS
         MOVE      ANS,FORM2
.
         COMPARE   PSECLEV,FORM2                 * Check against program
         GOTO      VLRAD900 IF LESS              * Security level insufficient
.
VLRAD800 MOVE      ZERO,EXIT                     * Valid
         GOTO      VLRAD999
.
VLRAD900 MOVE      ONE,EXIT                      * Not Valid
.
VLRAD999 RETURN
.
         INC       COMCODKY    
         INC       PATCODKY    
         INC       COMCODIO/INC
         INC       IBASMAIO/INC
         INC       IBAUSEIO/INC
         INC       IBAOVRIO/INC
.
ENDSLFN  ENDPROC
