.*****************************************************************************
.*                             ATWLLINK                                      *
.*                  Attend W/L link records                                  *
.*****************************************************************************
.* V10.15.01  14.11.2019  Ebon Clements     TSK 0880573                      *
.*            Added inactive cat code test to ATTENDST                       *
.* V10.05.01  16.10.2014  Ebon Clements     CAR 291413                       *
.*            Added POLX0000                                                 *
.*****************************************************************************
.
ATWLLINK  CALL      ATTWLPAC
          CALL      ATTWLPAS
.
AWLLNK99  RETURN
+
.*****************************************************************************
.*                             ATTWLPAC                                      *
.*                  Attend W/L link record PAC                               *
.*****************************************************************************
.
ATTWLPAC  COMPARE   ONE,HBWAIT                   * using w/l ?
          GOTO      AWLPAC99 IF NOT EQUAL        * no - finished
.
          COMPARE   FOUR,OBASTAT                 * status attended ?
          GOTO      AWLPAC99 IF NOT EQUAL        * no - finished
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDWTOPA2                     * visit record on watopaaf ?
          BRANCH    OVRCD,AWLPAC99               * no - finished
.
          PACK      KEY19,WTOPURNO,WTOPCODE,WTOPCOUN,SP70
          CALL      RDTREA1                      * wattr1af record on file ?
          BRANCH    OVRCD,AWLPAC99               * no - finished
.
.         Set the pre-admission date using the booking date, then update
.         the record
.
          MOVE      OBADATE,WTWMREAD
          CALL      UPTREA1      
.
          PACK      KEY19,WTOPURNO,WTOPCODE,WTOPCOUN,SP70
          CALL      RDWTTX11                     * wattx1af record on file ?
          BRANCH    OVRCD,AWLPAC99               * no - finished
.
.         Set the pre-admission status to "attended"
.
          MOVE      SP70,WTTXPAST
          CALL      ATTENDST
          BRANCH    EXIT,AWLPAC90
.
          MOVE      ACODE,WTTXPAST
.
.         Set the pre-admission time using the booking time, then update
.         the record
.
AWLPAC90  MOVE      OBATIME,WTTXPTIM
          CALL      UPWTTX11
.
AWLPAC99  RETURN
+
.*****************************************************************************
.*                             ATTWLPAS                                      *
.*                    Attend W/L link record PAS                             *
.*****************************************************************************
.
ATTWLPAS  COMPARE   ONE,HBWAIT                   * using w/l ?
          GOTO      AWLPAS99 IF NOT EQUAL        * no - finished
.
          COMPARE   FOUR,OBASTAT                 * status attended ?
          GOTO      AWLPAS99 IF NOT EQUAL        * no - finished
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDWTOPS2                     * visit record on watopsaf ?
          BRANCH    OVRCD,AWLPAS99               * no - finished
.
          PACK      KEY19,WTOSURNO,WTOSCODE,WTOSCOUN,SP70
          CALL      RDTREA1                      * wattr1af record on file ?
          BRANCH    OVRCD,AWLPAS99
.
.         Set the pre-anaesthetic date using the booking date, then update
.         the record
.
          MOVE      OBADATE,WTWMTRPA
          CALL      UPTREA1
.
          PACK      KEY19,WTOSURNO,WTOSCODE,WTOSCOUN,SP70
          CALL      RDWTTX11                     * wattx1af record on file ?
          BRANCH    OVRCD,AWLPAS99               * no - finished
.
          MOVE      SP70,WTTXPANS
          CALL      ATTENDST
          BRANCH    EXIT,AWLPAS90
.
          MOVE      ACODE,WTTXPANS
.
.         Set the pre-anaesthetic time using the booking time, then update
.         the record
.
AWLPAS90  MOVE      OBATIME,WTTXPANT
          CALL      UPWTTX11
.
AWLPAS99  RETURN
+
.*****************************************************************************
.*                              ATTENDST                                     *
.*        Set pre admission/pre anaesthetic status to attended.              *
.*        Cat XG indicator 1 = A                                             *
.* Returns:  EXIT   0 = Valid code found                                     *
.*                  1 = Code not found                                       *
.*           ACODE - Cat XG code for "attended"                              *
.*****************************************************************************
.
ATTENDST  PACK      KEY5,ANSX,ANSG,SP70
          CALL      RDSCODE1                     * position on Cat XG
ATTST100  CALL      RDKCODE1                     * read next record
          BRANCH    OVRCD,ATTST950               * eof - leave blank
.
          MATCH     "XG",TCODE                   * same Cat still ?
          GOTO      ATTST950 IF NOT EQUAL        * no - leave blank
.
          MATCH     ANSA,TCINDC1                 * Attended ?
          GOTO      ATTST100 IF NOT EQUAL        * no - get next code
.
          MATCH     "I",PTCOACTV                  * Skip inactive
          GOTO      ATTST100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      ATTST999
.
ATTST950  MOVE      ONE,EXIT
.
ATTST999  RETURN
+
.
.*****************************************************************************
. Write the OP visit to the COMWEB70 polling table to see if we
. need to recalculate the AH referrals expiry date
.*****************************************************************************
POLX0000  MOVE      ZERO,FORM10
.
          PACK      KEY10,Z70
          CALL      RSIBPOL1
          CALL      RPIBPOL1
          BRANCH    OVRCD,POLX1000
.
          MOVE      IBPLUNIQ,FORM10
POLX1000  ADD       ONE,FORM10
.
          MOVE      FORM10,IBPLUNIQ
          MOVE      "007",IBPLTYPE     * Update AH referral expiry
          MOVE      OBAURNO,IBPLURNO
          MOVE      OBAOUTNO,IBPLVISN
          PACK      IBPLSKEY,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
          MOVE      SP70,IBPLSPAR
.
          MOVE      FORM10,KEY10
          CALL      RAIBPOL1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRIBPOL1
            TRAPCLR   IO
            BRANCH    OVRCD,POLX1000
          ELSE
            GOTO      POLX1000
          ENDIF
.
POLX9999  RETURN
