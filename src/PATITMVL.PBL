.******************************************************************************
.*                                  PATITMVL                                  *
.* Validate MBS Item effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY17 populated with -                                         *
.*                      - MBS Item code                                       *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid MBS Item effective from and to dates           *
.*                   1 = Invalid MBS Item effective from and to dates         *
.*                                                                            *
.* Modifications:                                                             *
.* V11.00.00  02/03/2020  J.Tan           TSK 0838262                         *
.*            Created include                                                 *
.******************************************************************************
.
VIDT0000  UNPACK    KEY17,KEY9
.
          CALL      RDITEM1                 * Record on file so must be an
          BRANCH    OVRCD,VIDT5000          * update
.
          MATCH     VALTDATE,PTITETDT       * No change in effective from and
          GOTO      VIDT8000 IF EQUAL       * to dates so exit
.
VIDT5000  CALL      RDSITEM1                * Check for the same MBS item
          CALL      RDPITEM1                * with an earlier effective from
          BRANCH    OVRCD,VIDT6000          * date
.
          MATCH     KEY9,ITEMNO
          GOTO      VIDT6000 IF NOT EQUAL
.
          MATCH     SP70,PTITETDT           * No effective to date on previous
          GOTO      VIDT8000 IF EQUAL       * MBS Item.
.
          MATCH     VALFDATE,PTITETDT
          IF        !@LESS
            GOTO      VIDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VIDT6000 IF EQUAL
.
          MATCH     VALTDATE,PTITETDT
          IF        !@LESS
            GOTO      VIDT9000              * Effective to date overlap
          ENDIF
.
VIDT6000  CALL      RDSITEM1                * Check for the same MBS item
          CALL      RDKITEM1                * with an earlier effective from
          BRANCH    OVRCD,VIDT8000          * date
.
          MATCH     KEY9,ITEMNO
          GOTO      VIDT8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VIDT9000 IF EQUAL
.
          MATCH     PTITEFDT,VALFDATE
          IF        !@LESS
            GOTO      VIDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     PTITEFDT,VALTDATE
          IF        !@LESS
            GOTO      VIDT9000              * Effective to date overlap
          ENDIF
.
VIDT8000  MOVE      ZERO,EXIT
          GOTO      VIDT9999
.
VIDT9000  MOVE      ONE,EXIT
          GOTO      VIDT9999
.
VIDT9999  RETURN

