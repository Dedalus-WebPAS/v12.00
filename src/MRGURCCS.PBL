.******************************************************************************
.* System         : Clinical Costing Interface                                *
.*                                                                            *
.* Include Name   : MRGURCCS.PBL                                              *
.*                                                                            *
.* Function       : Merge a U/R for all Clinical Costing Interface            *
.*                  files which contain a U/R Number.                         *
.*                                                                            *
.* Author         : Justin Coates      12/07/1995                             *
.*                                                                            *
.* Parameters     : OCCURNO            the old urnumber                       *
.*                  NCCURNO            the new urnumber                       *
.*                                                                            *
.* Modifications                                                              *
.*                  07/03/2000  Steve Armstrong                               *
.*                  Mods to cater for left justified U/R in ccsupdaf          *
.*                                                                            *
.******************************************************************************

          DEFPROC   MRGURCCS
.
          INC       CCSEPSFD/INC
          INC       CCSUPDFD/INC
.
          DISPLAY   *P1:24,*EL,"Fixing Clinical Costing Interface Files";
.
          CALL      CCSEPS00                     * fix ccsepsaf
          CALL      CCSUPD00                     * fix ccsupdaf
          GOTO      MRGCCS99
+
.*****************************************************************************
.*        Change U/R for CCSEPSFD                                            *
.*****************************************************************************
.
CCSEPS00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCSEPSA5,"ccsepsaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCSEPS99               * file not found
.
          IF        LOGERR = 1
            MOVE      "ccsepsaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
CCSEPS10  PACK      KEY28,OCCURNO,SP30
          CALL      RSCCEP5                      * position in file
          CALL      RKCCEP5                      * read next record
          BRANCH    OVRCD,CCSEPS90               * eof - finished
.
          MATCH     OCCURNO,CCEPURN              * same U/R number still ?
          GOTO      CCSEPS90 IF NOT EQUAL        * no - ignore record
.
          MOVE      NCCURNO,CCEPURN              * load new U/R number
          CALL      UPCCEP5                      * update record
          GOTO      CCSEPS10                     * reposition in file
.
CCSEPS90  CLOSE     CCSEPSA5
.
CCSEPS99  RETURN
+
.*****************************************************************************
.*        Change U/R for CCSUPDFD                                            *
.*****************************************************************************
.
CCSUPD00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCSUPDA4,"ccsupdaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCSUPD99               * File not found
.
          IF        LOGERR = 1
            MOVE      "ccsupdaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
.         U/R numbers are left justified, so we need to modify the key
.         accordingly
.
CCSUPD10  UNPACK    OCCURNO,KEY8
          SQUEEZE   KEY8
          PACK      KEY8,KEY8,SP8
          PACK      KEY23,KEY8,SP30
          CALL      RSCCUP4                      * position on file
          CALL      RKCCUP4                      * read next record
          BRANCH    OVRCD,CCSUPD90               * eof - finished
.
          MATCH     KEY8,CCUPURN                 * same U/R number still ?
          GOTO      CCSUPD90 IF NOT EQUAL        * no - ignore record
.
          MOVE      NCCURNO,KEY8
          SQUEEZE   KEY8
          PACK      KEY8,KEY8,SP8
          PACK      CCUPURN,KEY8,SP10            * load new U/R number
          CALL      UPCCUP4                      * update record
          GOTO      CCSUPD10
.
CCSUPD90  CLOSE     CCSUPDA4
.
CCSUPD99  RETURN 
+
          INC       IBAOVRIO/INC
.
          INC       CCSEPSIO/INC
          INC       CCSUPDIO/INC
.
MRGCCS99  ENDPROC
+
