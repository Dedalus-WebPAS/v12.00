. *----------------------------------------------------------------------
. * Include Name  :   EMRPWDTM.PBL
. *
. * Function      :   CGI variables for EMRPWDFD - emrpwdaf.dat
. *
. * Includes Needed : EMRPWDFD - ABF EMR Dept Price Weights
. *                   EMRPWDIO - IO for ABF EMR Dept Price Weights
. * Modifications :
. *                 V11.01.01 10/08/2021 J.Tan   Task 0905305
. *                 Allow zero Price weight (ERPWD004)
. *
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for ABF EMR Dept price weights
.------------------------------------------------------------------------
.
CLRPWD00  MOVE      SP70,ERPWD001
          MOVE      SP70,ERPWD002
          MOVE      SP70,ERPWD003
          MOVE      ZERO,ERPWD004
          RETURN
.
.------------------------------------------------------------------------
. ABF EMR Dept PW Fields
.------------------------------------------------------------------------
ERPW0000  BRANCH    FLDITMNO,ERPW0010,ERPW0020,ERPW0030,ERPW0040
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ERPW9999
.
ERPW0010  UNPACK    DIM127,D1,KEY1,DIM127        * Contract ID
          MOVE      KEY1,F1
          BRANCH    F1,ERPW0012
.
          WRITE     HTMLFILE;EMPWCNTR;
          GOTO      ERPW9999
.
ERPW0012  PACK      KEY6,EMPWCNTR,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,ERPW9999
.
          WRITE     HTMLFILE;PMCIDESC;
          GOTO      ERPW9999
.
ERPW0020  WRITE     HTMLFILE;EMPWURGV;
          GOTO      ERPW9999
.
ERPW0030  WRITE     HTMLFILE;EMPWURGD;
          GOTO      ERPW9999
.
ERPW0040  WRITE     HTMLFILE;EMPWPWEI;
          GOTO      ERPW9999
.
ERPW9999  RETURN
.
.------------------------------------------------------------
. Set Parameters for ABF EMR Dept PW
.------------------------------------------------------------
SPPWD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPPWD001,SPPWD002,SPPWD003,SPPWD004
.
          MOVE      ONE,EXIT
          GOTO      SPPWD999
.
SPPWD001  MOVE      QRYDATA,ERPWD001
          PACK      ERPWD001,ERPWD001,SP70
          GOTO      SPPWD999
.
SPPWD002  MOVE      QRYDATA,ERPWD002
          PACK      ERPWD002,ERPWD002,SP70
          GOTO      SPPWD999
.
SPPWD003  MOVE      QRYDATA,ERPWD003
          PACK      ERPWD003,ERPWD003,SP70
          GOTO      SPPWD999
.
SPPWD004  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ERPWD004
          GOTO      SPPWD999
.
SPPWD999  RETURN
.
.------------------------------------------------------------------------
.         Move work variables to ABF EMR Dept PW file variables
.------------------------------------------------------------------------
MVERPW00  MATCH     ERPWD001,SP70
          IF        !@EQUAL
            MOVE      ERPWD001,EMPWCNTR
          ENDIF
.
          MATCH     ERPWD002,SP70
          IF        !@EQUAL
            MOVE      ERPWD002,EMPWURGV
          ENDIF
.
          MATCH     ERPWD003,SP70
          IF        !@EQUAL
            MOVE      ERPWD003,EMPWURGD
          ENDIF
.
.         COMPARE   ERPWD004,ZERO
.         IF        !@EQUAL
            MOVE      ERPWD004,EMPWPWEI
.         ENDIF
.
MVERPW99  RETURN
