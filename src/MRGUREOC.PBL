.******************************************************************************
.* System         : Episode of Care & ACC                                     *
.*                                                                            *
.* Include Name   : MRGUREOC.PBL                                              *
.*                                                                            *
.* Function       : Merge a U/R for all EOC & ACC                             *
.*                  files which contain a U/R Number.                         *
.*                                                                            *
.* Author         : Howellsy           21/10/1996                             *
.*                                                                            *
.* Parameters     : OLDURNO            the old urnumber                       *
.*                  NEWURNO            the new urnumber                       *
.*                                                                            *
.* Modifications  :                                                           *
.*                  14/09/1999  Jill Habasque SRF 645173                      *
.*                  Added SAVEPNO so that episodes would be linked to the     *
.*                  correct eposide of care.                                  *
.*                  12/09/1997  Steve Armstrong    SRF 643554                 *
.*                  Mods to only call EOCLNK00 from EOCREF00                  *
.*                  (previously called EOCREF00 prior to calling EOCREF00     *
.*                  which meant that the new episode number had not been set) *
.******************************************************************************
.
          DEFPROC   MRGUREOC
.
          INC       EOCLNKFD/INC
          INC       EOCREFFD/INC
.
SAVEPNO   FORM      5
FORM5     FORM      5
NINES     INIT      "99999"
SAVKEY13  DIM       13
.
          DISPLAY   *P1:24,*EL,"Fixing Episode of Care Files ";
          CALL      EOCREF00
          GOTO      MRGEOC99
.
.*********************************************************************= 
.         Change U/R for EOCLNKFD
.*********************************************************************= 
.
EOCLNK00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EOCLNKA1,"eoclnkaf"
          TRAPCLR   IO
          BRANCH    OVRCD,EOCLNK99           * file not found
.
          IF        LOGERR = 1
            MOVE      "eoclnkaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
          DISPLAY   *P71:24,*V2LON,"eoclnkaf"
.
EOCLNK10  PACK      KEY23,OLDURNO,SP20,SP10
          CALL      RSECLNK1
          CALL      RKECLNK1
          BRANCH    OVRCD,EOCLNK90
.
          MATCH     OLDURNO,ECLNURNO
          GOTO      EOCLNK90 IF NOT EQUAL
.
          COMPARE   SAVEPNO,ECLNEPNO
          GOTO      EOCLNK90 IF NOT EQUAL
.
          MOVE      NEWURNO,ECLNURNO
          MOVE      ECRFEPNO,ECLNEPNO
          CALL      UPECLNK1
          GOTO      EOCLNK10
.
EOCLNK90  CLOSE     EOCLNKA1
.
EOCLNK99  RETURN
+
.*********************************************************************
.         Change U/R for EOCREFFD
.*********************************************************************
.
EOCREF00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EOCREFA1,"eocrefaf"
          TRAPCLR   IO
          BRANCH    OVRCD,EOCREF99           * file not found
.
          DISPLAY   *P71:24,*V2LON,"eocrefaf"
.
EOCREF10  IF        LOGERR = 1
            MOVE      "eocrefaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
          PACK      KEY13,OLDURNO,SP8,SP10,SP8
          CALL      RSECREF1
          CALL      RKECREF1
          BRANCH    OVRCD,EOCREF90
.
          MATCH     OLDURNO,ECRFURNO
          GOTO      EOCREF90 IF NOT EQUAL
.
          PACK      SAVKEY13,ECRFURNO,ECRFEPNO,SP20
.
. ------- calculate the episode number ----------------------
.
          MOVE      ONE,FORM5
          PACK      KEY13,NEWURNO,NINES
          CALL      RSECREF1
          CALL      RPECREF1
          BRANCH    OVRCD,EOCREF20
.
          MATCH     NEWURNO,ECRFURNO
          GOTO      EOCREF20 IF NOT EQUAL
.
          MOVE      ECRFEPNO,FORM5
          ADD       ONE,FORM5
.
EOCREF20  MOVE      SAVKEY13,KEY13
          CALL      RDECREF1
.
          MOVE      ECRFEPNO,SAVEPNO
          MOVE      NEWURNO,ECRFURNO
          MOVE      FORM5,ECRFEPNO
          PACK      KEY13,ECRFURNO,ECRFEPNO,SP20
          CALL      UPECREF1
.
          CALL      EOCLNK00                  * update the link records
.                                             * with this episode
.
          GOTO      EOCREF10
.
EOCREF90  CLOSE     EOCREFA1
.
EOCREF99  RETURN
+
          INC       IBAOVRIO/INC
          INC       EOCLNKIO/INC
          INC       EOCREFIO/INC
.
MRGEOC99  ENDPROC
+
