.******************************************************************************
.* Include Name   : KYOUTCTY.PBL                                              *
.* Description    : Keyin of an Outpatient Clinic Type from OUTCTYFD          *
.* Author         : Justin Coates           27/04/1992                        *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.* Returns        : OCTCTYP  - Keyed in code                                  *
.*                  OCTDESC  - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
.* Mods :           24/06/1994  bez          acc rehab mods 
.*                  Rename "Clinic Type" if parameter set.
.*                  Fixed return of description.
.******************************************************************************
.
KYOUTCTY  
.-------- Rename "Clinic Type" if parameter set
          OPEN      CONTROLF,"controlf"          
          READ      CONTROLF,THIRTY2;*2,OTCNRCLI:
                                     *4,OTCNCTYP
          IF        OTCNRCLI=0
            MOVE      "Clinic Type ",OTCNCTYP    * parameter not set
          ENDIF
          STRIP     OTCNCTYP  
.
KYOTCT00  MOVE      ONE,FORM1               * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KOTCT100  MATCH     SP6,CKYIDEF6
          GOTO      KOTCT120 IF EQUAL       * no default
.
          MOVE      CKYIDEF6,OCTCTYP
          KEYIN     *PCCOL:CVERT,*DV,OCTCTYP:
                    *PCCOL:CVERT,*V2LON,*RV,OCTCTYP:
                    *PCCOL:CVERT,*DV,OCTCTYP
          GOTO      KOTCT150
.
KOTCT120  KEYIN     *PCCOL:CVERT,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,OCTCTYP:
                    *PCCOL:CVERT,*DV,OCTCTYP
.
KOTCT150  PACK      OCTCTYP,OCTCTYP,SP6
          MATCH     SP6,OCTCTYP
          GOTO      KOTCT790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,OCTCTYP
          GOTO      KOTCT820 IF EQUAL       * exitchar entered
          MATCH     QUEST,OCTCTYP
          GOTO      KOTCT200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        FORM1=1
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,FORM1              * set as ? performed
          ENDIF
.
          CALL      OUTDSCTY                * display valid entries
.
          IF        CNEWDS = 0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not performed
            MOVE      ECOL,CCOL
            MOVE      EVERT,CVERT
          ELSE
            DISPLAY   *P1:24,*+,OTCNCTYP," : ",*EL;
            MOVE      "24",CVERT
            MOVE      "15",CCOL
            MOVE      ZERO,FORM1              * set ? as entered
          ENDIF
          GOTO      KOTCT100
.
.         validate what was entered
.
KOTCT200  PACK      KEY12,IDDD,OCTCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD,KOTCT400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KOTCT800
.
KOTCT400  DISPLAY   *P1:24,*B,"Invalid ",*+,OTCNCTYP,".  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOTCT100
          DISPLAY   *P1:24,*+,OTCNCTYP," : ",*EL;
          GOTO      KOTCT100
.
.         nothing entered
.
KOTCT790  MOVE      SP30,OCTDESC            * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KOTCT800 IF EQUAL       * valid entry
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOTCT100
          DISPLAY   *P1:24,*+,OTCNCTYP," : ",*EL;
          GOTO      KOTCT100
.
.         valid entry
.
KOTCT800  IF        FORM1=0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not entered
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP
          GOTO      KOTCT999 
.
.         exitchar entered
.
KOTCT820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP6,OCTCTYP
          IF        FORM1=0
            CALL      FRESCR00                * free the screen
          ENDIF
.
KOTCT999  RETURN
+
