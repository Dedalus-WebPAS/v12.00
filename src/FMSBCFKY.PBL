.-------------------------------------------------------------------------------
.   System    :   FMS Financial System  
.                           
.   Program   :   FMSBCFFM 
.                            
.   Routines  :   KBCFFM00 Keyin Batch   - Returns a valid record
.                 ABCFFM00 Accept Batch  - Returns a key NOT on file
.   
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.
.   Internal  :   HBCFFM00 Help Display for Batch  
.                            
.   Redisplay :   RBCFFM00 Screen Redisplay Called for Batch  
.-------------------------------------------------------------------------------
.   Mods      :    
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KBCFFM00  MOVE      UNDLN70,FMBCBAT
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMBCBAT:
                    *PCCOL:CVERT,*V2LON,*ZF,*JR,FMBCBAT
.
          ENDSET    FMBCBAT
          APPEND    SP70,FMBCBAT
          RESET     FMBCBAT
          MATCH     SP70,FMBCBAT             * System Code Spaces so exit set
          GOTO      KBCFFM90 IF EQUAL
          MATCH     "0000000000",FMBCBAT     * System Code Zeros so exit set
          GOTO      KBCFFM90 IF EQUAL
.
          MOVE      "0000000000",KEY4
          PACK      KEY5,KEY4,EXITCHAR
          MATCH     KEY5,FMBCBAT         * Exit Selected
          GOTO      KBCFFM95 IF EQUAL
.
          PACK      KEY5,KEY4,QUEST
          MATCH     KEY5,FMBCBAT            * Help Required  
          GOTO      KBCFFM10 IF NOT EQUAL
.
          CALL      HBCFFM00                  * Display List Of FMBCBATs
          PACK      CDATASAV,FMBCBAT,SP70    * save data entered
          CALL      RBCFFM00                  * Redisplay Routine
          PACK      FMBCBAT,CDATASAV,SP70    * restore data entered
.
          ENDSET    FMBCBAT
          APPEND    SP70,FMBCBAT
          RESET     FMBCBAT
          MATCH     SP70,FMBCBAT             * System Code Spaces so exit set
          GOTO      KBCFFM00 IF EQUAL
.
KBCFFM10  DISPLAY   *PCCOL:CVERT,*V2LON,FMBCBAT
          PACK      KEY5,FMBCBAT,SP70
          CALL      RDFMBC1                                   
          BRANCH    OVRCD,KBCFFM80
          MOVE      ZERO,EXIT                 * no entry so exit is set
          GOTO      KBCFFM99
.
KBCFFM80  DISPLAY   *P1:24,*EL,*B,"Batch Does Not Exist - ";
          CALL      HITENTER
          GOTO      KBCFFM00
.
KBCFFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      KBCFFM99
.
KBCFFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
KBCFFM99  RETURN
.-------------------------------------------------------------------------------
. Accept Code Keyin - Code Must Not Exist 
.-------------------------------------------------------------------------------
ABCFFM00  MOVE      UNDLN70,FMBCBAT
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMBCBAT:
                    *PCCOL:CVERT,*V2LON,*JR,FMBCBAT: 
                    *PCCOL:CVERT,*V2LON,*DV,FMBCBAT
.
          ENDSET    FMBCBAT
          APPEND    SP70,FMBCBAT
          RESET     FMBCBAT
          MATCH     SP70,FMBCBAT             * System Code Spaces so exit set
          GOTO      ABCFFM90 IF EQUAL
          GOTO      KBCFFM90 IF EQUAL
          MATCH     "0000000000",FMBCBAT     * System Code Zeros so exit set
.
          MOVE      "0000000000",KEY4
          PACK      KEY5,KEY4,EXITCHAR
          MATCH     KEY5,FMBCBAT         * Exit Selected
          GOTO      ABCFFM95 IF EQUAL
.
          PACK      KEY5,KEY4,QUEST
          MATCH     KEY5,FMBCBAT            * Help Required  
          GOTO      ABCFFM10 IF NOT EQUAL
          CALL      HBCFFM00                  * Display List Of FMBCBATs
          CALL      RBCFFM00                  * Redisplay Routine
.
          ENDSET    FMBCBAT
          APPEND    SP70,FMBCBAT
          RESET     FMBCBAT
          MATCH     SP70,FMBCBAT             * System Code Spaces so exit set
          GOTO      ABCFFM00 IF EQUAL
.
ABCFFM10  DISPLAY   *PCCOL:CVERT,*V2LON,FMBCBAT
          PACK      KEY5,FMBCBAT
          MOVE      ZERO,EXIT                 * no entry so exit is set
          CALL      RDFMBC1                                   
          BRANCH    OVRCD,ABCFFM99
.
          DISPLAY   *P1:24,*EL,*B,"Batch Already Exists - ";
          CALL      HITENTER
          GOTO      ABCFFM00
.
ABCFFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      ABCFFM99
.
ABCFFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
ABCFFM99  RETURN
.------------------------------------------------------------------------------
. Display List of All Codes
.------------------------------------------------------------------------------
HBCFFM00  KEYIN     *P1:4,*EF,*P1:5,*V2LON,*ULON,"Item":
                    *P6:5,"Batch",*P12:5,"Entered",*P23:5,"Status":
                    *P39:5,"Post to",*P50:5,"Type",*P75:5,"Total":
                    *P1:4,*HOFF,"    From Batch No : ":
                    *P21:4,*V2LON,*DV,UNDLN5:
                    *P21:4,*V2LON,*ZF,*JR,*DE,KEY5:
                    *P21:4,*V2LON,*DV,KEY5
.
          PACK      KEY5,KEY5,SP70
          MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
          MOVE      ZERO,CPAGENO
          CALL      RSFMBC1
.
HBCFFM10  CALL      RKFMBC1
          BRANCH    OVRCD,HBCFFM20
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     FMBCBAT,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
.
          MOVE      FMBCSTA,F1
          MOVE      "Btch in Use",FMBCDST
          LOAD      FMBCDST,F1,FMBCDST1,FMBCDST2,FMBCDST3,FMBCDST4,FMBCDST5
          REP       " 0",FMBCDAT
          UNPACK    FMBCDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P2:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P6:STDROW,FMBCBAT,*P12:STDROW,CPCDATE,*P23:STDROW,FMBCDST
.
          UNPACK    FMBCPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      TBAT0000
          MOVE      FMBCFMT,FMBCFTOT
          FEDIT     FMBCTOT,FMBCFTOT
.
          DISPLAY  *P39:STDROW,CPCDATE,*P50:STDROW,FMBCDTY:
                   *P61:STDROW,FMBCFTOT
.
          COMPARE   "17",XITEMNO
          GOTO      HBCFFM10 IF NOT EQUAL
.
HBCFFM20  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, ";
          COMPARE   "17",XITEMNO
          GOTO      HBCFFM25 IF NOT EQUAL
          DISPLAY   "(",*V2LON,"N",*HOFF,")ext page, ";
.
HBCFFM25  COMPARE   ZERO,CPAGENO
          GOTO      HBCFFM26 IF EQUAL
          DISPLAY   "(",*V2LON,"P",*HOFF,")revious page, ";
.
HBCFFM26  KEYIN     "e(",*V2LON,"X",*HOFF,")it ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      HBCFFM30 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      HBCFFM30 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      HBCFFM35 IF EQUAL
          MATCH     "  X",CCITEMD        * Exit
          GOTO      HBCFFM91 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      HBCFFM90 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      HBCFFM90 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HBCFFM90 IF LESS       * Invalid Entry
          GOTO      HBCFFM92               * Item Selected
.
HBCFFM30  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      HBCFFM90 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      HBCFFM10
.
HBCFFM35  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      HBCFFM90 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          COMPARE   "17",XITEMNO      
          GOTO      HBCFFM36 IF EQUAL 
          CALL      RPFMBC1            * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      HBCFFM37 IF EQUAL 
.
HBCFFM36  CALL      RPFMBC1
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HBCFFM36 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HBCFFM10 IF EQUAL   * Display Down The Page
HBCFFM37  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HBCFFM36
.
HBCFFM90  BEEP                          * invalid entry
          GOTO      HBCFFM20
.
HBCFFM91  MOVE      SP70,FMBCBAT
          GOTO      HBCFFM99
.
HBCFFM92  MOVE      SP70,FMBCBAT
          LOAD      FMBCBAT,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
.
HBCFFM99  RETURN
.*******************************************************************************
. Determine Batch Type Description
.*******************************************************************************
TBAT0000  MATCH     "CR",FMBCTRT
          GOTO      TBAT0100 IF NOT EQUAL
          MOVE      "Credit Note",FMBCDTY
          GOTO      TBAT9999
.
TBAT0100  MATCH     "CS",FMBCTRT
          GOTO      TBAT0200 IF NOT EQUAL
          MOVE      "Cash Rec.  ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0200  MATCH     "CJ",FMBCTRT
          GOTO      TBAT0300 IF NOT EQUAL
          MOVE      "Cash Journ.",FMBCDTY
          GOTO      TBAT9999
.
TBAT0300  MATCH     "IN",FMBCTRT
          GOTO      TBAT0400 IF NOT EQUAL
          MOVE      "Invoices   ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0400  MATCH     "JA",FMBCTRT
          GOTO      TBAT0500 IF NOT EQUAL
          MOVE      "Journals   ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0500  MATCH     "PY",FMBCTRT
          GOTO      TBAT0600 IF NOT EQUAL
          MOVE      "Payments   ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0600  MATCH     "RF",FMBCTRT
          GOTO      TBAT0700 IF NOT EQUAL
          MOVE      "Refunds    ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0700  MATCH     "SC",FMBCTRT
          GOTO      TBAT0800 IF NOT EQUAL
          MOVE      "Supply Cred",FMBCDTY
          GOTO      TBAT9999
.
TBAT0800  MATCH     "RI",FMBCTRT
          GOTO      TBAT0900 IF NOT EQUAL
          MOVE      "Reinstate. ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0900  MATCH     "CJ",FMBCTRT
          GOTO      TBAT0950 IF NOT EQUAL
          MOVE      "Cash Journ.",FMBCDTY
          GOTO      TBAT9999
.
TBAT0950  MATCH     "CC",FMBCTRT
          GOTO      TBAT0960 IF NOT EQUAL
          MOVE      "Cancel Pay ",FMBCDTY
          GOTO      TBAT9999
.
TBAT0960  MATCH     "PP",FMBCTRT
          GOTO      TBAT1000 IF NOT EQUAL
          MOVE      "Prompt Pay ",FMBCDTY
          GOTO      TBAT9999
.
TBAT1000  MOVE      "Invalid    ",FMBCDTY
.
TBAT9999  RETURN
