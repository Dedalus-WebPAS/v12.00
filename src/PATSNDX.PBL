.
. PATSNDX.PBL
. -----------
.        Subroutine to Change the surname GSRSNAM into a Sound-ex key GSRSKEY
.        - Amended 25 Oct 2004 Steve Armstrong CAR 52359
.                              Mods for "TZ" to be the same as "Z"
.        - Amended 2.Apr.2001  G.Saunders  Use new patgsr file instead.
.
SOUNDEX  CLEAR     GSRSKEY
         RESET     GSRSNAM
         RETURN    IF EOS
.
.        Make sure the surname contains only upper case characters
.
         MOVE      GSRSNAM,KEYSTR
         REP       AREP,KEYSTR
.
.        Ignore all characters from "(" onward
.
         MOVE      "(",C1
         SCAN      C1,KEYSTR
         GOTO      SNDX0 IF NOT EQUAL
.
         BUMP      KEYSTR,-1
         APPEND    SP20,KEYSTR
         RESET     KEYSTR
.
.        Ignore trailing S's
.
SNDX0    SCAN      SCANS,KEYSTR
         GOTO      SNDXA IF NOT EQUAL
.
         CMOVE     SP1,KEYSTR
         RESET     KEYSTR
.
.        Validate the first character - must be alpha
.
SNDXA    MOVE      KEYSTR,C2
         REP       "KC",C2
         SCAN      C2,AREP
         GOTO      SNDXB IF EQUAL
.
         BUMP      KEYSTR
         GOTO      SNDXA IF NOT EOS
.
.        Move this first character to the SOUND-EX key
.
SNDXB    RESET     AREP
         PACK      GSRSKEY WITH C2,SP20
         REP       " 0",GSRSKEY
.
.        Loop to build up key
.
SNDXC    BUMP      GSRSKEY
         GOTO      SNDXE IF EOS
.
SNDXD    BUMP      KEYSTR
         GOTO      SNDXE IF EOS
.
.        Check if this is a valid character
.
         MOVE      KEYSTR,C1
         SCAN      C1,AREP
         GOTO      SNDXD IF NOT EQUAL
.
         RESET     AREP
.
.        Check for special pairs of characters
.
         MATCH     SCANCK,KEYSTR        * Ignore the C in CK
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANGH,KEYSTR        * Ignore the G in GH
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANLM,KEYSTR        * Ignore the L in LM
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANMN,KEYSTR        * Ignore the M in MN
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANSC,KEYSTR        * Ignore the S in SC
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANTZ,KEYSTR        * Ignore the T in TZ
         GOTO      SNDXD IF EQUAL
.
.        Check for special pairs of characters (Check with previous char)
.
         PACK      SPAIR WITH C2,C1
.
         MATCH     SCANMP,SPAIR        * Ignore the P in MP
         GOTO      SNDXD IF EQUAL
.
         MATCH     SCANST,SPAIR        * Ignore the T in ST
         GOTO      SNDXD IF EQUAL
.
.        Ignore if this is the same as the previous character
.
         CMATCH    C1,C2
         GOTO      SNDXD IF EQUAL
.
         MOVE      C1,C2
.
.        Convert the character to a SOUND-EX value and if non-zero, add to key
.
         REP       SREP,C1
         MATCH     "0",C1
         GOTO      SNDXD IF EQUAL
.
         CMOVE     C1,GSRSKEY
         GOTO      SNDXC
.
.        Finished loop
.
SNDXE    RESET     GSRSKEY
         RETURN
