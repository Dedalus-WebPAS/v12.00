.------------------------------------------------------------
.  File          :  WATPKIPR.PBL
.
.  Function      :  Update Keyword Index File for W/List Procedure Table
.
.  Parameters    :  WPCODE - Key to waiting list procedure 
.
.  Modifications :
.
.  V10.12.01 30/01/2018  Nicole Torrisi TSK 0851284
.            Added Procedure Code as keyword to watpkiaf
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  WTPKCODE    DIM       9     * Procedure Code
.  WTPKKKWD    DIM       50    * Key Word
.  WTPKSPAR    DIM       50    * Spare
.
.  Index 1   WTPKCODE, WTPKKKWD
.  Index 2   WTPKKKWD, WTPKCODE
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the WPGE0000 routine to call the HCPBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    HCPBW000
.------------------------------------------------------------
          DEFPROC   WATPKIUP
.
          INC       WATPKIFD/INC
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       50
KEYWRD02  DIM       50
KEYWRD03  DIM       50
KEYWRD04  DIM       50
KEYWRD05  DIM       50
KEYWRD06  DIM       50
KEYWRD07  DIM       50
KEYWRD08  DIM       50
KEYWRD09  DIM       50
KEYWRD10  DIM       50
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
WPGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     WATPKIA1,"watpkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,WPGE9999
.
          MOVE     WPCODE,KEYINDEX
          CALL     WTPCL000           * Remove Current Key Word Index
          BRANCH   EXIT,WPGE9999
.
          MOVE     WPCODE,KEY9          * Validate Record on File
          CALL     RDPROA1 
          BRANCH   OVRCD,WPGE1900     * If Not then it Must be a Delete/Clear
.
          MOVE     WPCODE,KEYINDEX
          MOVE     WPDESC,KEYWORDS
          CALL     WATPW000
.
          MOVE     WPCODE,KEYWORDS    * 0851284 - Add procedure code as keyword
          CALL     WATPW000
.
WPGE1900  CLOSE    WATPKIA1
.
WPGE9999  GOTO     WATPKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
WTPCL000  PACK     KEY59,KEYINDEX,SP70
          CALL     RSWTPKI1
          CALL     RKWTPKI1
          BRANCH   OVRCD,WTPCL999
          MATCH    KEYINDEX,WTPKCODE 
          GOTO     WTPCL999 IF NOT EQUAL
.
          PACK     KEY59,WTPKCODE,WTPKKKWD,SP70
          CALL     DEWTPKI1
          GOTO     WTPCL000
.
WTPCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
WATPW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      WATPW000 IF NOT EOS
            GOTO      WATPW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      WATPW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
WATPW100  MOVE      KEYWORDS,KEY50
          PACK      KEY50,KEY50,SP70
          REP       UPPLOW,KEY50             * Always Upper Case
          PACK      KEY59,KEYINDEX,KEY50
          UNPACK    KEY59,WTPKCODE,WTPKKKWD
          CALL      RDWTPKI1
          IF        OVRCD=1
            CALL      WRWTPKI1
          ENDIF
          GOTO      WATPW190
.
.         Check for any more words
.
WATPW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      WATPW000
.
WATPW999  RETURN
.
          INC       WATPKIIO/INC
          INC       IBAOVRIO/INC
.
WATPKIEP  ENDPROC                          * End of Procedure
.
