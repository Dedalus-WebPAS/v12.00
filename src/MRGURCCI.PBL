.******************************************************************************
.* System         : Clinical Costing Interface                                *
.*                                                                            *
.* Include Name   : MRGURCCI.PBL                                              *
.*                                                                            *
.* Function       : Merge a U/R for all Clinical Costing Interface            *
.*                  files which contain a U/R Number.                         *
.*                                                                            *
.*                  This also has the procedure CCIVIS00 which will change    *
.*                  the UR for each visit if the UR index does not exist.     *
.*                                                                            *
.* Author         : Justin Coates      12/07/1995                             *
.*                                                                            *
.* Parameters     : OLDURNO            the old urnumber                       *
.*                  NEWURNO            the new urnumber                       *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 23/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*        V5.07.01  09/02/2000  Steve Armstrong                               *
.*                  Mods to use 2nd index for ccierraf (by U/R number)        *
.*        V5.03.02  03/04/1996 Whirlpool         SRF 614630                   *
.*                  actualy did the other part of the srf junior you spastic  *
.*                  and fixed the I*U on the cciutlaf                         *
.*        V5.03.01  22/03/1996  Greg Horvat      SRF 614630                   *
.*                  Changed the ccibtraf update to use the 3rd index          *
.*                  10/10/1995  Steve Armstrong   SRF 611544                  *
.*                  Added update of ccierraf                                  *
.*                                                                            *
.******************************************************************************

          DEFPROC   MRGURCCI
.
          INC       CCIBAUFD/INC
          INC       CCIBTRFD/INC
          INC       CCIERRFD/INC
          INC       CCIUTLFD/INC
.
SAVKEY16  DIM       16
SAVKEY23  DIM       23
SAVKEY42  DIM       42
SAVKEY46  DIM       46
SAVQUANT  FORM      8
.
          DISPLAY   *P1:24,*EL,"Fixing Clinical Costing Interface Files";
          CALL      CCIBAU00
          CALL      CCIBTR00
          CALL      CCIERR00
          CALL      CCIUTL00
          GOTO      MRGCCI99
.
.*********************************************************************= 
.         Change U/R for CCIBAUFD
.*********************************************************************= 
CCIBAU00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIBAUA1,"ccibauaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIBAU99           * file not found
.
          IF        LOGERR = 1
            MOVE      "ccibauaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
          PACK      KEY16,SP20
.
CCIBAU15  CALL      RSCCBAU1
CCIBAU20  CALL      RKCCBAU1
          BRANCH    OVRCD,CCIBAU90
          MATCH     OLDURNO,CCBAURNO
          GOTO      CCIBAU20 IF NOT EQUAL    * no index on U/R number so
.                                              loop over all records.
.
          PACK      SAVKEY16,CCBABATN,CCBADATE
          MOVE      NEWURNO,CCBAURNO
          CALL      UPCCBAU1
          MOVE      SAVKEY16,KEY16
          GOTO      CCIBAU15
.
CCIBAU90  CLOSE     CCIBAUA1
CCIBAU99  RETURN
+
.*********************************************************************= 
.         Change U/R for CCIBTRFD
.*********************************************************************= 
CCIBTR00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIBTRA1,"ccibtraf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIBTR99          * File not found
.
          OPEN      CCIBTRA3,"ccibtraf"
.
          IF        LOGERR = 1
            MOVE      "ccibtraf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
CCIBTR10  PACK      KEY46,OLDURNO,SP30,SP20
          CALL      RSCCBTR3                * Position on & read the ccibtraf
          CALL      RKCCBTR3                  with the U/R number index
          BRANCH    OVRCD,CCIBTR90
.
          MATCH     OLDURNO,CCBTURNO
          GOTO      CCIBTR90 IF NOT EQUAL   * Different U/R number, finished
.
          PACK      SAVKEY46,CCBTBATN,CCBTUNIQ,CCBTSDAT,CCBTDEPT,CCBTEVNT:
                             CCBTPROC,CCBTURNO   * Save old U/R no 1st index
          PACK      KEY46,CCBTBATN,CCBTUNIQ,CCBTSDAT,CCBTDEPT,CCBTEVNT:
                          CCBTPROC,NEWURNO
          CALL      RDCCBTR1                * See if new U/R exists
          BRANCH    OVRCD,CCIBTR20          * Doesn't, continue with update
.
          MOVE      SAVKEY46,KEY46          * Does, delete old U/R record
          CALL      RDCCBTR1                * See if old U/R exists
          BRANCH    OVRCD,CCIBTR30          * Doesn't, display error
.
          MOVE      SAVKEY46,KEY46
          CALL      DECCBTR1                * Delete old U/R record
          GOTO      CCIBTR10
.
CCIBTR20  MOVE      SAVKEY46,KEY46          * Update old U/R record
          CALL      RDCCBTR1                * See if old U/R exists
          BRANCH    OVRCD,CCIBTR30          * Doesn't, display error
.
          MOVE      NEWURNO,CCBTURNO
          CALL      UPCCBTR1                * Update old U/R record
          GOTO      CCIBTR10
.
CCIBTR30  DISPLAY   *P1:24,*EL,*B,"Merge on ccibtraf file unsuccessful.  ":
                    "Call IBA.  ",*W3;
.
CCIBTR90  CLOSE     CCIBTRA1
CCIBTR99  RETURN 
+
.*********************************************************************
.         fix the CCIUTLFD
.*********************************************************************
CCIUTL00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIUTLL5,"cciutlaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIUTL99           * file not found
.
          IF        LOGERR = 1
            MOVE      "cciutlaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
CCIUTL10  PACK      KEY42,OLDURNO,SP30,SP10
          CALL      RSCCUTL5
          CALL      RKCCUTL5
          BRANCH    OVRCD,CCIUTL90
          MATCH     OLDURNO,CCUTURNO
          GOTO      CCIUTL90 IF NOT EQUAL
.
          MOVE      CCUTQUNT,SAVQUANT
          PACK      SAVKEY42,CCUTURNO,CCUTDATE,CCUTDEPT,CCUTEVNT,CCUTPROC:
                             CCUTBATN
.
.----- Check if the new ur is on file with same details ------
.
          PACK      KEY42,NEWURNO,CCUTDATE,CCUTDEPT,CCUTEVNT,CCUTPROC:
                             CCUTBATN
          CALL      RDCCUTL5
          IF        OVRCD = 1
            MOVE      SAVKEY42,KEY42
            CALL      RDCCUTL5
            BRANCH    OVRCD,CCIUTL10       ( If this happens somethings stuffed)
            MOVE      NEWURNO,CCUTURNO
            CALL      UPCCUTL5
          ELSE
            ADD       SAVQUANT,CCUTQUNT
            CALL      UPCCUTL5
            MATCH     SAVKEY42,KEY42
            IF        !@EQUAL
              MOVE      SAVKEY42,KEY42
              CALL      DECCUTL5
            ENDIF
          ENDIF
          GOTO      CCIUTL10
.
CCIUTL90  CLOSE     CCIUTLL5
CCIUTL99  RETURN
+
.**********************************************************************
.*        Fix ccierraf                                                *
.**********************************************************************
.
CCIERR00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIERRA1,"ccierraf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIERR99               * file not found
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIERRA2,"ccierraf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIERR95               * file not found
.
          IF        LOGERR = 1
            MOVE      "ccierraf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
CCIERR05  PACK      KEY23,OLDURNO,SP30
          CALL      RSCCERR2                     * position at start of file
CCIERR10  CALL      RKCCERR2                     * read next record
          BRANCH    OVRCD,CCIERR90               * eof - finished
.
          MATCH     OLDURNO,CCERURNO             * same U/R number ?
          GOTO      CCIERR90 IF NOT EQUAL        * no - ignore
.
.         Save current key
.
          PACK      SAVKEY23,CCERDATE,CCERDEPT,CCERURNO,CCERPTYP
.
.         See if record already exists on file
.
          PACK      KEY23,CCERDATE,CCERDEPT,NEWURNO,CCERPTYP
          CALL      RACCERR1                     * record on file already ?
          BRANCH    OVRCD,CCIERR50               * no
.
          MOVE      SAVKEY23,KEY23
          CALL      DECCERR1                     * yes - delete
          GOTO      CCIERR05
.
.         Update U/R number
.
CCIERR50  MOVE      SAVKEY23,KEY23
          CALL      RDCCERR1                     * re-read record
          MOVE      NEWURNO,CCERURNO             * update U/R number
          CALL      UPCCERR1
          GOTO      CCIERR05
.
CCIERR90  CLOSE     CCIERRA2
CCIERR95  CLOSE     CCIERRA1
.
CCIERR99  RETURN
+
          INC       IBAOVRIO/INC
          INC       CCIBAUIO/INC
          INC       CCIBTRIO/INC
          INC       CCIERRIO/INC
          INC       CCIUTLIO/INC
.
MRGCCI99  ENDPROC
+
.---------------------------------------------------------------------
.         change CCI for a visit only if index 5 does not exist
.         this will be called by the routine which loops over each 
.          visit for a UR.  If index 5 exists it will do nothing as
.          the merge will be done by the above procedure
.---------------------------------------------------------------------
          DEFPROC   CCIVIS00
.
          INC       CCIUTLFD/INC
.
SAVKEY42  DIM       42
.
CCIV0000  MOVE      ZERO,OVRCD              * Check if the CCI system exists
          TRAP      OVERCOND IF IO
          OPEN      CCIUTLL1,"cciutlaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CCIV9999
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIUTLL5,"cciutlaf"
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      CCIV9999 IF EQUAL       * index exists so fixed later
.
          OPEN      CCIUTLL3,"cciutlaf"     * do for a visit
.
          IF        LOGERR = 1
            MOVE      "cciutlaf",XXFILE
            TRAP      WERR0000 NORESET GIVING XXDESC IF IO
          ENDIF
.
          PACK      KEY42,PVIBILL,SP20,SP20
CCIV1000  CALL      RSCCUTL3
          CALL      RKCCUTL3
          BRANCH    OVRCD,CCIV9999          * different visit
.
          MATCH     PVIBILL,CCUTEVNT
          GOTO      CCIV9999 IF NOT EQUAL   * different visit
.
          PACK      SAVKEY42,CCUTEVNT,CCUTDATE,CCUTDEPT:
                             CCUTURNO,CCUTPROC,CCUTBATN
          MOVE      NEWURNO,CCUTURNO        * set new UR
          CALL      UPCCUTL3
          MOVE      SAVKEY42,KEY42
          GOTO      CCIV1000
.
          INC       CCIUTLIO/INC
          INC       IBAOVRIO/INC
.
CCIV9999  ENDPROC
+
