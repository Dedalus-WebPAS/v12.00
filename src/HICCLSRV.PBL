.***************************************************************************
.*                               HICCLSRV.PBL   (HIC Claims Server)        *
.*        Check the selected batch status                                  *
.*        that have been "extracted" have now been "sent", OR if any       *
.*        "sent" files have now been "acknowledged".                       *
.*                                                                         *
.* Requires  :   Parameters read - PTCNHICO                                *
.*               Open files      - HICBTC05                                *
.*                                                                         *
.* Date      :   03/05/2005                                                *
.* Author    :   Steve Armstrong                                           *
.* Mods      :                                                             *
.*               V9.04.03 01/12/05 J.Tan    CAR  77215                     *
.*               Mods writting to Batch history with HIC as the user id    *
.*               V9.04.02 11/07/05 Davin    CAR  66767                     *
.*               Don't append ".txt" to extract filename                   *
.*               V9.04.01 15/06/05 J.Tan    CAR  56701                     *
.*               Mods to write to history file                             *
.***************************************************************************
.
HICCLSRV 
.         DISPLAY   *P1:24,*EL,"Checking and updating claim statuses... ";
.
          PACK      KEY33,SRCHBTCH,SP70
          CALL      RSHCBTC1
          CALL      RKHCBTC1
          BRANCH    OVRCD,HCCLM999
          MATCH     " 0",HCBTSTAT
          GOTO      HCCLM100 IF EQUAL          * status of Batched
          MATCH     " 1",HCBTSTAT
          GOTO      HCCLM100 IF EQUAL          * status of Extracted
          MATCH     " 2",HCBTSTAT
          GOTO      HCCLM999 IF NOT EQUAL      * not status of sent
.
.         Check that there is an extract filename present
.
HCCLM100  MATCH     HCBTEFNM,SP70              * blank extract name ?
          GOTO      HCCLM999 IF EQUAL          * yes - ignore
.
.         Look in each of the inbound messenger directories (sent,ack & nack)
.         to see if the extract file is present.  If found, then update the
.         status of the batch according to which directory it was found in.
.
          MOVE      ZERO,FORM2
HCCLM200  COMPARE   THREE,FORM2                * checked in nack directory ?
          GOTO      HCCLM999 IF EQUAL          * yes - no status change
.                                                      so get next record
.
          ADD       ONE,FORM2
          LOAD      PATHDESC,FORM2,OUTPATH1:   * "sent"
                                   OUTPATH2:   * "ack"
                                   OUTPATH3    * "nack"
          STRIP     PATHDESC
.
.         Load up the batch file name and path
.
          MOVE      PTCNHICO,EXTRNAME
          STRIP     EXTRNAME
          ENDSET    EXTRNAME
          APPEND    PATHDESC,EXTRNAME
          APPEND    SLASH,EXTRNAME
          APPEND    HCBTEFNM,EXTRNAME
          RESET     EXTRNAME
.
.         Check if the file exists in the corresponding directory
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO             * set trap
          OPEN      EXTRFILE,EXTRNAME
          TRAPCLR   IO
          BRANCH    OVRCD,HCCLM200             * trap sprung
.
.         The file exists, so update the status of the batch
.         according to which directory it was found in:
.              2 = found in "sent"
.              3 = found in "ack"
.              4 = found in "nack"
.
          ADD       ONE,FORM2
          MOVE      HCBTSTAT,SAVESTAT          * History Status
          MOVE      FORM2,HCBTSTAT             * load status
          CALL      UPHCBTC1                   * update record
.
          CALL      BTCHIS00                   * write to history file
.
HCCLM999  RETURN
+
.------------------------------------------------------------
. Write record to batch history audit file
.------------------------------------------------------------
BTCHIS00  MOVE      HCBTBTCH,HCHSBTCH            * Batch Number
          MOVE      HCBTPMCI,HCHSPMCI            * MCI
          MOVE      HCBTPYEE,HCHSPYEE            * Payee Provider No
          MOVE      HCBTPSRV,HCHSPSRV            * Service Provider No
.
          PACK      HCHSTDAT,CCC,CYY,CMM,CDD     * Transaction Date
          REP       " 0",HCHSTDAT
          CLOCK     TIME,HCHSTTIM                * Transaction Time
.
          MOVE      "HIC       ",HCHSTUID        * Web user id
          MOVE      HCBTSTAT,HCHSSTAT
          MOVE      SP70,HCHSSPAR                * Spare
.
BTCHIS10  PACK      KEY49,HCHSBTCH,HCHSPSRV,HCHSPMCI,HCHSPYEE,HCHSTDAT:
                          HCHSTTIM,SP70
          CALL      RAHCHIS1
          BRANCH    OVRCD,BTCHIS20
          CLOCK     TIME,HCHSTTIM
          GOTO      BTCHIS10
.
BTCHIS20  CALL      WRHCHIS1                   * Write History Record
.
BTCHIS99  RETURN
+
