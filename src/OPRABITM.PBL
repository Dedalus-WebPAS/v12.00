.----------------------------------------------------------------------
. Modifications
.
. V11.05.02  19.12.2024 DA Sarkies      TSK 0954547
.            Commented out <cat> tags to prevent browser incompatibility 
. V11.05.01  26.11.2024  Ebon Clements  Task 0954547
.            Added <cat-c> start and end tag to category=XX tag
. V10.10.01  30/05/2017  Peter Vela    TSK 0837660
.            Created include
.
.------------------------------------------------------------------------
.   Clear Parameters for Theatre Antibiotics Table
.------------------------------------------------------------------------
CLRABI00  MOVE      SP70,OPABI001
          MOVE      SP70,OPABI002
          MOVE      SP70,OPABI003
          MOVE      SP70,OPABI004
          MOVE      SP70,OPABI005
          MOVE      SP70,OPABI006
          MOVE      SP70,OPABI007
          PACK      OPABI008,SP70,SP70,SP70
          MOVE      SP70,OPABI009
          MOVE      SP70,OPABI010
          MOVE      SP70,OPABI011
          MOVE      SP70,OPABI012
          MOVE      SP70,OPABI013
          MOVE      SP70,OPABI014
          MOVE      SP70,OPABI015
          MOVE      SP70,OPABI016
.
CLRABI99  RETURN
+
.------------------------------------------------------------
. Output Theatre Antibiotics Field
.------------------------------------------------------------
OPAI0000  BRANCH    FLDITMNO,OPAI0100,OPAI0200,OPAI0300,OPAI0400,OPAI0500:
                             OPAI0600,OPAI0700,OPAI0800,OPAI0900,OPAI1000:
                             OPAI1100,OPAI1200,OPAI1300,OPAI1400,OPAI1500:
                             OPAI1600,OPAI1700
.
OPAI0100  WRITE     HTMLFILE;OPAIUNIQ;
          GOTO      OPAI9999
.
OPAI0200  MOVE      "ab",CATEGORY
          MOVE      OPAIANTB,CODE
          CALL      CODITM00
          GOTO      OPAI9999
.
OPAI0300  WRITE     HTMLFILE;OPAICNTR;
          GOTO      OPAI9999
.
OPAI0400  MOVE      "ab",CATEGORY
          MOVE      OPAIPANT,CODE
          CALL      CODITM00
          GOTO      OPAI9999
.
OPAI0500  WRITE     HTMLFILE;OPAITGIV;
          GOTO      OPAI9999
.
OPAI0600  MOVE      "tu",CATEGORY
          MOVE      OPAITUNK,CODE
          CALL      CODITM00
          GOTO      OPAI9999
.
OPAI0700  MOVE      "aC",CATEGORY
          MOVE      OPAIACNT,CODE
          CALL      CODITM00
          GOTO      OPAI9999
.
OPAI0800  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          UNPACK    OPAICOMM,KEY99,KEY1,KEY100
          IF        F1=1
            WRITE     HTMLFILE;KEY100;
          ELSE
            WRITE     HTMLFILE;KEY99;
          ENDIF
          GOTO      OPAI9999
.
OPAI0900  MATCH     SP70,OPAICDAT
          GOTO      OPAI9999 IF EQUAL
          UNPACK    OPAICDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OPAI9999
.
OPAI1000  WRITE     HTMLFILE;OPAICTIM;
          GOTO      OPAI9999
.
OPAI1100  WRITE     HTMLFILE;OPAICUID;
          GOTO      OPAI9999
.
OPAI1200  MATCH     SP70,OPAIUDAT
          GOTO      OPAI9999 IF EQUAL
          UNPACK    OPAIUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OPAI9999
.
OPAI1300  WRITE     HTMLFILE;OPAIUTIM;
          GOTO      OPAI9999
.
OPAI1400  WRITE     HTMLFILE;OPAIUUID;
          GOTO      OPAI9999
.
OPAI1500  WRITE     HTMLFILE;OPAIDELT;
          GOTO      OPAI9999
.
OPAI1600  WRITE     HTMLFILE;OPAIUDEL;
          GOTO      OPAI9999
.
OPAI1700  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          MOVE      SP70,CODE
          IF        F1=0
            MOVE      OPAIANTB,CODE    * Default value of Antibiotic code
          ELSE
            MOVE      OPAIPANT,CODE    * Default value of Prophylactic code
          ENDIF
          MOVE      "ab",CATEGORY
          CALL      ABPR0000       * Selection of Antibiotic/Prophylactic codes
          GOTO      OPAI9999
.
OPAI9999  RETURN
+
.------------------------------------------------------------
.   Selection list of Antibiotic/Prophylactic  codes
.------------------------------------------------------------
ABPR0000  MOVE      SP70,TDESC
          PACK      KEY5,CATEGORY,SP70
          PACK      KEY5,KEY2,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      CATEGORY,TCODE
            MOVE      "Invalid Category",TDESC
          ENDIF
          WRITE     HTMLFILE;"<!--<cat-c category=#"",TCODE,"#" name=#"",TDESC:
                             "#" map=#"",THCSCOD,"#"></cat-c>-->"
.
          PACK      PTCDKEY2,CATEGORY,SP70
          CALL      RDSCODE2
ABPR1000  CALL      RDKCODE2
          BRANCH    OVRCD,ABPR9999
          MATCH     CATEGORY,TCODE
          GOTO      ABPR9999 IF NOT EQUAL
          MATCH     SP70,ACODE
          GOTO      ABPR1000 IF EQUAL
.
          MATCH     "B",TCINDC1
          GOTO      ABPR2000 IF EQUAL       * for Antibiotic and Prophylactic
.
          IF        F1=0
            MATCH     SP70,TCINDC1
            GOTO      ABPR2000 IF EQUAL     * Antibiotic only
          ELSE
            MATCH     "P",TCINDC1
            GOTO      ABPR2000 IF EQUAL     * Prophylactic only
          ENDIF
          GOTO      ABPR1000
.
ABPR2000  PACK      KEY40,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                          TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                          TCINDC11,THCSCOD,PTCDNHCP,TCFORM6:
                          TCINDC12,TCINDC13,TCINDC14,TCINDC15,TCINDC16:
                          TCINDC17,TCINDC18,TCINDC19,TCINDC20,TCINDC21:
                          PTCOACTV
.
          MATCH     CODE,ACODE
          IF        @EQUAL
            IF        CATACTIV=1
              MATCH     "I",PTCOACTV               * Display active only
              GOTO      ABPR1000 IF EQUAL
            ENDIF
.
            WRITE     HTMLFILE;"<option value=#"",ACODE,KEY40,"#" selected>":
                               TDESC,"</option>"
          ELSE
            IF        CATACTIV<>2
              MATCH     "I",PTCOACTV
              GOTO      ABPR1000 IF EQUAL
            ENDIF
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,ABPR1000
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<option value=#"",ACODE,KEY40,"#">",TDESC:
                               "</option>"
          ENDIF
          GOTO      ABPR1000
ABPR9999  RETURN
+
.------------------------------------------------------------
.   Set Parameters for theatre antibiotics table
.------------------------------------------------------------
SOPABI00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SOPABI01,SOPABI02,SOPABI03,SOPABI04,SOPABI05:
                       SOPABI06,SOPABI07,SOPABI08,SOPABI09,SOPABI10:
                       SOPABI11,SOPABI12,SOPABI13,SOPABI14,SOPABI15:
                       SOPABI16
.
          MOVE      ONE,EXIT
          GOTO      SOPABI99
.
SOPABI01  MOVE      QRYDATA,OPABI001
          GOTO      SOPABI99
.
SOPABI02  MOVE      QRYDATA,OPABI002
          GOTO      SOPABI99
.
SOPABI03  MOVE      QRYDATA,OPABI003
          GOTO      SOPABI99
.
SOPABI04  MOVE      QRYDATA,OPABI004
          GOTO      SOPABI99
.
SOPABI05  MOVE      QRYDATA,OPABI005
          GOTO      SOPABI99
.
SOPABI06  MOVE      QRYDATA,OPABI006
          GOTO      SOPABI99
.
SOPABI07  MOVE      QRYDATA,OPABI007
          GOTO      SOPABI99
.
SOPABI08  MOVE      QRYDATA,OPABI008
          GOTO      SOPABI99
.
SOPABI09  MATCH     SP70,QRYDATA
          GOTO      SOPABI99 IF EQUAL
          GOTO      SOPABI99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OPABI009,CCENT,CYEAR,CMON,CDAY
          GOTO      SOPABI99
.
SOPABI10  MOVE      QRYDATA,OPABI010
          GOTO      SOPABI99
.
SOPABI11  MOVE      QRYDATA,OPABI011
          GOTO      SOPABI99
.
SOPABI12  MATCH     SP70,QRYDATA
          GOTO      SOPABI99 IF EQUAL
          GOTO      SOPABI99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OPABI012,CCENT,CYEAR,CMON,CDAY
          GOTO      SOPABI99
.
SOPABI13  MOVE      QRYDATA,OPABI013
          GOTO      SOPABI99
.
SOPABI14  MOVE      QRYDATA,OPABI014
          GOTO      SOPABI99
.
SOPABI15  MOVE      QRYDATA,OPABI015
          GOTO      SOPABI99
.
SOPABI16  MOVE      QRYDATA,OPABI016
          GOTO      SOPABI99
.
SOPABI99  RETURN
+
.------------------------------------------------------------------------
.         Move work variables to theatre antibiotics file variables
.------------------------------------------------------------------------
MVOPRABI  PACK      OPAIUNIQ,OPABI001,SP70
          PACK      OPAIANTB,OPABI002,SP70
          PACK      OPAICNTR,OPABI003,SP70
          PACK      OPAIPANT,OPABI004,SP70
          PACK      OPAITGIV,OPABI005,SP70
          PACK      OPAITUNK,OPABI006,SP70
          PACK      OPAIACNT,OPABI007,SP70
          PACK      OPAICOMM,OPABI008,SP70,SP70,SP70
          RETURN
+
