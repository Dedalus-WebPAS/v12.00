.******************************************************************************
.* Include Name    : PATCHADT                                  
.* Function        : Cancel and Change routines for Admissions, Discharges and
.*                   Transfers
.* Author          : Mike Laming (Code extracted from PATWEB96)       
.* Date            : 09/04/2002
.* Usage           : used in PATWEB96
.*                   and     DGSRVADT (Datagate Server program)
.*
.* Entry Points    : CHGADM00      Change Admission
.*                   CLADM000      Cancel Admission
.*                   CHDSC000      Change Discharge
.*                   CLDSC000      Cancel Discharge
.*                   CHMTR000      Cancel Transfer
.* Requirements    :
.*                   Admission No. to be in ADMISSNO
.*                                             
.*                                           
.* Modifications   :
.*                                           
.*        V12.00.01 29/05/2025 Bella Turco     TSK 0955096
.*                  Alphanumeric changes                           
.*        V11.01.01 10/11/2021 J.Tan           TSK 0880410
.*                  Mod for System Health fund on hold invoice
.*        V10.04.01 28/04/2014 Steve Armstrong     CAR 261630
.*                  Removed reference to PATAULFD, PATAUEFD & PATAUTFD
.*                  (no longer in use)
.******************************************************************************
.*        V10.03.03 15/05/2012 J.Tan               CAR 255708
.*                  Mods checking for Hold Invoices From Date
.*        V10.03.02 29/03/2012  Peter Vela        CAR 262683
.*                  Fixed RDK in CALBFE00
.*        V10.03.01 09/01/2012  J.Tan  CAR 257430
.*                  Replaced RDPTHLFA1 with RDPTHLF1
.******************************************************************************
.*        V10.02.02 25/06/2011 Steve Armstrong    CAR 240722
.*                  Mods to cater for changes to PATLOCFD.
.*                       - LSNAME & LGNAME extended to 40 chars.
.*                       - PTLCGNM2 added.
.*        V10.02.01 23/03/2011  Mike Laming       CAR 240107
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO
.******************************************************************************
.*        V10.01.04 02/02/2011  Steve Armstrong   CAR 237520
.*                  Mods to remove Detente interface (PATDETNT)
.*        V10.01.03 06/12/2010  Ebon Clements     CAR 233927
.*                  Added reason for hold update to invoice pending
.*        V10.01.02 01/12/2010  Peter Vela        CAR 233148
.*                  Initialised pattranf variables to spaces before write
.*        V10.01.01 04/11/2010  Jill Parkinson    CAR 233008
.*                  Removed references to PTCNUUNA
.******************************************************************************
.*        V10.00.02 12/04/2010  Steve Armstrong   CAR 219111
.*                  Mods for change from KEY17 to KEY25 in PATUNAFD.
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast messages
.******************************************************************************
.*        V9.12.01   15/06/2009 Ebon Clements  CAR 159088 
.*                   Added OERTIME - Expected return from leave time 
.*        V9.08.02   15/03/07 J.Tan    CAR 136149
.*                   Mods to use default claim code from system parameter
.*        V9.08.01   06/10/2006 J.Tan  CAR 119972
.*                   Mods CFEETYP=5 for NZ Private 
.*        V9.06.01   11/05/06 J.Tan    CAR 90040
.*                   Added Discharge status to patunaaf file
.*        V9.04.02   03/08/05 J.Tan    CAR 62750
.*                   Removed redefined amount variables
.*        V9.04.01   27/09/2004  Ebon Clements CAR 
.*                   Multi hospital changes for ADDVIS00 - does not get used
.*        V9.03.01   21/10/2003  J.Tan  CAR 44172
.*                   Mods for Misc.item theatre file (pmsmtiaf)
.*        V9.02.01   24/04/2002  Mike Laming  Mods to PATWEB96 SRF 3566
.*                   1) Replace CHAD0000 routine with new version.
.*                   2) Other minor changes
.*           ~       10/04/2002  Mike Laming  Datagate Mods
.*                   Add RPLYCODE field to contain all Datagate message codes
.*                                           
.******************************************************************************
.-----------------------------------------------------------------------------
. Change Admission Date/Time
.-----------------------------------------------------------------------------
CHGADM00  MOVE      ADMISSNO,AADMNO
.
.        Check if some one is accessing this patient
.
          MOVE      AADMNO,KEY8
          CALL      RDPTMIS1
          BRANCH    OVRCD,CHGADM90,CHGADM91
.
          COMPARE   ONE,ASTAT
          GOTO      CHGADM92 IF EQUAL
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHGADM93
.
          MOVE      AADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,CHGADM94
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
          MOVE      ADATE,OLDADATE
          REP       " 0",OLDADATE
.
          MATCH     PBDATE,PTMIS001
          GOTO      CHGADM95 IF LESS
.
          MATCH     SP70,PTMIS002
          IF        @EQUAL|@EOS
            GOTO      CHGADM89
          ENDIF
.
          MATCH     ADATE,PTMIS001
          GOTO      CHGADM50 IF NOT EQUAL
.
          MATCH     ATIME,PTMIS002
          GOTO      CHGADM50 IF NOT EQUAL
.
.                                           * HPS Code Starts Here
.         This code is used to update care type.(refer to patweb9801035.html)
          MATCH     ACARE,TRNACARE
          GOTO      CHGADM50 IF NOT EQUAL
.                                           * HPS Code  Ends Here
.
          GOTO      CHGADM99                * No Change
.
CHGADM50  CALL      CHTRNS00
          BRANCH    EXIT,CHGADM99
          CALL      DATRAN00                * Check Date Change Range
          BRANCH    EXIT,CHGADM99
.
          MOVE      ADMISSNO,KEY8
          CALL      RDPTMIS1
          BRANCH    OVRCD,CHGADM90,CHGADM91
.
          CALL      CHGAD000                * Change Admission Date
          BRANCH    EXIT,CHGADM99
.
          MOVE      "Admission Date Changed.",WEBTITLE
          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      CHGADM99
.
CHGADM89  CLEAR     WEBTITLE
          APPEND    "Admission time cannot be blank ",WEBTITLE
          APPEND    APORT,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02999",RPLYCODE        * eGate error msg "Missing PMI Mast"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM90  MOVE      "Invalid Admission Number.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admiss'n"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM91  CLEAR     WEBTITLE
          APPEND    "Patient Admission Busy on Terminal ",WEBTITLE
          APPEND    APORT,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Admission Busy"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM92  MOVE      "Admission not Admitted.  ",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02113",RPLYCODE        * eGate error msg "Patient not Admi"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM93  CLEAR     WEBTITLE
          APPEND    "Master File Data Missing",WEBTITLE
          APPEND    " for Patient.  Contact I.B.A Immediately.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02106",RPLYCODE        * eGate error msg "Missing PMI Mast"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM94  CLEAR     WEBTITLE
          APPEND    "Visit File Record Missing",WEBTITLE
          APPEND    " for Patient.  Contact I.B.A Immediately.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02101",RPLYCODE        * eGate error msg "Missing Visit"
          MOVE      ONE,EXIT
          GOTO      CHGADM99
.
CHGADM95  MOVE      "Birth date cannot be after Admission Date.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02204",RPLYCODE        * eGate error msg "Birth after Admi"
          MOVE      ONE,EXIT
          CALL      UUPTMIS1
          GOTO      CHGADM99
.
CHGADM99  RETURN
.
.-----------------------------------------------------------------------------
.         Major part of CHGADM00
.-----------------------------------------------------------------------------
CHGAD000  CALL      ACTASAV0          *Save old values for use in the audit file
          CALL      MEHC0000                * update mental health details
.
.         update the patdayaf file if appropriate
.
          MOVE      FIVE,OPTION
          CALL      PATD0000                * update the patdayaf file
.
.      Update Admission record in Transfer file
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CHGAD900
.
          MATCH     TADMN,AADMNO
          GOTO      CHGAD900 IF NOT EQUAL
.
          CMATCH    "A",TMOVE
          GOTO      CHGAD900 IF NOT EQUAL
.
          MOVE      PTMIS001,TDATE
          MOVE      PTMIS002,TTIME
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
.        Update the Fee's Raised Indicator - Use the earlier of the two dates
.
          MOVE      ADATE,TDATE
.
          MATCH     PTMIS001,TDATE
          GOTO      CHGAD010 IF LESS
.
          MOVE      PTMIS001,TDATE
.
CHGAD010  CALL      UPDCFR00
.
.        Update the Ward Usage Statistics and Admission/Discharge Statistics
.
CHGAD020  MOVE      ADATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                  * Get the financial period for date
          BRANCH    EXIT,CHGAD100          * Date not in period file
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      ONE,COUNT              * Number of Admissions
          MOVE      SEQ,FORM2              * Subtract old date
          CALL      UPNUMW00
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,ASRCE
          GOTO      CHGAD030 IF NOT EQUAL
.
          MOVE      "UNK",ASRCE
.
CHGAD030  PACK      KEY8,SADYEAR,CODEB,ASRCE
          CALL      CANSTA00
.
          MATCH     SP3,ATYPE
          GOTO      CHGAD040 IF NOT EQUAL
.
          MOVE      "UNK",ATYPE
.
CHGAD040  PACK      KEY8,SADYEAR,CODEA,ATYPE
          CALL      CANSTA00
.
.        Now add in data for new date
.
CHGAD050  MOVE      PTMIS001,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CHGAD110           * Date not in period file
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      ONE,FORM2               * Add new date
          CALL      UPNUMW00
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,ASRCE
          GOTO      CHGAD060 IF NOT EQUAL
.
          MOVE      "UNK",ASRCE
.
CHGAD060  PACK      KEY8,SADYEAR,CODEB,ASRCE
          CALL      RDSTAD1
          IF        OVRCD=0
            CALL      ADDSTAD
          ENDIF
.
          MATCH     SP3,ATYPE
          GOTO      CHGAD070 IF NOT EQUAL
.
          MOVE      "UNK",ATYPE
.
CHGAD070  PACK      KEY8,SADYEAR,CODEA,ATYPE
          CALL      RDSTAD1
          IF        OVRCD=0
            CALL      ADDSTAD
          ENDIF
          CALL      ADMHCS00
.
.        If U/R number is spaces ignore it
.
          MATCH     ZEROUR,AURNO
          GOTO      CHGAD130 IF EQUAL
.
.        Read and Update the new admission date to patient visit file
.
          UNPACK    PTMIS001,XCENT,XYEAR,XMON,XDAY
.
          MOVE      AURNO,PVIURNO
          MOVE      AADMNO,PVIBILL
          MOVE      ADATE,PVIDATE
          REP       " 0",PVIDATE
          MOVE      PVIBILL,KEY8
CHGAD080  CALL      RLPTVIS1
          BRANCH    OVRCD,CHGAD120,CHGAD090
.
.        Update the admission date
.
          PACK      PVIDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",PVIDATE
          CALL      UPVISA1
          CALL      UUPTVIS1
.
          PROC      CALCDLOS                * calc discharge los & ward
          PACK      KEY8,AADMNO
          CALL      RDADSCH1
          BRANCH    OVRCD,CHGAD130
.
          CALL      UPDSCH1
          GOTO      CHGAD130
.
.      Visit file locked
.
CHGAD090  CLEAR     WEBTITLE
          APPEND    "Visit ",WEBTITLE
          APPEND    PVIBILL,WEBTITLE
          APPEND    " Locked on Port ",WEBTITLE
          APPEND    PVILOCK,WEBTITLE
          MOVE      "02160",RPLYCODE        * eGate error msg "Visit Locked"
          RESET     WEBTITLE
          GOTO      CHGAD080
.
.        Period record not found in file
.
CHGAD100  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not Found in Period file.  ";
          GOTO      CHGAD020
.
CHGAD110  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not Found in Period file.  ";
          GOTO      CHGAD050
.
.        If not exist - Write a new record for Visit file
.
CHGAD120  PACK      PVIDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",PVIDATE
          PACK      KEY24,PVIURNO,PVIDATE,PVIBILL
          MOVE      THREE,PVITYPE
          MOVE      ADOCTA,PVIDOCT
          MOVE      ONE,PVISTAT
          MOVE      SP2,PVILOCK
          MOVE      ONE,PVITRAN
          MOVE      SP20,PVISPAR
.
          CALL      WRVISA1
..        CALL      ADDVIS00
.
.        Update Admission record - Check if Last Accom Date (ALACDTE) has
.        to be altered as well (Yes is no invoices printed, Maybe otherwise)
.
CHGAD130  MOVE      ALACDTE,SALACDAT
          MOVE      PTMIS001,ADATE
          MOVE      PTMIS002,ATIME
.
          COMPARE   ZERO,AINVPRT
          GOTO      CHGAD140 IF EQUAL
.
.        An invoice has been printed. If the new ADATE is less than ALACDTE
.        then don't alter ALACDTE, otherwise make ALACDTE the same as ADATE
.
          MATCH     ALACDTE,PTMIS001
          GOTO      CHGAD150 IF LESS
.
CHGAD140  MOVE      ADATE,ALACDTE
CHGAD150  MOVE      TRNACARE,ACARE          * HPS Code is here
          CALL      UPLMISS1
.
.         Broadcast change of admission details for Datagate
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICAC                * Change admission details 
.
.        Update Audit File if required
.
          BRANCH    CAUDA,CHGAD160
.
          MOVE      "B",ACTION
          CALL      WRAAUD
          MOVE      "C",ACTION
          CALL      ACTASAV0
          CALL      WRAAUD
.
CHGAD160  MOVE      THREE,PTUNTYPE
          MOVE      SADATE,PTUNODTE
          CALL      ADMHCS00                * Admission record
.
          COMPARE   THREE,ASTAT
          GOTO      CHGAD170 IF NOT EQUAL
.
          CALL      DISHCS00                * Discharge record
.
CHGAD170  MOVE      "Admission Changed.  ",WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      CHGAD999
.
CHGAD900  CLEAR     WEBTITLE
          APPEND    "Admission Transfer Record not Found.  Contact ",WEBTITLE
          APPEND    "I.B.A. Immediately.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admiss'n"
          MOVE      ONE,EXIT
          GOTO      CHGAD999
.
CHGAD999  RETURN
.
.-----------------------------------------------------------------------------
. Cancel Admission (un-admit by admission number)
.-----------------------------------------------------------------------------
CLADM000  MOVE      ADMISSNO,AADMNO
.
.        Check if some one is accessing this patient
.
          MOVE      AADMNO,KEY8
          CALL      RDPTMIS1
          BRANCH    OVRCD,CLADM960,CLADM970
.
          COMPARE   TWO,ASTAT
          GOTO      CLADM900 IF NOT EQUAL
.
          COMPARE   ZERO,AINVPRT
          GOTO      CLADM910 IF NOT EQUAL
.
          PACK      KEY14,AADMNO,SP20
          CALL      RSPMMTI1
          CALL      RKPMMTI1
          BRANCH    OVRCD,CLADM010
.
          MATCH     PMMIVISN,DAADMNO
          GOTO      CLADM920 IF EQUAL
.
.        Display basic patient details, and check that this is the right person
.
CLADM010  MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CLADM930
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,CLADM930
.
          MOVE      AADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,CLADM940
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
.        Write the MHMS adjustment data if necessary
.
          CALL      MEHB0000                * update MEH details
.
.       update the patdayaf file if appropriate
.
          MOVE      THREE,OPTION
          CALL      PATD0000                * update the patdayaf file
.
.       delete a PATDADFD record if appropriate
.
          BRANCH    PTCNUADT,CLADM020       * PATDADFD control parameter is on
          GOTO      CLADM030
.
CLADM020  PACK      KEY8,AADMNO
          CALL      RDPTDAD1                * read PATDADFD record
          BRANCH    OVRCD,CLADM030          * record not found
          CALL      DEPTDAD1                * delete the PATDADFD record
.
.        Update the Waiting Lists Treatment File if Needed
.
CLADM030  PACK      KEY8,AADMNO
          CALL      RDBKREC1
          BRANCH    OVRCD,CLADM060
.
          MOVE      ONE,BKRESTAT
          CALL      UPBKREC1
.
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
          CALL      RDTREA1
          BRANCH    OVRCD,CLADM060
.
. if the procedure is not performed, must delete the new W/L record made and
. return the original back to scheduled
.
          IF        WMSTAT = 8
            MOVE      WMBOOK,KEY8
            CALL      RDWTPNP1
            IF        OVRCD = ZERO
              PACK      KEY19,WMURNO,WMCODE,WTNPNCNT
              CALL      DETREA1
            ENDIF
          ENDIF
.
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
          CALL      RDTREA1
          MOVE      TWO,WMSTAT
          MOVE      "01",WTWMBSCD
          MOVE      ONE,NBRFLAG
          CALL      UPTREA1
          CALL      WRTHIS00
.
.        Release the bed
.
CLADM060  MOVE      AWARD,LWARD
          MOVE      ABED,LBED
          CALL      RELBED00
.
          MOVE      SP3,STWARD
.
.        Delete records from the additional charges file
.
          COMPARE   ZERO,PTCNADDC
          GOTO      CLADM080 IF EQUAL       * Not using additional charging
.
CLADM070  PACK      KEY16,AADMNO,SP20
          CALL      RSPTAH1                 * Position on & read the additional
          CALL      RKPTAH1                 * charges file
          BRANCH    OVRCD,CLADM080
.
          MATCH     AADMNO,PTAHADMN
          GOTO      CLADM080 IF NOT EQUAL
.
          PACK      KEY16,PTAHADMN,PTAHDATE
          CALL      DEPTAH1                 * Delete a record from the
          GOTO      CLADM070                * additional charges file
.
.         Broadcast cancel admission message to Datagate BEFORE deleting
.         the transfer file record
.
CLADM080  CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TWO,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCA                * cancel admission bcast   *C-90222
.
.        Delete all transfer file records
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CLADM140
.
          MATCH     TADMN,AADMNO
          GOTO      CLADM140 IF NOT EQUAL
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          CALL      DETRAN2
.
.        Set up parameters for Ward Usage file update
.
CLADM090  MOVE      TDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CLADM100           * Date not in period file
.
.        Check what type of record has been deleted
.
          MATCH     ANSA,TMOVE
          GOTO      CLADM110 IF EQUAL
.
          MATCH     ANSL,TMOVE
          GOTO      CLADM120 IF EQUAL
.
          MATCH     ANSR,TMOVE
          GOTO      CLADM130 IF EQUAL
.
.        Check if this was a ward transfer record
.
          MATCH     STWARD,TWARD
          GOTO      CLADM080 IF EQUAL
.
.        Update the Ward Usage statistics
.
          PACK      KEY9,STWARD,DRGYR,DRGNUM
          MOVE      THREE,COUNT             * Number of Transfers out
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      TWO,COUNT               * Number of Transfers in
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          MOVE      TWARD,STWARD
          GOTO      CLADM080
.
.        Period record missing from date range file
.
CLADM100  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not Found in Period file.  ";
          GOTO      CLADM090
.
.        We have deleted the Admission record - update the Ward Usage stats
.
CLADM110  PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      ONE,COUNT               * Number of Admissions
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          MOVE      TWARD,STWARD
          GOTO      CLADM080
.
.        We have deleted a Leave record - Update the Ward Usage statistics
.
CLADM120  PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      FIVE,COUNT              * Number of On-leaves
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          MOVE      TWARD,STWARD
          GOTO      CLADM080
.
.        We have deleted a Return record - Update the Ward Usage statistics
.
CLADM130  PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      SIX,COUNT               * Number of Returns
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          MOVE      TWARD,STWARD
          GOTO      CLADM080
.
.        Delete the record from the Invoice pending file
.        If Using the Invoice Pending File
.
CLADM140  IF        PTCNIPEN = 0
            MOVE      AADMNO,KEY8
            CALL      DEIPEN1
          ENDIF
.
.        Update the Admission record
.
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
          MOVE      ONE,ASTAT
          CALL      UPLMISS1
.
.        Delete the entry from the In-patient location file
.
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      DELOCA1
.
.        Update the Fee's Raised Indicator
.
          MOVE      ADATE,TDATE
          CALL      UPDCFR00
.
.        Write an Audit record if needed
.
          BRANCH    CAUDA,CLADM150
          MOVE      "D",ACTION
          CALL      ACTASAV0
          CALL      WRAAUD
.
.        Write HCS Admission audit file (Unadmit)
.
CLADM150  MOVE      ONE,PTUNTYPE            * Unadmit record
          MOVE      ADATE,PTUNODTE
          CALL      ADMHCS00
.
.        Update the Admission/Discharge Statistics
.
          MOVE      ADATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CLADM950           * Date not in period file
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,ASRCE
          GOTO      CLADM160 IF NOT EQUAL
.
          MOVE      "UNK",ASRCE
CLADM160  PACK      KEY8,SADYEAR,CODEB,ASRCE
          CALL      CANSTA00
.
          MATCH     SP3,ATYPE
          GOTO      CLADM170 IF NOT EQUAL
.
          MOVE      "UNK",ATYPE
CLADM170  PACK      KEY8,SADYEAR,CODEA,ATYPE
          CALL      CANSTA00
.
.       Check if there is a standby patient for the bed just vacanted
.
          CALL      SBYPT000
.
          MOVE      "Patient Unadmitted.  ",WEBTITLE
          PACK      KEY8,ADMISSNO           * Restore correct admission num
          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      CLADM999
.
CLADM900  MOVE      "Cannot unadmit until undischarged.  ",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02201",RPLYCODE        * eGate error msg "Must be Un-dsched
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM910  MOVE      "Admission has been Invoiced.  ",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02202",RPLYCODE        * eGate error msg "Adm. Invoiced"
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM920  MOVE      "Admission has Misc. Items.  ",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02102",RPLYCODE        * eGate error msg "Missing Transfer"
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM930  MOVE      "Master File Data Missing",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02106",RPLYCODE        * eGate error msg "Missing PMI Mast"
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM940  MOVE      "Visit File Record Missing",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02101",RPLYCODE        * eGate error msg "Missing Visit"
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM950  MOVE      "Date not found in period file",WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMIS1
          MOVE      "02142",RPLYCODE        * eGate error msg "not on Period fil
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM960  MOVE      "Invalid Admission Number",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admiss'n
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM970  CLEAR     WEBTITLE
          APPEND    "Patient Busy on Terminal ",WEBTITLE
          APPEND    APORT,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Busy on another
          MOVE      ONE,EXIT
          GOTO      CLADM999
.
CLADM999  RETURN
.-----------------------------------------------------------------------------
. Change Discharge Date
.-----------------------------------------------------------------------------
CHDSC000  MOVE      ADMISSNO,KEY8   *Check if some one is accessing this patient
          CALL      RDPTMIS1
          BRANCH    OVRCD,CHDSC992,CHDSC993
          CALL      RDPMVX11
          BRANCH    OVRCD,CHDSC992,CHDSC993
.
.        Check that this person can have their discharge date changed
.
          MOVE      PTDSC001,TRANDATE
          MOVE      PTDSC002,TRANTIME
          COMPARE   THREE,ASTAT
          GOTO      CHDSC994 IF NOT EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,CHDSC995
.
.        Check that this is the right person
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHDSC997
.
          MOVE      AADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,CHDSC998
.
          MATCH     SP70,PTDSC002
          IF        @EQUAL|@EOS
            GOTO      CHDSC970
          ENDIF
.
          MATCH     DDATE,PTDSC001
          IF        @EQUAL
            MATCH     DTIME,PTDSC002
            GOTO      CHDSC900 IF EQUAL
          ENDIF
.
          CALL      CHKDDS00                * Check discharge date is before
          COMPARE   ONE,EXIT                * the next admision date adn time
          IF        @EQUAL
            MOVE      ADMISSNO,KEY8
            CALL      UUPTMIS1
            GOTO      CHDSC999
          ENDIF
.
.        Check that we are not discharging into the future
.
          PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
.
          MATCH     PTDSC001,XDATE
          GOTO      CHDSC005 IF NOT LESS
.
          CLOCK     TIME,CTIMEIS
          MATCH     PTDSC002,CTIMEIS
          GOTO      CHDSC990 IF LESS
.
.        Check that the new discharge date is greater than the second last
.        record in the bed transfer file (last record is the old discharge
.        record)
.
CHDSC005  PACK      KEY30,AADMNO,PTDSC001,PTDSC002,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CHDSC010
.
          MATCH     TADMN,AADMNO
          GOTO      CHDSC010 IF NOT EQUAL
.
          MATCH     "D",TMOVE
          GOTO      CHDSC010 IF EQUAL
          GOTO      CHDSC991                * Disc date later than bed movement
.
.        Date is okay as far as the bed transfer file is concerned.
.        Check if change is more than CERRDAY days, and if so, check if
.        the user is less than CERRSEC security level. If so, not allowed.
.
CHDSC010  UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE2,XCENT,XYEAR,XMON,XDAY
.
          MOVE      PTDSC001,WDATE1
          CALL      DIFFDY00
          MULT      COUNT,NBRDAYS           * To ensure it is positive
.
          COMPARE   NBRDAYS,CERRDAY
          GOTO      CHDSC020 IF LESS
.
.        Check if change being attempted within CERRDAY days of discharge
.
          PACK      WDATE2,CCC,CYY,CMM,CDD
          REP       " 0",WDATE2
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE1,XCENT,XYEAR,XMON,XDAY
          CALL      DIFFDY00
          MULT      COUNT,NBRDAYS           * To ensure it is positive
.
          COMPARE   NBRDAYS,CERRDAY
          GOTO      CHDSC030 IF LESS
          GOTO      CHDSC040
.
CHDSC020  COMPARE   CERRSEC,SECLEV
          GOTO      CHDSC040 IF NOT LESS
.
          CLEAR     WEBTITLE
          APPEND    "Change for more than ",WEBTITLE
          APPEND    CERRDAY,WEBTITLE
          APPEND    " days.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02999",RPLYCODE        * eGate error msg "Misc. Error"
          MOVE      ONE,EXIT
          GOTO      CHDSC999
.
CHDSC030  COMPARE   CERRSEC,SECLEV
          GOTO      CHDSC040 IF NOT LESS
.
          CLEAR     WEBTITLE
          APPEND    "Change not within ",WEBTITLE
          APPEND    CERRDAY,WEBTITLE
          APPEND    " days of discharge.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02999",RPLYCODE        * eGate error msg "Misc. Error"
          MOVE      ONE,EXIT
          GOTO      CHDSC999
.
.        Print a warning message if new discharge date is not the same
.        year/month as the old discharge date
.
CHDSC040  UNPACK    PTDSC001,DIM6
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      KEY6,XCENT,XYEAR,XMON
.
          MATCH     DIM6,KEY6
          GOTO      CHDSC050 IF NOT LESS
.
          DISPLAY   "Warning: New Discharge Date in a Later ":
                    "Month to Old Discharge Date.";
.
.        Save old values for use in the audit file
.
CHDSC050  CALL      ACTBSAV0
.
.        Update the patdayaf file if appropriate
.
          MOVE      SIX,OPTION
          CALL      PATD0000                * update the patdayaf file
.
.        Update the Discharge record in the Transfer file
.
          PACK      KEY30,AADMNO,DDATE,DTIME,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CHDSC980
.
          MATCH     TADMN,AADMNO
          GOTO      CHDSC980 IF NOT EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      CHDSC980 IF NOT EQUAL
.
          MOVE      PTDSC001,TDATE
          MOVE      PTDSC002,TTIME
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
.        Update the Fee's Raised Indicator - Use the earlier of the two dates
.
          MOVE      DDATE,TDATE
.
          MATCH     PTDSC001,TDATE
          GOTO      CHDSC060 IF LESS
          MOVE      PTDSC001,TDATE
.
CHDSC060  CALL      UPDCFR00                * Update the Control File
.
.        Update the Ward Usage Statistics and Admission/Discharge Statistics
.
CHDSC065  MOVE      DDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CHDSC981           * Date not in period file
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      FOUR,COUNT              * Number of Discharges
          MOVE      SEQ,FORM2               * Subtract old date
          CALL      UPNUMW00
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,DDEST
          GOTO      CHDSC070 IF NOT EQUAL
.
          MOVE      "UNK",DDEST
.
CHDSC070  PACK      KEY8,SADYEAR,CODED,DDEST
          CALL      CANSTA00
.
.        Now add in data for new date
.
CHDSC075  MOVE      PTDSC001,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CHDSC982           * Date not in period file
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      ONE,FORM2               * Add new date
          CALL      UPNUMW00
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,DDEST
          GOTO      CHDSC080 IF NOT EQUAL
.
          MOVE      "UNK",DDEST
.
CHDSC080  PACK      KEY8,SADYEAR,CODED,DDEST
          CALL      RDSTAD1
          IF        OVRCD=0
            CALL      ADDSTAD
          ENDIF
.
.        Update discharge record
.
          MOVE      PTDSC001,DDATE
          MOVE      PTDSC002,DTIME
.
          PROC      CALCDLOS                * Calculate Discharge Ward & LOS
          CALL      UPDSCH1
.
          MOVE      FOUR,PTUNTYPE
          MOVE      SDDATE,PTUNODTE
          CALL      DISHCS00
.
.         Broadcast change of discharge details for Datagate
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      THREE,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICDC                * change discharge details *C-90222
.
.        Update the audit file if required
.
          BRANCH    CAUDB,CHDSC900
.
          MOVE      "B",ACTION
          CALL      WRBAUD
          MOVE      "C",ACTION
          CALL      ACTBSAV0
          CALL      WRBAUD
.
.         Check if invoice fully printed ?
.
CHDSC900  REP       " 0",ALACDTE
          MATCH     ALACDTE,SDDATE          * check with the old disc.date
          GOTO      CHDSC920 IF NOT EQUAL
.
.        Allow for the case where this is a day case not yet invoiced
.
          COMPARE   ZERO,AINVPRT
          GOTO      CHDSC920 IF EQUAL
.
          MOVE      PTDSC001,ALACDTE        * set to the new discharged date
          CALL      UPLMISS1
          GOTO      CHDSC960
.
.        Check if patient has changed from daycase to overnight or
.        from overnight to daycase
.
CHDSC920  CALL      CFEE0000                * recalculate bed fees
.
.        Correct last accomodation date in admission record
.
CHDSC960  MOVE      "Discharge Date Changed.  ",WEBTITLE
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      CHDSC999
.
CHDSC970  MOVE      "Cannot have blank discharge time.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02210",RPLYCODE        * eGate error msg "Blank Disch Time"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC980  MOVE      "Discharge Transfer Record not Found.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02102",RPLYCODE        * eGate error msg "Missing Transfer"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC981  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not found in Period File. Trying Again...",*W5
          GOTO      CHDSC065
.
CHDSC982  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not found in Period File. Trying Again...",*W5
          GOTO      CHDSC075
.
CHDSC990  MOVE      "Cannot Discharge into the Future.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02211",RPLYCODE        * eGate error msg "Future Dsch Date"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC991  UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
          PACK      DISPDTTM,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,SP1,TTIME
          CLEAR     WEBTITLE
          APPEND    "Discharge Date/Time Must be >= Bed Movement on ",WEBTITLE
          APPEND    DISPDTTM,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          MOVE      "Discharge must be after Bed Movement",WEBTITLE
          MOVE      "02212",RPLYCODE        * eGate error msg "Dsch after Bed M"
          MOVE      ONE,EXIT
          GOTO      CHDSC999
.
CHDSC992  MOVE      "Invalid Admission Number.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admiss'n"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC993  MOVE      "Patient Admission Busy on Terminal.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Admission Locked"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC994  MOVE      "Admission not Discharged.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02213",RPLYCODE        * eGate error msg "Patient not Dsch"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC995  MOVE      "No Discharged Data Available.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02105",RPLYCODE        * eGate error msg "Discharge Missin"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC996  MOVE      "Admission not Fully Invoiced.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02214",RPLYCODE        * eGate error msg "Adm partially In"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC997  MOVE      "Master File Data Missing for Patient",WEBTITLE
          CALL      WEBERR00
          MOVE      "02106",RPLYCODE        * eGate error msg "Missing PMI Mast"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC998  MOVE      "Visit File Record Missing for Patient",WEBTITLE
          CALL      WEBERR00
          MOVE      "02101",RPLYCODE        * eGate error msg "Visit Missing"
          MOVE      ONE,EXIT
          MOVE      ADMISSNO,KEY8
          CALL      UUPTMIS1
          GOTO      CHDSC999
.
CHDSC999  RETURN
.-----------------------------------------------------------------------------
.Cancel Discharge (un-discharge by admission number)
.-----------------------------------------------------------------------------
CLDSC000  MOVE      ZERO,STBYFLAG
          MOVE      ADMISSNO,AADMNO
.
.        Check if some one is accessing this patient
.
          MOVE      AADMNO,KEY8
          CALL      RDPTMIS1
          BRANCH    OVRCD,CLDSC900,CLDSC910
.
.        Check that this is the patient desired
.
          COMPARE   THREE,ASTAT
          GOTO      CLDSC920 IF NOT EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,CLDSC930
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,CLDSC905
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,CLDSC905
.
.        Check if this U/R already has a current admission
.
          MOVE      AADMNO,SAVADMN
          MOVE      AURNO,PURNO
          MOVE      PURNO,PVIURNO
          PACK      KEY24,PVIURNO,SP20
          CALL      RDSVISA2
CLDSC010  CALL      RDKVISA2
          BRANCH    OVRCD,CLDSC020
.
          MATCH     PURNO,PVIURNO
          GOTO      CLDSC020 IF NOT EQUAL
.
.        Check if INPATIENT
.
          COMPARE   THREE,PVITYPE
          GOTO      CLDSC010 IF NOT EQUAL
.
.        Read the admission file
.
          MOVE      PVIBILL,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,CLDSC020
.
          BRANCH    ASTAT,CLDSC010,CLDSC940,CLDSC010,CLDSC940,CLDSC010
          GOTO      CLDSC010
.
CLDSC020  MOVE      SAVADMN,KEY8            * restore entered admission no.
          CALL      RDMISS1
.
....                                        * PTCNUCEP no longer used *D-240107
....      COMPARE   ONE,PTCNUCEP            * Using consultant episodes ?
....      GOTO      CLDSC030 IF NOT EQUAL   * No, continue with undischarge
....
.....    Check that this episode has not had disease coding
....
....      PACK      KEY30,AADMNO,DDATE,DTIME,SP6
....      CALL      RDSTRAN2
....      CALL      RDKTRAN2
....      BRANCH    OVRCD,CLDSC950
.
....      MATCH     TADMN,AADMNO
....      GOTO      CLDSC950 IF NOT EQUAL
.
....      CMATCH    "D",TMOVE
....      GOTO      CLDSC950 IF NOT EQUAL
....
....      CALL      CEPC0000                * Check current episode for coding
....      BRANCH    EXIT,CLDSC999
....                                        *                  * end  *D-240107
.
.        Determine which Ward/Bed this patient is to go into
.
CLDSC030  UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      KEY30,AADMNO,DDATE,DTIME,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CLDSC950
.
          MATCH     TADMN,AADMNO
          GOTO      CLDSC950 IF NOT EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      CLDSC950 IF NOT EQUAL
.
. Check if patient was discharged whilst on leave
.
          PACK      SKEY30,TADMN,TDATE,TTIME,TWARD,TBED
          MOVE      ZERO,ONLEAVE
          CALL      RDPTRAN2
          MATCH     "R",TMOVE               * return from leave record?
          GOTO      CLDSC040 IF NOT EQUAL
.         COMPARE   ONE,TFORM6B             * Computer generated?
.         GOTO      CLDSC040 IF NOT EQUAL
          MOVE      ONE,ONLEAVE             * pat. was on leave when discharged
.
CLDSC040  MOVE      SKEY30,KEY30
          CALL      RDTRAN2                 * re-read "D" record
          BRANCH    ONLEAVE,CLDSC050        *branch if on leave when discharged
.
.        Check that Ward and bed have not been taken
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD,CLDSC980
.
          MATCH     ZEROVISN,WADMNO
          IF        !@EQUAL
            MATCH     ZEROVISN,WSTBY
            GOTO      CLDSC050 IF EQUAL
          ELSE
            GOTO      CLDSC050
          ENDIF
.
          MATCH     AADMNO,WADMNO
          GOTO      CLDSC990 IF NOT EQUAL
.
. check the pataxe file to determine whether the letter of cert. has been run
.
CLDSC050  IF        PTCNAXEU = 1
            PACK      KEY8,AADMNO,SP10
            CALL      RDPTAXE1
            BRANCH    OVRCD,CLDSC970
.
            IF        PTAXDAMI > 2
              GOTO      CLDSC960
            ENDIF
.
            MOVE      ZERO,PTAXDAMI         * not processed
            CALL      UPPTAXE1
          ENDIF
.
CLDSC055  MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,CLDSC060
.
          PACK      KEY5,ANSD,ANSD,DDEST
          CALL      RDCODE1
          BRANCH    OVRCD,CLDSC060
.
.        If discharged as deceased return deceased indicator to alive
.
          MATCH     ANSD,TCINDC1
          IF        @EQUAL
            MOVE      PURNO,KEY8
            CALL      RDMAST1
            MOVE      "0",PCEASE
            MOVE      SP70,PDECDTE
            MOVE      SP70,PMPXDETY
            CALL      UPMAST1
            CALL      UUPTMAS1
            CALL      UPPMPX21
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      FOUR,HL7TRGID
            MOVE      "PATCHADT",HL7INCLD
            PROC      DGCLICUP              * write PMI changes
          ENDIF
.
CLDSC060  BRANCH    ONLEAVE,CLDSC090        * Dont do if on leave
.
          MATCH     SP3,TBED
          GOTO      CLDSC080 IF EQUAL
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
.
          MATCH     ZEROVISN,WADMNO
          GOTO      CLDSC070 IF EQUAL
.
          MATCH     ZEROVISN,WSTBY
          IF        @EQUAL
            MOVE      AADMNO,WSTBY
            MOVE      ONE,STBYFLAG
            GOTO      CLDSC075
          ENDIF
.
          MATCH     AADMNO,WADMNO
          GOTO      CLDSC990 IF NOT EQUAL
.
CLDSC070  MOVE      AADMNO,WADMNO
CLDSC075  MOVE      ZERO,WPLUR
          CALL      UPWARD1
.
          GOTO      CLDSC090
.
.        Add a No-Bed record
.
CLDSC080  MOVE      APLUR,NBPLUR
          MOVE      SP3,NBROOM
          PACK      KEY13,TWARD,AADMNO,NBPLUR
          CALL      RANOBE1
          IF        OVRCD=1
            CALL      WRNOBE1
          ENDIF
.
CLDSC090  PACK      SKEY30,TADMN,TDATE,TTIME,TWARD,TBED
.
.------ update the patdayaf file if appropriate ------
.
          MOVE      ONE,OPTION
          CALL      PATD0000                * update the patdayaf file
.
.------ update or delete a PATDADFD record if appropriate (29/05/90) -------
.
          BRANCH    PTCNUADT,CLDSC100       * PATDADFD control parameter is on
          GOTO      CLDSC120
.
CLDSC100  PACK      KEY8,AADMNO
          CALL      RDPTDAD1                * read PATDADFD record
          BRANCH    OVRCD,CLDSC120          * record not found
.
          MATCH     SP5,PTDAADTS            * Adm Transfer Source = spaces ?
          GOTO      CLDSC110 IF EQUAL       * yes
.
          MOVE      SP5,PTDADCTS            * set Dsch Transfer Source to blank
          CALL      UPPTDAD1                * update the PATDADFD record
          GOTO      CLDSC120
.
CLDSC110  CALL      DEPTDAD1                * delete PATDADFD record
.
.         Broadcast undischarge to Datagate BEFORE deleting the transfer
.         file record
.
CLDSC120  CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      FIVE,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCD                * send cancel discharge   *C-90222
.
.        Delete Discharge Transfer file record
.                                              to Datagate
          MOVE      TWARD,AWARD
          MOVE      TBED,ABED
          MOVE      SKEY30,KEY30
          CALL      DETRAN2
.
.------   check if previous patient was on leave when discharged  ---
.
          IF        ONLEAVE = 1
            MOVE      SKEY30,KEY30
            CALL      RDSTRAN2
            CALL      RDPTRAN2
            PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL      DETRAN2               * delete record
.
.  Write on leave record for  this patient
.
            MOVE      TWARD,OWARD
            MOVE      TBED,OBED
            MOVE      TADMN,OADMNO
            PACK      KEY14,OWARD,OBED,OADMNO
            CALL      RDONLV1
            IF        OVRCD = 1
              MOVE      SP3,OROOM
              MOVE      SP8,OERDATE
              MOVE      SP8,OERTIME
              CALL      WRONLV1
            ENDIF
          ENDIF
.
.        Fix up the Ward Usage Statistics
.
CLDSC130  MOVE      TDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CLDSC140           * Date not in period file
.
          PACK      KEY9,TWARD,DRGYR,DRGNUM
          MOVE      FOUR,COUNT              * Number of Discharges
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
          GOTO      CLDSC150
.
CLDSC140  UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE," not Found in Period file. Trying again... ",*W5
          GOTO      CLDSC130
.
.        Delete Discharge record
.
CLDSC150  MOVE      DADMNO,KEY8
          CALL      DEDSCH1
.
          MOVE      TWO,PTUNTYPE
          MOVE      DDATE,PTUNODTE
          CALL      DISHCS00
.
.        Update Admission record
.
          IF        STBYFLAG=1
            MOVE      SP70,AWARD
            MOVE      SP70,ABED
          ENDIF
.
          IF        ONLEAVE=1
            MOVE      FOUR,ASTAT
          ELSE
            MOVE      TWO,ASTAT
          ENDIF
          CALL      UPLMISS1
.
          CALL      MEHA0000                * process MEH data
.
.        Fix the Waiting Lists File if Needed
.
          PACK      KEY8,AADMNO
          CALL      RDBKREC1
          BRANCH    OVRCD,CLDSC160
.
          MOVE      THREE,BKRESTAT
          CALL      UPBKREC1
.
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
          CALL      RDTREA1
          BRANCH    OVRCD,CLDSC160
.
. if the procedure is not performed, must delete the new W/L record made and
. return the original back to admitted
.
          IF        WMSTAT = 8
            OPEN      WATPNPA1,"watpnpaf";
            MOVE      WMBOOK,KEY8
            CALL      RDWTPNP1
            IF        OVRCD = ZERO
              PACK      KEY19,WMURNO,WMCODE,WTNPNCNT
              CALL      DETREA1
            ENDIF
            CLOSE     WATPNPA1
          ENDIF
.
          MOVE      FOUR,WMSTAT
          MOVE      ANSN,WTNBRSFL
          MOVE      ANSD,WTNBRTYP
          CALL      UPTREA1
          CALL      WRWATNBR
.
.        Put the patient into the Inpatient Location file
.
CLDSC160  MOVE      AURNO,LURNO
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      WRLOCA1
.
.        Update the Fee's Raised Indicator
.
          MOVE      DDATE,TDATE
          CALL      UPDCFR00
.
.        Check if a record exists in PATIPEFD, and if not, add one
.
. If Using the Invoice Pending File
.
          IF        PTCNIPEN = 0
            MOVE      AADMNO,IPADMNO
            MOVE      THREE,IPSYSTM
            MOVE      SP70,IPSITE
.
            MOVE      SP70,PENDHOSP
            IF        IBCNMHOS=1
              MOVE      IPADMNO,KEY8
              CALL      RDPMVX11
              IF        OVRCD=0
                MOVE      PMVXMHOS,PENDHOSP         * Hospital ID
              ENDIF
            ENDIF
            MOVE      " 3",PENDSYST                 * Inpatient
.
            MOVE      TDATE,PENDSDAT            * as at date
            MOVE      ADATE,PENDADAT            * admission date
            MOVE      SP70,PENDDDAT             * discharged date
            MOVE      AFUNDH,PENDFUND       
            MOVE      ACLAIM,PENDCLAM        
            CALL      IPRH0000                
            MOVE      TWO,PTIPRSTA            
            MOVE      SP70,PTIPSVAR           
.
            PACK      KEY8,IPADMNO,SP70
            CALL      RDIPEN1
            IF        OVRCD=1
              CALL      WRIPEN1
            ELSE
              CALL      UPIPEN1
            ENDIF
          ENDIF
.
.        Write a delete record to the Audit File if wanted
.
CLDSC170  BRANCH    CAUDB,CLDSC180
          MOVE      "D",ACTION
          MOVE      DADMNO,SDADMNO          * added for V5.01.125
          CALL      WRBAUD
.
.        Fix up the Admission Discharge statistics
.
CLDSC180  MOVE      DDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,CLDSC999           * Date not in period file
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MATCH     SP3,DDEST
          GOTO      CLDSC190 IF NOT EQUAL
.
          MOVE      "UNK",DDEST
CLDSC190  PACK      KEY8,SADYEAR,CODED,DDEST
          CALL      CANSTA00
.
.         Check if the patient was a day case (before undischarged)
.
          MATCH     DDATE,ADATE
          GOTO      CLDSC200 IF NOT EQUAL
.
.         Yes. -  Change the fee back to use the inpatient rate
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CLDSC200
.
          MATCH     TADMN,AADMNO
          GOTO      CLDSC200 IF NOT EQUAL
.
          MOVE      ADATE,NEWTDATE
          MOVE      TWARD,NEWTWARD
          MOVE      TATYPE,NEWTATYP
          MOVE      TRBTYP,NEWTBTYP
          MOVE      TCHSTAT,NEWTCHST
          MOVE      TDEPT,NEWTDEPT
          CALL      BRAT0000
          MOVE      SAVEND,TRBEND
          MOVE      ADDEND,PTTRAEND
          MOVE      SAVPAT,TRATEF
          MOVE      SAVHF,TRATEH
          MOVE      ADISC,TDISC
          MOVE      ZERO,TALLW
          MOVE      SAVEXTR,TEXTRA
          MOVE      AUSR2,TCHSTAT
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
CLDSC200  MOVE      "Patient Undischarged.  ",WEBTITLE
.
.                                           * HPS Code Starts Here
.
          PACK      KEY8,TADMN,SP70
          CALL      DEMRPDS1
.
.                                           * HPS Code Ends Here

          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      CLDSC999
.
CLDSC900  MOVE      "Invalid Admission Number.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admiss'n"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC905  MOVE      "Invalid UR Number.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02106",RPLYCODE        * eGate error msg "Missing PMI Mast"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC910  MOVE      "Patient Admission Busy on Terminal ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Admission Locked"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC920  MOVE      "Admission not Discharged.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02215",RPLYCODE        * eGate error msg "Admn not Dischar"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC930  MOVE      "No Discharge Data Available.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02105",RPLYCODE        * eGate error msg "Missing Discharg"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC940  MOVE      "Patient Currently Admitted ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02114",RPLYCODE        * eGate error msg "Currently Admitt"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC950  MOVE      "Discharge Transfer Record not Found.",WEBTITLE
          CALL      WEBERR00
          MOVE      "02102",RPLYCODE        * eGate error msg "Missing Transfer"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC960  MOVE      "Cannot Undischarge - Certification has been done",WEBTITLE
          CALL      WEBERR00
          MOVE      "02216",RPLYCODE        * eGate error msg "Cretific is done"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC970  MOVE      "Pataxeaf Record Missing",WEBTITLE
          CALL      WEBERR00
          MOVE      "02107",RPLYCODE        * eGate error msg "Missing Admn Ext"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC980  MOVE      "Ward/Bed not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02130",RPLYCODE        * eGate error msg "Invalid Ward/Bed"
          MOVE      ONE,EXIT
          GOTO      CLDSC999
.
CLDSC990  CLEAR     WEBTITLE
          APPEND    "Ward - ",WEBTITLE
          APPEND    TWARD,WEBTITLE
          APPEND    " / Bed - ",WEBTITLE
          APPEND    TBED,WEBTITLE
          APPEND    " has been taken. ",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02131",RPLYCODE        * eGate error msg "Ward/Bed occupied
          MOVE      ONE,EXIT
.
CLDSC999  MOVE      AADMNO,KEY8
          CALL      UUPTMIS1
          RETURN
.
.                                           * HPS Code Starts Here
.***********************************************************
.         CHMTR000 - Cancel Transfer/movement routine      *
.                       WHEN UPDATEY=6                     *
.***********************************************************
.
CHMTR000  MOVE      ADMISSNO,AADMNO
.
          MOVE      ZERO,LASTFLAG
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2

          CALL      RDSTRAN2
          CALL      RDKTRAN2
          IF        OVRCD=1
            MOVE      ONE,LASTFLAG
            GOTO      CHMTR005
          ENDIF
          MATCH     AADMNO,TADMN
          GOTO      CHMTR005 IF EQUAL
.
          MOVE      ONE,LASTFLAG
.
CHMTR005  IF        LASTFLAG=1
            CALL      CNLBT000
            GOTO      CHMTR999
          ENDIF
.
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS,SP70
          CALL      RDTRAN2
          BRANCH    OVRCD,CHMTR095
.
          MOVE      TWARD,STWARD            * Save ward
          MOVE      STWARD,LSTWARD
          MOVE      TBED,STBED              * Save Bed

          MOVE      STWARD,LSTWARD
          MOVE      TBED,STBED              * Save Bed
          MOVE      STBED,LSTBED
          MOVE      TDATE,STDATE            * Save date
          MOVE      TTIME,STTIME            * Save time
.
          MATCH     "C",TMOVE               * Cancel change
          GOTO      CHMTR010 IF EQUAL
.
          MATCH     "L",TMOVE               * Cancel leave
          GOTO      CHMTR040 IF EQUAL
.
          MATCH     "R",TMOVE               * Return option
          GOTO      CHMTR060 IF EQUAL
.
          GOTO      CHMTR095
.
.Change Record
.
CHMTR010  COMPARE   "0",CHGCARE             * Check for change in care class
          IF        !@EQUAL
            PACK      KEY24,CHADMN,CHDATE,CHTIME,SP70
            CALL      DECHCO1
            GOTO      CHMTR999
          ENDIF
.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
          PACK      KEY30 WITH AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      RDTRAN2
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      SIX,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCT                * b'cast cancel transfer
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          CALL      DETRAN2                 * Removing from transfer file
.
          CALL      CHKDIS00                                           *I-90201
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2                * Looping through transfer file
CHMTR012  CALL      RDKTRAN2
          BRANCH    OVRCD,CHMTR015
.
          MATCH     AADMNO,TADMN
          GOTO      CHMTR015 IF NOT EQUAL
.
          CALL      PUBFEE00
          CALL      UPTRN000                * Updating transfer file
          GOTO      CHMTR012
.
CHMTR015  COMPARE   ZERO,AINVPRT            * Checking for invoice printing
          GOTO      CHMTR090 IF NOT EQUAL
          GOTO      CHMTR999
.
. End Change Record
.
. Leave Record
.
CHMTR040  CALL      CHKLVE00
          BRANCH    EXIT,CHMTR095
.
CHMTR045  BRANCH    LEAVEFLG,CHMTR050,CHMTR054,CHMTR056
.
CHMTR050  CALL      GETLBT00                * Get last ward and bed before leave
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS
          CALL      RDTRAN2
          BRANCH    OVRCD,CHMTR095
.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
          PACK      KEY30 WITH AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      RDTRAN2
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      SEVEN,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCT                * b'cast cancel transfer   *C-90222
          CALL      DELONL00                * Reverse last leave record
          GOTO      CHMTR059
.
CHMTR054  CALL      GETLDT00                * Get last bed and ward details
.
          PACK      KEY30,DELKEY30
          CALL      RDTRAN2
          BRANCH    OVRCD,CHMTR095
.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record

.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
          PACK      KEY30 WITH AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      RDTRAN2
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      EIGHT,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCT                * b'cast cancel transfer   *C-90222
          CALL      DELRET00                * Reverse last return record
          CALL      GETLBT00                * Get last ward and bed before leave
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS
          CALL      RDTRAN2
          BRANCH    OVRCD,CHMTR095
          CALL      DELONL00                * Reverse last on leave record
          GOTO      CHMTR059

CHMTR056  CALL      DELREC00                * Remove leave and return record
          GOTO      CHMTR059
.
CHMTR059  CALL      CALBFE00                * Recalculate all bed fees
          GOTO      CHMTR999
.
. End Leave Record
.
. Return From Leave
.
CHMTR060  PACK      SAVKEY30,SP70
          MOVE      ZERO,LOOPFLG
          PACK      KEY30,AADMNO,Z70        * Work out what ward and bed the
          CALL      RDSTRAN2                * patient should be on leave from
CHMTR065  CALL      RDPTRAN2                * as we are going to remove the
          BRANCH    OVRCD,CHMTR095          * return from leave record
.
          MATCH     AADMNO,TADMN
          GOTO      CHMTR095 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE
          IF        @EQUAL
            COMPARE   ZERO,LOOPFLG
            IF        @EQUAL
              PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
              MOVE      ONE,LOOPFLG
            ENDIF
          ENDIF
.
          MATCH     ANSL,TMOVE
          GOTO      CHMTR065 IF NOT EQUAL
.
          MOVE      TWARD,LSTWARD
          MOVE      TBED,LSTBED
.
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS
.
          MATCH     KEY30,SAVKEY30          * Can only reverse last return
          GOTO      CHMTR096 IF NOT EQUAL
          CALL      RDTRAN2
          BRANCH    OVRCD,CHMTR095
.
          CALL      DELRET00                * Deleting return record
          GOTO      CHMTR999
.
. End Return Leave
.
CHMTR090  MOVE      "Invoice has been printed,removing movements",ERR1
          MOVE      " will not change the invoice rates",ERR2
          PACK      WEBTITLE,ERR1,ERR2
          CALL      WEBERR00
          MOVE      "02225",RPLYCODE        * eGate error msg "Rates not change"
          MOVE      ONE,EXIT
          GOTO      CHMTR999
.
CHMTR095  MOVE      "Invalid Transfer Record",WEBTITLE
          CALL      WEBERR00
          MOVE      "02102",RPLYCODE        * eGate error msg "Missing Transfer"
          MOVE      ONE,EXIT
          GOTO      CHMTR999
.
CHMTR096  MOVE      "Invalid option - must be the last return on file",WEBTITLE
          CALL      WEBERR00
          MOVE      "02221",RPLYCODE        * eGate error msg "Only cancel last"
          MOVE      ONE,EXIT
          GOTO      CHMTR999
.
CHMTR999  RETURN
.                                           * HPS Code Ends Here
.
. ----------------------------------------- * Start of Addition        *I-90201
.-----------------------------------------------------------
.         CHKDIS00 - Check if next record is a discharge or on leave record
.-----------------------------------------------------------
CHKDIS00  MOVE      SP70,PREVWARD
          MOVE      SP70,PREVBED
.
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS,SP70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CHKDIS99
.
          MOVE      TWARD,PREVWARD
          MOVE      TBED,PREVBED
.
          PACK      KEY30,AADMNO,TRANDATE,TRANTIME,TRANWARD,TRANBEDS,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CHKDIS99
.
          MATCH     ANSD,TMOVE
          GOTO      CHKDIS10 IF EQUAL
.
          MATCH     ANSL,TMOVE
          GOTO      CHKDIS10 IF EQUAL
.
CHKDIS10  PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP70
.
.  if deleting a change record directly before the discharge or on leave
.  record, the record must be updated with the previous records ward and bed
.
          PACK      KEY30,TADMN,TDATE,TTIME,PREVWARD,PREVBED,SP70
          CALL      RDATRAN2
          COMPARE   ZERO,OVRCD
          GOTO      CHKDIS99 IF EQUAL
.
          PACK      KEY30,SAVKEY30
          CALL      RDTRAN2
          BRANCH    OVRCD,CHKDIS99
.
          MOVE      PREVWARD,TWARD
          MOVE      PREVBED,TBED
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
CHKDIS99  RETURN
. ----------------------------------------- * End of Addition          *I-90201
.
. ****************************************************************************
. *                          Routines CALLed from above
. ****************************************************************************
.-----------------------------------------------------------------------------
.        Subroutine to save the Admission Variables before changes for
.        use in the audit file PATAUAA1
.-----------------------------------------------------------------------------
ACTASAV0  MOVE      AADMNO,SAADMNO
          MOVE      AURNO,SAURNO
          MOVE      ADATE,SADATE
          MOVE      ATIME,SATIME
          MOVE      ASTAT,SASTAT
          MOVE      APORT,SAPORT
          MOVE      ATRANNO,SATRANNO
          MOVE      AINVPRT,SAINVPRT
          MOVE      ALACDTE,SALACDTE
          MOVE      ACANCEL,SACANCEL
          MOVE      AWARD,SAWARD
          MOVE      ABED,SABED
          MOVE      ADOCTR,SADOCTR
          MOVE      ADOCTA,SADOCTA
          MOVE      ADOCTL,SADOCTL
          MOVE      ASRCE,SASRCE
          MOVE      ATYPE,SATYPE
          MOVE      ACLSS,SACLSS
          MOVE      ACARE,SACARE
          MOVE      AFUNDH,SAFUNDH
          MOVE      AFNDTB,SAFNDTB
          MOVE      AFNDMM,SAFNDMM
          MOVE      AELIG,SAELIG
          MOVE      AVISIT,SAVISIT
          MOVE      AALERG,SAALERG
          MOVE      AILLN,SAILLN
          MOVE      ADIAG1,SADIAG1
          MOVE      ADIAG2,SADIAG2
          MOVE      ADISC,SADISC
          MOVE      ADOCTT,SADOCTT
          MOVE      ACLSSFT,SACLSSFT
          MOVE      AALLOW,SAALLOW
          MOVE      AMBS,SAMBS
          MOVE      ACLAIM,SACLAIM
          MOVE      ADIET,SADIET
          MOVE      APLOCCR,SAPLOCCR
          MOVE      ASTAY,SASTAY
          MOVE      ADYHOSP,SADYHOSP
          MOVE      AMEMB,SAMEMB
          MOVE      AMEMBNO,SAMEMBNO
          MOVE      ACOMM1,SACOMM1
          MOVE      ACOMM2,SACOMM2
          MOVE      APLUR,SAPLUR
          MOVE      APURGE,SAPURGE
          MOVE      AUSR1,SAUSR1
          MOVE      AUSR2,SAUSR2
          MOVE      AUSR3,SAUSR3
          MOVE      AUSR4,SAUSR4
          MOVE      AUSR5,SAUSR5
          MOVE      AUSR6,SAUSR6
          MOVE      AUSR7,SAUSR7
.
          MOVE      ADSCHI,SADSCHI
          MOVE      ARDRNAM,SARDRNAM
          MOVE      AFNDYY,SAFNDYY
          MOVE      AFNDCG,SAFNDCG
          MOVE      AOCCDTE,SAOCCDTE
          MOVE      ACODDTE,SACODDTE
          MOVE      ARETDTE,SARETDTE
          MOVE      AMUMADM,SAMUMADM
          MOVE      DPTMIPVI,SDPTMIPV
          MOVE      ABWGHT,SABWGHT
          MOVE      PMVXRHC1,SPTMSRFG
          MOVE      PMVXRH1G,SPTMSGPP
          MOVE      PTMSFHAN,SPTMSFHA
          MOVE      PTMSECRA,SPTMSECR
          MOVE      PTMSMGIN,SPTMSMGI
          MOVE      PTMIDDIG,SPTMIDIG
          MOVE      PMVXRH1C,SPTMIGPT
          MOVE      PTMIDOFR,SPTMIDOF
          MOVE      PTMIREFD,SPTMIREF
          MOVE      PTMIDFCN,SPTMIDFC
          MOVE      PTMIPHPU,SPTMIPHP
          MOVE      PTMIAGNC,SPTMIAGN
          MOVE      PTMIESSF,SPTMIESS
          MOVE      PTMIUSR8,SPTMIUS8
          MOVE      PTMIUSR9,SPTMIUS9
          MOVE      PTMIUSR0,SPTMIUS0
          MOVE      PTMIPDRG,SPTMSDRG
          MOVE      PTMIOPER,SPTMIOPE
          MOVE      PTMIXWRD,SPTMIXWD
          MOVE      PTMIADOC,SPTMIADC
          MOVE      PTMIREGS,SPTMIREG
          MOVE      PTMIUYN1,SPTMIUY1
          MOVE      PTMIUYN2,SPTMIUY2
.
ACTASAV9  RETURN
.
.-----------------------------------------------------------------------------
. Subroutine from IBAPAT34.PBL to save all the before changes before
. writing to patbaud.
.-----------------------------------------------------------------------------
ACTBSAV0  MOVE      DURNO,SDURNO
          MOVE      DADMNO,SDADMNO
          MOVE      DDATE,SDDATE
          MOVE      DTIME,SDTIME
          MOVE      DSTAT,SDSTAT
.
          MOVE      DDEST,SDDEST
          MOVE      DDIAG,SDDIAG
          MOVE      DDIAG2,SDDIAG2
          MOVE      DUSD1,SDUSD1
          MOVE      DUSD2,SDUSD2
          MOVE      DUSD3,SDUSD3
          MOVE      DUSD4,SDUSD4
          MOVE      DUSD5,SDUSD5
          MOVE      DFMBS,SDFMBS
          MOVE      PTDSDMDC,SDMDC
          MOVE      PTDSDDRG,SDDRG
          MOVE      DWLREASN,SDWLREAS
          MOVE      PTDSSIDX,SPTDSSID
          MOVE      PTDSVOGU,SPTDSVOG
          MOVE      PTDSOPER,SPTDSOPE
          MOVE      PTDSUYN1,SPTDSUY1
          MOVE      PTDSUYN2,SPTDSUY2
          MOVE      PTDSUYN3,SPTDSUY3
          MOVE      PTDSUYN4,SPTDSUY4
          MOVE      PTDSDWRD,SPTDSDWR
          MOVE      PTDSDLOS,SPTDSDLO
          MOVE      DCEASE,SDCEASE
.
ACTBSAV9  RETURN
.
.-----------------------------------------------------------------------------
. ADDSTAD
. Subroutine from IBAPAT34.PBL to update the appropriate variable in the file.
.-----------------------------------------------------------------------------
ADDSTAD   LOAD      FORM6,INDEX,SADMTH1,SADMTH2,SADMTH3,SADMTH4:
                                SADMTH5,SADMTH6,SADMTH7,SADMTH8:
                                SADMTH9,SADMTH10,SADMTH11,SADMTH12
          ADD       COUNT,FORM6
          STORE     FORM6,INDEX,SADMTH1,SADMTH2,SADMTH3,SADMTH4:
                                SADMTH5,SADMTH6,SADMTH7,SADMTH8:
                                SADMTH9,SADMTH10,SADMTH11,SADMTH12
          CALL      UPSTAD1
          RETURN
.
.-----------------------------------------------------------------------------
.       Check for HCS Indicator
.       From IBAPAT05 - ADMHCS.
.-----------------------------------------------------------------------------
ADMHCS00  COMPARE   ONE,CHCS
          GOTO      ADMHCS10 IF NOT EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      ADMHCS10 IF NOT EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,ADMHCS10
.
          MATCH     CHCSDTE,DDATE
          GOTO      ADMHCS99 IF LESS
.
.       Write to HCS Admission audit file
.

ADMHCS10  MOVE      AADMNO,PTUNADMN
          CALL      IBACLOCK
          PACK      PTUNDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTUNDATE
          MOVE      CTIMEIS,PTUNTIME
          REP       " 0",PTUNTIME
.
          MOVE      SP70,PTUNDTYP           * Discharge status
          MATCH     "2",PTUNTYPE
          GOTO      ADMHCS14 IF EQUAL       * Undischarged
          MATCH     "4",PTUNTYPE
          GOTO      ADMHCS16 IF NOT EQUAL   * Not changing disc.date
.
ADMHCS14  MOVE      DSTAT,PTUNDTYP          * Discharge status
.
.        In the event of the same person being changed twice in one day,
.        ignore the second change
.
ADMHCS16  MATCH     SP1,PTUNTYPE            * Don't write a blank alteration
          GOTO      ADMHCS99 IF EQUAL
          MOVE      PTUNTYPE,FTYPE
          BRANCH    FTYPE,ADMHCS20,ADMHCS20,ADMHCS20,ADMHCS20
          GOTO      ADMHCS99
.
ADMHCS20  PACK      KEY25,PTUNADMN,PTUNDATE,PTUNTIME,PTUNTYPE
          CALL      RDAUNAA1
          COMPARE   ZERO,OVRCD
          GOTO      ADMHCS99 IF EQUAL
.
          CALL      WRUNAA1
.
ADMHCS99  RETURN
.
.***************************************************************************
.*  BRAT0000  :  Subroutine to calculate the bed rate                      *
.***************************************************************************
BRAT0000  MOVE      ZERO,SAVHF
          MOVE      ZERO,SAVPAT
          MOVE      ZERO,HFPORT
          MOVE      ZERO,ADISC
          MOVE      SP2,AALLOW
          MOVE      ZERO,SAVEND
          MOVE      ZERO,SAVEXTR
          MOVE      ZERO,NOFEE
.
          MOVE      TWARD,NWARD
          MOVE      TATYPE,SAVATYP
          MOVE      TRBTYP,SAVBTYP
.
.        Check the indicator saying what type of charging we are doing
.
          COMPARE   ZERO,CFEETYP            * bed fee charging ?
          GOTO      BRAT7500 IF NOT EQUAL   * no, use the standard routine
.
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PATFN1A1,"patfn1af"
.
.         Display the daily Bed Fee
.
          MOVE      ADYHOSP,NOHOSP
.
.         Display the current bed fee and also option to change bed type
.
          MOVE      ZERO,COMFLAG            * Generate computer rate
          MOVE      ONE,DSPONLY
          CALL      PATDSBFE
          MOVE      ZERO,DSPONLY
          BRANCH    NOFEE,BRAT8000
          GOTO      BRAT9999
.
.       Get the rate to be charged (non-bed fee file)
.
BRAT7500  PACK      NEWTDATE,ADATE
          MOVE      NWARD,NEWTWARD
          MOVE      TATYPE,NEWTATYP
          MOVE      SAVBTYP,NEWTBTYP
          MOVE      AUSR2,NEWTCHST
          MOVE      ACLSSFT,NEWTDEPT
          PROC      PATTRRAT                * calculate new rate
          BRANCH    EXIT,BRAT8000           * couldn't find rate
.
          MOVE      NEWTPPOR,SAVPAT
          MOVE      NEWTRPOR,SAVHF
          MOVE      NEWTRPOR,HFPORT
          MOVE      ZERO,TRATEG
          MOVE      NEWTXTRA,SAVEXTR
          ADD       NEWTXTRA,SAVPAT
          MOVE      NEWTBEND,SAVEND
.
          MOVE      ZERO,OVRCD
          MOVE      ZERO,NOFEE
          GOTO      BRAT9999
.
BRAT8000  MOVE      ZERO,SAVEND
          MOVE      ZERO,ADDEND
          MOVE      ZERO,SAVPAT
          MOVE      ZERO,SAVHF
.
.         MOVE      ONE,NOFEE
.         MOVE      "Invalid Rate Combination",WEBTITLE
.         CALL      WEBERR00
.         MOVE      ONE,EXIT
.         CALL      UUPTMAS1
          GOTO      BRAT9999
.
BRAT9999  RETURN
.
.-----------------------------------------------------------------------------
. Recalculate all bed fees for this admission
.-----------------------------------------------------------------------------
CALBFE00  PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2                * Looping through transfer file
CALBFE10  CALL      RDKTRAN2
          BRANCH    OVRCD,CALBFE99
.
          MATCH     AADMNO,TADMN
          GOTO      CALBFE99 IF NOT EQUAL
.
          CALL      NEWR0000                * Calculating new rates
          CALL      UPTRN000                * Updating transfer file
.
          GOTO      CALBFE10
.
CALBFE99  RETURN
.
.-----------------------------------------------------------------------------
.        Subtract one from appropriate statistic
.-----------------------------------------------------------------------------
CANSTA00  BRANCH    PTCNSTAU,CANSTA99     * branch if using the new adm. & dsch.
.                                         stats 1 report
          CALL      RDSTAD1
          BRANCH    OVRCD,CANSTA99
.
          LOAD      FORM5,FORM2 OF SADMTH1,SADMTH2,SADMTH3,SADMTH4:
                    SADMTH5,SADMTH6,SADMTH7,SADMTH8:
                    SADMTH9,SADMTH10,SADMTH11,SADMTH12:
                    SADMTH13
          SUB       ONE,FORM5
          STORE     FORM5,FORM2 OF SADMTH1,SADMTH2,SADMTH3,SADMTH4:
                    SADMTH5,SADMTH6,SADMTH7,SADMTH8:
                    SADMTH9,SADMTH10,SADMTH11,SADMTH12:
                    SADMTH13
          CALL      UPSTAD1
.
CANSTA99  RETURN
.
...---------------------------------------------------------------------------
... Check if final episode has had disease coding. From IBAPAT05
... Returns: EXIT  = 0  No coding performed
...                  1  Coding Performed
...---------------------------------------------------------------------------
..CEPC0000  PACK      KEY18,AADMNO,TADOCT,PTTREPNO,SP10
..          CALL      RSPTMRD1                     * position in disease file
..CEPC1000  CALL      RKPTMRD1                     * read next record
..          BRANCH    OVRCD,CEPC9000               * end of file
...
..          MATCH     AADMNO,PTMDADMN              * same admission number ?
..          GOTO      CEPC9000 IF NOT EQUAL        * no - finished
...
..          MATCH     TADOCT,PTMDADOC              * same doctor ?
..          GOTO      CEPC9000 IF NOT EQUAL        * no - finished
...
..          COMPARE   PTTREPNO,PTMDEPNO            * same episode number ?
..          GOTO      CEPC9000 IF NOT EQUAL        * no - finished
...
..          CLEAR     WEBTITLE
..          APPEND    "Disease coding has already been done for",WEBTITLE
..          APPEND    " this episode, please delete it before",WEBTITLE
..          APPEND    " undischarging.  ",WEBTITLE
..          RESET     WEBTITLE
..          CALL      WEBERR00
..          MOVE      "02217",RPLYCODE      * eGate error msg "Disease Coding d"
..          MOVE      ONE,EXIT
..          GOTO      CEPC9999
...
..CEPC9000  MOVE      ZERO,EXIT
..          GOTO      CEPC9999
...
..CEPC9999  RETURN
.
.***********************************************************
.         Recalculate bed fees
.***********************************************************
CFEE0000  CALL      CKCM0000            * Check if casemix funding patient ?
          IF        CMIXFLAG = 1
.
.           Change overnight/daycase rate for casemix if necessary
.
            MOVE      SP10,PTHFBCAT           * Initialise table type
            MATCH     SP6,AFUNDH
            IF        !@EQUAL
              PACK      KEY14,AFUNDH,AFNDTB
              CALL      RDFUND1                 * read health fund file
            ENDIF
.
            MATCH     DDATE,ADATE
            IF        @EQUAL
              CALL      PATDYCAS           * charge casemix daycase rate
            ELSE
              CALL      CMRT0000           * charge casemix overnight rate
            ENDIF
.
          ELSE
.
.            If this is now a day case, then change all rates to day case rates
.
            MATCH     ADATE,DDATE
            GOTO      CFEE1000 IF NOT EQUAL
.
            COMPARE   ONE,CDYCASE
            GOTO      CFEE1000 IF NOT EQUAL
.
            IF        CFEETYP = 0 | CFEETYP = 4 
              CALL      PATDYCAS
            ENDIF
          ENDIF
          GOTO      CFEE9999
.
.         Check if patient was a daycase before date was changed
.
CFEE1000  MATCH     ADATE,SDDATE
          GOTO      CFEE9999 IF NOT EQUAL
.
          COMPARE   ONE,CDYCASE
          GOTO      CFEE9999 IF NOT EQUAL  * Not doing day case automatic charge
.
.         Patient was a day case before the discharged date changed,
.         therefore change all rates to Inpatients rates again.
.
          PACK      KEY30 WITH AADMNO,SP20,SP10
          CALL      RDSTRAN2
CFEE2000  CALL      RDKTRAN2
          BRANCH    OVRCD,CFEE9999
.
          MATCH     TADMN,AADMNO
          GOTO      CFEE9999 IF NOT EQUAL
.
          PACK      NEWTDATE,TDATE
          MOVE      TWARD,NEWTWARD
          MOVE      TATYPE,NEWTATYP
          MOVE      TRBTYP,NEWTBTYP
          MOVE      TCHSTAT,NEWTCHST
          MOVE      TDEPT,NEWTDEPT
          CALL      BRAT0000
          MOVE      SAVEND,TRBEND
          MOVE      ADDEND,PTTRAEND
          MOVE      SAVPAT,TRATEF
          MOVE      SAVHF,TRATEH
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
          GOTO      CFEE2000
.
CFEE9999  RETURN
.
.-----------------------------------------------------------
. Check Admission Date Does Not Overlap the Previous Admission.
.          (Now issues a warning only)
.-----------------------------------------------------------
CHAD0000  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CHAD9999
.
          MOVE      AADMNO,SAVADMNO
          PACK      KEY24,AURNO,Z70
          CALL      RDSVISA2
CHAD1000  CALL      RDPVISA2
          BRANCH    OVRCD,CHAD9800
.
          MATCH     AURNO,PVIURNO             * same ur?
          GOTO      CHAD9800 IF NOT EQUAL     * No
.
          COMPARE   THREE,PVITYPE             * Inpatient?
          GOTO      CHAD1000 IF NOT EQUAL     * No
.
          MATCH     SAVADMNO,DPVIBILL         * same admission no?
          GOTO      CHAD1000 IF EQUAL
.
          IF        ASTAT=1|ASTAT=5
            GOTO      CHAD1000
          ENDIF
.
.
. ------- display a warning message -----------------------------------------
.
..ML..    WRITE     HTMLFILE;"<RETURN_VALUE TYPE=WARNING>":
..ML..              "Admission may overlap other visits.":
..ML..              "</RETURN_VALUE>"
.
.
. ------- reposition/reread data ------------------------------------------
.
CHAD9800  MOVE      SP8,DDATE
          PACK      KEY8,AADMNO,SP10
          CALL      RDDSCH1
.
          MOVE      AADMNO,KEY8
          CALL      RDVISA1
.
CHAD9999
..ML..    WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
..ML..              " OK ":
..ML..              "</RETURN_VALUE>"
          RETURN
.
.-----------------------------------------------------------------------------
.  Check discharge date and time are before the next admission - supervisor
.-----------------------------------------------------------------------------
CHKDDS00  MOVE      ZERO,EXIT
          PACK      KEY24,AURNO,ADATE,AADMNO,SP30
          CALL      RDSVISA2
CHKDDS10  CALL      RDKVISA2
          BRANCH    OVRCD,CHKDDS90
.
          MATCH     PVIURNO,AURNO                * Same U/R
          GOTO      CHKDDS90 IF NOT EQUAL
.
          MATCH     PVIBILL,AADMNO
          GOTO      CHKDDS10 IF EQUAL            * Same admission
.
          COMPARE   THREE,PVITYPE
          GOTO      CHKDDS10 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,CHKDDS10
.
          COMPARE   ONE,ASTAT                    * Ignore pre admissions
          GOTO      CHKDDT10 IF EQUAL
.
          COMPARE   FIVE,ASTAT                   * Ignore cancelled admissions
          GOTO      CHKDDT10 IF EQUAL
.
          MATCH     PTDSC001,PVIDATE
          GOTO      CHKDDS85 IF LESS
.
          MATCH     PVIDATE,PTDSC001
          IF        @EQUAL
            MATCH     PTDSC002,ATIME
            GOTO      CHKDDS85 IF LESS
          ENDIF
.
          GOTO      CHKDDS10
.
.CHKDDS80  CLEAR     WEBTITLE
.          APPEND    "Failed to Check Dates on Admission ",WEBTITLE
.          APPEND    PVIBILL,WEBTITLE
.          RESET     WEBTITLE
.          CALL      WEBERR00
.          MOVE      ONE,EXIT
.          GOTO      CHKDDS90
.
CHKDDS85  CLEAR     WEBTITLE
          APPEND    "Discharged Date Overlaps Admission Supervisor",WEBTITLE
          APPEND    PVIBILL,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02218",RPLYCODE        * eGate error msg "Dscg Date overlap
          MOVE      ONE,EXIT
          GOTO      CHKDDS90
.
CHKDDS90  PACK      KEY8,ADMISSNO           * Read original admission and visit
          CALL      RDMISS1                 * details
          CALL      RDVISA1
.
CHKDDS99  RETURN
.
.------------------------------------------------------------
.  Check discharge date and time are before the next admission
.------------------------------------------------------------
CHKDDT00  MOVE      ZERO,EXIT
          PACK      KEY24,AURNO,SP70
          CALL      RDSVISA2
CHKDDT10  CALL      RDKVISA2
          BRANCH    OVRCD,CHKDDT90
.
          MATCH     PVIURNO,AURNO                * Same U/R
          GOTO      CHKDDT90 IF NOT EQUAL
.
          MATCH     DPVIBILL,ADMISSNO
          GOTO      CHKDDT10 IF EQUAL            * Same admission
.
          COMPARE   THREE,PVITYPE
          GOTO      CHKDDT10 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,CHKDDT10
.
          COMPARE   ONE,ASTAT                    * Ignore pre admissions
          GOTO      CHKDDT10 IF EQUAL
.
.         COMPARE   TWO,ASTAT                    * current admission
.         GOTO      CHKDDT85 IF EQUAL
.
.         COMPARE   FOUR,ASTAT                    * on leave
.         GOTO      CHKDDT85 IF EQUAL
.
          COMPARE   FIVE,ASTAT                   * Ignore cancelled admissions
          GOTO      CHKDDT10 IF EQUAL
.
          COMPARE   THREE,ASTAT
          IF        @EQUAL
            MOVE      AADMNO,KEY8
            CALL      RDDSCH1
            BRANCH    OVRCD,CHKDDT10
.
            MATCH     TRANDATE,DDATE
            GOTO      CHKDDT10 IF LESS
            GOTO      CHKDDT85 IF NOT EQUAL
.
            MATCH     TRANTIME,DTIME
            GOTO      CHKDDT10 IF LESS
            GOTO      CHKDDT10 IF EQUAL
            GOTO      CHKDDT85 IF NOT EQUAL
          ENDIF
.
          GOTO      CHKDDT10
.
CHKDDT80  CLEAR     WEBTITLE
          APPEND    "Failed to Check Dates on Admission ",WEBTITLE
          APPEND    PVIBILL,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDDT90
.
CHKDDT85  CLEAR     WEBTITLE
          APPEND    "Discharged Date Overlaps Admission ",WEBTITLE
          APPEND    PVIBILL,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDDT90
.
.CHKDDT86  CLEAR     WEBTITLE
.          APPEND    "Missing Discharge Record ",WEBTITLE
.          APPEND    PVIBILL,WEBTITLE
.          RESET     WEBTITLE
.          CALL      WEBERR00
.          MOVE      ONE,EXIT
.          GOTO      CHKDDT90
.
CHKDDT90  PACK      KEY8,ADMISSNO           * Read original admission and visit
          CALL      RDMISS1                 * details
          CALL      RDVISA1
.
CHKDDT99  RETURN
.
.-----------------------------------------------------------------------------
. Output key for the last transfer record for a patient
.-----------------------------------------------------------------------------
CHKLVE00  MOVE      ZERO,LEAVEFLG
          MOVE      ZERO,LOOPFLG
          MOVE      ZERO,FNDLFLG
          PACK      SAVKEY30,KEY30
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
CHKLVE10  CALL      RDPTRAN2
          BRANCH    OVRCD,CHKLVE90
.
          MATCH     AADMNO,TADMN
          GOTO      CHKLVE95 IF NOT EQUAL
.
          ADD       ONE,LOOPFLG
.
          MATCH     ANSL,TMOVE
          IF        @EQUAL
            PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
            MATCH     SAVKEY30,KEY30
            IF        @EQUAL
.
              COMPARE   ONE,FNDLFLG
              IF        @EQUAL
                MOVE     THREE,LEAVEFLG
                GOTO      CHKLVE95
              ENDIF
.
              COMPARE   ONE,LOOPFLG
              IF        @EQUAL
                MOVE      ONE,LEAVEFLG
                GOTO      CHKLVE95
              ENDIF
.
              COMPARE   TWO,LOOPFLG
              IF        @EQUAL
                MOVE      TWO,LEAVEFLG
                GOTO      CHKLVE95
              ENDIF
.
            ELSE
              MOVE      ONE,FNDLFLG
            ENDIF
          ENDIF                                  * delete it and its return
.
          GOTO      CHKLVE10
.
CHKLVE90  MOVE      ONE,EXIT                     * Error processing
          GOTO      CHKLVE99
.
CHKLVE95  MOVE      ZERO,EXIT
.
CHKLVE99  RETURN
.
.-----------------------------------------------------------------------------
. Check that the new admission date is less than the second record
. in the bed transfer file (first record is the old admission record)
.-----------------------------------------------------------------------------
CHTRNS00  PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
CHTRNS10  CALL      RDKTRAN2
          BRANCH    OVRCD,CHTRNS98
.
          MATCH     TADMN,AADMNO
          GOTO      CHTRNS98 IF NOT EQUAL
.
          MATCH     "A",TMOVE
          GOTO      CHTRNS10 IF EQUAL
.
          MATCH     TDATE,PTMIS001
          GOTO      CHTRNS98 IF LESS
          GOTO      CHTRNS90 IF NOT EQUAL
.
          MATCH     TTIME,PTMIS002
          GOTO      CHTRNS98 IF LESS
.
          MOVE      TTIME,DIM5
          UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
          PACK      DISPDTTM,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,SP1,DIM5
          CLEAR     WEBTITLE
          APPEND    "The Admission Time will be After the First Bed ",WEBTITLE
          APPEND    "Movement (",WEBTITLE
          APPEND    DISPDTTM,WEBTITLE
          APPEND    "). Change to an earlier Admission Time ",WEBTITLE
          APPEND    "if this is the Correct Admission Date.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02219",RPLYCODE        * eGate error msg "Adms overlaps Mov
          MOVE      ONE,EXIT
          GOTO      CHTRNS99
.
.        The new admission date is later than a bed movement
.
CHTRNS90  UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
          PACK      DISPDTTM,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          CLEAR     WEBTITLE
          APPEND    "Admission date must not be later than previous ",WEBTITLE
          APPEND    "bed movement on ",WEBTITLE
          APPEND    DISPDTTM,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02219",RPLYCODE        * eGate error msg "Adms overlaps Mov
          MOVE      ONE,EXIT
          GOTO      CHTRNS99
.
CHTRNS98  MOVE      ZERO,EXIT
CHTRNS99  RETURN
.
.-----------------------------------------------------------------------------
.         CKCM0000 - to determine if it will be casemix funded
.-----------------------------------------------------------------------------
CKCM0000  MOVE      ZERO,CMIXFLAG
          PACK      KEY18,ACLAIM,AFUNDH,CMCODE,SP70
          MOVE      ADATE,CEFFDATE
          CALL      VALCMXFN
          IF        EXIT=0
            MOVE      ONE,CMIXFLAG           * casemix
          ENDIF
CKCM9999  RETURN
.
.-----------------------------------------------------------------------------
.  Cancel admission special funding arrangement code
.-----------------------------------------------------------------------------
CLAMFN00  PACK      KEY25,AADMNO,ANSL,STDATE,STTIME
          CALL      RDPTASF1                * Read the admission SFA file
          IF        OVRCD<>1
            PACK      KEY25,PTAFADMN,PTAFTYPE,PTAFDATE,PTAFTIME
            CALL      DEPTASF1              * Delete the admission SFA
          ENDIF
CLAMFN99  RETURN
.
.------------------------------------------------------------
. Cancelling Bed Transfer / On Leave
.------------------------------------------------------------
CLBTOL00
.
.         Check if the old bed has been taken
.
          MATCH     SP3,LSTBED
          GOTO      CLBTOL99 IF EQUAL       * No-Bed wards are always available
.
          MOVE      LSTWARD,TWARD
          MOVE      LSTBED,TBED
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD,CLBTOL90
.
          MATCH     ZEROVISN,WADMNO
          GOTO      CLBTOL91 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CLBTOL99
.
CLBTOL90  CLEAR     WEBTITLE
          APPEND    "Ward/Bed ",WEBTITLE
          APPEND    TWARD,WEBTITLE
          APPEND    SLASH,WEBTITLE
          APPEND    TBED,WEBTITLE
          APPEND    " Not Found",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02130",RPLYCODE        * eGate error msg "Invalid Ward/Bed"
          MOVE      ONE,EXIT
          GOTO      CLBTOL99
.
CLBTOL91  CLEAR     WEBTITLE
          APPEND    "Ward/Bed ",WEBTITLE
          APPEND    TWARD,WEBTITLE
          APPEND    SLASH,WEBTITLE
          APPEND    TBED,WEBTITLE
          APPEND    " has been taken by Admission ",WEBTITLE
          APPEND    WADMNO,WEBTITLE
          APPEND    " Please Transfer this Person Before ",WEBTITLE
          APPEND    "Cancelling Patient's Transfer ",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02131",RPLYCODE        * eGate error msg "Ward/Bed Occupid"
          MOVE      ONE,EXIT
          GOTO      CLBTOL99
.
CLBTOL99  RETURN
.
.-----------------------------------------------------------------------------
.         CMRT0000 - Check for casemix overnight rates
.         Check if the casemix Sameday patient is really day case
.-----------------------------------------------------------------------------
CMRT0000  MOVE      ZERO,LOWFLAG
          MOVE      ZERO,CMIXFLAG
          PACK      KEY30,DADMNO,SP20
          CALL      RDSTRAN2
CMRT1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CMRT9999
.
          MATCH     DADMNO,TADMN
          GOTO      CMRT9999 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE
          IF        @EQUAL
            MOVE      ADATE,FROMDATE
            MOVE      DDATE,TODATE
            CALL      NHOSPDAY           * Length of stay
            ADD       ONE,NBRDAYS
            CALL      VLUM0000
            IF        NBRDAYS<=PTHLCOFF
              MOVE      ONE,LOWFLAG      * Set to use low outliers
            ENDIF
            PACK      KEY30,DADMNO,SP20    * reposition
            CALL      RDSTRAN2
            CALL      RDKTRAN2
          ENDIF
.
          MOVE      ZERO,BDAYS
          MOVE      ADATE,FROMDATE
          MOVE      TDATE,TODATE
          MATCH     FROMDATE,TODATE           * same date ?
          GOTO      CMRT2000 IF EQUAL
.
.        We need to recalculate rates for casemix Same Day patient who stays
.        overnight
.
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED  * save current key
          CALL      NHOSPDAY            * get bed days
          MOVE      NBRDAYS,BDAYS
          ADD       ONE,BDAYS
          MOVE      SAVKEY30,KEY30
          CALL      RDTRAN2             * reposition
.
CMRT2000  MOVE      ZERO,ADMFLAG        * set No to admission flag
          MOVE      ONE,MSGFLAG         * no error messages
          MOVE      TRBTYP,SAVBTYP      * set bed type
          PROC      PATGNCMX
          BRANCH    NOFEE,CMRT1000
.
          CMATCH    ANSA,TMOVE
          GOTO      CMRT3000 IF NOT EQUAL * Not admission record
          MOVE      LUMPAT,PTTRLSPT
          MOVE      LUMHF,PTTRLSRB
.
CMRT3000  MOVE      DAYHF,TRATEH
          MOVE      DAYPAT,TRATEF
          MOVE      DAYEND,TRBEND
          MOVE      ADDHF,PTTRADRB
          MOVE      ADDPAT,PTTRADPT
          MOVE      ADDEND,PTTRAEND
          MOVE      ZERO,PTTREPSD
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
          GOTO      CMRT1000
.
CMRT9999  RETURN
.
.------------------------------------------------------------
.  Cancel Last Bed Transfer - by Admission number
.------------------------------------------------------------
CNLBT000  PACK      KEY8,ADMISSNO
          CALL      RDPTMIS1
          BRANCH    OVRCD,CNLBT990,CNLBT991
.
          BRANCH    ASTAT,CNLBT992,CNLBT010,CNLBT993,CNLBT010,CNLBT992
.
CNLBT010  MOVE      SP1,STMOVE
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
CNLBT020  CALL      RDKTRAN2
          BRANCH    OVRCD,CNLBT030
.
          MATCH     TADMN,AADMNO
          GOTO      CNLBT030 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          MOVE      TMOVE,LASTTRAN          * save the last tran record
          MOVE      PTTREPNO,STEPNO         * save last episode number
          MOVE      TADOCT,SAVDOCT          * save last doctor
          PACK      MEHKEY24,AADMNO,TDATE,TTIME
          MOVE      TDATE,LCHGDATE          * save last transfer date
          MOVE      TTIME,LCHGTIME          * save last transfer time
          MOVE      TURNO,LCHGURNO          * save U/R No.
          GOTO      CNLBT020
.
CNLBT030  MATCH     SP1,STMOVE
          GOTO      CNLBT994 IF EQUAL
.
          CMATCH    ANSA,STMOVE
          GOTO      CNLBT995 IF EQUAL
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,CNLBT996
.
          PACK      KEY8,AADMNO
          CALL      RDVISA1
          BRANCH    OVRCD,CNLBT997
.
          CALL      LTRND000                * Find Last two transaction details
.
          MATCH     SP8,LSTDATE
          GOTO      CNLBT994 IF EQUAL
.
          MATCH     ALACDTE,STDATE
          GOTO      CNLBT040 IF NOT LESS
          GOTO      CNLBT998
.
CNLBT040  UNPACK    STDATE,XCENT,XYEAR,XMON,XDAY
.
.         Determine type of record
.
          CMATCH    ANSL,STMOVE
          GOTO      CNLBT060 IF EQUAL
.
          CMATCH    ANSR,STMOVE
          GOTO      CNLBT070 IF EQUAL
.
          CMATCH    ANSC,STMOVE
          GOTO      CNLBT989  IF NOT EQUAL
.
.         Change record - check for a change in ward/bed
.
          MATCH     STWARD,LSTWARD
          GOTO      CNLBT050 IF NOT EQUAL
.
          MATCH     STBED,LSTBED
          GOTO      CNLBT050 IF NOT EQUAL
.
.        Check for a change in doctor/admission type
.
          MATCH     STADOCT,LSTADOC
          GOTO      CNLBT075 IF NOT EQUAL
.
          MATCH     STDEPT,LSTUNIT
          GOTO      CNLBT075 IF NOT EQUAL
.
          MATCH     STATYPE,LSTATYP
          GOTO      CNLBT075 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CNLBT999
.
CNLBT050  MOVE      THREE,CANTYPE
          MOVE      "Cancelling Bed Transfer",WEBTITLE
          CALL      CLBTOL00                * check if old bed has been taken
          BRANCH    EXIT,CNLBT999
          GOTO      CNLBT080
.
CNLBT060  MOVE      FIVE,CANTYPE
          MOVE      "Cancelling Leave From Bed",WEBTITLE
          CALL      CLBTOL00                * check if old bed has been taken
          BRANCH    EXIT,CNLBT999
          GOTO      CNLBT080
.
CNLBT070  MOVE      "Cancelling Return From Leave",WEBTITLE
          MOVE      FOUR,CANTYPE
          GOTO      CNLBT080
.
CNLBT075  MOVE      TWO,CANTYPE
          MOVE      "Cancelling Amission Type/Attending Doctor",WEBTITLE
.
CNLBT080  CALL      DELTRN00                * Delete appropriate transfer record
          BRANCH    EXIT,CNLBT999
          MOVE      ZERO,EXIT
          GOTO      CNLBT999
.
.  Web Error displays
.
CNLBT989  MOVE      "Invalid Transfer Record Found",WEBTITLE
          CALL      WEBERR00
          MOVE      "02102",RPLYCODE        * eGate error msg "Missing Transfer"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT990  MOVE      "Invalid Admission Number",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admissio"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT991  MOVE      "Patient Locked",WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Admission Locked"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT992  MOVE      "Admission Not Admitted",WEBTITLE
          CALL      WEBERR00
          MOVE      "02113",RPLYCODE        * eGate error msg "Not admitted    "
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT993  MOVE      "Admission Is Discharged",WEBTITLE
          CALL      WEBERR00
          MOVE      "02112",RPLYCODE        * eGate error msg "patient discharg"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT994  MOVE      "Admission Transfer Record not found",WEBTITLE
          CALL      WEBERR00
          MOVE      "02100",RPLYCODE        * eGate error msg "Missing Admissio"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT995  MOVE      "Admission has no Transfers",WEBTITLE
          CALL      WEBERR00
          MOVE      "02226",RPLYCODE        * eGate error msg "Admiss has no Tr"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT996  MOVE      "Master File Data Missing",WEBTITLE
          CALL      WEBERR00
          MOVE      "02106",RPLYCODE        * eGate error msg "Patient Master m"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT997  MOVE      "Visit File Record Missing",WEBTITLE
          CALL      WEBERR00
          MOVE      "02101",RPLYCODE        * eGate error msg "Missing Visit re"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT998  MOVE      "Transaction has Already been Invoiced",WEBTITLE
          CALL      WEBERR00
          MOVE      "02225",RPLYCODE        * eGate error msg "Already Invoiced"
          MOVE      ONE,EXIT
          GOTO      CNLBT999
.
CNLBT999  RETURN
.
.-----------------------------------------------------------------------------
. Date is okay as far as the bed transfer file is concerned.
. Check if change is more than CERRDAY days, and if so, check if
. the user is less than CERRSEC security level. If so, not allowed.
.-----------------------------------------------------------------------------
.
DATRAN00  CALL      CHAD0000             * check admission date does not overlap
          BRANCH    EXIT,DATRAN99
.
          MOVE      ADATE,WDATE2
          MOVE      PTMIS002,ATIME
.
          MOVE      PTMIS001,WDATE1
          CALL      DIFFDY00
          MULT      COUNT,NBRDAYS            * To ensure it is positive
.
          COMPARE   NBRDAYS,CERRDAY
          GOTO      DATRAN10 IF LESS
.
.        Check if change being attempted within CERRDAY days of discharge
.
          PACK      WDATE2,CCC,CYY,CMM,CDD
          REP       " 0",WDATE2
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE1,XCENT,XYEAR,XMON,XDAY
          CALL      DIFFDY00
          MULT      COUNT,NBRDAYS            * To ensure it is positive
.
          COMPARE   NBRDAYS,CERRDAY
          GOTO      DATRAN20 IF LESS
.
          GOTO      DATRAN30
.
DATRAN10  COMPARE   CERRSEC,SECLEV
          GOTO      DATRAN30 IF NOT LESS
.
          CLEAR     WEBTITLE
          APPEND    "Change for More than ",WEBTITLE
          APPEND    CERRDAY,WEBTITLE
          APPEND    " Days.  ",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02222",RPLYCODE        * eGate error msg "Transfer overlaps
          MOVE      ONE,EXIT
          GOTO      DATRAN99
.
DATRAN20  COMPARE   CERRSEC,SECLEV
          GOTO      DATRAN30 IF NOT LESS
.
          CLEAR     WEBTITLE
          APPEND    "Change not Within ",WEBTITLE
          APPEND    CERRDAY,WEBTITLE
          APPEND    " Days of Discharge.  ",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      "02223",RPLYCODE        * eGate error msg "Transfer overlaps
          MOVE      ONE,EXIT
          GOTO      DATRAN99
.
.        Print a warning message if new admission date is not the same
.        year/month as the old admission date
.
DATRAN30  MOVE      PTMIS001,DIM4
          MOVE      ADATE,KEY4
.
          MATCH     KEY4,DIM4
          GOTO      DATRAN40 IF NOT LESS
.
          CLEAR     WEBTITLE
          APPEND    "Warning: New Admission Date in a Prior ",WEBTITLE
          APPEND    "Month to Old Admission Date.",WEBTITLE
          RESET     WEBTITLE
.
.        Check the new Admission Date/Time against the current date/time
.
DATRAN40  PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
.
          MATCH     XDATE,PTMIS001
          GOTO      DATRAN99 IF LESS
          GOTO      DATRAN60 IF EQUAL
.
DATRAN50  MOVE      "Cannot Admit into the Future.  ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02140",RPLYCODE        * eGate error msg "Cannot admit ahea
          MOVE      ONE,EXIT
          GOTO      DATRAN99
.
.        Current date - check the admission time against current time
.
DATRAN60  CLOCK     TIME,CTIMEIS
          MATCH     ATIME,CTIMEIS
          GOTO      DATRAN50 IF LESS
.
DATRAN99  RETURN
.
.------------------------------------------------------------
.  Cancel the appropriate type of transfer file record
.------------------------------------------------------------
DELTRN00  BRANCH    CANTYPE,DELTRN90,DELTRN20,DELTRN30,DELTRN40,DELTRN50
.
DELTRN20  CALL      DELADC00                * delete admission type/doctor chge
          GOTO      DELTRN99
.
DELTRN30  CALL      DELBTR00                * delete a bed transfer
          GOTO      DELTRN99
.
DELTRN40  CALL      DELRET00                * delete a return from leave
          GOTO      DELTRN99
.
DELTRN50  CALL      DELONL00                * delete an on leave
          GOTO      DELTRN99
.
DELTRN90  MOVE      "Incorrect Cancellation Type",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELTRN99
.
DELTRN99  RETURN
.
.------------------------------------------------------------
. Delete an admission type/attending doctor change
.------------------------------------------------------------
DELADC00  MOVE      LSTATYP,ATYPE
          MOVE      LSTADOC,ADOCTA
          MOVE      LSTUNIT,ACLSSFT
          CALL      UPLMISS1
          CALL      UUPTMIS1
.
.        If Doctor has changed update the visit file
.
DELADC10  MATCH     SP6,LSTADOC
          IF        !@EQUAL
            PACK      KEY8,AADMNO
            CALL      RLPTVIS1
            IF        OVRCD = 0
              MOVE      LSTADOC,PVIDOCT
              CALL      UPVISA1
              CALL      UUPTVIS1
            ELSE
              IF        OVRCD = 2
                GOTO      DELADC90          * Error, patient locked
              ENDIF
            ENDIF
          ENDIF
.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
          RESET     KEY30
          PACK      KEY30,AADMNO,STDATE,STTIME,STWARD,STBED,SP70
          CALL      RDTRAN2
.
          CALL      PMIGTNID                 * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      NINE,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCT                * b'cast cancel transfer  *C-090222
.
.        Delete transfer file record
.
          PACK      KEY30,AADMNO,STDATE,STTIME,STWARD,STBED,SP70
          CALL      DETRAN2
.
.        Update the control file with the transfer date if necessary
.
          CALL      UPDCFR00
.
          CALL      MEHD0000                * update mental health details
.
          MOVE      ZERO,EXIT
          GOTO      DELADC99
.
DELADC90  CLEAR     WEBTITLE
          APPEND    "Visit ",WEBTITLE
          APPEND    PVIBILL,WEBTITLE
          APPEND    " Locked on Port ",WEBTITLE
          APPEND    PVILOCK,WEBTITLE
          CALL      WEBERR00
          MOVE      "02160",RPLYCODE        * eGate error msg "Visit Locked    "
          MOVE      ONE,EXIT
.
DELADC99  RETURN
.
.------------------------------------------------------------
. Delete a bed transfer
.------------------------------------------------------------
DELBTR00  MOVE      STWARD,AWARD
          MOVE      STBED,ABED
          CALL      RELBED00                * release aadmno in award/abed
          MOVE      LSTWARD,AWARD
          MOVE      LSTBED,ABED
.
.         put the patient into the old bed
.
          MATCH     SP3,LSTBED
          GOTO      DELBTR10 IF EQUAL
.
          PACK      KEY6,LSTWARD,LSTBED,SP70
          CALL      RDWARD1
.
          MATCH     ZEROVISN,WADMNO
          GOTO      DELBTR20 IF NOT EQUAL
.
.         put the patient into this bed
.
          MOVE      AADMNO,WADMNO
          CALL      UPWARD1
          GOTO      DELBTR30
.
DELBTR10  MOVE      APLUR,NBPLUR            * put patient into no-bed ward
          MOVE      SP3,NBROOM
          PACK      KEY13,LSTWARD,AADMNO,NBPLUR
          CALL      RANOBE1
          IF        OVRCD=1
            CALL      WRNOBE1
          ENDIF
          GOTO      DELBTR30
.
DELBTR20  MOVE      AADMNO,WSTBY            * Someone has grabbed this bed -
          CALL      UPWARD1                 * put the patient on standby
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
.
DELBTR30  MOVE      LSTADOC,ADOCTA          * in case doctor also changed
          MOVE      LSTUNIT,ACLSSFT         * in case unit changed
          MOVE      LSTATYP,ATYPE           * Admission type may have changed
          CALL      UPLMISS1
.
.         Update the Ward Usage Statistics
.
DELBTR40  MATCH     LSTWARD,STWARD          * old ward??
          GOTO      DELBTR50 IF EQUAL
.
          MOVE      LSTDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,DELBTR90           * Date not in period file
.
          PACK      KEY9,LSTWARD,DRGYR,DRGNUM
          MOVE      THREE,COUNT             * Number of Transfers out
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          MOVE      STDATE,CPTDATE          * new ward??
          REP       " ",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,DELBTR90           * Date not in period file
.
          PACK      KEY9,LSTWARD,DRGYR,DRGNUM
          MOVE      THREE,COUNT             * Number of Transfers out
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
.         Check for a standby patient to be put into the old bed
.
DELBTR50  MOVE      STWARD,LWARD
          MOVE      STBED,LBED
          MOVE      AADMNO,TADMN
          CALL      SBYPT000
          MOVE      TADMN,AADMNO
.
          CALL      UPDFIL00              * Update files used by transfer type
.
          PACK      KEY8,AADMNO
          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      DELBTR99
.
DELBTR90  MOVE      "Date not found in period File",WEBTITLE
          CALL      WEBERR00
          MOVE      "02224",RPLYCODE        * eGate error msg "Not in Period Fl"
          MOVE      ONE,EXIT
          GOTO      DELBTR99
.
DELBTR91  MOVE      "Visit Locked",WEBTITLE
          CALL      WEBERR00
          MOVE      "02160",RPLYCODE        * eGate error msg "Visit Locked    "
          MOVE      ONE,EXIT
          GOTO      DELBTR99
.
DELBTR99  CALL      UUPTMIS1
          RETURN
.
.-----------------------------------------------------------------------------
. Delete an on leave record - put the patient back into old bed
.-----------------------------------------------------------------------------
DELONL00  MATCH     SP3,LSTBED
          GOTO      DELONL10 IF EQUAL
.
          PACK      KEY6,LSTWARD,LSTBED
          CALL      RDWARD1
.
          MATCH     ZEROVISN,WADMNO
          GOTO      DELONL20 IF NOT EQUAL
.
.         Put the patient into this bed
.
          MOVE      AADMNO,WADMNO
          CALL      UPWARD1
          GOTO      DELONL30
.
.         Put the patient into a No-Bed ward
.
DELONL10  MOVE      APLUR,NBPLUR
          MOVE      SP3,NBROOM
          PACK      KEY13,LSTWARD,AADMNO,NBPLUR
          CALL      RANOBE1
          IF        OVRCD=1
            CALL      WRNOBE1
          ENDIF
          GOTO      DELONL30
.
.         Someone has grabbed this bed - put the patient on Standby
.
DELONL20  MOVE      AADMNO,WSTBY
          CALL      UPWARD1
          MOVE      SP3,LSTWARD
          MOVE      SP3,LSTBED
.
.         Update the admisison status
.
DELONL30  MOVE      TWO,ASTAT
          MOVE      LSTWARD,AWARD
          MOVE      LSTBED,ABED
          CALL      UPLMISS1
.
.         Cancel admission special funding arrangement code
.
          IF        PTCNHDPS=3
            CALL      CLAMFN00              * cancel admission funding
          ENDIF
.
.         Delete the On-leave record
.
          PACK      KEY14,STWARD,STBED,AADMNO
          CALL      DEONLV1
.
.         Update the Ward Usage Statistics
.
          MOVE      STDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,DELONL90           * Date not in period file
.
          PACK      KEY9,STWARD,DRGYR,DRGNUM
          MOVE      FIVE,COUNT
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
.         Write a HCS audit record (An Undischarge Record)
.
          CALL      DISHCS00
.
          CALL      UPDFIL00
          MOVE      ZERO,EXIT
          GOTO      DELONL99
.
DELONL90  MOVE      "Date not found in period File",WEBTITLE
          CALL      WEBERR00
          MOVE      "02224",RPLYCODE        * eGate error msg "Trn date not on "
          MOVE      ONE,EXIT
.
DELONL99  RETURN
.
.-----------------------------------------------------------------------------
. Delete the leave record and return from leave record
.-----------------------------------------------------------------------------
DELREC00  PACK      KEY30,SAVKEY30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,DELREC99
.
          MATCH     AADMNO,TADMN
          GOTO      DELREC99 IF NOT EQUAL
.
          MATCH     ANSL,TMOVE
          IF        @EQUAL
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
            PACK      KEY30 WITH AADMNO,STDATE,STTIME,STWARD,STBED
            CALL      RDTRAN2
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      TEN,HL7TRGID
            MOVE      "PATCHADT",HL7INCLD
            PROC      DGCLICCT              * b'cast cancel transfer *C-90222
            PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL      DETRAN2
          ENDIF
.
          PACK      KEY30,SAVKEY30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,DELREC99
.
          MATCH     AADMNO,TADMN
          GOTO      DELREC99 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE
          IF        @EQUAL
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
            PACK      KEY30 WITH AADMNO,STDATE,STTIME,STWARD,STBED
            CALL      RDTRAN2
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      TEN1,HL7TRGID
            MOVE      "PATCHADT",HL7INCLD
            PROC      DGCLICCT              * b'cast cancel transfer *C-90222
            PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL      DETRAN2
          ENDIF
.
DELREC99  RETURN
.
.-----------------------------------------------------------------------------
.  Delete a return record from leave - Release the current bed
.-----------------------------------------------------------------------------
DELRET00  MOVE      STWARD,AWARD
          MOVE      STBED,ABED
          CALL      RELBED00                * release aadmno in award/abed
.
.         write an on-leave record
.
          MOVE      SP3,OROOM
          PACK      KEY14,STWARD,STBED,AADMNO
          CALL      RDONLV1
          IF        OVRCD = 1
              MOVE      SP3,OROOM
              MOVE      SP8,OERDATE
              MOVE      SP8,OERTIME
              CALL      WRONLV1
          ENDIF
.
.         Update HCS Audit File ( An Unadmit record)
.
          CALL      ADMHCS00
.
.         Update the admission status
.
          PACK      KEY8,AADMNO
          CALL      RDMISS1
.
          MOVE      FOUR,ASTAT
          MOVE      LSTWARD,AWARD
          MOVE      LSTBED,ABED
          CALL      UPLMISS1
.
.         Update the Ward Usage Statistics
.
          MOVE      LSTDATE,CPTDATE
          REP       " 0",CPTDATE
          CALL      GPERD                   * Get the financial period for date
          BRANCH    EXIT,DELRET90
.
          PACK      KEY9,LSTWARD,DRGYR,DRGNUM
          MOVE      SIX,COUNT               * Number of Returns
          MOVE      SEQ,FORM2
          CALL      UPNUMW00
.
          CALL      UPDFIL00              * Update files used by transfer type
.
          PACK      KEY8,AADMNO
          CALL      UUPTMIS1
          MOVE      ZERO,EXIT
          GOTO      DELRET99
.
DELRET90  MOVE      "Date not found in period File",WEBTITLE
          CALL      WEBERR00
          MOVE      "02224",RPLYCODE        * eGate error msg "Trn date not on "
          MOVE      ONE,EXIT
.
DELRET99  RETURN
.
.-----------------------------------------------------------------------------
. Subroutine to calculate the difference between WDATE1 and WDATE2
.-----------------------------------------------------------------------------
DIFFDY00  MOVE      ONE,COUNT
.
          MATCH     WDATE1,WDATE2
          GOTO      DIFFDY50 IF NOT LESS
.
          MOVE      SEQ,COUNT             * Difference negative
.
          MOVE      WDATE1,XDATE
          MOVE      WDATE2,WDATE1
          MOVE      XDATE,WDATE2
.
DIFFDY50  DAYSEP    WDATE1,WDATE2,NBRDAYS
          MULT      COUNT,NBRDAYS
.
DIFFDY99  RETURN
.
.-----------------------------------------------------------------------------
.        Write to HCS Discharge audit file
.        From IBAPAT05 - DISHCS.
.-----------------------------------------------------------------------------
DISHCS00  COMPARE   ONE,CHCS
          GOTO      DISHCS10 IF NOT EQUAL
.
          MATCH     CHCSDTE,DDATE
          GOTO      DISHCS99 IF LESS
.
.         Write to the Unadmit/Undischarge/Adm & Dsch change audit file
.
DISHCS10  MOVE      DADMNO,PTUNADMN
          CALL      IBACLOCK
          PACK      PTUNDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTUNDATE
          MOVE      CTIMEIS,PTUNTIME
          REP       " 0",PTUNTIME
.
          MOVE      SP70,PTUNDTYP           * Discharge status
          MATCH     "2",PTUNTYPE
          GOTO      ADMHCS14 IF EQUAL       * Undischarged
          MATCH     "4",PTUNTYPE
          GOTO      ADMHCS16 IF NOT EQUAL   * Not changing disc.date
.
DISHCS14  MOVE      DSTAT,PTUNDTYP          * Discharge status
.
.        In the event of the same person being changed twice in one day,
.        ignore the second change
.
DISHCS16  MATCH     SP1,PTUNTYPE             * Don't write a blank alteration
          GOTO      DISHCS99 IF EQUAL
          MOVE      PTUNTYPE,FTYPE
          BRANCH    FTYPE,DISHCS20,DISHCS20,DISHCS20,DISHCS20
          GOTO      DISHCS99
.
DISHCS20  PACK      KEY25,PTUNADMN,PTUNDATE,PTUNTIME,PTUNTYPE
          CALL      RDAUNAA1
          COMPARE   ZERO,OVRCD
          GOTO      DISHCS99 IF EQUAL
.
.    Get hospital number for EDI extract if using multi hospitals
.
          MOVE      SP4,PTUNHOSP
          IF        IBCNMHOS = 1
            PACK      KEY30,DADMNO,ZEDS
            CALL      RDSTRAN2                * Position after last pat record
            CALL      RDPTRAN2                * read pat previous record
            BRANCH    OVRCD,DISHCS30
.
            MATCH     DADMNO,TADMN
            GOTO      DISHCS30 IF NOT EQUAL

            PACK      KEY6,TWARD,SP10         * Multiple hospitals.
            CALL      RDWARD1                 * Get hospital number from
            BRANCH    OVRCD,DISHCS30
.                                             ward file.
            MOVE      PTWDHOSN,PTUNHOSP
          ENDIF

DISHCS30  PACK      KEY25,PTUNADMN,PTUNDATE,PTUNTIME,PTUNTYPE
          CALL      WRUNAA1
.
DISHCS99  RETURN
.
.-----------------------------------------------------------------------------
. Get last bed and ward before this leave record
.-----------------------------------------------------------------------------
GETLBT00  MOVE      SP70,LSTWARD
          MOVE      SP70,LSTBED
          PACK      KEY30,AADMNO,Z70        * Work out what ward and bed the
          CALL      RDSTRAN2                * patient should be on leave from
GETLBT10  CALL      RDPTRAN2                * as we are going to remove the
          BRANCH    OVRCD,GETLBT99          * return from leave record
.
          MATCH     AADMNO,TADMN
          GOTO      GETLBT99 IF NOT EQUAL
.
          MATCH     ANSL,TMOVE
          GOTO      GETLBT10 IF EQUAL
.
          MOVE      TWARD,LSTWARD
          MOVE      TBED,LSTBED

GETLBT99  RETURN
.
.------------------------------------------------------------------------------
. Get last bed and ward before this return record
.------------------------------------------------------------------------------
GETLDT00  MOVE      SP70,LSTWARD
          MOVE      SP70,LSTBED
          MOVE      ZERO,LOOPFLG
          MOVE      SP70,DELKEY30
          PACK      KEY30,AADMNO,Z70        * Work out what ward and bed the
          CALL      RDSTRAN2                * patient should be on leave from
GETLDT10  CALL      RDPTRAN2                * as we are going to remove the
          BRANCH    OVRCD,GETLDT99          * return from leave record
.
          MATCH     AADMNO,TADMN
          GOTO      GETLDT99 IF NOT EQUAL
.
          ADD       ONE,LOOPFLG
.
          COMPARE   ONE,LOOPFLG
          IF        @EQUAL
            PACK      DELKEY30,TADMN,TDATE,TTIME,TWARD,TBED
          ENDIF
.
          MATCH     ANSL,TMOVE
          GOTO      GETLDT10 IF NOT EQUAL
.
          MOVE      TWARD,LSTWARD
          MOVE      TBED,LSTBED

GETLDT99  RETURN
.
.------------------------------------------------------------------------------
.  Find transfer record before deleted record
.------------------------------------------------------------------------------
GETREC00  PACK      KEY30,AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      RDSTRAN2
          CALL      RDPTRAN2            * get record before deleted one
.
          MOVE      ADATE,NEWTDATE
          MOVE      TWARD,NEWTWARD
          MOVE      TATYPE,NEWTATYP
          MOVE      TRBTYP,NEWTBTYP
          MOVE      TCHSTAT,NEWTCHST
          MOVE      TDEPT,NEWTDEPT
.
          CALL      BRAT0000
          MOVE      SAVEND,TRBEND
          MOVE      ADDEND,PTTRAEND
          MOVE      SAVPAT,TRATEF
          MOVE      SAVHF,TRATEH
          MOVE      ADISC,TDISC
          MOVE      ZERO,TALLW
          MOVE      SAVEXTR,TEXTRA
          MOVE      AUSR2,TCHSTAT
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
GETREC99  RETURN
.
.------------------------------------------------------------
.  Last two transaction details
.------------------------------------------------------------
LTRND000  MOVE      SP8,STDATE
          MOVE      SP8,STTIME
          MOVE      SP3,STWARD
          MOVE      SP3,STBED
          MOVE      SP3,STATYPE
          MOVE      SP6,STADOCT
          MOVE      SP6,STDEPT
          MOVE      SP1,STMOVE
          MOVE      ZERO,STEPNO
          MOVE      ZERO,STRATEF
.
..        Loop over the transfer file
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
LTRND010  CALL      RDKTRAN2
          BRANCH    OVRCD,LTRND999
.
          MATCH     TADMN,AADMNO
          GOTO      LTRND999 IF NOT EQUAL
.
          MOVE      STDATE,LSTDATE
          MOVE      STTIME,LSTTIME
          MOVE      STWARD,LSTWARD
          MOVE      STBED,LSTBED
          MOVE      STATYPE,LSTATYP
          MOVE      STADOCT,LSTADOC
          MOVE      STDEPT,LSTUNIT
          MOVE      STEPNO,LSTEPNO
          MOVE      STRATEF,LSTRATE
.
          MOVE      TDATE,STDATE
          MOVE      TTIME,STTIME
          MOVE      TWARD,STWARD
          MOVE      TBED,STBED
          MOVE      TATYPE,STATYPE
          MOVE      TADOCT,STADOCT
          MOVE      TDEPT,STDEPT
          MOVE      PTTREPNO,STEPNO
          MOVE      TMOVE,STMOVE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      TCHSTAT,STCHSTAT
.
.         Calculate new rate
.
          MOVE      TRATEF,STRATEF
          ADD       TRATEH,STRATEF
          ADD       PTTRADPT,STRATEF
          ADD       PTTRADRB,STRATEF
          ADD       TEXTRA,STRATEF
          SUB       TDISC,STRATEF
          SUB       TALLW,STRATEF
          GOTO      LTRND010
.
LTRND999  RETURN
.
.------------------------------------------------------------
. Handles Mental Health data when Undischarging a Patient.
. From IBAPAT05
.------------------------------------------------------------
MEHA0000  COMPARE   ONE,MHCNUSE
          GOTO      MEHA9999 IF NOT EQUAL   * not using mental health
.
          MATCH     SP3,PTMXLS
          GOTO      MEHA9999 IF EQUAL       * not a mental health patient
.
          PACK      KEY5,ANSL,ANSS,PTMXLS
          CALL      RDCODE1
          BRANCH    OVRCD,MEHA9999          * invalid legal status
.
.         update status on visit file - MEHVI1FD
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,MEHA9999          * not a mental health
.
          MOVE      TWO,AUDIFLAG            * before change
          CALL      MHAUDVIS                * write audit
          MOVE      ONE,MHVISTAT            * set to In-Hospital
          CALL      UPMHVIS1
          MOVE      THREE,AUDIFLAG          * after change
          CALL      MHAUDVIS                * write audit
.
.         delete discharge record - MEHDS1FD
.
          MOVE      AADMNO,KEY8
          CALL      DEMHDSC1
          MOVE      FOUR,AUDIFLAG           * delete
          CALL      MHAUDDSC                * write audit
.
.         check if change of legal status on discharge date
.
MEHA1000  PACK      KEY21,AADMNO,DDATE,SP20
          CALL      RSMHLEG1
MEHA1500  CALL      RKMHLEG1
          BRANCH    OVRCD,MEHA5000          * no change of legal status
.
          MATCH     AADMNO,MHLEADMN
          GOTO      MEHA5000 IF NOT EQUAL
.
          MATCH     ANSR,MHLEFLAG           * review status change only ?
          GOTO      MEHA1500 IF EQUAL       * yes - ignore
.
          MATCH     ANSN,MHLEFLAG           * no status change ?
          GOTO      MEHA1500 IF EQUAL       * yes - ignore
.
          PACK      DIM8,DDATE,SP10
          MATCH     DIM8,MHLEDATE
          GOTO      MEHA5000 IF NOT EQUAL
.
.         delete the MEHLEGFD record for the discharge date & time
.
          PACK      KEY21,AADMNO,DDATE,MHLETIME
          CALL      DEMHLEG1
.
          GOTO      MEHA1000
.
.         check if change of legal status on discharge date
.
MEHA5000  PACK      KEY21,AADMNO,DDATE,SP20
          CALL      RSMHCJA1
          CALL      RKMHCJA1
          BRANCH    OVRCD,MEHA9000          * no change of legal status
.
          MATCH     AADMNO,MHCJADMN
          GOTO      MEHA9000 IF NOT EQUAL
.
          PACK      DIM8,DDATE,SP10
          MATCH     DIM8,MHCJDATE
          GOTO      MEHA9000 IF NOT EQUAL
.
.         delete the MEHCJAFD record for the discharge date & time
.
          PACK      KEY21,AADMNO,DDATE,MHCJTIME
          CALL      DEMHCJA1
.
          GOTO      MEHA5000
.
.         update the Master file with the previous Legal Status
.
MEHA9000  PACK      KEY21,AADMNO,DDATE,Z20
          CALL      RSMHLEG1
          CALL      RPMHLEG1
          BRANCH    OVRCD,MEHA9999          * no change of legal status
.
          MATCH     AADMNO,MHLEADMN
          GOTO      MEHA9999 IF NOT EQUAL
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          MOVE      MHLESTAT,PTMXLS
          CALL      UPMAST1
          CALL      UUPTMAS1
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TEN2,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICUP       * send new details to Datagate      
.
MEHA9999  RETURN
.
.------------------------------------------------------------------------------
. Handles Mental Health data when Un-admittiing a patient
. From IBAPAT05
.------------------------------------------------------------------------------
MEHB0000  COMPARE   ONE,MHCNUSE
          GOTO      MEHB9999 IF NOT EQUAL   * not using mental health
.
          MATCH     SP3,PTMXLS
          GOTO      MEHB9999 IF EQUAL       * not a mental health patient
.
          PACK      KEY5,ANSL,ANSS,PTMXLS
          CALL      RDCODE1
          BRANCH    OVRCD,MEHB9999          * invalid legal status
.
.         update status on visit file - MEHVI1FD
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,MEHB9999          * not a mental health patient
.
          MOVE      TWO,AUDIFLAG            * before change
          CALL      MHAUDVIS                * write audit
          MOVE      ZERO,MHVISTAT           * set to Pre-Admitted
          CALL      UPMHVIS1
          MOVE      THREE,AUDIFLAG          * after change
          CALL      MHAUDVIS                * write audit
.
.         delete all records in MEHLEGFD
.
          PACK      KEY21,AADMNO,SP20
          CALL      RSMHLEG1
.
MEHB1000  CALL      RKMHLEG1
          BRANCH    OVRCD,MEHB1200          * end of file
.
          PACK      SAVKEY21,MHLEADMN,MHLEDATE,MHLETIME,SP20
          MATCH     MHLEADMN,AADMNO
          GOTO      MEHB1200 IF NOT EQUAL   * different patient
.
          PACK      KEY21,MHLEADMN,MHLEDATE,MHLETIME
          CALL      DEMHLEG1
.
          MOVE      SAVKEY21,KEY21
          CALL      RSMHLEG1
          GOTO      MEHB1000
.
.         delete all records in MEHCJAFD
.
MEHB1200  PACK      KEY21,AADMNO,SP20
          CALL      RSMHCJA1
.
MEHB1300  CALL      RKMHCJA1
          BRANCH    OVRCD,MEHB1500          * end of file
.
          PACK      SAVKEY21,MHCJADMN,MHCJDATE,MHCJTIME,SP20
          MATCH     MHCJADMN,AADMNO
          GOTO      MEHB1500 IF NOT EQUAL   * different patient
.
          PACK      KEY21,MHCJADMN,MHCJDATE,MHCJTIME
          CALL      DEMHCJA1
.
          MOVE      SAVKEY21,KEY21
          CALL      RSMHCJA1
          GOTO      MEHB1300
.
.         delete all records in MEHDIAFD
.
MEHB1500  PACK      KEY16,AADMNO,SP8        * start on MEHLEGFD
          CALL      RSMHDIA1
.
MEHB1600  CALL      RKMHDIA1
          BRANCH    OVRCD,MEHB2000          * end of file
.
          MATCH     MHDIADMN,AADMNO
          GOTO      MEHB2000 IF NOT EQUAL   * different patient

          PACK      KEY16,MHDIADMN,MHDIDATE
          CALL      DEMHDIA1
          GOTO      MEHB1600
.
.         delete all records in MEHREVFD
.
MEHB2000  PACK      KEY21,AADMNO,SP20
          CALL      RSMHREV1
          CALL      RKMHREV1
          BRANCH    OVRCD,MEHB2500          * end of file
.
          MATCH     AADMNO,MHRVADMN
          GOTO      MEHB2500 IF NOT EQUAL   * different patient
.
          PACK      KEY21,MHRVADMN,MHRVDATE,MHRVTIME
          CALL      DEMHREV1
          GOTO      MEHB2000
.
.         delete all records in MEHCJRFD
.
MEHB2500  PACK      KEY21,AADMNO,SP20
          CALL      RSMHCJR1
          CALL      RKMHCJR1
          BRANCH    OVRCD,MEHB3000          * end of file
.
          MATCH     AADMNO,MHCRADMN
          GOTO      MEHB3000 IF NOT EQUAL   * different patient
.
          PACK      KEY21,MHCRADMN,MHCRDATE,MHCRTIME
          CALL      DEMHCJA1
          GOTO      MEHB2500
.
.         delete all records in MEHDL1FD
.
MEHB3000  PACK      KEY24,AADMNO,SP20
          CALL      RSMHDLV1
.
MEHB3100  CALL      RKMHDLV1
          BRANCH    OVRCD,MEHB9999          * end of file
.
          MATCH     AADMNO,MHDLADMN
          GOTO      MEHB9999 IF NOT EQUAL   * different patient
.
          PACK      KEY24,MHDLADMN,MHDLDATE,MHDLTIME
          CALL      DEMHDLV1
          MOVE      FOUR,AUDIFLAG           * delete
          CALL      MHAUDDLV                * write audit
          GOTO      MEHB3100
.
MEHB9999  RETURN
.
.------------------------------------------------------------------------------
. Handles Mental Health data when :
. Changing Admission Date.
. Para's  : OLDADATE      old admission date
.           PTMIS002      new admission date
.------------------------------------------------------------------------------
MEHC0000  COMPARE   ONE,MHCNUSE
          GOTO      MEHC9999 IF NOT EQUAL   * not using mental health
.
          MATCH     SP3,PTMXLS
          GOTO      MEHC9999 IF EQUAL       * not a mental health patient
.
          PACK      KEY5,ANSL,ANSS,PTMXLS
          CALL      RDCODE1
          BRANCH    OVRCD,MEHC9999          * invalid legal status
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,MEHC9999          * not a mental health patient
.
.         update MEHDIAFD if a record on old admission date
.
          PACK      KEY16,AADMNO,OLDADATE
          CALL      RDMHDIA1
          BRANCH    OVRCD,MEHC5000          * no record on the date
.
.         see if a record on the new admission date
.
          PACK      KEY16,AADMNO,PTMIS002   * new admission date
          CALL      RDMHDIA1
          BRANCH    OVRCD,MEHC1000          * no record on new date
          CALL      DEMHDIA1                * delete old record
.
MEHC1000  PACK      KEY16,AADMNO,OLDADATE   * have a record on new date
          CALL      RDMHDIA1
          BRANCH    OVRCD,MEHC5000          * no record
          MOVE      PTMIS002,OLDADATE
          CALL      UPMHDIA1
.
.         update MEHLEGFD if a record on old admission date
.
MEHC5000  PACK      KEY21,AADMNO,OLDADATE,SP20
          CALL      RSMHLEG1
MEHC5100  CALL      RKMHLEG1
          BRANCH    OVRCD,MEHC7000          * no record on the date
.
          MATCH     AADMNO,MHLEADMN
          GOTO      MEHC7000 IF NOT EQUAL
.
          MATCH     OLDADATE,MHLEDATE
          GOTO      MEHC7000 IF NOT EQUAL
          MOVE      MHLETIME,DIM5
.
.         see if a record on the new admission date & time
.
          PACK      KEY21,AADMNO,PTMIS002,DIM5   * new admission date
          CALL      RDMHLEG1
          BRANCH    OVRCD,MEHC6000          * no record on new date
          CALL      DEMHLEG1                * delete old record
.
MEHC6000  PACK      KEY21,AADMNO,OLDADATE,DIM5  * have a record on new date
          CALL      RDMHLEG1
          BRANCH    OVRCD,MEHC7000          * no record
          MOVE      PTMIS002,MHLEDATE
          MOVE      DIM5,MHLETIME
          CALL      UPMHLEG1
          GOTO      MEHC5100
.
.         update MEHCJAFD if a record on old admission date
.
MEHC7000  PACK      KEY21,AADMNO,OLDADATE,SP20
          CALL      RSMHCJA1
MEHC7100  CALL      RKMHCJA1
          BRANCH    OVRCD,MEHC9999          * no record on the date
.
          MATCH     AADMNO,MHCJADMN
          GOTO      MEHC9999 IF NOT EQUAL
.
          MATCH     OLDADATE,MHCJDATE
          GOTO      MEHC9999 IF NOT EQUAL
          MOVE      MHCJTIME,DIM5
.
.         see if a record on the new admission date & time
.
          PACK      KEY21,AADMNO,PTMIS002,DIM5   * new admission date
          CALL      RDMHCJA1
          BRANCH    OVRCD,MEHC8000          * no record on new date
          CALL      DEMHCJA1                * delete old record
.
MEHC8000  PACK      KEY21,AADMNO,OLDADATE,DIM5  * have a record on new date
          CALL      RDMHCJA1
          BRANCH    OVRCD,MEHC9999          * no record
          MOVE      PTMIS002,MHCJDATE
          MOVE      DIM5,MHCJTIME
          CALL      UPMHCJA1
          GOTO      MEHC7100
.
MEHC9999  RETURN
.
.*********************************************************************
.*                  MEHD0000                    Called by : DELTREC  *
.*               (from IBAPAT05)                                     *
.*        Handles Mental Health data when :                          *
.*        Cancelling last transfer file record.                      *
.*        Para's  : LASTTRAN      last tran record                   *
.*                  MEHKEY24      key for MEHDLVFD record            *
.*********************************************************************
MEHD0000  COMPARE   ONE,MHCNUSE
          GOTO      MEHD9999 IF NOT EQUAL   * not using mental health
.
          MATCH     SP3,PTMXLS
          GOTO      MEHD9999 IF EQUAL       * not a mental health patient
.
          PACK      KEY5,ANSL,ANSS,PTMXLS
          CALL      RDCODE1
          BRANCH    OVRCD,MEHD9999          * invalid legal status
.
          MATCH     ANSL,LASTTRAN
          GOTO      MEHD5000 IF EQUAL       * last record a leave record
.
          MATCH     ANSR,LASTTRAN
          GOTO      MEHD9999 IF NOT EQUAL   * not a return record
.
.         last record was a return record
.
          UNPACK    MEHKEY24,KEY8,DIM8A,KEY8
          PACK      KEY30,AADMNO,DIM8A,KEY8,Z10
          CALL      RDSTRAN2
.
MEHD1000  CALL      RDPTRAN2
          BRANCH    OVRCD,MEHD9999          * tran file stuffed
.
          MATCH     AADMNO,TADMN
          GOTO      MEHD9999 IF NOT EQUAL   * tran file stuffed
.
          MATCH     "L",TMOVE
          GOTO      MEHD1000 IF NOT EQUAL   * not the leave record
.
.         have the leave record so get the leave type
.
          MOVE      SP1,TCINDC1
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,MEHD9999          * invalid type of leave
.
.         now update the visit status
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,MEHD9999          * no MH visit record
.
          MOVE      TWO,AUDIFLAG            * before change
          CALL      MHAUDVIS                * write audit
          REP       " 2T3H4A5",TCINDC1      * set the leave status
          MOVE      TCINDC1,MHVISTAT        * set status
          CALL      UPMHVIS1
          MOVE      THREE,AUDIFLAG          * after change
          CALL      MHAUDVIS                * write audit
          IF        MHCNKLER = 1
            PACK      KEY24,MEHKEY24
            CALL      RDMHLER1              * See if on file
            BRANCH    OVRCD,MEHD9999
            CALL      DEMHLER1              * delete record
          ENDIF
          GOTO      MEHD9999
.
.         last record was a leave record
.
MEHD5000  MOVE      AADMNO,KEY8             * update status on MEHVISFD
          CALL      RDMHVIS1
          BRANCH    OVRCD,MEHD9999
.
          MOVE      TWO,AUDIFLAG            * before change

          CALL      MHAUDVIS                * write audit
          MOVE      ONE,MHVISTAT            * set to In-hospital
          CALL      UPMHVIS1
          MOVE      THREE,AUDIFLAG          * after change
          CALL      MHAUDVIS                * write audit
.
          MOVE      MEHKEY24,KEY24          * delete MEHDLVFD record
          CALL      DEMHDLV1
          MOVE      FOUR,AUDIFLAG           * delete
          CALL      MHAUDDLV                * write audit
.
MEHD9999  RETURN
.
.------------------------------------------------------------------------------
. NEWR0000 - From IBAPMS03
. Calculate the rate for a transfer record
.------------------------------------------------------------------------------
NEWR0000  PACK      NEWTDATE,TDATE
          MOVE      TWARD,NEWTWARD
          MOVE      TATYPE,NEWTATYP
          MOVE      TRBTYP,NEWTBTYP
          MOVE      TCHSTAT,NEWTCHST
          MOVE      TDEPT,NEWTDEPT
.
          CALL      BRAT0000
          MOVE      SAVEND,TRBEND
          MOVE      ADDEND,PTTRAEND
          MOVE      SAVPAT,TRATEF
          MOVE      SAVHF,TRATEH
          MOVE      ADISC,TDISC
          MOVE      ZERO,TALLW
          MOVE      SAVEXTR,TEXTRA
          MOVE      AUSR2,TCHSTAT
.
.         Check if we have a zero rate
.
          COMPARE   ZERO,TRATEF
          GOTO      NEWR8500 IF NOT EQUAL
.
          COMPARE   ZERO,TEXTRA
          GOTO      NEWR9999 IF EQUAL
.
.         We have a non-zero rate
.
NEWR8500  MOVE      ONE,CHRGFLAG
.
NEWR9999  RETURN
+
.*************************************************************************
.*                               PATD0000                                *
.*              Update the patdayaf files if neccessary                  *
.*************************************************************************
PATD0000  COMPARE   ZERO,PTCNDAYF           * skip if not using patdayaf
          GOTO      PATD9999 IF EQUAL           file for statistics
.
          MATCH     SP8,PTCNNDAT            * Don't update patdayaf files if
          GOTO      PATD9999 IF EQUAL         the update has not been run
.                                             before
          MATCH     "00000000",PTCNNDAT
          GOTO      PATD9999 IF EQUAL
.
          MOVE      PTMIS001,WDATE
          BRANCH    OPTION,PATD1000,PATD1000,PATD2000,PATD2000,PATD5000:
                           PATD6000,PATD9999,PATD9999,PATD9999,PATD9999:
                           PATD9999
.
          GOTO      PATD9999
.
.------ Update the patdayaf files for an undischarged patient ------
.
PATD1000  MOVE      DDATE,STRTDATE
          REP       " 0",STRTDATE
.
          MATCH     ZERO8,STRTDATE
          GOTO      PATD9999 IF EQUAL
.
          UNPACK    PTCNNDAT,CCENT,CYEAR,CMON,CDAY
.
          CALL      SUBD0000            * subtract a day from PTCNNDAT
.
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYAD            * add records to the patdayaf files
.
          GOTO      PATD9999
.
.------ Update the patdayaf files for an unadmitted patient ------
.
PATD2000  MOVE      ADATE,STRTDATE
          REP       " 0",STRTDATE
.
          UNPACK    PTCNNDAT,CCENT,CYEAR,CMON,CDAY
.
          CALL      SUBD0000            * subtract a day from PTCNNDAT
.
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYDL            * delete records from patdayaf files
.
          GOTO      PATD9999
.
.------ Update the patdayaf files for a change in admission date ------
.
PATD5000  MATCH     WDATE,ADATE         * match new date to admission date
          GOTO      PATD5500 IF LESS
.
          MOVE      WDATE,STRTDATE
          REP       " 0",STRTDATE
.
          MOVE      ADATE,ENDDATE
          REP       " 0",ENDDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYAD            * add records to the patdayaf files
.
          GOTO      PATD9999
.
.------ admission date is less than the new date ------
.
PATD5500  MOVE      ADATE,STRTDATE
          REP       " 0",STRTDATE
.
          MOVE      WDATE,ENDDATE
          REP       " 0",ENDDATE
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      SUBD0000            * subtract a day from the end date
.
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYDL            * delete records from patdayaf files
.
          GOTO      PATD9999
.
.------ Update the patdayaf files for a change in discharge date ------
.
PATD6000  MATCH     WDATE,DDATE         * match new date to discharge date
          GOTO      PATD6500 IF LESS
.
          MOVE      WDATE,STRTDATE
          REP       " 0",STRTDATE
.
          MOVE      DDATE,ENDDATE
          REP       " 0",ENDDATE
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON              * convert to julian date
.
          ADD       ONE,JULDAY          * add one to the start date
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON
.
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYDL            * delete records from patdayaf files
.
          GOTO      PATD9999
.
.------ discharge date is less than the new date ------
.
PATD6500  MOVE      DDATE,STRTDATE
          REP       " 0",STRTDATE
.
          MOVE      WDATE,ENDDATE
          REP       " 0",ENDDATE
          MOVE      AADMNO,ADMISNO
.
          CALL      PATDAYAD            * add records to the patdayaf files
.
PATD9999  RETURN
.
.------------------------------------------------------------------------------
.         Public Hospital uses PATRATE file for the bed fee
.------------------------------------------------------------------------------
PUBFEE00  MOVE      ZERO,RCGPAT
          MOVE      ZERO,RCGHF
          MOVE      ZERO,REDAYS
          MOVE      ACLSS,RCLSS
          MOVE      SAVCHST,RCHST
.
          PACK      KEY12,ACLAIM,ATYPE,ACLSS,AUSR2
          CALL      RDRATE1
          BRANCH    OVRCD,PUBFEE90                *unlock before exiting
.
          MOVE      SVTRATEF,ROPAT               *save the old rate
          ADD       SVTEXTRA,ROPAT
.
          MOVE      RCPDAY,SVTRATEF
          MOVE      RCEDAY,SVTEXTRA
.
PUBFEE90  MOVE      AADMNO,KEY8
          CALL      UUPTMIS1
.
PUBFEE99  RETURN
.
.------------------------------------------------------------------------------
. Subroutine to release the patient AADMNO in ward/bed AWARD/ABED
. From IBAPAT05 - RELBED.
.------------------------------------------------------------------------------
RELBED00  PACK      KEY6,AWARD,ABED
          CALL      RDWARD1
          BRANCH    OVRCD OF RELBED20
.
.        Check for a No-Bed ward
.
          MATCH     SP3,ABED
          GOTO      RELBED10 IF EQUAL
.
.        Check that this is the correct patient
.
          MATCH     WADMNO,AADMNO
          GOTO      RELBED20 IF NOT EQUAL
.
          MOVE      ZEROVISN,WADMNO
          MOVE      ZERO,WPLUR
          CALL      UPWARD1
          GOTO      RELBED99
.
.        No-Bed ward - just delete the record from the No-Bed file
.
RELBED10  MOVE      APLUR,NBPLUR
          PACK      KEY13,AWARD,AADMNO,NBPLUR
          CALL      RDNOBE1
          BRANCH    OVRCD OF RELBED20
.
          CALL      DENOBE1
          GOTO      RELBED99
.
.        Standby Patient - search through the standby index
.
RELBED20  MOVE      AADMNO,WSTBY
          PACK      KEY14,WSTBY,SP70
          CALL      RDSWARD4
          CALL      RDKWARD4
          BRANCH    OVRCD OF RELBED90
.
          MATCH     WSTBY,AADMNO
          GOTO      RELBED90 IF NOT EQUAL
.
          PACK      KEY6,WWARD,WBED
          CALL      RDWARD1
          MOVE      ZEROVISN,WSTBY
          CALL      UPWARD1
          GOTO      RELBED99
.
RELBED90  MOVE      "Ward Bed Incosistency",WEBTITLE
          CALL      WEBERR00
          MOVE      "02122",RPLYCODE        * eGate error msg "Ward/Bed Inconsi"
          MOVE      ONE,EXIT
.
RELBED99  RETURN
.
.------------------------------------------------------------------------------
. Subroutine to put a standby patient into ward/bed LWARD/LBED
. From IBAPAT05 - STBYPAT.
.------------------------------------------------------------------------------
SBYPT000  PACK      KEY6,LWARD,LBED
          CALL      RDWARD1
          BRANCH    OVRCD,SBYPT999
.
          MATCH     ZEROVISN,WSTBY
          GOTO      SBYPT999 IF EQUAL
.
.        Get the patient details for this standby patient
.
          MOVE      WSTBY,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,SBYPT999
.
.        Check that this patient is actually on standby
.
          MATCH     SP3,AWARD
          GOTO      SBYPT999 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      SBYPT999 IF NOT EQUAL
.
.        Check that we can actually put the patient into the bed
.
          MATCH     ZEROVISN,WADMNO
          GOTO      SBYPT999 IF NOT EQUAL
.
.         Check if this Admission Number is in use
.
          MOVE      AADMNO,KEY8
          CALL      RDPTMIS1
          COMPARE   ZERO,OVRCD
          GOTO      SBYPT990 IF NOT EQUAL
.
.        Put this standby patient into the bed
.
          MOVE      WSTBY,WADMNO
          MOVE      ZERO,WPLUR
          MOVE      ZEROVISN,WSTBY
          CALL      UPWARD1
.
.        Update the admission record with the ward/bed
.
          MOVE      LWARD,AWARD
          MOVE      LBED,ABED
          MOVE      ZERO,APLUR
          CALL      UPMISS1
          CALL      UUPTMIS1
.
SBYPT990  MOVE      "Patient Admission Locked ",WEBTITLE
          CALL      WEBERR00
          MOVE      "02109",RPLYCODE        * eGate error msg "Admission Locked"
          MOVE      ONE,EXIT
          GOTO      SBYPT999
.
SBYPT999  RETURN
.
.------------------------------------------------------------------------------
. Routine to subtract one from a given date  - From IBAPAT34
. Requires : CCENT, CYEAR, CMON, CDAY
. Returns  : CC,YY, MM, DD
.------------------------------------------------------------------------------
SUBD0000  MOVE      CYEAR,YY
          MOVE      CCENT,CC
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON           * convert to julian date
.
          SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON              * convert back to DD,MM,CC,YY format
.
SUBD9999  RETURN
.
.------------------------------------------------------------------------------
. Update the control file with the transfer date if necessary
.------------------------------------------------------------------------------
UPDCFR00  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY8;*148,CFRDTE
          MATCH     CFRDTE,TDATE
          GOTO      UPDCFR99 IF NOT LESS
          MOVE      TDATE,CFRDTE
          WRITAB    CONTROLF,EIGHTY8;*148,CFRDTE
.
UPDCFR99  RETURN
.
.------------------------------------------------------------------------------
.  Update common files used by bed/on-leave/return from leave transfer types
.------------------------------------------------------------------------------
UPDFIL00
.         If doctor has changed update the visit file
.
          MATCH     SP6,LSTADOC
          IF        !@EQUAL
            CALL      UPDVIS00              * update visit file
          ENDIF
.
.         Broadcast transfer cancellation to Datagate BEFORE deleting record
.
          PACK      KEY30,AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      RDTRAN2
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TEN3,HL7TRGID
          MOVE      "PATCHADT",HL7INCLD
          PROC      DGCLICCT                * b'cast cancel transfer   *C-90222
.
.         Delete transfer file record
.
          PACK      KEY30,AADMNO,STDATE,STTIME,STWARD,STBED
          CALL      DETRAN2
.
          IF        CANTYPE=5
            MATCH     STDATE,ADATE
            IF        @EQUAL
              CALL      GETREC00            * get record before deleted one
            ENDIF
          ENDIF
.
.         Update the control file with the transfer date if necessary
.
          CALL      UPDCFR00                * Update the control file
.
          CALL      MEHD0000                * update mental health details
.
UPDFIL99  RETURN
.
.------------------------------------------------------------
. Update Visit File
.------------------------------------------------------------
UPDVIS00  PACK      KEY8,AADMNO
          CALL      RLPTVIS1
          IF        OVRCD=0
            MOVE      LSTADOC,PVIDOCT
            CALL      UPVISA1
            CALL      UUPTVIS1
          ELSE
            IF        OVRCD = 2
              GOTO      DELBTR91
            ENDIF
          ENDIF
.
UPDVIS99  RETURN
.
.------------------------------------------------------------------------------
. Subroutine to update the Patient Ward Usage file
. From IBAPAT05 - UPDNUMW.
.------------------------------------------------------------------------------
UPNUMW00  BRANCH    PTCNNUMM,UPNUMW99      * branch if file is updated
          MOVE      ZERO,COUNT
.                                           using the monthly update
UPNUMW10  CALL      RLNUMW1
          BRANCH    OVRCD,UPNUMW99,UPNUMW80
          LOAD      WORK,COUNT,PADMN,PTRIN,PTROUT,PDSCH,PLEAV,PRETN
          ADD       FORM2,WORK
          STORE     WORK,COUNT,PADMN,PTRIN,PTROUT,PDSCH,PLEAV,PRETN
          CALL      UPNUMW1
          CALL      UUNUMW1
          GOTO      UPNUMW99
.
UPNUMW80  COMPARE   TEN,COUNT
          GOTO      UPNUMW82 IF EQUAL
          DISPLAY   *W,"Waiting on Record Lock PATNUMFD"
          ADD       ONE,COUNT
          GOTO      UPNUMW10
.
UPNUMW82  MOVE      "Record Lock PATNUMBF",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          MOVE      "02136",RPLYCODE        * eGate error msg "Ward Usage Locked
          MOVE      ONE,EXIT
.
UPNUMW99  RETURN
.
.------------------------------------------------------------------------------
.* UPTRN000: Update the transfer file with a new set of rates.            *
.*           Ignore all records before ALACDTE (the last accom. date) as *
.*           they have already been invoiced, but allow for the inserting *
.*           of a new record on that date to cater for a new rate.        *
.*                                                                        *
.* RETURNS   : CHRGFLAG = 0 if the all the new rates are zero             *
.*                        1 if there is a non-zero rate posted            *
.------------------------------------------------------------------------------
UPTRN000  MOVE      ZERO,CHRGFLAG          * Assume nothing is being charged
          MOVE      ONE,NOCHANGE           * Assume we don't need a new record
          MOVE      SP1,STMOVE             * initialise last movement type
.
          PACK      KEY30,PVIBILL,SP30
          CALL      RDSTRAN2
UPTRN100  CALL      RDKTRAN2
          BRANCH    OVRCD,UPTRN800         * Finished processing patient
.
          MATCH     TADMN,PVIBILL
          GOTO      UPTRN800 IF NOT EQUAL  * Finished processing patient
.
.         Is this record outside any invoice period ?
.
          MATCH     CLACDATE,TDATE         * check against starting charge date
          GOTO      UPTRN200 IF LESS
.
          MATCH     ALACDTE,TDATE          * check against previous invoices
          GOTO      UPTRN200 IF LESS
.
.         We have an record outside the invoice period. Check if we have to
.         post a record to allow for a change in rate at the end of the last
.         invoice. We do this by check if a rate change is required, and if
.         so and there is not a record with the same date as ALACDTE, we must
.         write one with ALACDTE as the date.
.
          BRANCH    NOCHANGE,UPTRN300      * No new record needed
.
          MATCH     ALACDTE,TDATE
          GOTO      UPTRN300 IF EQUAL      * This record will get the new rate
.
          MATCH     CLACDATE,TDATE
          GOTO      UPTRN300 IF EQUAL      * This record will get the new rate
.
          MATCH     SP1,STMOVE             * unknown last move ?
          GOTO      UPTRN300 IF EQUAL      * This record will get the new rate
.
          MATCH     ANSL,STMOVE            * last move was going on-leave ?
          GOTO      UPTRN300 IF EQUAL      * This record will get the new rate
.
.         Write a new record to set up the change of rate on the first day
.         of the next invoice
.
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
.
          MOVE      ALACDTE,TDATE          * use last accomodate date
          MATCH     CLACDATE,ALACDTE       * check if 1st charging date needed
          GOTO      UPTRN150 IF NOT LESS
.
          MOVE      CLACDATE,TDATE         * use 1st date of charging
.
.         Set up the rest of the record
.
UPTRN150  MOVE      "01:00:00",TTIME       * Dummy time value
          MOVE      STWARD,TWARD           * Restore old ward
          MOVE      STBED,TBED             * Restore old bed
.
          MOVE      ZERO,TDISC             * Set up discount amount
          MOVE      ZERO,TALLW             * Set up allowance amount
          MOVE      ANSC,TMOVE             * Write a change record
          MOVE      STATYPE,TATYPE         * Restore old admission type
          MOVE      STADOCT,TADOCT         * Restore old attending doctor
          MOVE      STDEPT,TDEPT           * Restore old department
          MOVE      STEPNO,PTTREPNO        * Restore episode number
          MOVE      STRBTYP,TRBTYP         * Restore old bed type
          MOVE      STRBEND,TRBEND         * Restore old ending bed day range
          MOVE      STCHSTAT,TCHSTAT       * Restore old chargable status
          MOVE      PASSCODE,PTTROPER      * operator Id
.
.         We have a record to be updated. Find the new rate
.
          CALL      NEWR0000               * Get the new rates
          MOVE      SP3,PTTRLTYP            * clear type of leave value
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2                * Write this new record
.
          MOVE      ONE,NOCHANGE           * Set flag to say no new rec needed
.
.         We have posted the new record. Now update the record we had just
.         read in before posting the new record.
.
          MOVE      SAVKEY30,KEY30
          CALL      RDTRAN2                * Re-read last record
          GOTO      UPTRN300               * Continue processing
.
.         We have a record that has already been invoice. Don't update. Just
.         save the details and check if anything needs to be changed.
.
UPTRN200  MOVE      TWARD,STWARD           * Save old ward
          MOVE      TBED,STBED             * Save old bed
          MOVE      TATYPE,STATYPE         * Save old admission type
          MOVE      TADOCT,STADOCT         * Save old attending doctor
          MOVE      TDEPT,STDEPT           * Save old department
          MOVE      PTTREPNO,STEPNO        * Save episode number
          MOVE      TRBTYP,STRBTYP         * Save old bed type
          MOVE      TRBEND,STRBEND         * Save old ending bed day range
          MOVE      TMOVE,STMOVE           * Save old movement type
.
          MOVE      TRATEF,STRATEF
          MOVE      TEXTRA,STEXTRA
          MOVE      TRATEG,STRATEG
          MOVE      TRATEH,STRATEH
.
          CALL      NEWR0000               * Work out the new rate
.
          COMPARE   TRATEF,STRATEF         * Basic rate changed ?
          GOTO      UPTRN210 IF NOT EQUAL  * Yes, set flag accordingly
.
          COMPARE   TRATEH,STRATEH         * Rebate rate changed ?
          GOTO      UPTRN210 IF NOT EQUAL  * Yes, set flag accordingly
.
          COMPARE   TRATEG,STRATEG         * Government rate changed ?
          GOTO      UPTRN210 IF NOT EQUAL  * Yes, set flag accordingly
.
          COMPARE   TEXTRA,STEXTRA         * Extra rate changed ?
          GOTO      UPTRN210 IF NOT EQUAL  * Yes, set flag accordingly
          MOVE      ONE,NOCHANGE           * No new record needed
          GOTO      UPTRN100               * Get the next record
.
.         The rate should have changed. Remember this so the rate can be
.         changed at the end of the invoiced period
.
UPTRN210  MOVE      ZERO,NOCHANGE          * A new rate record is needed
          GOTO      UPTRN100               * Get the next record
.
.         Update the current record with a new rate
.
UPTRN300  CALL      NEWR0000               * Calculate the new rate
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2                * Update with the new rate
          MOVE      ONE,NOCHANGE           * Don't need to post a new record
          GOTO      UPTRN100               * Get the next record
.
.         Finished looping over transfer file. Check if we have to
.         post a record to allow for a change in rate at the end of the last
.         invoice. We do this by check if a rate change is required, and if
.         so write one with ALACDTE as the date.
.
UPTRN800  BRANCH    NOCHANGE,UPTRN999      * No new record needed
.
          COMPARE   TWO,ASTAT              * Is the patient currently admitted?
          GOTO      UPTRN999 IF NOT EQUAL  * No. Don't need to write a new rec
.
.         Write a new record to set up the change of rate on the first day
.         of the next invoice
.
          MOVE      ALACDTE,TDATE          * use last accomodate date
.
          MATCH     CLACDATE,ALACDTE       * check if 1st charging date needed
          GOTO      UPTRN850 IF NOT LESS
          MOVE      CLACDATE,TDATE         * use 1st date of charging
.
.         Make sure date is not in the future
.
UPTRN850  PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
.
          MATCH     TDATE,XDATE
          GOTO      UPTRN999 IF LESS
.
.         Set up the rest of the record
.
          MOVE      PVIBILL,TADMN          * Set up admission number
          MOVE      "01:00:00",TTIME       * Dummy time value
          MOVE      STWARD,TWARD           * Restore old ward
          MOVE      STBED,TBED             * Restore old bed
.
          MOVE      PVIURNO,TURNO          * Set up U/R Number
          MOVE      ZERO,TDISC             * Set up discount amount
          MOVE      ZERO,TALLW             * Set up allowance amount
          MOVE      ANSC,TMOVE             * Write a change record
          MOVE      STATYPE,TATYPE         * Restore old admission type
          MOVE      STADOCT,TADOCT         * Restore old attending doctor
          MOVE      STDEPT,TDEPT           * Restore old department
          MOVE      STRBTYP,TRBTYP         * Restore old bed type
          MOVE      STRBEND,TRBEND         * Restore old ending bed day range
          MOVE      STCHSTAT,TCHSTAT       * Restore old chargable status
          MOVE      PASSCODE,PTTROPER       * operator Id
.
.         We have a record to be updated. Find the new rate
.
          CALL      NEWR0000               * Get the new rates
          MOVE      SP3,PTTRLTYP            * clear type of leave value
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2                * Write this new record
.
UPTRN999  RETURN
.
.------------------------------------------------------------------------------
.         VLUM0000 - Overnight lumpsum
.------------------------------------------------------------------------------
VLUM0000  MOVE      ZERO,NOFEE
          MOVE      ZERO,PTHLLREB
          MOVE      ZERO,PTHLLPAT
          PACK      KEY21,ACLAIM,AFUNDH,PTHFBCAT,CMDRG
          CALL      RDPTHLF1
          IF        OVRCD = 1
            PACK      KEY21,ACLAIM,SP6,SP3,CMDRG
            CALL      RDPTHLF1
            IF        OVRCD = 1
              READ      CONTROLF,TWENTY;*193,PTCNDCLM
              CALL      DCLM0000
              PACK      KEY21,PTCNDCLM,SP6,SP3,CMDRG
              CALL      RDPTHLF1
              BRANCH    OVRCD,VLUM9000        * no lumpsum charges
            ENDIF
          ENDIF
          GOTO      VLUM9999
.
VLUM9000  MOVE      ONE,NOFEE
.
VLUM9999  RETURN
+
