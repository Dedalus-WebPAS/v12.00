.
. *********************************************************
. *                                                       *
. * Include Name :  PATCOMN3.PBL                          *
. *                                                       *
. * Function     :  Subroutine Include for                *
. *                 Patient standard library              *
. *                                                       *
. * Subroutines  :  GPERD    : Get the period for a date  *
. *                 KEYPER   : Keyin a period (MM/CCYY)   *
. *                                                       *
. * Files needed :  IBACOMM.INC    IBA Special Variables  *
. *                 PATCOMM.INC    Special Variables      *
. *                 PATDRGFD.INC   Date Range File        *
. *                 STD001IO       Standard IBA library   *
. *                                                       *
. * Modifications:  V5.00  08/05/89 Graeme Williams       *
. *                        Include Written                *
. *                 V5.01  04/07/89 Graeme Williams       *
. *                        Added KEYPER, keyin of period  *
. *                 V5.02  02/04/1993  Graeme Williams    *
. *                        Preserve line 24 on errors in  *
. *                        KEYPER                         *
. *                                                       *
. *********************************************************
.
. *****************************************************************************
. * GPERD      : Get the period for a date                                    *
. *                                                                           *
. * Parameters : CPTDATE  Date to determine period for (CCYYMMDD)             *
. * Returns    : EXIT     0 = Period found                                    *
. *                       1 = Date not found in date range file               *
. *              DRGYR    Period year for Date (CCYY)                         *
. *              DRGNUM   Period code for Date (MM)                           *
. *                                                                           *
. *****************************************************************************
.
GPERD     OPEN      PATDRGA2,"patdrgaf" * Open the date range file
          PACK      KEY14,CPTDATE,SP6   * Key for to find this date in file
          CALL      RDSDRGA2            * Position in Date range file
          CALL      RDKDRGA2            * Get the first record from given date
          BRANCH    OVRCD,GPERD900      * No record found in file
.
          MATCH     DRGFDTE,CPTDATE     * Is the given date before this period
          GOTO      GPERD900 IF LESS    * Yes. Date not in file
.
          MOVE      ZERO,EXIT           * Period found
          GOTO      GPERD999            * Return with the period
.
.         The requested date wasn't in the file
.
GPERD900  UNPACK    SP6,DRGYR,DRGNUM    * Blank out period code
          MOVE      ONE,EXIT            * Period record not found
.
GPERD999  CLOSE     PATDRGA2            * Close the date range file
          RETURN
.
. *****************************************************************************
. * KEYPER     : Keyin a period code. This is basically the same as KEYMONTH  *
. *              in IBADATE, except that it allows 13 for the month           *
. *              This subroutine uses subroutines and labels from IBADATE     *
. *                                                                           *
. * Parameters : CVERT    Line for keyin of period code                       *
. *              CCOL     Column for keyin of period code                     *
. *              CHIGHLT  0 = Use highlighting in keyin                       *
. *                       1 = Don't use highlighting in keyin                 *
. *              CCENT    Default period century                              *
. *              CYEAR    Default period year                                 *
. *              CMON     Default period month                                *
. *                                                                           *
. * Returns    : OVRCD    0 = Period entered                                  *
. *                       1 = No period code entered                          *
. *              ICENT    Period century                                      *
. *              IYEAR    Period year                                         *
. *              IMON     Period month                                        *
. *              CCENT    Period century                                      *
. *              CYEAR    Period year                                         *
. *              CMON     Period month                                        *
. *              CQUEST   0 = No question mark entered                        *
. *                       1 = Question mark entered (OVRCD = 1)               *
. *                                                                           *
. *****************************************************************************
.
KEYPER    MOVE       ONE,IDAY
          MOVE       IDAY,XDAY      *** FOR DAY CHECK IN KEYYEAR
          MOVE       CMON,XMON
          MOVE       CYEAR,XYEAR
          MOVE       CCENT,XCENT
          MOVE       ZERO,OVRCD
.
          MATCH      SP2,CMON
          CALL       DSPMONTH IF NOT EQUAL
.
          MATCH      SP2,CMON
          CALL       UDLMONTH IF EQUAL
.
          COMPARE    ZERO,ECOL
          GOTO       C13KMT1 IF NOT EQUAL
          MOVE       "24",EVERT
          MOVE       "50",ECOL
.
C13KMT1   MOVE       CCOL,CCOL2
          MOVE       CCOL,CCOL3
          ADD        THREE,CCOL3
.
K13MTH0   CALL       KEYM13
          BRANCH     OVRCD OF STOPDATE
.
          CALL       KEYYEAR
          BRANCH     OVRCD OF K13MTH0,KEYPER
.
          MOVE       IMON,CMON
          MOVE       IYEAR,CYEAR
          MOVE       ICENT,CCENT
          REP        " 0",CMON
          REP        " 0",CYEAR
          REP        " 0",CCENT
          RETURN
.
.         Key in the Period Code
.
KEYM13    MOVE       ZERO,OVRCD
          MOVE       ZERO,CDCHG
          MOVE       ZERO,CQUEST
          MATCH      SP2,CMON
          GOTO       RETKM13 IF NOT EQUAL
.
.         NEW DATE
.
          DISPLAY    *PCCOL2:CVERT,UNDLN2
.
          BRANCH     CHIGHLT OF KEYM13N
          KEYIN      *PCCOL2:CVERT,*V2LON,*JR,*ZF,*+,XMON;
          GOTO       KEYM13O
.
KEYM13N   KEYIN      *PCCOL2:CVERT,*JR,*ZF,*+,XMON;
KEYM13O   RESET      XMON
          GOTO       OVERCOND IF EOS
.
          MATCH      "0?",XMON
          GOTO       CNOQST IF EQUAL         *** ? OPTION
.
          MOVE       XMON,IMON
          GOTO       CONTM13
.
.         IF A CDAY HAS A VALUE RETAIN VARIABLE  (OLD DATE)
.
RETKM13   MOVE       CMON,XMON
          BRANCH     CHIGHLT OF RETKM13N
          KEYIN      *PCCOL2:CVERT,*V2LON,*ZF,*JR,*RV,*+,XMON;
          GOTO       RETKM13O
.
RETKM13N  KEYIN      *PCCOL2:CVERT,*ZF,*JR,*RV,*+,XMON;
.
RETKM13O  MATCH      CMON,XMON
          GOTO       CNOCHG IF EQUAL
.
          MATCH      "0?",XMON
          GOTO       CNOQST IF EQUAL         *** ? OPTION
.
          MOVE       XMON,IMON
.
CONTM13   COMPARE    ZERO,IMON 
          GOTO       OVERCOND IF EQUAL
.
          COMPARE    ONE,IMON 
          GOTO       INVM13 IF LESS
.
          COMPARE    TEN4,IMON
          GOTO       INVM13 IF NOT LESS
.
          MOVE       IMON,XMON
          REP        " 0",XMON
          BRANCH     CHIGHLT OF CONTM13N
          DISPLAY    *PCCOL2:CVERT,*V2LON,XMON;
          RETURN
.
CONTM13N  DISPLAY    *PCCOL2:CVERT,XMON;
          RETURN
.
INVM13    MOVE      "Invalid Period",DDISPMSG
          CALL      DATEMSGC                 * found in IBADATE.PBL
          GOTO      KEYM13
