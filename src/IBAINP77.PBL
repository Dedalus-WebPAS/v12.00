.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAINP77                                                 *
.* Description    :  Patient With X Days Stay After Operation Report          *
.******************************************************************************
.* Date           :  07/12/1994                                               *
.* Author         :  Greg Horvat                                              *
.* Function       :  This report will list all patients for a given admission *
.*                   or discharge date range who have a certain (or greater)  *
.*                   length of stay after the operation date. This is         *
.*                   calculated from the last operation date.                 *
.* Modifications  :                                                           *
.* V12.00.01 16/05/2025  J.Tan          TSK 0955096                           *
.*           Added alpanumeric visit number generation                        *
.* V11.00.01 12/03/2020  J.Tan          TSK 0838262                           *
.*           Added Effective from and to date to MBS Item file                *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT0000                                      *
.* V10.03.01  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  24/10/2003  J.Tan   CAR 44172                             *
.*                  Mods to use Misc.Theatre item charges file (pmsmtiaf)     *
.*        V9.02.01  04/09/2002  Jill Habasque SRF 17573	                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.08.02  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B03  29/01/1999 Nicole Harrington                             *
.*                   507 rebounds                                             *
.*        V5.07.B02  19/11/1998 Jill Habasque                                 *
.*                   Mods to include century                                  *
.******************************************************************************
.*                   V6.03.B05 02/02/1995 Gabrielle                           *
.*                             Changed Program Name                           *
.*        V6.03.B06 24/02/1995  Greg Horvat      New Release 6.03             *
.*                  Recompiled for PATITMVD - Added PATCKEXC & changed to     *
.*                  display linked MBS code & description                     *
.*        V5.03.00  12/07/1995  Steve O'Gorman                                *
.*                  Ported from release 6.03                                  *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATDO1FD/INC            * Doctors file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATCODFD/INC            * Codes file
          INC       PATDTRFD/INC            * Patient debtors file
          INC       PATMA1FD/INC            * Patient master file
          INC       PATMI1FD/INC            * Patient admission file
          INC       PMSVX1FD/INC            * Patient admission file
          INC       PATTRNFD/INC            * Transfer file
          INC       PATITMFD/INC            * Items    file
          INC       PMSMTIFD/INC            * Misc.Theatre item file
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=148, KEY="U44-51,1-8,9-16,17-19"
TEMP2     IFILE     SQL, FIXED=148, KEY="U62-69,1-8,9-16,17-19"
TEMP3     IFILE     SQL, FIXED=148, KEY="U1-8,9-16,17-19"
.
FILELIN1  INIT      " 148 U1-8,9-16,17-19 U44-51,1-8,9-16,17-19"
FILELIN2  INIT      " 148 U1-8,9-16,17-19 U62-69,1-8,9-16,17-19"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
URNO      DIM       8        8          1       U/R number
ADMISS    DIM       8        8          9       Admission number
TMPCOUNT  FORM      3        3          17      Tempfile counter
PATNAME   DIM       24       24         20      Patient name
ADMDATE   DIM       8        8          44      Admission date (CCYYMMDD)
DSPADDTE  DIM       10       10         52      Display admission date
DSCDATE   DIM       8        8          62      Discharge date (CCYYMMDD)
DSPDSDTE  DIM       10       10         70      Display discharge date
ADOCTOR   DIM       6        6          80      Attending doctor
ADOCTORD  DIM       20       20         86      Attending doctor description
LOSTHEA   FORM      5        4         106      Length of stay in theatre
DSPTHDTE  DIM       10       10        110      Display theatre item date
THETDESC  DIM       28       28        120      Theatre item description
.
.End of record                         148
.
. ----- Program Variables -----
.
BLNKLINE  FORM      1                       * Print a blank line flag
.
CALDAYS   FORM      5                       * For NHOSPDAY
CMDLINE   DIM       80
CURDATE   DIM       8                       * Current date (CCYYMMDD)
.
DIM9      DIM       9
DIM20     DIM       20
DIM1A     DIM       1
DSPEDDTE  DIM       10                      * Display ending date
DSPSTDTE  DIM       10                      * Display starting date
.
ENDDATE   DIM       8                       * Ending date (CCYYMMDD)
.
FNAME     DIM       8                       * Tempfile name
FROMDATE  DIM       8                       * For NHOSPDAY
.
JYEAR     FORM      2                       * For NHOSPDAY
LOSDAYS   FORM      2                       * Length of stay days requested
NBRDAYS   FORM      5                       * For NHOSPDAY
PATCOUNT  FORM      3                       * Patient counter for report
STRTDATE  DIM       8                       * Starting date (CCYYMMDD)
STRTOPER  DIM       9
ENDOPER   DIM       9
DSPSOPER  DIM       80
DSPEOPER  DIM       80
.
RTDAYS    FORM      4
SKEY30    DIM       30
THEADATE  DIM       8                       * Theatre date
TODATE    DIM       8                       * For NHOSPDAY
.
WDATE1    DIM       8                       * For NHOSPDAY
WDATE2    DIM       8                       * For NHOSPDAY
.
YESTDATE  DIM       8                       * Yesterday's date
.
. ----- Program Constants -----
.
OYEAR     FORM      "365"
.
SP80      INIT      "                                        ":
                    "                                        "
.
ZED22     INIT      "zzzzzzzzzzzzzzzzzzzzzz"
Z9        INIT      "zzzzzzzzz"
FINISH    INIT      "Finish"
START     INIT      "Start"
.
PRGID     INIT      "IBAINP77"
PRGNAM    INIT      "Patients With X Days Stay After Operation Report"
VERSION   INIT      "V12.00.01"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Choose option
          BRANCH    EXIT,ML9000
          BRANCH    OPTION,ML2000,ML5000
.                          Admin, Disch
.
. ----- By Admission Date Range -----
.
ML2000    CALL      ADMD0000                * Keyin admission date
          BRANCH    EXIT,ML1000
.
ML2500    CALL      OPER0000                * Keyin operation range
          BRANCH    EXIT,ML2000
.
ML3000    CALL      LOSA0000                * Keyin the LOS after the operation
          BRANCH    EXIT,ML2500
.
ML4000    CALL      BLNK0000                * Print a blank line (Y/N) ?
          BRANCH    EXIT,ML3000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4500,ML4000,ML1000
.                         Yes    No     Cancel
.
ML4500    CALL      LADM0000                * Load admission data
          CALL      PADM0000                * Print admission data
          GOTO      ML1000
.
. ----- By Discharge Date Range -----
.
ML5000    CALL      DSCD0000                * Keyin discharge date
          BRANCH    EXIT,ML1000
.
ML5500    CALL      OPER0000                * Keyin operation range
          BRANCH    EXIT,ML5000
.
ML6000    CALL      LOSA0000                * Keyin the LOS after the operation
          BRANCH    EXIT,ML5500
.
ML7000    CALL      BLNK0000                * Print a blank line (Y/N) ?
          BRANCH    EXIT,ML6000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML7500,ML7000,ML1000
.                         Yes    No     Cancel
.
ML7500    CALL      LDSC0000                * Load discharge data
          CALL      PDSC0000                * Print discharge data
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * Delete tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA2,"pmsmtiaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
.
. ----- Setup Current & Yesterdays Dates -----
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          MOVE      CCC,CC
          MOVE      CYY,YY
          MOVE      CMM,MM
          MOVE      CDD,DD
          CALL      DMYCON
.
          MOVE      JULDAY,FORM4
          SUB       OYEAR,FORM4
          SUB       LEAPYR,FORM4
          COMPARE   ZERO,FORM4
          GOTO      INIT1000 IF LESS
.
          COMPARE   ZERO,JULYR
          IF        @EQUAL
            MOVE      "99",JULYR
            SUB       ONE,JULCC
          ELSE
            SUB       ONE,JULYR
          ENDIF
          MOVE      ZERO,JULDAY
INIT1000  SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      YESTDATE,CC,YY,MM,DD
          REP       " 0",YESTDATE
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                               Choose Option                                *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P67:2,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Admission Date":
                    *P1:6,*V2LON,"2",*HOFF," = By Discharge Date":
                    *P1:8,*HOFF,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
          BRANCH    OPTION,OPTN9999,OPTN99999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 ADMD0000               Called by: ML0000   *
.*                         Keyin The Admission Date                           *
.******************************************************************************
ADMD0000  MOVE      ZERO,EXIT
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
ADMD1000  DISPLAY   *P1:10,*EF,"Starting Admission Date : ":
                    *P1:11,*EL,"Ending   Admission Date : ";
          MOVE      "10",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,ADMD9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
. ----- Check If Starting Date Is Greater Tha Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      ADMD1000
          ENDIF
.
. ----- Enter Ending Date -----
.
ADMD2000  MOVE      "11",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,ADMD1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
. ----- Check If Ending Date Is Greater Tha Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      ADMD2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      ADMD2000
          ENDIF
.
          GOTO      ADMD9999
.
ADMD9000  MOVE      ONE,EXIT
ADMD9999  RETURN
+
.******************************************************************************
.*                                 OPER0000               Called by: ML0000   *
.*                        Keyin Operation Type Range                          *
.******************************************************************************
OPER0000  
          DISPLAY   *P1:13,*EF,"Starting Theatre Item   : ":
                    *P1:14,*EL,"Ending   Theatre Item   : ";
          MOVE      "13",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          CALL      KEYINMBS
          BRANCH    EXIT,OPER3000,OPER9500
.
          DISPLAY   *P27:13,*EL,*V2LON,AMBS,*P40:13,*HOFF,IDESC
          MOVE      AMBS,STRTOPER  
          PACK      DSPSOPER,STRTOPER,SP2,IDESC
          GOTO      OPER5000
.
OPER3000  MOVE      SP9,STRTOPER
          DISPLAY   *P27:13,*EL,*V2LON,START  
          PACK      DSPSOPER,START
.          
OPER5000  MOVE      "14",EVERT
          MOVE      "27",ECOL 
          MOVE      SP30,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          CALL      KEYINMBS
          BRANCH    EXIT,OPER7000,OPER9500
.
          DISPLAY   *P27:14,*EL,*V2LON,AMBS,*P40:14,*HOFF,IDESC
          MOVE      AMBS,ENDOPER  
          PACK      DSPEOPER,ENDOPER,SP2,IDESC
          GOTO      OPER8000
.
OPER7000  MOVE      Z9,ENDOPER
          DISPLAY   *P27:14,*EL,*V2LON,FINISH
          PACK      DSPEOPER,FINISH
          GOTO      OPER9000
.          
OPER8000  
.
. ----- Check That The Ending Operation is Greater Than The Starting Op. -----
.
          MATCH     STRTOPER,ENDOPER
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending operation must be greater than ":
                      "the starting operation. ";
            CALL      HITENTER
            GOTO      OPER0000
          ENDIF
.
.
OPER9000  MOVE     ZERO,EXIT
          GOTO     OPER9999
.
OPER9500  MOVE     ONE,EXIT
          GOTO     OPER9999
.
OPER9999  RETURN
+
.******************************************************************************
.*                                 LADM0000               Called by: ML0000   *
.*                        Load The Admission Details                          *
.******************************************************************************
LADM0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TMPCOUNT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Loading : ";
          CALL      OPEN0000                * Open the tempfile
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS3                * Position on the admission file
LADM1000  CALL      RDKMISS3                * Read the admission file
          BRANCH    OVRCD,LADM9999
.
          MATCH     ADATE,ENDDATE
          GOTO      LADM9999 IF LESS        * Out of date range, exit
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      LADM1000              * Ignore pre-admitted or cancelled
          ENDIF                               patients
.
          DISPLAY   *P12:24,*V2LON,AADMNO;
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LADM1000
.
          PACK      KEY22,AADMNO,ZED22
          CALL      RDSDTRN1                * Position on the DTR file
LADM2000  CALL      RDPDTRN1                * Read the DTR file
          BRANCH    OVRCD,LADM3000
.
          MATCH     AADMNO,TADMNO
          GOTO      LADM3000 IF NOT EQUAL   * Different admin, get next admin
.
          COMPARE   TWO,TRECTYPE
          GOTO      LADM2000 IF NOT EQUAL   * Not a theatre item, read next
.
. ------- check the theatre item  
.
          MATCH     SP9,STRTOPER
          IF        !@EQUAL
            MATCH     STRTOPER,TITEMNO
            GOTO      LADM2000 IF LESS
          ENDIF  
.
          MATCH     Z9,ENDOPER
          IF        !@EQUAL
            MATCH     TITEMNO,ENDOPER
            GOTO      LADM2000 IF LESS
          ENDIF
.
          PACK      THEADATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",THEADATE
          GOTO      LADM8000
.
.         Check for items in Misc.Theatre item file (pmsmtiaf)
.
LADM3000  MOVE      "2",PMMIRTYP
          PACK      KEY15,AADMNO,PMMIRTYP,ZED22
          CALL      RSPMMTI2                * Position on pmsmtiaf file
LADM4000  CALL      RPPMMTI2                * Read pmsmtiaf file
          BRANCH    OVRCD,LADM1000
          MATCH     PMMIVISN,DAADMNO
          GOTO      LADM1000 IF NOT EQUAL
          MATCH     "2",PMMIRTYP
          GOTO      LADM1000 IF NOT EQUAL   * Not theatre item
.
. ------- check the theatre item  
.
          MATCH     SP9,STRTOPER
          IF        !@EQUAL
            MATCH     STRTOPER,PMMIITEM
            GOTO      LADM4000 IF LESS
          ENDIF  
.
          MATCH     Z9,ENDOPER
          IF        !@EQUAL
            MATCH     PMMIITEM,ENDOPER
            GOTO      LADM4000 IF LESS
          ENDIF
.
          MOVE      PMMITDAT,THEADATE
          REP       " 0",THEADATE
          MOVE      PMMIITEM,TITEMNO
          MOVE      PMMIDESC,TDDESC
.
LADM8000  MOVE      SP8,DDATE
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Read the discharge file
          IF        OVRCD=1
            MOVE      YESTDATE,TODATE       * Use yesterday's date
          ELSE
            MOVE      DDATE,TODATE          * Use discharge date
          ENDIF
          MOVE      THEADATE,FROMDATE
          CALL      RTIODAYS                * Calculate no. of days in hospital
          MOVE      NBRDAYS,LOSTHEA
          IF        LOSTHEA<LOSDAYS
            GOTO      LADM1000              * No. of days in theatre is less
          ENDIF                               than the days requested
.
          CALL      CLER0000                * Clear the tempfile variables
          MOVE      PURNO,URNO
          MOVE      AADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format admission date
          MOVE      CPDATE,DSPADDTE
          MATCH     SP8,DDATE
          IF        !@EQUAL
            MOVE      DDATE,DSCDATE
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE               * Format discharge date
            MOVE      CPDATE,DSPDSDTE
          ENDIF
          MOVE      ADOCTA,ADOCTOR
          PACK      KEY6,ADOCTOR
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ADOCTORD
          ENDIF
          UNPACK    THEADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format theatre date
          MOVE      CPDATE,DSPTHDTE
.
          CLEAR     THETDESC
          APPEND    TITEMNO,THETDESC
          APPEND    SP1,THETDESC
          APPEND    TDDESC,THETDESC
          APPEND    SP30,THETDESC
          RESET     THETDESC
          PACK      KEY15,URNO,ADMISS,TMPCOUNT
          CALL      WRTEMP3                 * Write to the 3rd tempfile
          DISPLAY   *P35:24,*V2LON,ADMISS;
          GOTO      LADM1000
.
LADM9999  RETURN
+
.******************************************************************************
.*                                 PADM0000               Called by: ML0000   *
.*                             Print The Report                               *
.******************************************************************************
PADM0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000                * Print the report heading
          PACK      KEY25,SP30
          CALL      RSTEMP1                 * Position on the 1st tempfile
          CALL      RKTEMP1                 * Read the 1st tempfile
          BRANCH    OVRCD,PADM9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY25,SP30
          CALL      RSTEMP1                 * Position on the 1st tempfile
PADM1000  CALL      RKTEMP1                 * Read the 1st tempfile
          BRANCH    OVRCD,PADM9000
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the report heading
            CALL      UND132                * Print an underline
          ENDIF
.
          SETLPTR   ADOCTORD,18
          UNPACK    DSPADDTE,KEY8
          PRINT     *1,"|",*2,URNO,*10,"|",*11,ADMISS,*19,"|",*20,PATNAME:
                    *44,"|",*45,KEY8;
.
          UNPACK    DSPDSDTE,KEY8
          PRINT     *53,"|",*54,KEY8:
                    *62,"|",*63,ADOCTOR,*70,*+,ADOCTORD,*-,*88,"|",*89,LOSTHEA:
                    *94,"|",*95,DSPTHDTE,*103,"|",*104,THETDESC,*132,"|"
          ADD       ONE,CLNO
          CALL      PBLK0000                * Print a blank line
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      PADM1000
.
PADM9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PADM9999  RETURN
+
.******************************************************************************
.*                                 DSCD0000               Called by: ML0000   *
.*                         Keyin The Discharge Date                           *
.******************************************************************************
DSCD0000  MOVE      ZERO,EXIT
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DSCD1000  DISPLAY   *P1:10,*EF,"Starting Discharge Date : ":
                    *P1:11,*EL,"Ending   Discharge Date : ";
          MOVE      "10",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DSCD9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
. ----- Check If Starting Date Is Greater Tha Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DSCD2000  MOVE      "11",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DSCD1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
. ----- Check If Ending Date Is Greater Tha Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check If Discharge Date Range Exists -----
.
          PACK      KEY16,STRTDATE,SP30
          CALL      RDSDSCH2                * Position on discharge file
          CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,DSCD3000
.
          MATCH     DDATE,ENDDATE
          GOTO      DSCD9999 IF NOT LESS    * Patients found in date range, exit
.
DSCD3000  DISPLAY   *P1:24,*EL,*B,"No discharged patients found in the ":
                    "date range. ";
          CALL      HITENTER
          GOTO      DSCD1000
.
DSCD9000  MOVE      ONE,EXIT
DSCD9999  RETURN
+
.******************************************************************************
.*                                 LDSC0000               Called by: ML0000   *
.*                        Load The Admission Details                          *
.******************************************************************************
LDSC0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TMPCOUNT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Loading : ";
          CALL      OPEN0000                * Open the tempfile
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on the discharge file
LDSC1000  CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,LDSC9999
.
          MATCH     DDATE,ENDDATE
          GOTO      LDSC9999 IF LESS        * Out of date range, exit
.
          DISPLAY   *P12:24,*V2LON,DADMNO;
          PACK      KEY8,DURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LDSC1000
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LDSC1000
.
          COMPARE   "3",ASTAT
          GOTO      LDSC1000 IF NOT EQUAL   * Patient not discharge, get next
.
          PACK      KEY22,DADMNO,ZED22
          CALL      RDSDTRN1                * Position on the DTR file
LDSC2000  CALL      RDPDTRN1                * Read the DTR file
          BRANCH    OVRCD,LDSC3000
.
          MATCH     DADMNO,TADMNO
          GOTO      LDSC3000 IF NOT EQUAL   * Different admin, get next admin
.
          COMPARE   TWO,TRECTYPE
          GOTO      LDSC2000 IF NOT EQUAL   * Not a theatre item, read next
.
. ------- check the theatre item  
.
          MATCH     SP9,STRTOPER
          IF        !@EQUAL
            MATCH     STRTOPER,TITEMNO
            GOTO      LDSC2000 IF LESS
          ENDIF  
.
          MATCH     Z9,ENDOPER
          IF        !@EQUAL
            MATCH     TITEMNO,ENDOPER
            GOTO      LDSC2000 IF LESS
          ENDIF
.
          PACK      THEADATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",THEADATE
          GOTO      LDSC8000
.
.         Check for items in Misc.Theatre item file (pmsmtiaf)
.
LDSC3000  MOVE      "2",PMMIRTYP
          PACK      KEY15,DADMNO,PMMIRTYP,ZED22
          CALL      RSPMMTI2                * Position on pmsmtiaf file
LDSC4000  CALL      RPPMMTI2                * Read pmsmtiaf file
          BRANCH    OVRCD,LDSC1000
          MATCH     DDADMNO,PMMIVISN
          GOTO      LDSC1000 IF NOT EQUAL   * Different admin, get next admin
          MATCH     "2",PMMIRTYP
          GOTO      LDSC1000 IF NOT EQUAL   * Not a theatre item, read next
.
. ------- check the theatre item  
.
          MATCH     SP9,STRTOPER
          IF        !@EQUAL
            MATCH     STRTOPER,PMMIITEM
            GOTO      LDSC4000 IF LESS
          ENDIF  
.
          MATCH     Z9,ENDOPER
          IF        !@EQUAL
            MATCH     PMMIITEM,ENDOPER
            GOTO      LDSC4000 IF LESS
          ENDIF
.
          MOVE      PMMITDAT,THEADATE
          REP       " 0",THEADATE
          MOVE      PMMIITEM,TITEMNO
          MOVE      PMMIDESC,TDDESC
.
LDSC8000  MOVE      THEADATE,FROMDATE
          MOVE      DDATE,TODATE
          CALL      NHOSPDAY                * Calculate no. of days in hospital
          MOVE      NBRDAYS,LOSTHEA
          IF        LOSTHEA<LOSDAYS
            GOTO      LDSC1000              * No. of days in theatre is less
          ENDIF                               than the days requested
.
          CALL      CLER0000                * Clear the tempfile variables
          MOVE      PURNO,URNO
          MOVE      DADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format admission date
          MOVE      CPDATE,DSPADDTE
          MOVE      DDATE,DSCDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format discharge date
          MOVE      CPDATE,DSPDSDTE
          MOVE      ADOCTA,ADOCTOR
          PACK      KEY6,ADOCTOR
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ADOCTORD
          ENDIF
          UNPACK    THEADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format theatre date
          MOVE      CPDATE,DSPTHDTE
          CLEAR     THETDESC
          APPEND    TITEMNO,THETDESC
          APPEND    SP1,THETDESC
          APPEND    TDDESC,THETDESC
          APPEND    SP30,THETDESC
          RESET     THETDESC
          PACK      KEY19,URNO,ADMISS,TMPCOUNT
          CALL      WRTEMP3                 * Write to the 3rd tempfile
          DISPLAY   *P35:24,*V2LON,ADMISS;
          GOTO      LDSC1000
.
LDSC9999  RETURN
+
.******************************************************************************
.*                                 PDSC0000               Called by: ML0000   *
.*                             Print The Report                               *
.******************************************************************************
PDSC0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000                * Print the report heading
          PACK      KEY25,SP30
          CALL      RSTEMP2                 * Position on the 2nd tempfile
          CALL      RKTEMP2                 * Read the 2nd tempfile
          BRANCH    OVRCD,PDSC9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY25,SP30
          CALL      RSTEMP2                 * Position on the 2nd tempfile
PDSC1000  CALL      RKTEMP2                 * Read the 2nd tempfile
          BRANCH    OVRCD,PDSC9000
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the report heading
            CALL      UND132                * Print an underline
          ENDIF
.
          SETLPTR   ADOCTORD,18
          UNPACK    DSPADDTE,KEY8
          PRINT     *1,"|",*2,URNO,*10,"|",*11,ADMISS,*19,"|",*20,PATNAME:
                    *44,"|",*45,KEY8;
.
          UNPACK    DSPDSDTE,KEY8
          PRINT     *53,"|",*54,KEY8:
                    *62,"|",*63,ADOCTOR,*70,*+,ADOCTORD,*-,*88,"|",*89,LOSTHEA:
                    *94,"|",*95,DSPTHDTE,*103,"|",*104,THETDESC,*132,"|"
          ADD       ONE,CLNO
          CALL      PBLK0000                * Print a blank line
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,ADMISS;
          GOTO      PDSC1000
.
PDSC9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PDSC9999  RETURN
+
.******************************************************************************
.*                                 LOSA0000               Called by: ML0000   *
.*                   Keyin Length Of Stay After Operation                     *
.******************************************************************************
LOSA0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:16,*EF,"Length of Stay After Operation Date : ";
LOSA1000  MOVE      "5",LOSDAYS
          KEYIN     *P39:16,*V2LON,*RV,LOSDAYS;
.
          COMPARE   ZERO,LOSDAYS
          GOTO      LOSA9000 IF EQUAL
.
          IF        LOSDAYS<1 | LOSDAYS>99
            BEEP
            GOTO      LOSA1000
          ENDIF
          GOTO      LOSA9999
.
LOSA9000  MOVE      ONE,EXIT
LOSA9999  RETURN
+
.******************************************************************************
.*                                 BLNK0000               Called by: ML0000   *
.*                         Print A Blank Line (Y/N) ?                         *
.******************************************************************************
BLNK0000  MOVE      ZERO,EXIT
          MOVE      ZERO,BLNKLINE
          DISPLAY   *P1:18,*EL,"Print a blank line between each line (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
BLNK1000  KEYIN     *P46:18,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      BLNK9000 IF EQUAL       * Exit from question
.
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,BLNKLINE          * Blank line
            GOTO      BLNK9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,BLNKLINE         * No blank line
            GOTO      BLNK9999
          ENDIF
.
          BEEP
          GOTO      BLNK1000
.
BLNK9000  MOVE      ONE,EXIT
BLNK9999  RETURN
+
.******************************************************************************
.*                                 CLER0000               Called by: LADM0000 *
.*                       Clear The Tempfile Variables              & LDSC0000 *
.******************************************************************************
CLER0000  UNPACK    SP80,URNO,ADMISS,PATNAME,ADMDATE,DSPADDTE,DSCDATE
          UNPACK    SP80,DSPDSDTE,ADOCTOR,ADOCTORD,DSPTHDTE
          UNPACK    SP30,THETDESC
CLER9999  RETURN
+
.******************************************************************************
.*                                 HEAD0000               Called by: PADM0000 *
.*                         Print The Report Heading                & PDSC0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print main report heading
          IF        OPTION=1
            MOVE      "Admission",DIM9
          ELSE
            MOVE      "Discharge",DIM9
          ENDIF
          PRINT     *40,"Starting ",DIM9," Date : ",DSPSTDTE,*N:
                    *40,"Ending   ",DIM9," Date : ",DSPEDDTE,*N,*N:
                    *40,"Starting Theatre Item   : ",DSPSOPER,*N:
                    *40,"Ending   Theatre Item   : ",DSPEOPER,*N,*N:
                    *40,"Length of Stay After Operation Date : ",LOSDAYS,*N
          ADD       "8",CLNO
          CALL      UND132                  * Print an underline
          PRINT     *1,"|U/R No",*10,"|Admn No",*19,"|Patient Name":
                    *44,"|Adm Date",*53,"|Dsc Date",*62,"|Attending Doctor":
                    *88,"|LOS",*94,"|ItemDate",*103,"|Theatre Item",*132,"|"
          ADD       "1",CLNO
HEAD9999  RETURN
+
.******************************************************************************
.*                                 PBLK0000               Called by: PADM0000 *
.*                            Print A Blank Line                   & PDSC0000 *
.******************************************************************************
PBLK0000  IF        BLNKLINE=1
            PRINT     *1,"|",*10,"|",*19,"|",*44,"|",*53,"|",*62,"|":
                      *88,"|",*94,"|",*103,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
PBLK9999  RETURN
+
.******************************************************************************
.*                                 OPEN0000               Called by: LADM0000 *
.*                             Open The Tempfile                   & LDSC0000 *
.******************************************************************************
OPEN0000  CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          BRANCH    OPTION,OPEN1000,OPEN2000
.                          Admin,   Disch
.
OPEN1000  APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP3,FNAME
          GOTO      OPEN9999
.
OPEN2000  APPEND    FILELIN2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP2,FNAME
          OPEN      TEMP3,FNAME
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
          CLOSE     TEMP3
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Delete the tempfile
KILL9999  RETURN
+
.******************************************************************************
.*                        I/O Rountines For The Tempfile                      *
.******************************************************************************
RSTEMP1   RESET     KEY25
          READ      TEMP1,KEY25;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;URNO,ADMISS,TMPCOUNT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,ADOCTOR,ADOCTORD,LOSTHEA,DSPTHDTE:
                          THETDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP2   RESET     KEY25
          READ      TEMP2,KEY25;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;URNO,ADMISS,TMPCOUNT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,ADOCTOR,ADOCTORD,LOSTHEA,DSPTHDTE:
                          THETDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP3   RESET     KEY19
          WRITE     TEMP3,KEY19;URNO,ADMISS,TMPCOUNT,PATNAME,ADMDATE,DSPADDTE:
                                DSCDATE,DSPDSDTE,ADOCTOR,ADOCTORD,LOSTHEA:
                                DSPTHDTE,THETDESC
          RETURN
.
.==============================================================================
.
          INC       STD001IO
.
          INC       PATDO1IO/INC            * Doctors file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATDTRIO/INC            * Patient debtors file
          INC       PATMA1IO/INC            * Patient master file
          INC       PATMI1IO/INC            * Patient admission file
          INC       PMSVX1IO/INC            * Patient admission file
          INC       PATTRNIO/INC            * Transfer file
          INC       PATITMIO/INC            * Items    file
          INC       PMSMTIIO/INC            * Misc.Theatre item file
.
          INC       PATITMRD
          INC       KEYINMBS                * MBS Keyin
          INC       NHOSPDAY                * Calculate no. of days in hospital
          INC       RTIODAYS                * Calculate no. of days in hospital
          INC       PATDSITM                * MBS ?   
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
