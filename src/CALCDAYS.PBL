.******************************************************************************
.*                                                                            *
.*                                  CALCDAYS                                  *
.*                                  --------                                  *
.*                                                                            *
.*    Include to calculate the number of days between 2 dates inclusive,      *
.*    allowing the dates to be any number of years apart and taking into      *
.*    account leap years.                                                     *
.*                                                                            *
.*    AUTHOR           : DIG                                                  *
.*                                                                            *
.*    DATE             : 23/05/91                                             *
.*                                                                            *
.*    VARIABLES NEEDED : CDYSDAYS   FORM     6   Number of days calculated    *
.*                       CDYSFDTE   DIM      8   (CCYYMMDD) From Date         *
.*                       CDYSTDTE   DIM      8   (CCYYMMDD) To   Date         *
.*                       CDYSYEAR   FORM     2   Work Year variable           *
.*                                                                            *
.*    FILES NEEDED     : STD001FD + STDIO001                                  *
.*                                                                            *
.*    PARAMETERS       : CDYSFDTE - From date for calculation                 *
.*                       CDYSTDTE - To   date for calculation                 *
.*                                                                            *
.*    RETURNS          : CDYSDAYS - Number of days between the 2 dates        *
.*                                  inclusive. N.B. You must subtract one     *
.*                                  from this variable if you only want the   *
.*                                  difference between the 2 dates.           *
.*                                                                            *
.******************************************************************************
.* Mods     : V6.06     18/01/1999  Jill Habasque                             *
.*                      Changed to use century                                *
.*            V5.07     06/10/98  Jim Stathopoulos                            *
.*                      507 Changes                                           *
.*                                                                            *
.******************************************************************************
CALCDAYS  UNPACK    CDYSTDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                  * convert to julien date
.
          MOVE      JULDAY,CDYSDAYS
          MOVE      JULYR,CDYSYEAR          
          PACK      C4YEAR,JULCC,JULYR        * move to date CCYY to a form
          REP       " 0",C4YEAR
          MOVE      C4YEAR,FORM4
          PACK      C4YEAR,SP8
.
          UNPACK    CDYSFDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                  * convert to julien date
.
.------ compare the years ------
.
CALDY050  PACK      C4YEAR,JULCC,JULYR      * move from date CCYY to a form
          REP       " 0",C4YEAR
          MOVE      C4YEAR,FORM4A
.
CALDY100  COMPARE   FORM4A,FORM4             * see if the years are the same
          GOTO      CALDY250 IF EQUAL
.
          ADD       "365",CDYSDAYS          * add a year
          ADD       LEAPYR,CDYSDAYS         * add leap year
.
          ADD       ONE,FORM4A              * increment the start year
          ADD       ONE,JULYR     
.
          MOVE      "0",LEAPYR              * see if the next year is a leap
          MOVE      JULYR,JULWK               year
          DIVIDE    FOUR,JULWK
          MULT      FOUR,JULWK
.
          COMPARE   JULWK,JULYR             * if we end up with the same year
          GOTO      CALDY100 IF NOT EQUAL     then we have a leap year
.
          MOVE      ONE,LEAPYR
.
          GOTO      CALDY100
.
.------ our years are now the same so simply subtract the from date from -----
.------ the total and add one as we want the start and end days inclusive -----
.
CALDY250  SUB       JULDAY,CDYSDAYS
          ADD       ONE,CDYSDAYS
.
CALDY999  RETURN
+
