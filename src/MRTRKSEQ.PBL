.******************************************************************************
.* System         : Medical Records                                           *
.* Include        : MRTRKSEQ.PBL                                              *
.* Name           : Medical Records Tracking Sort Order                       *
.******************************************************************************
.* Date           : 16/05/2011                                                *
.* Author         : Mike Laming                                               *
.* Function       : Routines to read required Sort Sequence for the Hospital  *
.*                  and to apply that sequence to the List selected.          *
.* Modifications  :                                                           *
.*        V10.02.00 17/05/2011  Mike Laming   CAR 240716                      *
.*                  New - 2 routines to establish correcking Sort Order -     *
.*                  includes the old CTYPEUR logic if new parameters are blank*
.******************************************************************************
.
GTSRTIND  MOVE      SP1,SORTORDN
.
          READ      CONTROLF,NINTY7;*175,MRCNSRTO
          MOVE      MRCNSRTO,SORTORDN       * set Sort Order No. from Parameter
.                                           * If multi hospital
          IF        IBCNMHOS=1
            PACK      KEY3,WBSEHOSP,SP70    * Use "USERID" info for hospital
            CALL      RDPTHSP1              * get Sort Order from Hospital recd
            IF        OVRCD <> 1
              MOVE      PTHSTORD,SORTORDN
            ENDIF
          ENDIF
.
GTSRTI99  RETURN
.
.******************************************************************************
.       <option value="0">Straight Number (default)</option>
.       <option value="1">Terminal Digit . . (7-8,5-6,2-4)</option>
.       <option value="2">Middle Digit . . . (5-6,2-4,7-8)</option>
.       <option value="3">Original Terminal .(7-8,5-6,1-4)</option>
.       <option value="4">Original Default . (6-7,1-5,8)</option>
.******************************************************************************
MRSRTORD  MATCH     SP1,SORTORDN
          GOTO      MRSRT800 IF EQUAL       * use old routine if SORTORDN=blank
.
          MOVE      ZERO,F1
          MOVE      SORTORDN,F1
.                                           * SORTORDR contains a UR Number
          BRANCH    F1,MRSRT100,MRSRT200,MRSRT300,MRSRT400
.
          GOTO      MRSRT999                * use the UR No. as sort sequence
.
MRSRT100  UNPACK    SORTORDR,T1A,T3A,T2A,T2B
          PACK      SORTORDR,SP1,T2B,T2A,T3A     * (7-8,5-6,2-4)
          GOTO      MRSRT999
.
MRSRT200  UNPACK    SORTORDR,T1A,T3A,T2A,T2B
          PACK      SORTORDR,SP1,T2A,T3A,T2B     * (5-6,2-4,7-8)
          GOTO      MRSRT999
.
MRSRT300  UNPACK    SORTORDR,T4A,T2A,T2B
          PACK      SORTORDR,T2B,T2A,T4A         * (7-8,5-6,1-4)
          GOTO      MRSRT999
.
MRSRT400  UNPACK    SORTORDR,T5A,T2A,T1A
          PACK      SORTORDR,T2A,T5A,T1A         * (6-7,1-5,8)
          GOTO      MRSRT999
.
.                                           * use old logic if SORTORDN=blank
MRSRT800  IF        CTYPEUR = 4
            UNPACK    SORTORDR,T5A,T2A,T1A
            PACK      SORTORDR,T2A,T5A,T1A         * (6-7,1-5,8)
          ELSE
            UNPACK    SORTORDR,T4A,T2A,T2B
            PACK      SORTORDR,T2B,T2A,T4A         * (7-8,5-6,1-4)
          ENDIF
.
MRSRT999  RETURN
.******************************************************************************
