.**************************************************************************
.*   Program Name : OPPRTREQ  -  Print Theatre Requisitions               *
.*                  Need to Read OPRDETAF, OPRSESAF, PATMA1AF             *
.*                                                                        *
.*   Author       : ROD                                                   *
.*   Date         : 18/02/91                                              *
.*   Mod's        :                                                       *
.**************************************************************************
.* V11.03.01 31/01/2023  Thanh T.           TSK 0919770                   *
.*           Changed GDESC400, GDESC200, PITEM100 to cater for adding     *
.*           Hospital field                                               *
.**************************************************************************
.*        V10.15.01 10/12/2019  Peter Vela  Task 0871644                  *
.*                  Added Surgical Preferences functionality              *
.*        V9.04.01  09/03/2005  Lina Vo     CAR 56404                     *
.*                  Re-wrote routine to be used by OPRWEB06.              *
.*        V5.01.02  28/10/1992  Graeme Williams                           *
.*                  Print operation description in heading                *
.*        V5.01.03  12/04/1994  Rob Leonard   Taranaki Mod (3.2.2)        *
.*                  Allow Aneasthetist as well as Surgeon (OPCNANTH)      *
.*        V5.01.04  06/09/1994 Max White      Taranaki Mods               *
.*                  Print Tray Contents on Theatre Requisition Prints     *
.*                  if control file parameter OPCNTREQ is set.            *
.*        V5.01.05  12/09/1994 Max White      Hawkes Bay Mods             *
.*                  Print Requisition details by Surgeon & Anaesthetist.  *
.*                                                                        *
.**************************************************************************
.*                                                                        *
.*   Parameters   : ODDAUNIQ - Unique Number                              *
.*                  TEAMNUMB - Team Number                                *
.*                                                                        *
.*   Returns      : None                                                  *
.**************************************************************************
OPPRTREQ  CALL      IBACLOCK
          PACK      CDATE,CDD,SLASH,CMM,SLASH,CCC,CYY
          REP       " 0",CDATE
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
.
          MOVE      ZERO,FIRSTITM   * set print first item flag false
          MOVE      ZERO,FIRSTEQP   * set print first equipment flag false
          MOVE      ZERO,GRNDITEM   * Total quantity of items
          MOVE      ZERO,GRNDTRAY   * Total quantity of trays
          MOVE      ZERO,TTYPTRAY   * Total type of trays
          MOVE      ZERO,TPERTRAY   * Total items per tray
          MOVE      ZERO,TOTLITEM   * Total for items only 
          MOVE      ZERO,GRNDEQPM   * Total for Equipment 
.
          CALL      GHEDDT00
.
          MOVE      OPDAUNIQ,OPRQUNIQ
          MOVE      TEAMNUMB,OPRQTEAM
          PACK      KEY36,OPRQUNIQ,OPRQTEAM,ZERO,SP30
          CALL      RSOPREQ3
PRREQ100  CALL      RKOPREQ3                      * get next record
          BRANCH    OVRCD,PRREQ900                * end of requisition file ?
.
.         Initial page headings
.
          IF        CPAGENO=0
            CALL      PRTHED00 
          ENDIF
.
          MATCH     OPDAUNIQ,OPRQUNIQ             * still same unique Id ?
          GOTO      PRREQ900 IF NOT EQUAL
.
          MATCH     TEAMNUMB,OPRQTEAM             * still same Team no. ?
          GOTO      PRREQ900 IF NOT EQUAL
.
.
. Print Item details, first check if we need a new page
.
          IF        CLNO>55
            CALL      PRTHED00                    * print standard headings
            IF        FIRSTITM=1 | FIRSTEQP=1
              CALL      PTHED000                  * print table headings
            ENDIF
          ENDIF
.
          MATCH     "0",OPRQTYPE
          IF        @EQUAL 
            CALL      PTHED000                    * print table headings
            CALL      GDESC000
            PRINT     *1,"| ",OPRQITEM:
                      *19,"| ",OPITDESC:
                      *77,"| Tray":
                      *89,"| ",TDESC:
                      *117,"|    ",OPRQAMNT:
                      *132,"|"
            ADD       ONE,CLNO
            ADD       OPRQAMNT,GRNDTRAY
            ADD       ONE,TTYPTRAY
            CALL      PITEM000           
          ENDIF
.
          MATCH     "1",OPRQTYPE
          IF        @EQUAL
            IF        FIRSTITM=0
              CALL      PTHED000                  * print table headings
              MOVE      ONE,FIRSTITM
            ENDIF
            CALL    GDESC000
            PRINT     *1,"| ",OPRQITEM:
                      *19,"| ",OPITDESC:
                      *77,"| Item":
                      *89,"| ",TDESC:
                      *117,"|    ",OPRQAMNT:
                      *132,"|"
            ADD       ONE,CLNO
            ADD       OPRQAMNT,TOTLITEM
            ADD       OPRQAMNT,GRNDITEM
          ENDIF
.
          MATCH     "2",OPRQTYPE
          IF        @EQUAL
            IF        FIRSTEQP=0
              IF        FIRSTITM=1
                CALL      UND132                      * print underlines
                PRINT     *1,"| Total Items":
                          *122,TOTLITEM:
                          *132,"|"
                CALL      UND132                      * print underlines
                PRINT     *N
                ADD       TWO,CLNO
              ENDIF
              CALL      PTHED000                  * print table headings
              MOVE      ONE,FIRSTEQP
            ENDIF
            CALL      GDESC000
            PRINT     *1,"| ",OPRQITEM:
                      *19,"| ",OPITDESC:
                      *77,"| Equipment":
                      *89,"| ",TDESC:
                      *117,"|    ",OPRQAMNT:
                      *132,"|"
            ADD       ONE,CLNO
            ADD       OPRQAMNT,GRNDEQPM
          ENDIF
          GOTO      PRREQ100
.
PRREQ900  IF        FIRSTEQP=1
            CALL      UND132                      * print underlines
            PRINT      *1,"| Total Equipments":
                      *122,GRNDEQPM:
                      *132,"|"
            CALL      UND132                      * print underlines
            ADD       ONE,CLNO
          ELSE
            IF        FIRSTITM=1
              CALL      UND132                      * print underlines
              PRINT     *1,"| Total Items":
                        *122,TOTLITEM:
                        *132,"|"
              CALL      UND132                      * print underlines
              ADD       ONE,CLNO 
            ENDIF
          ENDIF
.
          IF        CLNO>55
            CALL      PRTHED00                    * print standard headings
          ENDIF
.
          PRINT     *N,*1,"Total Type of Trays          :",TTYPTRAY:
                    *N,*1,"Total Quantity of Trays      :",GRNDTRAY: 
                    *N,*1,"Total Quantity of Items      :",GRNDITEM: 
                    *N,*1,"Total Quantity of Equipments :",GRNDEQPM
          PRINT     *N,*1,"*** End of report ***" 
.
PRREQ999  RETURN
+
. *****************************************************************************
. * GHEDDT00 : Get Header Details                                             *
. *****************************************************************************
GHEDDT00  MOVE      PSNAME,PACSNAME              * format patient name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNME30
.
.         get Clinic name
.
          PACK      CLINNAME,OPSECLIN,SP70
          IF        OPCNCLSU=1
            PACK      KEY6,OPSECLIN,SP70      * using doctor
            CALL      RDDOCT1
            IF        OVRCD=0
              MOVE      DGNAME,PACGNAME
              MOVE      DSNAME,PACSNAME
              MOVE      DTITL,PACTITLE
              MOVE      ONE,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,CLINNAME
            ENDIF
          ELSE
            PACK      KEY6,OPSECLIN,SP70      * using clinic
            CALL      RDOPCLI1
            IF        OVRCD=0         
              MOVE      OPCLDESC,CLINNAME
            ENDIF
          ENDIF
.
.         get Anaesthetist name
.
          PACK      ANAENAME,OPSEANAE,SP70
          PACK      KEY6,OPSEANAE,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,ANAENAME
          ENDIF
.
.         get format Session date
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,SESSDATE
.
.         get Session date day of week
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYDESC,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
.
.         get theatre room description
.
          PACK      KEY6,OPDATHET
          CALL      RDOPOPR1
          IF        OVRCD=0
            MOVE      OPRMDESC,OPRRMNME
          ELSE
            PACK      OPRRMNME,OPDATHET,SP70
          ENDIF
.
.         get Operating Surgeon name 
.
          MOVE      CLINNAME,SURGNAME
          MOVE      ZERO,F2
          PACK      KEY35,OPDAUNIQ,TEAMNUMB,F2,SP70
          CALL      RSOPTSM1
          CALL      RKOPTSM1
          BRANCH    OVRCD,GHEDDT99  
.
          PACK      KEY13,OPTSUNID,OPTSTMNO,OPTSSIND
          MATCH     KEY13,KEY35
          GOTO      GHEDDT99 IF NOT EQUAL
.
          PACK      SURGNAME,OPTSSCDE,SP70
          PACK      KEY6,OPTSSCDE,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,SURGNAME
          ENDIF
.
GHEDDT99  RETURN
.
. *****************************************************************************
. * PRTHED00 : Print the Heading                                              *
. *****************************************************************************
PRTHED00  CALL      HEAD132                  * Print the standard heading
.
          PRINT     *N,*5,"Patient : ",PATNME30:
                       *46,"Clinic : ",CLINNAME:
                       *86,"Date/Time/Day: ",SESSDATE,", ",OPSETIME,", ",DAYDESC
.
          PRINT     *5,"U/R     : ",PURNO:
                    *46,"Theatre: ",OPRRMNME:
                    *86,"Team No      : ",TEAMNUMB
.
          PRINT     *5,"Visit No: ",OPDAADMN:
                    *46,"Surgeon: ",SURGNAME:
                    *86,"Anaesthetist : ",ANAENAME:
                    *N
          ADD       FIVE,CLNO
.
PRTHED99  RETURN
+
.**************************************************************************
.*                            PTHED000              Called by: PRREQ000   *
.*                   print the table headings                             *
.**************************************************************************
PTHED000  CALL      UND132                        * print underlines
          PRINT     *1,"| Code":
                    *19,"| Description":
                    *77,"| Type":
                    *89,"| Class":
                    *117,"|  Quantity ":
                    *132,"|"
          CALL      UND132                        * print underlines
          ADD       ONE,CLNO
.
PTHED999  RETURN
+
.**************************************************************************
.*                            PTRIT000              Called by: PRREQ000   *
.*                      print the tray items                              *
.**************************************************************************
PITEM000  MOVE      ZERO,TPERTRAY
          PACK      KEY33,OPRQITEM,SP10
          CALL      RSOPTRB1
PITEM100  CALL      RKOPTRB1
          BRANCH    OVRCD,PITEM900
.
          MATCH     OPTDCODE,OPRQITEM
          GOTO      PITEM900 IF NOT EQUAL
.
          MATCH     "1",OPCNPTMC
          IF        @EQUAL
.
            IF        CFEETYP=FIVE
.
              PACK      KEY37,OPTDITEM,SP70
              CALL      RSNZMCH3
PITEM110      CALL      RKNZMCH3
              BRANCH    OVRCD,PITEM200
.
              MOVE      OPTDITEM,DIM9
.
              MATCH     DIM9,NZMCMCHG
              GOTO      PITEM200 IF NOT EQUAL
.
              MATCH     "1",NZMCACFL
              GOTO      PITEM110 IF EQUAL
.
              MOVE      NZMCDESC,OPITDESC
              GOTO      PITEM250
.
            ELSE
.
              PACK      KEY9,OPTDITEM,SP70
              IF        IBCNMHOS=1
                PACK      KEY3,WBSEHOSP,SP70
                CALL      RDPTHSP1
                BRANCH    OVRCD,PITEM120
                PACK      KEY29,PTHSDCLM,SP6,SP3,KEY9,CURRDATE,SP10
              ELSE
PITEM120        PACK      KEY29,PTCNDCLM,SP6,SP3,KEY9,CURRDATE,SP10
              ENDIF
              CALL      PATMCHRD
.
              IF        EXIT=ZERO
                MOVE      MDESC,OPITDESC
                GOTO      PITEM250
              ENDIF
.
              GOTO      PITEM200
.
            ENDIF
.
          ELSE
.
PITEM200    PACK      KEY15,OPTDITEM,SP70
            CALL      RDOPITE1
            IF        OVRCD=ONE
              MOVE      "Missing description",OPITDESC
            ENDIF
.
          ENDIF
.
PITEM250  IF        CLNO>55
            CALL      PRTHED00                      * print standard headings
            CALL      PTHED000                      * print table headings
          ENDIF
.
PITEM300  PRINT     *1,"| ",OPTDITEM:
                    *19,"| ",OPITDESC:
                    *77,"| ":
                    *89,"| ":
                    *117,"|    ",OPTDQTY:
                    *132,"|"
          ADD       ONE,CLNO
          ADD       OPTDQTY,TPERTRAY
..          ADD       OPTDQTY,GRNDITEM
.                    
          GOTO      PITEM100
.
PITEM900  CALL      UND132
          PRINT     *1,"| Total Items per Tray ",OPRQITEM:
                    *122,TPERTRAY:
                    *132,"|"
.
          IF        OPRQAMNT>1
            MULT      OPRQAMNT,TPERTRAY
            PRINT     *1,"| Total Quantity of Items for Tray ",OPRQITEM:
                      *122,TPERTRAY:
                      *132,"|"
            ADD       ONE,CLNO
          ENDIF
          ADD       TPERTRAY,GRNDITEM
.
          CALL      UND132
          PRINT     *N;
          ADD       TWO,CLNO
.
PITEM999  RETURN
+
.**************************************************************************
.*                              GDESC000         Called by: PRREQ000      *
.*                      get the class description                         *
.**************************************************************************
GDESC000  PACK      KEY5,ANSO,ANSG,OPRQCLSS
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      GDESC200 IF EQUAL
.
          MOVE      "<Missing Class>",TDESC
.
GDESC200  MATCH     "1",OPRQTYPE                  * item ?
          GOTO      GDESC300 IF EQUAL
          MATCH     "2",OPRQTYPE                  * tray ?
          GOTO      GDESC400 IF EQUAL
.
. get item description from OPRTRAFD 
.
          MOVE      "Missing description",OPITDESC    * assume not on file
          PACK      KEY18,OPRQITEM,SP70
          CALL      RDOPTRA1
          BRANCH    OVRCD,GDESC999
          MOVE      OPTHDESC,OPITDESC             * move to dim 50
          GOTO      GDESC999
.
. get item description from OPRITEFD/PATMCHFD/NZPMCHFD 
.
GDESC300  MATCH     "1",OPCNPTMC
          IF        @EQUAL 
.
            IF        CFEETYP=FIVE
.
              PACK      KEY37,OPRQITEM,SP70
              CALL      RSNZMCH3
GDESC310      CALL      RKNZMCH3
              BRANCH    OVRCD,GDESC350
.
              MOVE      OPRQITEM,DIM9
.
              MATCH     DIM9,NZMCMCHG
              GOTO      GDESC350 IF NOT EQUAL
.
              MATCH     "1",NZMCACFL
              GOTO      GDESC310 IF EQUAL
.
              MOVE      NZMCDESC,OPITDESC
              GOTO      GDESC999
.
            ELSE
.
              PACK      KEY9,OPRQITEM,SP70
              IF        IBCNMHOS=1
                PACK      KEY3,WBSEHOSP,SP70
                CALL      RDPTHSP1
                BRANCH    OVRCD,GDESC320
                PACK      KEY29,PTHSDCLM,SP6,SP3,KEY9,CURRDATE,SP10
              ELSE
GDESC320        PACK      KEY29,PTCNDCLM,SP6,SP3,KEY9,CURRDATE,SP10
              ENDIF
              CALL      PATMCHRD
.
              IF        EXIT=ZERO
                MOVE      MDESC,OPITDESC
                GOTO      GDESC999
              ENDIF
.
              GOTO      GDESC350
.
            ENDIF
.
          ELSE
.
GDESC350    PACK      KEY15,OPRQITEM,SP70
            CALL      RDOPITE1
            COMPARE   ZERO,OVRCD
            GOTO      GDESC999 IF EQUAL
            MOVE      "Missing description",OPITDESC
            GOTO      GDESC999
.
          ENDIF
.
GDESC400  MOVE      "Missing description",OPITDESC
          PACK      KEY18,OPRQITEM,SP70
          CALL      RDOPEQP1 
          BRANCH    OVRCD,GDESC999
          MOVE      OPEQDESC,OPITDESC             * move to dim 50
          GOTO      GDESC999
.
GDESC999  RETURN
+
