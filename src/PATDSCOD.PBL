.
.     PATDSCOD : Display Codes File Entries
.
.     This include displays entries in the codes file for a given
.      category.
.                   Input variable : CODE  contains category
.     Modifications :
.         V6.03.B00 28/12/1994  Greg Horvat      New Release 6.03
.                   Changed to loop with the 2nd index & ignore inactive codes
.
.
.         19/06/1998  Nick
.                     Removed TRAPs for 6.05
.:
PATDSCOD  OPEN      PATCODE2,"patcodes"
          PACK      KEY5,CODE,SP5
          MOVE      SP2,TCODE
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPBCD
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,CODE,": ",TDESC
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.
          PACK      PTCDKEY2,CODE,SP30
          CALL      RDSCODE2
DISPLP1   CALL      RDKCODE2
          BRANCH    OVRCD OF ENDLP1
.
          MATCH     TCODE,CODE
          GOTO      ENDLP1 IF NOT EQUAL
.
          MATCH     SP3,ACODE
          GOTO      DISPLP1 IF EQUAL        * Ignore blank codes
.
          MATCH     ANSI,PTCOACTV
          GOTO      DISPLP1 IF EQUAL        * Ignore inactive codes
.
          DISPLAY   *PCCOL:CVERT,*V2LON,ACODE,SP2,*HOFF,TDESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      DISPLP1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       TWENTY6,CCOL
          COMPARE   "70",CCOL
          GOTO      DISPLP1 IF LESS
.
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSE,ANS
          GOTO      ENDLP2 IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.
          GOTO      DISPLP1
.
.       IF MISSING CATEGORY SHOW  CODE MISSING
.
DISPBCD   DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*BLINKON:
                    "Category : ",CODE," missing in Codes Master File"
          GOTO      ENDLP1
.
ENDLP0    NORETURN
.
ERROR     NORETURN
.         DISPLAY *P1:23,*DBON,"TCODE = [",TCODE,"]   ACODE = [",ACODE,"]":
.                        "TDESC = [",TDESC,"]   THCSCOD = [",THCSCOD,"]":
.                 *P1:24,"TCFORM6 = [",TCFORM6,"]   TCINDC1 = [",TCINDC1,"]":
.                        "TCINDC2 = [",TCINDC2,"]   TCINDC3 = [",TCINDC3,"]"
           KEYIN ANS;
           DISPLAY *P1:23,*EF;
.
.      End of codes
.
ENDLP1    
          BRANCH    CNEWDS,ENDLP2
          KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                               " to continue ",*EOFF,ANS;
ENDLP2    MOVE      ZERO,OVRCD
          CLOSE     PATCODE2
DISPRET   RETURN
