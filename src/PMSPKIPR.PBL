.------------------------------------------------------------
.  File          :  PMSPKIPR.PBL
.
.  Function      :  Update Keyword Index File for HCP Practice Table
.
.  Parameters    :  PMHGPRAC,PMHGCNTR - Key to HCP Practice Table
.
.  Modifications :
.  V11.03.03  25.09.2023 DA Sarkies      TSK 0914515
.             Added code to remove dash from phone number
.  V11.03.02  08.09.2023 DA Sarkies      TSK 0914515
.             Added phone number to keyword search, and function to move 
.  V11.03.01  03.08.2023 DA Sarkies      TSK 0914515
.             Added phone number to keyword search, and function to move 
.             abbreviated state to field to filter by state
.  V10.11.01  08/08/2017 Tracey Nguyen   TSK 0299206
.             Added HCPSW000 and GENSNDEX to derive Sound-Ex keywords for
.             Practice Name
.-------------------------------------------------------------------------------
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PMHKKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PMHKKKWD
.  Index 2   PMHKKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the HCGE0000 routine to call the HCPBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    HCPBW000
.       MOVE    DGNAME,KEYWORDS
.       CALL    HCPBW000
.       MOVE    TDESC,KEYWORDS
.       CALL    HCPBW000
.------------------------------------------------------------
          DEFPROC   PMSPKIUP
.
          INC       PMSPKIFD/INC
          INC       GENSNDVR/INC      * Vars for GENSNDEX.PBL
.
KEYINDEX  DIM       12        * Code Index in Table
KEYWORDS  DIM       80        * String Containing the Key Words to be Indexed
EIGHTY    FORM      "80"
KEYWRDNO  FORM      2
KEYWRD00  DIM       80
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
TMPSTATE  DIM       60
.
Z1        INIT      "~"                         * Indicates Sound-Ex key
VICTORIA  INIT      "VICTORIA"
VIC       INIT      "VIC"
NEWSTMWL  INIT      "NEW SOUTH WALES"
NSW       INIT      "NSW"
QUEENSLD  INIT      "QUEENSLAND"
QLD       INIT      "QLD"
WESTAUST  INIT      "WESTERN AUSTRALIA"
WA        INIT      "WA"
STHAUST   INIT      "SOUTH AUSTRALIA"
SA        INIT      "SA"
NTHTERR   INIT      "NORTHERN TERRITORY"
NT        INIT      "NT"
TASSIE    INIT      "TASMANIA"
TAS       INIT      "TAS"
AUSCAPTY  INIT      "AUSTRALIAN CAPITAL TERRITORY"
ACT       INIT      "ACT"
OTHER     INIT      "OTH"

.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
HCGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PMSPKIA1,"pmspkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,HCGE9999
.
          PACK     KEYINDEX,PMHGPRAC,PMHGCNTR
          CALL     HCGCL000           * Remove Current Key Word Index
          BRANCH   EXIT,HCGE9999
.
          PACK     KEY12,PMHGPRAC,PMHGCNTR  * Validate Record on File
          CALL     RDPMHCG1
          BRANCH   OVRCD,HCGE1900     * If Not then it Must be a Delete/Clear
.
          PACK     KEYINDEX,PMHGPRAC,PMHGCNTR
          MOVE     PMHGPNAM,KEYWORDS
          CALL     HCPBW000
.
          MOVE     PMHGADD3,KEYWORDS
          CALL     HCPBW000

          MATCH    SP70,PMHGPTEL
          IF       !@EQUAL
            MOVE     PMHGPTEL,KEYWORDS
            REPLACE  "( ",KEYWORDS
            REPLACE  ") ",KEYWORDS
            REPLACE  "- ",KEYWORDS
            SQUEEZE  KEYWORDS
            CALL     HCPBW000
          ENDIF 
.
.         TSK 0299206 - Soundex
.
HCGE1100  MOVE     PMHGPNAM,KEYWORDS        * Practice Name
          CALL     HCPSW000
.
HCGE1200  CALL     HCST0000
.
HCGE1900  CLOSE    PMSPKIA1
.
HCGE9999  GOTO     PMSPKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
HCGCL000  PACK     KEY27,KEYINDEX,SP70
          CALL     RSPMPKI1
          CALL     RKPMPKI1
          BRANCH   OVRCD,HCGCL999
.
          MATCH    KEYINDEX,PMPKHCGC
          GOTO     HCGCL999 IF NOT EQUAL
.
          PACK     KEY27,PMPKHCGC,PMPKKKWD,SP70
          CALL     DEPMPKI1
          GOTO     HCGCL000
.
HCGCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
HCPBW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      HCPBW000 IF NOT EOS
            GOTO      HCPBW999               * entire name if blank
          ENDIF
          PACK      KEY80,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY80,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      HCPBW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
HCPBW100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY27,KEYINDEX,KEY15
          UNPACK    KEY27,PMPKHCGC,PMPKKKWD
          CALL      RDPMPKI1
          IF        OVRCD=1
            CALL      WRPMPKI1
          ENDIF
          GOTO      HCPBW190
.
.         Check for any more words
.
HCPBW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY80,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY80,KEYWORDS
          GOTO      HCPBW000
.
HCPBW999  RETURN
.
. TSK 0299206
.-----------------------------------------------------------------------------
. Determine Words to Index with Soundex algorithm
.-----------------------------------------------------------------------------
HCPSW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      HCPSW000 IF NOT EOS
            GOTO      HCPSW999               * entire name if blank
          ENDIF
          PACK      KEY80,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY80,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      HCPSW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handy form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Derive and save this sound-ex key word
.
HCPSW100  MOVE      KEYWORDS,SXKEYIN         * Keyword in to convert to Soundex
          CALL      GSNDX000                 * Derive Sound-Ex key
          PACK      KEY15,Z1,SXKEYOUT,SP70   * Add "~" to indicate Sound-Ex 
          PACK      KEY27,KEYINDEX,KEY15
          UNPACK    KEY27,PMPKHCGC,PMPKKKWD  * Write the Sound-Ex key
          CALL      RDPMPKI1
          IF        OVRCD=1
            CALL      WRPMPKI1
          ENDIF
          GOTO      HCPSW190
.
.         Check for any more words
.
HCPSW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY80,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY80,KEYWORDS
          GOTO      HCPSW000
.
HCPSW999  RETURN
.
. TSK 0925728
.-----------------------------------------------------------------------------
. Update entry with state for filter function    
.-----------------------------------------------------------------------------
HCST0000  MOVE      PMHGADD4,TMPSTATE 
          REP       UPPLOW,TMPSTATE
          STRIP     TMPSTATE 
.
          COMPARE   ONE,PTCNHDPS
          GOTO      HCST9999 IF EQUAL
.
          COMPARE   ZERO,PTCNHDPS
          GOTO      HCST9999 IF EQUAL
.
          MATCH     TMPSTATE,VICTORIA
          IF        @EQUAL
            MOVE      VIC,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,VIC     
          IF        @EQUAL
            MOVE      VIC,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,NEWSTMWL
          IF        @EQUAL
            MOVE      NSW,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,NSW
          IF        @EQUAL
            MOVE      NSW,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,QUEENSLD
          IF        @EQUAL
            MOVE      QLD,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,QLD
          IF        @EQUAL
            MOVE      QLD,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,WESTAUST
          IF        @EQUAL
            MOVE      WA,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,WA
          IF        @EQUAL
            MOVE      WA,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,STHAUST
          IF        @EQUAL
            MOVE      SA,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,SA 
          IF        @EQUAL
            MOVE      SA,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,NTHTERR
          IF        @EQUAL
            MOVE      NT,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,NT
          IF        @EQUAL
            MOVE      NT,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,TASSIE
          IF        @EQUAL
            MOVE      TAS,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,TAS
          IF        @EQUAL
            MOVE      TAS,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,AUSCAPTY
          IF        @EQUAL
            MOVE      ACT,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MATCH     TMPSTATE,ACT
          IF        @EQUAL
            MOVE      ACT,PMHGSTAT
            GOTO      HCST9990
          ENDIF
.
          MOVE      OTHER,PMHGSTAT
.
HCST9990  CALL      UPPMHCG1
.
HCST9999  RETURN
.
.------------------------------------------------------------------------------
          INC       PMSPKIIO/INC
          INC       IBAOVRIO/INC
          INC       GENSNDEX               * Routine to generate Soundex key
.
PMSPKIEP  ENDPROC                          * End of Procedure
