.-------------------------------------------------------------------------
. Include Name  :   PMSSMHCD.PBL
.
. Function      :   Common functions for SMS history                     
.-------------------------------------------------------------------------
. Modifications Done :
.-------------------------------------------------------------------------
. V11.00.01  30/09/2020  Thanh T                TSK 0893522
.            Changed SMSS5000 to append latest response date/time when
.            to response status field when message status is pmsmstat = 1
.            (Response Received)  
. V10.14.01  26/08/2019  Thanh T                TSK 0879452
.            Changed SMSS0000 to include checking for booking date/time
.            against date message was sent     
. V10.08.00  15/07/2016  Ebon Clements          TSK 0294154
.            Created include
.-------------------------------------------------------------------------
SMSS0000  MOVE      SP70,KEY50
          CLEAR     KEY50
.
          PACK      KEY64,KEY2,KEY30,Z70
          CALL      RSPMSMH5
          CALL      RPPMSMH5
          BRANCH    OVRCD,SMSS9000
.
          MATCH     PMSMMTYP,KEY2
          GOTO      SMSS9000 IF NOT EQUAL
.
          MATCH     PMSMRKEY,KEY30
          GOTO      SMSS9000 IF NOT EQUAL
.
          MATCH     SP70,OBABKDTE 
          GOTO      SMSS2000 IF EQUAL
.
          PACK      SNDTETIM,PMSMSDAT,PMSMSTIM,SP70
          MATCH     OBABKDTE,SNDTETIM
          GOTO      SMSS9000 IF LESS
.
SMSS2000  MATCH     " 0",PMSMSTAT
          IF        @EQUAL
            APPEND    "Sent",KEY50
          ENDIF
.
          MATCH     " 1",PMSMSTAT
          IF        @EQUAL
            APPEND    "Response Received",KEY50
          ENDIF
.
          MATCH     " 2",PMSMSTAT
          IF        @EQUAL
            APPEND    "Error",KEY50
          ENDIF
.
          MATCH     " 3",PMSMSTAT
          IF        @EQUAL
            APPEND    "Re - Sent",KEY50
          ENDIF
.
          MATCH     " 4",PMSMSTAT
          IF        @EQUAL
            APPEND    "Waiting to be sent",KEY50
          ENDIF
.
          MATCH     " 5",PMSMSTAT
          IF        @EQUAL
            APPEND    "Error Corrected",KEY50
          ENDIF
.
          PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1
          CALL      RPPMSRR1
          BRANCH    OVRCD,SMSS9000
.
          MATCH     PMSMUMID,PMRRUMID
          GOTO      SMSS9000 IF NOT EQUAL
.
          UNPACK    PMRRRESA,RECVMESG     * Only take first 50 chars of msg
          REP       UPPLOW,RECVMESG       * Convert msg to uppercase
.
          PACK      KEY60,RECVMESG,SP70
          CALL      RSPMSRM2
SMSS5000  CALL      RKPMSRM2
          BRANCH    OVRCD,SMSS9000
.
          MATCH     RECVMESG,PMSRINBS
          GOTO      SMSS9000 IF NOT EQUAL
.
          MATCH     "1",PMSRACTV            * Active?
          GOTO      SMSS5000 IF NOT EQUAL
.
          APPEND    LBRACK,KEY50
.
          MATCH     "0",PMSRMVAL
          IF        @EQUAL
            APPEND   "No",KEY50
          ENDIF
.
          MATCH     "1",PMSRMVAL
          IF        @EQUAL
            APPEND   "Yes",KEY50
          ENDIF
.
          MATCH     "2",PMSRMVAL
          IF        @EQUAL
            APPEND   "Unknown",KEY50
          ENDIF
.
          APPEND    RBRACK,KEY50
.
          MATCH     " 1",PMSMSTAT
          IF        @EQUAL
            UNPACK    PMRRRDTE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN:
                                 JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            PACK      RSDATTIM,LBRACK,DISPDATE,SP1,ATTEXT:
                               SP1,PMRRRTIM,RBRACK
            APPEND    SP1,KEY50 
            APPEND    RSDATTIM,KEY50 
          ENDIF 
.
SMSS9000  RESET     KEY50
.
SMSS9999  RETURN
.
