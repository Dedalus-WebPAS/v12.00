.******************************************************************************
.*  Include Name  : COMCODKY.PBL                                              *
.*  Description   : Keyin of a codes file entry in V5.01                      *
.*  Author        : ROD                                                       *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CODE     - category of code                               *
.*                  CODEDATE - Date for inactive validation                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : CMCDACOD - Keyed in code                                  *
.*                  CMCDDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          :                                                           *
.******************************************************************************
COMCODKY  
CCODE010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      CCODE050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,CMCDACOD:
                    *PECOL:EVERT,*DV,CMCDACOD
.
          GOTO      CCODE080
.
CCODE050  MOVE      CKYIDEF3,CMCDACOD                 * default
          KEYIN     *PECOL:EVERT,*DV,CMCDACOD:     * default used
                    *PECOL:EVERT,*V2LON,*RV,CMCDACOD:
                    *PECOL:EVERT,*DV,CMCDACOD
.
.         check what was entered
.
CCODE080  CALL      KCCOD000
          BRANCH    EXIT,CCODE100,CCODE705,CCODE805,CCODE905
.                        "?"      valid    nothing  exitchar
          GOTO      CCODE010
.
.         a "?" was input
.
CCODE100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
CCODE105  CALL      COMCODDS
          BRANCH    CNEWDS,CCODE150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      CCODE010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
CCODE150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      CCODE200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN3:      * no default used
                    *P11:24,*V2LON,CMCDACOD:
                    *P11:24,*DV,CMCDACOD
.
          GOTO      CCODE300
.
CCODE200  MOVE      CKYIDEF3,CMCDACOD
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,CMCDACOD:     * default used
                    *P11:24,*V2LON,*RV,CMCDACOD:
                    *P11:24,*DV,CMCDACOD
.
.         repeat the validation of the code
.         check what was entered
.
CCODE300  CALL      KCCOD000
          BRANCH    EXIT,CCODE105,CCODE700,CCODE800,CCODE900
.                        "?"      valid    nothing  exitchar
.
          GOTO      CCODE150
.
.         a valid code was entered 
. 
CCODE700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,CMCDACOD
.
CCODE705  MOVE      FALSE,EXIT                    * valid input
          GOTO      CCODE999 
.
.         nothing was input
.
CCODE800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,CMCDACOD
.
CCODE805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      CCODE999 
.
.         EXITCHAR input
.
CCODE900  CALL      FRESCR00                * free the screen saved
.
CCODE905  MOVE      TWO,EXIT                * EXITCHAR
.
CCODE999  RETURN
+
.*********************************************************************
.*                  KCCOD000                    Called by : COMCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
KCCOD000  ENDSET    CMCDACOD
          APPEND    SP3,CMCDACOD
          RESET     CMCDACOD
.
          MATCH     EXITCHAR,CMCDACOD
          GOTO      KCCOD600 IF EQUAL       * exit char
.
          MATCH     SP3,CMCDACOD
          GOTO      KCCOD700 IF EQUAL       * nothing
.
          MATCH     QUEST,CMCDACOD
          GOTO      KCCOD800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
KCCOD500  PACK      KEY5,CODE,CMCDACOD
          CALL      RDCMCOD1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,KCCOD900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      KCCOD900 IF EQUAL       * Code Exists
          ENDIF
.
          MATCH     SP70,CMCDFMDT
          GOTO      KCCOD550 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE             * inactive ?
          GOTO      KCCOD510 IF LESS
.
          MATCH     SP70,CMCDTODT
          GOTO      KCCOD550 IF EQUAL
          MATCH     CODEDATE,CMCDFMDT             * inactive ?
          GOTO      KCCOD510 IF LESS
          GOTO      KCCOD550
.
KCCOD510  MOVE      ONE,HLEF                  * Inactive code
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*EL,*B,"Inactive code.  ";
          CALL      HITENTER
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      KCCOD999
.
KCCOD550  MOVE      TWO,EXIT                * valid code
          GOTO      KCCOD999
.
.         exitchar entered
.
KCCOD600  MOVE      FOUR,EXIT
          MOVE      SP3,CMCDACOD
          MOVE      SP20,CMCDDESC              * clear desc field
          GOTO      KCCOD999
.
.         nothing entered
.
KCCOD700  BRANCH    CKYIMAND,KCCOD950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,CMCDACOD
          MOVE      SP20,CMCDDESC              * clear desc field
          GOTO      KCCOD999
.
.         "?" entered
.
KCCOD800  MOVE      ONE,EXIT
          GOTO      KCCOD999
.
.         invalid code entered
.
KCCOD900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      KCCOD999
.
.         field is mandatory
.
KCCOD950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
KCCOD999  RETURN
+
          INCLUDE   COMCODDS
.
+
