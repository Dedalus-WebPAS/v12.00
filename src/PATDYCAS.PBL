.
. PATDYCAS.PBL
. ------------
.         V12.00.01 19/05/2025  J.Tan          TSK 0955096
.                   Added alpanumeric visit number generation
.         V11.04.01 13/03/2024  J.Tan          TSK 0919335
.                   Mod checking for HF history
.         V11.00.01 04/03/2020  J.Tan          TSK 0838262
.                   Added Effective from and to date to MBS Item file
.         V10.08.01 07/07/2016  J.Tan            CAR 0822042
.                   Fixed generating daycase rate for Johns hopkins
.         V10.07.01 16/02/2016  J.Tan            CAR 0310703
.                   Mods Public Hosp checking for to be invoiced amount
.         V10.05.01 19/02/2015  J.Tan            CAR 311352
.                   Mods for Public Hosp,if only Overnight setup (NO DAYCASE)
.         V10.04.01 30/04/2013  J.Tan            CAR 261630
.                   Removed port number from temp file name
.         V10.03.02 26/09/2013 J.Tan   CAR 291627
.                   Mods to check Inv.pending for Public Hosp.
.         V10.03.01 28/11/2011 J.Tan   CAR 255996
.                   Fixed reading bedfees file to search for default setup
.         V10.01.01 09/12/2010 J.Tan   CAR 233034
.                   Mods bed fees file to use health fund table type
.                   13/01/2010 J.Tan   CAR 233048
.                   Mods Theatre fees file to use health fund table type
.         V10.00.01 18/08/2010 J.Tan            CAR 222031
.                   Mods to set PMMIDUPD and PMMITUPD
.         V9.12.01  09/09/2009  J.Tan     CAR 205303
.                   Added Contract ID to Casemix Theatre fee file
.         V9.08.03  28/08/2008  J.Tan      CAR 176752
.                   Mods for SX bed fees
.         V9.08.02  04/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2
.                   Mods to PATDGWFD - Add field PTDWDRGV (add to Keys)  
.         V9.08.01  15/03/2007  J.Tan         CAR 136149
.                   Mods to use default claim code from system parameter
.         V9.03.02  21/12/2004  Jill Habasque CAR 54105
.                   Added trap before calling postaudm
.         V9.03.01  30/10/2004  J.Tan  CAR 44172   
.                   Mods for pmsmtiaf - Misc.Theatre item file 
.         V9.02.08  29/01/2003  Sandra Barcham  CAR 35840
.                   Removed close of PATITEM1
.         V9.02.07  18/06/2002  J.Tan
.                   Mods to check for ADYHOSP
.         V9.02.06  22/04/2002  Bronko Sosic 
.                   Removed close of PATFN1A1                 
.         V9.02.05  17/01/2002  Jill Habasque
.                   Removed close of PATBFEE                 
.         V9.02.04  06/12/2001  Tonii
.                   Fixed checking for casemix daycase flag
.         V9.02.03  03/12/2001  J.Tan
.                   Fixed calculating theatre items      
.         V9.02.02  26/11/2001  J.Tan
.                   Changed MBS amount charging indicator
.         V9.02.01  25/10/2001  J.Tan        
.                   If MBS amount is used for charging items, 
.                   no need recalculate items when discharging
.         V5.09.01  14/12/2000  Jill Habasque
.                   Casemix mods - changed to use DDATE & ADATE to calculate
.                   daycase patients
.         V5.08.01  06/06/2000  Jill Habasque                                 
.                   Changed from key10 to key14 for the health fund file      
.         V5.08.B01 25/02/2000  Greg Horvat      SRF 637565
.                   Changes for 4 character DRGs
.         V5.07.02  13/07/1999  Greg Horvat
.                   Replaced certain variables & with KEY variables
.         V5.07.01  28/04/99 J.Tan
.                   Mods for casemix funding
.                   08/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.        V5.01 08/05/89 J.Tan  (I.B.A)
.              Fixed Theatre banding charges.  SRF 100468
.        V5.01.01 28/09/89 Graeme Williams
.                 Put opens and closes into the include
.                 SRF 102454
.        V5.01.02 21/03/90 J.Tan
.                 Mods to keep Discount/Allowances. SRF 104323
.        V5.01.03 09/05/91 J.Tan       SRF 108518
.                 Fixed bed fee for "D" record in TRANS file
.        V5.01.04 17/06/91 J.Tan           SRF 108857
.                 Mods to write to item audit file
.        V5.01.05 21/01/92 J.Tan           SRF 112669
.                 Fixed TRBEND for discharge records
.        V5.01.06 06/01/1992 Graeme Williams
.                 Allow for CFEETYP = 4 (Board charging)
.        V5.01.07 14/01/1993  Graeme Williams        SRF 119996
.              Fixed calculation of daycase rates for theatre
.        V5.01.08 01/04/93  DIG
.                 Made sure TEXTRA variable is getting treated properly
.                 for additional charging
.         V5.02.01  20/02/1995  Greg Horvat      SRF 134879
.                   Recompiled for PATAUMFD & PATIOAUM - added new variable &
.                   changed to write to this new variable
.
.        This include will change all the rates for the current patient
.        to the daycase rate. This includes theatre fees as well as
.        accomodation charges
.
PATDYCAS MATCH      ADATE,DDATE
         RETURN     IF NOT EQUAL
         COMPARE    ZERO,ADYHOSP
         RETURN     IF NOT EQUAL             * not a daycase
.
         MOVE       ZERO,FORM12P2
         MOVE       ZERO,FORM3
.
DYCAS010 COMPARE    FOUR,CFEETYP
         GOTO       DYCAS050 IF NOT EQUAL
.
         PROC       BRDDY000                 * change to board day case rate
         GOTO       DYCASTHR                 * now do the theatre charges
.
.        Fix up the accomodation charges
.
DYCAS050 PACK       KEY30 WITH AADMNO,SP30
         CALL       RDSTRAN2
DYCASLP1 CALL       RDKTRAN2
         BRANCH     OVRCD OF DYCASTHR
.
         MATCH      TADMN,AADMNO
         GOTO       DYCASTHR IF NOT EQUAL
.
.        Update this record with the day case rate
.
         MATCH      ANSD,TMOVE          * "D" record has same rate as prev rec
         GOTO       UPDYR IF NOT EQUAL
.
         MOVE       STRATEF,FORM12P2
         ADD        STRATEH,FORM12P2
.
         MOVE       SPTTRADP,PTTRADPT
         MOVE       SPTTRADR,PTTRADRB
         MOVE       STRATEF,TRATEF      * use rate from previous record
         MOVE       STRATEH,TRATEH
         MOVE       STDISC,TDISC
         MOVE       STALLW,TALLW
         MOVE       STEXTRA,TEXTRA
         MOVE       STRBEND,TRBEND
         MOVE       SPTTRAEN,PTTRAEND
         GOTO       UPDYR6
.
.        Check if this patient should be funded by casemix
.
UPDYR    CMATCH     ANSA,TMOVE         * Check admission record
         IF         @EQUAL
           IF         CMXFLAG = 1
             CALL       DLUM0000           * Generate lumpsum daycase
             MOVE       PTLSLREB,PTTRLSRB  * rebate portion
             MOVE       PTLSLPAT,PTTRLSPT  * patient portion
             GOTO       UPDYR2
.
           ELSE
             GOTO       UPDYR4             * Generate normal rate
           ENDIF
         ELSE
           IF         CMXFLAG = 1 
             GOTO       UPDYR2             * Using casemix funding
           ELSE
             GOTO       UPDYR4
           ENDIF
         ENDIF
.
UPDYR2   MOVE       TRBTYP,SAVBTYP
         CALL       ADID0000           * Generate additional charge
         MOVE       PTADDREB,PTTRADRB  * rebate portion 
         MOVE       PTADDPAT,PTTRADPT  * patient portion
.
         MOVE       ZERO,TRBEND        * ending day for daily rate
         MOVE       ZERO,PTTRAEND      * ending day for additional
         MOVE       ZERO,TDISC
         MOVE       ZERO,TALLW    * a new bed fee is used with zero Disc./Allw.
         GOTO       UPDYR6
.
.        Normal rates
.
UPDYR4   IF         CFEETYP=5
           CALL       NZPBF000            * Calculate the day case bed fee
           BRANCH     NOFEE OF UPDYR8
         ELSE
           CALL       DAYBFEE             * Calculate the day case bed fee
           IF         NOFEE=1
.
.            Public Hospital - if Daycase is not setup, use Overnight rate
.
             IF         CHOSTYP=1 & CFEETYP=0
               COMPARE    ZERO,FORM3        * Check if Overnight rate found
               GOTO       UPDYR5 IF NOT EQUAL
             ENDIF
             GOTO       UPDYR8
           ENDIF
         ENDIF
.
UPDYR5   MOVE       BFPAT,TRATEF
         MOVE       BFHF,TRATEH
         MOVE       ZERO,PTTREPSD
.
.        Check if the patient was charged as a day case rate
.
         COMPARE    ZERO,TRBEND
         GOTO       UPDYR6 IF EQUAL
.
         MOVE       ZERO,PTTRAEND * ending day for additional
         MOVE       ZERO,TRBEND   * Patient was not charged as a day case
         MOVE       ZERO,TDISC    * rate when he was admitted. Therefore
         MOVE       ZERO,TALLW    * a new bed fee is used with zero Disc./Allw.
.
UPDYR6   PACK       TRCDATE,CCC,CYY,CMM,CDD
         REP        " 0",TRCDATE
         CLOCK      TIME,TRCTIME
         MOVE       WBSEUID,PTTRUUID
.
         CALL       UPTRAN2
.
UPDYR8   MOVE       TRATEF,STRATEF      * Save the rates for discharge record
         MOVE       TRATEH,STRATEH
         MOVE       PTTRADPT,SPTTRADP
         MOVE       PTTRADRB,SPTTRADR
         MOVE       TDISC,STDISC
         MOVE       TALLW,STALLW
         MOVE       TEXTRA,STEXTRA
         MOVE       TRBEND,STRBEND
         MOVE       PTTRAEND,SPTTRAEN
         GOTO       DYCASLP1
.
.        Find any theatre fees to recalculate
.        If MBS Amount is used for charging, do not need recalculate items
.
DYCASTHR PACK      KEY5,ANSC,ANSL,ACLAIM
         CALL      RDCODE1
         IF        OVRCD=0
           MATCH     "1",TCINDC2
           IF        @EQUAL
             GOTO      ENDDYCAS         * do not recalculate items
           ENDIF
         ENDIF
.
         MOVE       ZERO,FORM3
         MOVE       ZERO,FORM2
         OPEN       PMSMTIA2,"pmsmtiaf"
         OPEN       PATITEM1,"patitemn"
.
         MOVE       "2",PMMIRTYP
         PACK       KEY15,AADMNO,PMMIRTYP,SP20
         CALL       RSPMMTI2
DYCASLP2 CALL       RKPMMTI2
         BRANCH     OVRCD OF ENDDYCAS
.
         MOVE       AADMNO,DAADMNO
         MATCH      PMMIVISN,DAADMNO
         GOTO       ENDDYCAS IF NOT EQUAL
.
         MATCH      "2",PMMIRTYP
         GOTO       ENDDYCAS IF NOT EQUAL
.
         MOVE       PMMIITEM,PRTITCOD
         MOVE       PMMIITEM,KEY9
         PACK       KEY17,KEY9,PMMITDAT,SP70
         CALL       PATITMRD
.        CLOSE      PATITEM1
         COMPARE    ZERO,OVRCD
         GOTO       THRRECAL IF EQUAL
.
         MOVE       PMMIREFN,PRTITCOD
.
.        Recalculate theatre fee
.
THRRECAL MOVE       PMMIAMTT,FORM62
         MOVE       PMMITDAT,TSRVDATE       * Service date
         IF         CMXFLAG = 1 
           ADD        ONE,FORM2
           CALL       CMXT0000            * Get theatre charge from casemix 
         ELSE
           CALL       DAYTFEE
         ENDIF
         BRANCH     NOFEE OF DYCASLP2
.
         PACK       PMMIDUPD,CCC,CYY,CMM,CDD
         REP        " 0",PMMIDUPD
         CALL       IBACLOCK
         MOVE       CTIMEIS,PMMITUPD
         MOVE       WBSEUID,PMMIWUSR
         CALL       UPPMMTI2
.
         GOTO       DYCASLP2
.
.        If this is Public hospital, Add record to Invoice pending if
.        the bed fees is chargeable
.
ENDDYCAS IF         CHOSTYP=1 & CFEETYP=0
           MOVE      FOUR,PTIPRSTA              * Update Discharge
           MOVE      DDATE,PENDSDAT             * as at date
           CALL      CINVP000                   * Check for tobe invoiced
         ENDIF
.
         RETURN
.
.        Recalculate Bed fee with day case rate
.
DAYBFEE  OPEN       PATBFEE1,"patbfeef"
.
         COMPARE    NINE,CFEETYP
         GOTO       DAYBF60 IF EQUAL            * Johns Hopkins
.
         OPEN       PATFN1A1,"patfn1af"
         MOVE       SP70,PTHFBCAT
         PACK       KEY14,AFUNDH,AFNDTB,SP70
         CALL       RDFUND1
.
         MOVE       ZERO,BFENDDY
         MOVE       ZERO,NOFEE
         MOVE       ZERO,F1
         PACK       KEY18 WITH ACLAIM,AFUNDH,PTHFBCAT,TATYPE,TRBTYP,SP70
.
DAYBF10  PACK       KEY27 WITH KEY18,SP70
         CALL       RDSBFEE1
DAYBF11  CALL       RDKBFEE1
         BRANCH     OVRCD,DAYBF12
.
         PACK       DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
         MATCH      KEY18,DIM18
         GOTO       DAYBF12 IF NOT EQUAL
.
         MOVE       ADATE,CEFFDATE         * use admission date
         MOVE       PTBFCNID,KEY6         * Contract ID
         CALL       VALCONTR              * Validate the Contract ID
         BRANCH     EXIT,DAYBF11
.
         IF         CHOSTYP=1 & CFEETYP=0
           IF         BFENDDY<>0
             MOVE       ONE,FORM3         * found bed fees but NO Daycase rate
             GOTO       DAYBF90
           ENDIF
         ELSE
           COMPARE    ZERO,BFENDDY
           GOTO       DAYBF12 IF NOT EQUAL
         ENDIF
.
         MOVE       PTBFCNID,CONTRCID     * Contract ID
         GOTO       DAYBF99
.
DAYBF12  ADD        ONE,F1
         BRANCH     F1,DAYBF20,DAYBF30
         GOTO       DAYBF90
.
.        Try no health fund
.
DAYBF20  PACK       KEY18 WITH ACLAIM,SP9,TATYPE,TRBTYP,SP70
         GOTO       DAYBF10
.
.        Try zero claim code, no health fund
.
DAYBF30  READ       CONTROLF,TWENTY;*193,PTCNDCLM
         CALL       DCLM0000
         PACK       KEY18 WITH PTCNDCLM,SP9,TATYPE,TRBTYP,SP70
         GOTO       DAYBF10
.
.        Johns Hopkins bed fees
.
DAYBF60  OPEN       JHSBFEA1,"jhsbfeaf"
         MOVE       ZERO,BFENDDY
         PACK       KEY26 WITH ACLAIM,AFUNDH,AFNDTB,TATYPE,TRBTYP,BFENDDY
         CALL       RDJHBFE1
         COMPARE    ZERO,OVRCD
         GOTO       DAYBF99 IF EQUAL
.
.        Try no health fund
.
         PACK       KEY26 WITH ACLAIM,SP14,TATYPE,TRBTYP,BFENDDY
         CALL       RDJHBFE1
         COMPARE    ZERO,OVRCD
         GOTO       DAYBF99 IF EQUAL
.
.        Try zero claim code, no health fund
.
         READ       CONTROLF,TWENTY;*193,PTCNDCLM
         CALL       DCLM0000
         PACK       KEY26 WITH PTCNDCLM,SP14,TATYPE,TRBTYP,BFENDDY
         CALL       RDJHBFE1
         COMPARE    ZERO,OVRCD
         GOTO       DAYBF99 IF EQUAL
.
DAYBF90  MOVE       ONE,NOFEE
DAYBF99  RETURN
.
.
.        Recalculate NZ Private Bed fee with day case rate
.
NZPBF000 OPEN       NZPBFEA1,"nzpbfeaf"
         MOVE       ZERO,NOFEE
         MOVE       ZERO,F1
.
         PACK       KEY26 WITH PMVXMHOS,ACLAIM,AFUNDH,AFNDTB,TATYPE,TRBTYP,SP70
NZPBF200 CALL       EFFD0000                * Get the current effective date
         MATCH      SP70,EFTDATE
         GOTO       NZPBF500 IF EQUAL
.         
         MOVE       "  0",NZBFEDAY
         PACK       KEY37 WITH KEY26,EFTDATE,NZBFEDAY,SP70
         CALL       RDNZBFE1
         COMPARE    ZERO,OVRCD
         GOTO       NZPBF999 IF EQUAL
.
NZPBF500 BRANCH     F1 OF NZPBF520,NZPBF620
.
         PACK       KEY26 WITH PMVXMHOS,ACLAIM,SP6,SP8,TATYPE,TRBTYP,SP70
         MOVE       ONE,F1
         GOTO       NZPBF200
.
NZPBF520 OPEN       CONTROLF,"controlf"
         READ       CONTROLF,TWENTY;*193,PTCNDCLM
         CALL       DCLM0000        * Check Hospital claim code charging
.
         PACK       KEY26 WITH PMVXMHOS,PTCNDCLM,SP6,SP8,TATYPE,TRBTYP,SP70
         MOVE       TWO,F1
         GOTO       NZPBF200
.
NZPBF620 MOVE       ONE,NOFEE
         GOTO       NZPBF999
.
NZPBF800 MOVE       NZBFRATE,BFPAT
         MOVE       NZBFREBA,BFHF
NZPBF999 RETURN
+
.------------------------------------------------------------
.         Get the last effective date
.------------------------------------------------------------
EFFD0000  MOVE      SP70,EFTDATE
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY34,KEY26,KEY8,SP70
          PACK      KEY37,KEY34,SP70
          CALL      RSNZBFE1
          CALL      RKNZBFE1
          BRANCH    OVRCD,EFFD2000
          PACK      DIM34A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,NZBFEFDT,SP70
          MATCH     DIM34A,KEY34
          GOTO      EFFD4000 IF EQUAL
.
EFFD2000  CALL      RPNZBFE1                  * check for previous record
          BRANCH    OVRCD,EFFD9999
.
          PACK      DIM26A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,SP70
          MATCH     DIM26A,KEY26
          GOTO      EFFD9999 IF NOT EQUAL
.
EFFD4000  MOVE      NZBFEFDT,EFTDATE         * Save the effective date
EFFD9999  RETURN
.
.*******************************************************************************
.
.        Recalculate theatre fees for the daycase rate
.
DAYTFEE  MOVE       ONE,NOFEE            * Assume we don't find anything
         PACK       KEY9 WITH PRTITCOD,SP5
         PACK       KEY17,KEY9,PMMITDAT,SP70
         OPEN       PATITEM1,"patitemn"
         CALL       PATITMRD
         BRANCH     OVRCD OF DAYTED
.
         MOVE       ZERO,CNTRCEFR
         MOVE       ADATE,CEFFDATE
         PACK       KEY18 WITH ACLAIM,AFUNDH,AFNDTB,ANSD
         CALL       GETTFEES             * Get valid theatre fees
.
         ADD        ONE,FORM2
         LOAD       PMMIAMTP USING FORM2 OF TFPAT1,TFPAT2,TFPAT3:
                                            TFPAT4,TFPAT5,TFPAT6
.
         LOAD       PMMIRBAT USING FORM2 OF TFHF1,TFHF2,TFHF3:
                                            TFHF4,TFHF5,TFHF6
.
         MOVE       PMMIAMTP,PMMIAMTT
         ADD        PMMIRBAT,PMMIAMTT
         MOVE       ZERO,NOFEE
.
DAYTED   RETURN
.
.         Rate not found. Try another combination.
.
DAYTF700  UNPACK     KEY18,TFCLM,TFHFUND,TFHFTAB,ANS
          MATCH      SP6,TFHFUND
          GOTO       DAYTF710 IF NOT EQUAL
.
          MATCH      "0  ",TFCLM
          GOTO       DAYTF720 IF NOT EQUAL
.
          MOVE       ONE,NOFEE
          RETURN
.
.         Try again with a blank health fund
.
DAYTF710  UNPACK     SP20,TFHFUND,TFHFTAB
          GOTO       DAYTF100
.
.         Try again with a zero claim code
.
DAYTF720  MOVE       "0  ",TFCLM
          GOTO       DAYTF100
.
+
.         *******************
.         Casemix theatre fee
.         *******************
.
CMXT0000  OPEN      PATFN1A1,"patfn1af"
          MOVE      ZERO,FORM3
          MOVE      ZERO,NOFEE
          MOVE      FORM2,PTCTITMS          * Item sequence
          MOVE      ACLAIM,PTCTCLMN         * claim code
          MOVE      AFUNDH,PTCTHFND         * health fund
          PACK      KEY8,AFNDTB             * hf table
.
CMXT2200  MOVE      FORM2,PTCTITMS          * Restore item sequence
          PACK      KEY5,ANSC,ANSL,PTCTCLMN
          CALL      RDCODE1
          MOVE      TCFORM6,HFBAND          * default banding from claim code
.
          OPEN      PATFN1A1,"patfn1af"
.
          MATCH     SP70,PTCTHFND
          IF        !@EQUAL
            PACK      KEY14 WITH PTCTHFND,ZERO,ZERO,ZERO,ZERO,SP10
            CALL      RDFUND1
.
            MOVE      "03",KEY2     * check HF history for Theatre charging ind.
            PACK      KEY17,PTCTHFND,KEY2,TSRVDATE,SP70
            CALL      PATDDHRD
          ENDIF
.
          MOVE      HFBAND,THCFLG           * HF banding
.
          MOVE      SP10,PTHFBCAT
          PACK      KEY14 WITH PTCTHFND,KEY8,SP10
          CALL      RDFUND1
          MOVE      PTHFBCAT,PTCTTABT       * health fund table type
.
          PACK      PTCTMBSB,SP20
          PACK      PTCTCASM,SP20
.
.         Check if this is a valid DRG Code
.
          OPEN      PATDGWA1,"patdgwaf"
          PACK      KEY7,PMMIITEM,SP10                                *I-137125
          CALL      RSPTDGW1
          CALL      RKPTDGW1
          BRANCH    OVRCD,CMXT3000          * Invalid DRG code,must be item code
.
          MATCH     PMMIITEM,PTDWDRGC                                 *I-137125
          GOTO      CMXT3000 IF NOT EQUAL   * Invalid DRG code,must be item code
          GOTO      CMXT3200                * DRG Code
.
.         MBS Item -  use banding
.
CMXT3000  LOAD      PTCTMBSB,THCFLG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5,IBAND6:
                                IBAND7,IBAND8,IBAND9,IBAND10,IBAND11,IBAND12:
                                IBAND13,IBAND14,IBAND15,IBAND16
          PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTMBSB:
                          PTCTCASM,DCFLAG,PTCTITMS
          OPEN      PATCTFA2,"patctfaf"
          CALL      RDPTCTF2
          BRANCH    OVRCD,CMXT3400          * Try default
          GOTO      CMXT9000
.
.         DRG Code 
.
CMXT3200  PACK      PTCTCASM,PMMIITEM,SP20
          PACK      KEY33,CONTRCID,PTCTCLMN,PTCTHFND,PTCTTABT,PTCTCASM:
                          PTCTMBSB,DCFLAG,PTCTITMS
          OPEN      PATCTFA1,"patctfaf"
          CALL      RDPTCTF1
.         CLOSE     PATCTFA1
          BRANCH    OVRCD,CMXT3400          * Try default
          MOVE      ZERO,IAMT
          GOTO      CMXT9000
.
CMXT3400  ADD       ONE,FORM3
          BRANCH    FORM3,CMXT3600,CMXT6400
          GOTO      CMXT7000
.
.         Use default
.
CMXT3600  MOVE      ACLAIM,PTCTCLMN
          MOVE      SP10,PTCTHFND           * Try no health fund
          MOVE      SP10,PTCTTABT
          GOTO      CMXT2200
.
CMXT6400  READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      DCLM0000                * Check Hospital claim code charging
          MOVE      PTCNDCLM,PTCTCLMN       * Try default claim
          MOVE      SP10,PTCTHFND           * Try no health fund
          MOVE      SP10,PTCTTABT
          GOTO      CMXT2200
.
CMXT7000  MOVE      ONE,NOFEE               * no fee setup
          GOTO      CMXT9999
.
CMXT9000  MOVE      PTCTDPAT,PMMIAMTP       * Patient portion
          MOVE      PTCTDREB,PMMIRBAT       * Rebate portion
          MOVE      PMMIAMTP,PMMIAMTT
          ADD       PMMIRBAT,PMMIAMTT
CMXT9999  RETURN
+
. *****************************************************************************
. * BRDDY000 : Automatic change to daycase rate for board charging            *
. *****************************************************************************
.
          DEFPROC   BRDDY000
.
          INC       OUTBCHFD/INC             * outpatient charging
          INC       PATCALAG/INC             * data variables for CALCAGE
.
BJDAY     FORM      3
CJDAY     FORM      3
ENDAGE    FORM      3
.
.         Loop through the transfer file and recalculate rate for each record
.
BRDDY000  PACK      AGEDATE,DDATE            * date for age
          CALL      CALCAGE                  * calculate age
.
          PACK      KEY30,AADMNO,SP30        * loop through transfer file
          CALL      RDSTRAN2                 * position in file
BRDDY100  CALL      RDKTRAN2                 * get the next record
          BRANCH    OVRCD,BRDDY999           * no more records
.
          MATCH     AADMNO,TADMN             * correct patient ?
          GOTO      BRDDY999 IF NOT EQUAL    * no, finished patient
.
.         Check for the discharge record
.
          CMATCH    ANSD,TMOVE               * discharge record ?
          GOTO      BRDDY800 IF EQUAL        * yes, treat as a special case
.
.         Calculate the rate for this record, and post away
.
          CALL      DYRAT000                 * get the daycase rate
.
          MOVE      OTBCPPOR,TRATEF          * patient portion
          MOVE      OTBCBPOR,TRATEH          * rebate (board) portion
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,TRBEND              * daycase rate
          MOVE      ZERO,TRATEG              * no government rate
          MOVE      ZERO,TEXTRA              * no extra charge
          MOVE      ZERO,TDISC               * no discount
          MOVE      ZERO,TALLW               * no allowance
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2                  * update with new rate
          GOTO      BRDDY100
.
.         Process the discharge record as a special case. use the rate from
.         the previous record.
.
BRDDY800  MOVE      OTBCPPOR,TRATEF          * patient portion
          MOVE      OTBCBPOR,TRATEH          * rebate (board) portion
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,TRBEND              * daycase rate
          MOVE      ZERO,TRATEG              * no government rate
          MOVE      ZERO,TEXTRA              * no extra charge
          MOVE      ZERO,TDISC               * no discount
          MOVE      ZERO,TALLW               * no allowance
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      WBSEUID,PTTRUUID
.
          CALL      UPTRAN2
.
BRDDY999  GOTO      ENDBRDDY
.
. *****************************************************************************
. * DYRAT000 : Determine daycase rate for the current transfer record         *
. *****************************************************************************
.
DYRAT000  OPEN      OUTBCHA1,"outbchaf"
          PACK      KEY18,ACLAIM,AFUNDH,TATYPE,PSAGEYRS,SP20
          CALL      RSOTBCH1
          CALL      RKOTBCH1
          BRANCH    OVRCD,DYRAT900           * no record -> no rate
.
          MATCH     OTBCCLM,ACLAIM           * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCBRD,AFUNDH           * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCDEPT,TATYPE          * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          MOVE      OTBCAGEG,ENDAGE          * save the ending age in age group
.
.         Get the rate for the number of visits
.
          PACK      KEY18,ACLAIM,AFUNDH,TATYPE,ENDAGE,PTMIPVIS
          CALL      RSOTBCH1
          CALL      RKOTBCH1
          BRANCH    OVRCD,DYRAT900           * no record -> no rate
.
          MATCH     OTBCCLM,ACLAIM           * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCBRD,AFUNDH           * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCDEPT,TATYPE          * correct code ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
          COMPARE   OTBCAGEG,ENDAGE          * correct age group ?
          GOTO      DYRAT900 IF NOT EQUAL    * no -> no rate
.
.         We have the rate
.
          GOTO      DYRAT990
.
.         No rate on file
.
DYRAT900  MOVE      ZERO,OTBCPPOR            * no patient portion
          MOVE      ZERO,OTBCBPOR            * no board portion
.
DYRAT990  
.         CLOSE     OUTBCHA1
.
DYRAT999  RETURN
.
          INC       CALCAGE
          INC       OUTBCHIO/INC
          INC       IBAOVRIO/INC
.
ENDBRDDY  ENDPROC
