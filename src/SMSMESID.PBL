.------------------------------------------------------------------------------
. Include File   : SMSMESID.PBL
.
. Function       : This function will get the next sequence number from IBASEQFD
.                  to use for a SMS message unique id.
.
. Requires       : IBASEQFD to be included and open
.                  WEBERRFD to be included
.
. Usage          : CALL SMSMESID    * Get SMS message ID 
.
. Returns        : Use IBSQNEXT               
.
. Modifications  :
.
. V10.08.00  05/05/2016  Ebon Clements     TSK 0294154
.            Fixed set of maximum
. V10.07.00  12/02/2016  Ebon Clements     TSK 0294154
.            Created include
.------------------------------------------------------------------------------
GSMSID00  MOVE      ZERO,F2
.
GSMSID10  COMPARE   TWENTY,F2                * loop 20 times if ibaseqaf is
          GOTO      GSMSID95 IF NOT LESS     * locked - then error
.
          ADD       ONE,F2
.
          MOVE      "SMSID",KEY8             * read ibaseqaf for the next
          PACK      KEY8,KEY8,SP70           * SMSID sequential number
          CALL      RLIBSEQ1
          BRANCH    OVRCD,GSMSID90,GSMSID10
.
          ADD       ONE,IBSQNEXT
          CALL      UPIBSEQ1
          CALL      UUIBSEQ1
.
          IF        (IBSQNEXT>=IBSQMAXM) 
            MOVE      ZERO,IBSQNEXT            * go back to zero if max reached
            MOVE      "9999999999",IBSQMAXM   
            CALL      UPIBSEQ1
            CALL      UUIBSEQ1
            GOTO      GSMSID10
          ENDIF
.
          GOTO      GSMSID99
.
GSMSID90  MOVE      "SMSID   ",IBSQTYPE     * If no record on ibaseqaf for SMSID
          PACK      KEY8,IBSQTYPE,SP70      
          MOVE      ZERO,IBSQNEXT
          MOVE      "99999999999",IBSQMAXM
          CALL      RAIBSEQ1
          IF        OVRCD=1
            CALL      WRIBSEQ1
          ENDIF
          GOTO      GSMSID00 
.
GSMSID95  DISPLAY   *W1,"ibaseqaf SMS ID record locked."
          MOVE      "ibaseqaf SMS ID record locked.",WBERERR
          CALL      SMSERR00
          MOVE      ZERO,F2
          MOVE      ZERO,IBSQNEXT
          GOTO      GSMSID00
.
GSMSID99  RETURN
.
.------------------------------------------------------------------------------
. Log Error to Web Error Log
.------------------------------------------------------------------------------
SMSERR00  OPEN      WEBERRA1,"weberraf"
          CLOCK     TIME,CTIMEIS
          PACK      KEY24,PRGID,CCC,CYY,CMM,CDD,CTIMEIS,SP70
          REP       " 0",KEY24
          CALL      RDWBER1
          IF        OVRCD=0
            GOTO      SMSERR99
          ENDIF
.
          UNPACK    KEY24,WBERPID,WBERDAT,WBERTIM
          PACK      WBERUID,SP70
          PACK      WBERREP,SP70
          PACK      WBERTMP,SP70
          PACK      WBERURN,SP70
          PACK      WBERVIS,SP70
          MOVE      "1",WBERCNT
          MOVE      SP70,WBERSPA
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRWBER1
          TRAPCLR   IO
.
SMSERR99  RETURN

