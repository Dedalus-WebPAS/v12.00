.************************************************************************
.*  Procedure: DGCLIUMR  -  Send A37 message after UnMerge              *
.*                                                                      *
.*  Author   : Davin Sloan  (22/09/2023)                                *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension of U/R in context)  *
.*             PMSPX2FD (Patient Master Extn.2 of U/R in context)       *
.*             OLDURNO - U/R of patient being UnMerged                  *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V11.03.00  12/09/2023  Davin            TSK 0896442                 *
.*             Created routine                                          *
.************************************************************************
          DEFPROC   DGCLIUMR
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * CIS (HL7) message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND29;*247,PTCNRA37
.
          MOVE      ZERO,IBAMFLAG                * proprietary message = no
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using HL7 interface ?
          IF        !@LESS
            MATCH     "1",PTCNRA37               * yes - sending A37 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for HL7 message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          COMPARE   ONE,CISMFLAG
          GOTO      DGCLI999 IF NOT EQUAL        * not sending messages
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
.         We are sending HL7 messages, so first open the relevant holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"          * HL7 broadcast header table
          OPEN      PMSQPTA1,"pmsqptaf"          * PMI details holding table
          OPEN      PMSQPXA1,"pmsqpxaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      OLDURNO,PMQPOURN             * U/R of patient being UnMerged
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
          MOVE      "A37",H7CIMETP
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          CALL      WRCIS000
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
+
