.*********************************************************************
.* Include : CALLDURN                                                *
.* Desc    : Calculate length of phone call (hh:mm:ss)               *
.* FD Reqd : CALLDURN.INC                                            *
.* Para's  : TIMESTOP      the end time   (HH:MM:SS)                 *
.*           TIMESTRT      the start time (HH:MM:SS)                 *
.* Returns : CALLDURN      the duration (hh:mm:ss)                   *
.*********************************************************************
CALLDURN  MOVE      ZERO,DURATION
          MATCH     TIMESTOP,TIMESTRT
          GOTO      DURN9999 IF EQUAL       * no duration
          GOTO      DURN1000 IF LESS        * times not over midnight
.
.         operation went over midnight so make end time larger
.         eg 22:00 to 01:00 becomes 22:00 to 25:00 which gives 3 hours duration
.
          UNPACK    TIMESTOP,CHOUR,ANS,CMIN,ANS,CSEC
          MOVE      CHOUR,FORM2
          ADD       "24",FORM2
          MOVE      FORM2,CHOUR
          PACK      TIMESTOP,CHOUR,ANS,CMIN,ANS,CSEC
.
.         get end time as seconds
.
DURN1000  UNPACK    TIMESTOP,CHOUR,ANS,CMIN,ANS,CSEC
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM6A            * set up work variable
          MULT      "3600",FORM6A             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ASSIGN    (FORM2*60)+FORM6A,FORM6A
          MOVE      CSEC,ISEC               * get seconds as a form field
          ADD       ISEC,FORM6A            * the total minutes of end time
.
.         get start time as minutes
.
          UNPACK    TIMESTRT,CHOUR,ANS,CMIN,ANS,CSEC
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM6B            * set up work variable
          MULT      "3600",FORM6B             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ASSIGN    (FORM2*60)+FORM6B,FORM6B
          MOVE      CSEC,ISEC              * get seconds as a form field
          ADD       ISEC,FORM6B            * the total minutes of end time
.
.         duration = end seconds - start seconds
.
          SUB       FORM6B,FORM6A           * the duration
          MOVE      FORM6A,DURATION         * the duration
.
. we have the differnce in seconds, so now format seconds into hh:mm:ss
.
          UNPACK    SP6,CHOUR,CMIN,CSEC
.
. check how many hours
.
          ASSIGN    (DURATION/3600),IHOUR
          MOVE      IHOUR,CHOUR               * move to dim field
          IF        IHOUR>0
            ASSIGN    DURATION-(IHOUR*3600),DURATION  * no. seconds remaining
          ENDIF
.
. check how many minutes
.
          ASSIGN    (DURATION/60),IMIN
          MOVE      IMIN,CMIN               * move to dim field
          IF        IMIN>0
            ASSIGN    DURATION-(IMIN*60),DURATION    * no. seconds remaining
          ENDIF
.
          MOVE      DURATION,ISEC           * move to form field
          MOVE      ISEC,CSEC               * move to dim field
.
          REP       " 0",CHOUR
          REP       " 0",CMIN
          REP       " 0",CSEC
          IF        IHOUR=0
            PACK      CALLDURN,SP3,CMIN,COLON,CSEC
          ELSE
            PACK      CALLDURN,CHOUR,COLON,CMIN,COLON,CSEC
          ENDIF
.
DURN9999  RETURN

