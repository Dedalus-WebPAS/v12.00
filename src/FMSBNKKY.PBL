.********************************************************************
.   KBNKFM00 - keyin cheque/receipt number (OLD)
.   Requires  :   FMCHCHQ Cheque Account
.                 FMCHDES Cheque Account Description
.                 IBACRBFD + IO and CREBMAS4 open  (creditor file)
.                 CCHEQREC (0=cheque, 1=receipt, 2=cheques & direct debits)
.********************************************************************
KBNKFM00  MOVE      ZERO,CKEYTYP
          MOVE      FMCHCHQ,FMBNBNK
          MOVE      CCHEQREC,FMBNTYP
          IF        CCHEQREC=2
            MOVE      ZERO,FMBNTYP
          ENDIF
          CALL      KFMBNA00
.
          MOVE      FMBNREF,CRECNO
          MOVE      ZERO,CFORM8
          MOVE      FMBNREF,CFORM8
          MOVE      CFORM8,CHEQNO
          RETURN
.********************************************************************
.   ABNKFM00 - keyin non existing cheque/receipt number (OLD)
.   Requires  :   FMCHCHQ Cheque Account
.                 FMCHDES Cheque Account Description
.                 CCHEQREC (0=cheque, 1=receipt)
.********************************************************************
ABNKFM00  MOVE      ONE,CKEYTYP
          MOVE      FMCHCHQ,FMBNBNK
          MOVE      CCHEQREC,FMBNTYP
          CALL      KFMBNA00
.
          MOVE      FMBNREF,CRECNO
          MOVE      ZERO,CFORM8
          MOVE      FMBNREF,CFORM8
          MOVE      CFORM8,CHEQNO
          RETURN
.********************************************************************
.   XBNKFM00 - keyin cheque/receipt number (may or may not exist)
.   Requires  :   FMCHCHQ Cheque Account
.                 FMCHDES Cheque Account Description
.                 IBACRBFD + IO and CREBMAS4 open  (creditor file)
.                 CCHEQREC (0=cheque, 1=receipt)
.********************************************************************
XBNKFM00  MOVE      TWO,CKEYTYP
          MOVE      FMCHCHQ,FMBNBNK
          MOVE      CCHEQREC,FMBNTYP
          CALL      KFMBNA00
.
          MOVE      FMBNREF,CRECNO
          MOVE      ZERO,CFORM8
          MOVE      FMBNREF,CFORM8
          MOVE      CFORM8,CHEQNO
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSBNKKY
.   Routine   :   QFMBNA00 Cheque/Receipt      
.   Return    :           
.-------------------------------------------------------------------------------
QFMBNA00  OPEN      FMSBNKA5,"fmsbnkaf"
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "12",HTOP
            MOVE      "24",HBOT
            MOVE      "01",HLEF
            MOVE      "80",HRIG
          ENDIF
QFMBNA04
          CALL      GSCRN000
          BRANCH    EXIT,QFMBNA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON," Payment"
.
          MOVE      STDCOL01,STDCOL02
          ADD       " 9",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Creditor"           
.
          MOVE      STDCOL02,STDCOL03
          ADD       "26",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"   Date"               
.
          MOVE      STDCOL03,STDCOL04
          ADD       "11",STDCOL04
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"Status"             
.
          MOVE      STDCOL04,STDCOL05
          ADD       " 7",STDCOL05
          DISPLAY   *PSTDCOL05:STDROW,*V2LON,*ULON,"             Amount"
.
          MOVE      STDCOL05,STDCOL06
          ADD       "14",STDCOL06
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          MOVE      SP70,FMBNREF 
          MOVE      SP70,FMBNUNQ 
          PACK      SAVKEY80,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70               
          SETLPTR   SAVKEY80,16                                                 
          GOTO      QFMBNA50
QFMBNA08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,KEY15
          MOVE      ZERO,CF12P2
QFMBNA10
          IF        HELPOPTN=1       
            CALL      RKFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RKFMBN5 
            ELSE
              CALL      RKFMBN3 
            ENDIF
          ENDIF
QFMBNA12
          BRANCH    OVRCD,QFMBNA15
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA15 IF NOT EQUAL                                       
          CALL      CFMBNA00
          BRANCH    OVRCD,QFMBNA10
.
QFMBNA13
          MATCH     SP70,KEY15
          IF        @EQUAL
            MOVE      FMBNREF,KEY15
            MOVE      ZERO,CF12P2
          ENDIF
          MATCH     FMBNREF,KEY15
          IF        !@EQUAL
            ADD       ONE,XITEMNO
            ADD       ONE,STDROW
            STORE     KEY40,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
            DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
            MATCH     "DD-",KEY15
            IF        @EQUAL
              UNPACK    KEY15,KEY8,KEY7
            ELSE
              UNPACK    KEY15,KEY7,KEY8
            ENDIF
            DISPLAY   *PSTDCOL01:STDROW,KEY8  
            MOVE      APMACN1,KEY25
            DISPLAY   *PSTDCOL02:STDROW,KEY25 
            DISPLAY   *PSTDCOL03:STDROW,CPCDATE 
            MOVE      CSTATUS,KEY6
            DISPLAY   *PSTDCOL04:STDROW,KEY6  
            MOVE      "(999,999,999,999.99 ",KEY20
            FEDIT     CF12P2,KEY20
            DISPLAY   *PSTDCOL05:STDROW,KEY20 
            MOVE      ZERO,CF12P2
          ENDIF
          MOVE      FMBNREF,KEY15
          MOVE      KEY36,KEY40
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      FMBNNAM,APMACN1
          MOVE      FMBNCRE,KEY5
          CALL      RDAPMA1
          ADD       FMBNAMT,CF12P2
          MOVE      FMBNPRE,F1
          MOVE      CSTATUS0,CSTATUS
          LOAD      CSTATUS,F1,CSTATUS1,CSTATUS2
          IF        F1=2
            IF        CF12P2=0
              MOVE      CSTATUS3,CSTATUS
            ENDIF
          ENDIF
          COMPARE   MAXITEM,XITEMNO
          GOTO      QFMBNA10 IF NOT EQUAL
XXXXX     IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          GOTO      QFMBNA16
QFMBNA15
          MATCH     SP70,KEY15
          IF        !@EQUAL
            ADD       ONE,XITEMNO
            ADD       ONE,STDROW
            STORE     KEY36,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
            DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
            MATCH     "DD-",KEY15
            IF        @EQUAL
              UNPACK    KEY15,KEY8,KEY7
            ELSE
              UNPACK    KEY15,KEY7,KEY8
            ENDIF
            DISPLAY   *PSTDCOL01:STDROW,KEY8  
            DISPLAY   *PSTDCOL02:STDROW,APMACN1 
            DISPLAY   *PSTDCOL03:STDROW,CPCDATE 
            MOVE      CSTATUS,KEY6
            DISPLAY   *PSTDCOL04:STDROW,KEY6  
            MOVE      "(999,999,999,999.99 ",KEY20
            FEDIT     CF12P2,KEY20
            DISPLAY   *PSTDCOL05:STDROW,KEY20 
            MOVE      ZERO,CF12P2
          ENDIF
.
QFMBNA16
          UNPACK    SAVKEY80,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ                    
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
QFMBNA20
          CALL      HSCRI000
          BRANCH    EXIT,QFMBNA30,QFMBNA35,QFMBNA40,QFMBNA45,QFMBNA04:
                              QFMBNA91,QFMBNA50
          GOTO      QFMBNA92
QFMBNA30
          IF        HELPOPTN=1       
            CALL      RKFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RKFMBN5 
            ELSE
              CALL      RKFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA85
.
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA85 IF NOT EQUAL                                       
          CALL      CFMBNA00                      * Check Search
          BRANCH    OVRCD,QFMBNA30                * Ovrcd 1=Search More
.
          IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      SP70,KEY15
          GOTO      QFMBNA10
.
QFMBNA35
          MOVE      ST1,KEY36
          CALL      RDFMBN2 
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          PACK      KEY44,FMBNBNK,FMBNTYP,FMBNDAT,FMBNREF,FMBNUNQ,SP70
          PACK      KEY56,FMBNBNK,FMBNTYP,FMBNNAM,FMBNUNQ,SP70                  
          CALL      RDFMBN2 
          CALL      RDFMBN3 
          CALL      RDFMBN5 
          MOVE      SP70,KEY15
QFMBNA36
          IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA85
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          PACK      KEY44,FMBNBNK,FMBNTYP,FMBNDAT,FMBNREF,FMBNUNQ,SP70
          PACK      KEY56,FMBNBNK,FMBNTYP,FMBNNAM,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA85 IF NOT EQUAL                                       
.
          CALL      CFMBNA00                      * Check Search
          BRANCH    OVRCD,QFMBNA36                * Ovrcd 1=Search More
          MATCH     SP70,KEY15
          IF        @EQUAL
            MOVE      FMBNREF,KEY15
          ENDIF
          MATCH     FMBNREF,KEY15
          GOTO      QFMBNA36 IF EQUAL
.
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          MOVE      SP70,KEY15
QFMBNA37
          IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA38
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA38 IF NOT EQUAL                                       
          CALL      CFMBNA00                      * Check Search
          BRANCH    OVRCD,QFMBNA36                * Ovrcd 1=Search More
          MATCH     SP70,KEY15
          IF        @EQUAL
            MOVE      FMBNREF,KEY15
          ENDIF
          MATCH     FMBNREF,KEY15
          GOTO      QFMBNA37 IF EQUAL
          MOVE      FMBNREF,KEY15
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      QFMBNA37 IF NOT EQUAL
QFMBNA38
          MOVE      ZERO,XITEMNO
          MOVE      SP70,KEY15
          GOTO      QFMBNA10
QFMBNA40
          MOVE      ST1,KEY36
          CALL      RDFMBN2 
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          PACK      KEY44,FMBNBNK,FMBNTYP,FMBNDAT,FMBNUNQ,SP70                  
          PACK      KEY56,FMBNBNK,FMBNTYP,FMBNNAM,FMBNUNQ,SP70                  
          CALL      RDFMBN2 
          CALL      RDFMBN3 
          CALL      RDFMBN5 
          IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA85
.
          PACK      KEY36,FMBNBNK,FMBNTYP,SCHFLD01,SP70 
          CALL      RSFMBN2
          PACK      KEY44,FMBNBNK,FMBNTYP,SCHFLD03,SP70
          CALL      RSFMBN3
          PACK      KEY56,FMBNBNK,FMBNTYP,SCHFLD02,SP70
          CALL      RSFMBN5
.
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA85 IF NOT EQUAL                                       
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      QFMBNA08
.
QFMBNA45
          IF        HELPOPTN=1       
            CALL      RKFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RKFMBN5 
            ELSE
              CALL      RKFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA85
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA85 IF NOT EQUAL                                       
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY36,SAVKEY80,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          PACK      KEY36,FMBNBNK,FMBNTYP,Z70
          PACK      KEY44,FMBNBNK,FMBNTYP,Z70
          PACK      KEY56,FMBNBNK,FMBNTYP,Z70
          IF        HELPOPTN=1       
            CALL      RSFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RSFMBN5 
            ELSE
              CALL      RSFMBN3 
            ENDIF
          ENDIF
          MOVE      SP70,KEY15
QFMBNA46
          IF        HELPOPTN=1       
            CALL      RPFMBN2 
          ELSE
            IF        HELPOPTN=2       
              CALL      RPFMBN5 
            ELSE
              CALL      RPFMBN3 
            ENDIF
          ENDIF
          BRANCH    OVRCD,QFMBNA47
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ,SP70                  
          MATCH     SAVKEY80,KEY36                                              
          GOTO      QFMBNA47 IF NOT EQUAL                                       
          CALL      CFMBNA00                      * Check Search
          BRANCH    OVRCD,QFMBNA46                * Ovrcd 1=Search More
          MATCH     SP70,KEY15
          IF        @EQUAL
            MOVE      FMBNREF,KEY15
          ENDIF
          MATCH     FMBNREF,KEY15
          GOTO      QFMBNA46 IF EQUAL
          MOVE      FMBNREF,KEY15
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      QFMBNA46 IF NOT EQUAL
QFMBNA47
          MOVE      ZERO,XITEMNO
          MOVE      SP70,KEY15
          GOTO      QFMBNA10
QFMBNA50  
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
QFMBNA5A  KEYIN     *PSTDCOL01:STDROW,*V2LON,*JR,KEY8:
                    *PSTDCOL01:STDROW,*DV,KEY8
          ENDSET    KEY8
          APPEND    SP70,KEY8
          RESET     KEY8
          MATCH     SP8,KEY8
          GOTO      QFMBNA5B IF EQUAL
          TYPE      KEY8
          GOTO      QFMBNA5B IF EQUAL
          IF        CCHEQREC<>2
            GOTO      QFMBNA5A
          ELSE
            UNPACK    KEY8,KEY3,KEY5
            MATCH     "DD-",KEY3
            GOTO      QFMBNA5A IF NOT EQUAL
            TYPE      KEY5
            GOTO      QFMBNA5A IF NOT EQUAL
            REP       " 0",KEY5
            PACK      KEY8,KEY3,KEY5
            PACK      KEY36,FMBNBNK,FMBNTYP,KEY8,SP7,SP70
            GOTO      QFMBNA5C
          ENDIF
           
QFMBNA5B  PACK      KEY36,FMBNBNK,FMBNTYP,SP7,KEY8,SP70
.
QFMBNA5C  CALL      RSFMBN2
          PACK      SCHFLD01,KEY8,SP70
          MOVE      ONE,HELPOPTN
.
QFMBNA51
          MOVE      ZERO,SCHTST02
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02:
                    *PSTDCOL02:STDROW,*DV,SCHFLD02
          ENDSET    SCHFLD02
          APPEND    SP70,SCHFLD02
          RESET     SCHFLD02
          MATCH     SP70,SCHFLD02
          GOTO      QFMBNA53 IF EQUAL
          MATCH     "*",SCHFLD02
          IF        @EQUAL
            MOVE      ONE,SCHTST02
            RESET     SCHFLD02,2
            MOVE      SCHFLD02,SCHFLD02
            STRIP     SCHFLD02
            REP       UPPLOW,SCHFLD02
          ELSE
            MOVE      ZERO,SCHTST02
            PACK      KEY56,FMBNBNK,FMBNTYP,SCHFLD02,SP70
            CALL      RSFMBN5
            STRIP     SCHFLD02
            MOVE      TWO,HELPOPTN
          ENDIF
QFMBNA53  
          UNPACK    SP70,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          PACK      KEY5,CVERT,CCOL
          MOVE      STDROW,CVERT
          MOVE      STDCOL03,CCOL
          CALL      KEYDATE
          UNPACK    KEY5,KEY2,KEY3
          MOVE      KEY2,CVERT
          MOVE      KEY3,CCOL
          PACK      SCHFLD03,SP70
          MATCH     SP70,CMON
          GOTO      QFMBNA55 IF EQUAL 
.
QFMBNA54  PACK      KEY44,FMBNBNK,FMBNTYP,CCENT,CYEAR,CMON,CDAY,SP70 * go start
          PACK      SCHFLD03,CCENT,CYEAR,CMON,CDAY
          CALL      RSFMBN3
          MOVE      THREE,HELPOPTN
QFMBNA55  
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      QFMBNA08
.
QFMBNA70
          GOTO      QFMBNA75
QFMBNA72  MOVE      ONE,OVRCD
QFMBNA75  RETURN
QFMBNA85
          LOAD      KEY36,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ                       
          CALL      RDFMBN2 
QFMBNA90
          UNPACK    SAVKEY80,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ                    
          BEEP
          GOTO      QFMBNA20
QFMBNA91
          MOVE      SP70,FMBNLED 
          MOVE      SP70,FMBNACC 
          MOVE      SP70,FMBNDAT 
          MOVE      SP70,FMBNPRE 
          MOVE      SP70,FMBNPDT 
          MOVE      ZERO,FMBNAMT 
          MOVE      SP70,FMBNFYR 
          MOVE      SP70,FMBNBCH 
          MOVE      SP70,FMBNLNO 
          MOVE      SP70,FMBNCRE 
          MOVE      SP70,FMBNNAM 
          MOVE      SP70,FMBNSPAR
          MOVE      SP70,KEY36
          MOVE      SP70,FMBNREF 
          GOTO      QFMBNA96
QFMBNA92
          MOVE      SP70,KEY36
          MOVE      SP70,FMBNREF 
          LOAD      KEY36,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY36,FMBNBNK,FMBNTYP,FMBNREF,FMBNUNQ                       
QFMBNA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
QFMBNA99
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSBNKKY
.   Routine   :   KFMBNA00 Keyin Cheque              
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KFMBNA00   TOPIC     "FMBNREF "
.          MATCH     "0",FMBNTYP
.          GOTO      KFMBNB00 IF EQUAL   * Reference Keyin 
.
          MOVE      UNDLN70,KEY8 
          KEYIN     *PCCOL:CVERT,*DV,KEY8:                                   
                    *PCCOL:CVERT,*V2LON,*JR,KEY8:                            
                    *PCCOL:CVERT,*DV,KEY8 
.
          ENDSET    KEY8 
          APPEND    SP70,KEY8 
          RESET     KEY8 
.
          MATCH     SP70,KEY8 
          GOTO      KFMBNA90 IF EQUAL
.
          ENDSET    KEY8 
          CMATCH    EXITCHAR,KEY8 
          GOTO      KFMBNA95 IF EQUAL
.
          CMATCH    QUEST,KEY8 
          GOTO      KFMBNA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      QFMBNA00            * Cheque Help
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMBNREF 
          APPEND    SP70,FMBNREF 
          RESET     FMBNREF 
.
          MATCH     SP70,FMBNREF 
          GOTO      KFMBNA00 IF EQUAL
          MATCH     "DD-",FMBNREF
          IF        @EQUAL
            UNPACK    FMBNREF,KEY8,KEY7
          ELSE
            UNPACK    FMBNREF,KEY7,KEY8
          ENDIF
.
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY8                                 
KFMBNA10
          RESET     KEY8 
          TYPE      KEY8
          GOTO      KFMBNA30 IF EQUAL
.
          IF        CCHEQREC<>2
            GOTO      KFMBNA76
          ENDIF
          UNPACK    KEY8,KEY3,KEY5
          MATCH     "DD-",KEY3
          GOTO      KFMBNA76 IF NOT EQUAL
          TYPE      KEY5
          GOTO      KFMBNA76 IF NOT EQUAL
.
          REP       " 0",KEY5
          PACK      FMBNREF,KEY3,KEY5,SP7
          MOVE      FMBNREF,CORREF
          GOTO      KFMBNA40
.
KFMBNA30  PACK      FMBNREF,SP7,KEY8
          MOVE      FMBNREF,CORREF
.
KFMBNA40  PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,SP70
          CALL      RSFMBN2 
          CALL      RKFMBN2 
          BRANCH    OVRCD,KFMBNA80
          PACK      KEY31,FMBNBNK,FMBNTYP,FMBNREF,SP70
          MOVE      CORREF,FMBNREF
          MATCH     KEY31,KEY36
          GOTO      KFMBNA80 IF NOT EQUAL
          BRANCH    CKEYTYP,KFMBNA75
KFMBNA70  
          MOVE      ZERO,EXIT
          GOTO      KFMBNA99
KFMBNA75  
          MOVE      "Cheque Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMBNA00
KFMBNA76
          MOVE      "Invalid Payment Number - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMBNA00
KFMBNA80
          MOVE      SP70,FMBNLED 
          MOVE      SP70,FMBNACC 
          MOVE      SP70,FMBNDAT 
          MOVE      SP70,FMBNPRE 
          MOVE      SP70,FMBNPDT 
          MOVE      ZERO,FMBNAMT 
          MOVE      SP70,FMBNFYR 
          MOVE      SP70,FMBNBCH 
          MOVE      SP70,FMBNLNO 
          MOVE      SP70,FMBNCRE 
          MOVE      SP70,FMBNNAM 
          MOVE      SP70,FMBNSPAR
          MOVE      SP70,FMBNUNQ
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMBNA99,KFMBNA99
          MOVE      "Cheque Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMBNA00
KFMBNA90
          MOVE      ONE,EXIT
          GOTO      KFMBNA99
KFMBNA95
          MOVE      TWO,EXIT
KFMBNA99  TOPIC     PRGID
          RETURN
.
CFMBNA00  MOVE      ZERO,OVRCD
.
          MATCH     SP70,SCHFLD02
          GOTO      CFMBNA99 IF EQUAL
.
          IF        SCHTST02=1
            MOVE      FMBNNAM,KEY30
            REP       UPPLOW,KEY30
            SCAN      SCHFLD02,KEY30
            IF        !@EQUAL 
              MOVE      ONE,OVRCD
            ENDIF
          ELSE
            MATCH     SCHFLD02,FMBNNAM
            IF        !@EQUAL
              MOVE      ONE,OVRCD
              IF        HELPOPTN=2
                MOVE      ZERO,OVRCD 
              ENDIF
            ENDIF
          ENDIF
.
CFMBNA99  RETURN
.
