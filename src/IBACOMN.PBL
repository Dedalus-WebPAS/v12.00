.******************************************************************************
.         IBACOMN.PBL - This include contains the standard routines that
.                       can be used by most programs.
.******************************************************************************
.
. Mods:
.        V11.02.02  10/02/2022  Jacob Jackson   TSK 0864505                   
.                   Rename 'PRNAME' to 'PRFRNAME' due to conflict 
.        V11.02.01  20/01/2022  Jacob Jackson  TSK 0864505
.                   Added PRNM0000 to determine preferred name format 
.        V11.01.01  10/05/2021  Jill Parkinson Task 0905847
.                   Added check for HL7ORUMS and HL7MDORU in HITENTER
.        V11.00.02  09/04/2020  J.Tan           TSK 0868813
.                   Added PACSNAMX,PACFNAMX (PACFRMT=3)
.        V11.00.01  05/03/2020  Jill Parkinson Task 0879613 
.                   Removed SEXDSC00 
.        V10.10.01  07/04/2017  Peter Vela       TSK 0833050
.                   Added Other to SEXDSC00
.        V10.04.01  05/06/2014  Steve Armstrong  CAR 299293
.                   Added check for HL7RECVR in HITENTER
.         V9.06.01  08/06/2006  Ramaprasad    CAR 107723
.                   Added HEAD132C for handling the dynamic program name
.         V9.03.04  29/06/2004  Peter Vela    CAR 51164
.                   Added functionality for DSEXDE13
. *       V9.03.03  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
. *                 - Add Sex Description routine SEXDSC00 with Code "R" -    *
. *                 "Intersex" added                                          *
.         V9.02.02  02/01/2002  Ebon Clements                     
.                   RESET PRGID after web test in HITENTER
.         V9.02.01  19/11/2001  Dean Cramer                       
.                   Included Web test in HITEXIT
.         V5.07.00  10/09/1998  Glenn Berry      507 Changes
.                   Changed IBACLOCK for Y2K
.
.******************************************************************************
.
.-----------------------------------------------------------------------------
.  This Routine Displays The Top Two Lines Of Screen
.     Old Display was 
.          DISPLAY   *ES,PRGID,*P20:1,CNAME,*P72:1,CDATE:
.                    *P1:2,VERSION,*P20:2,PRGNAM
.-----------------------------------------------------------------------------
DISPHEAD  STRIP     CNAME
          MOVELPTR  CNAME,CCOL
          DIV       TWO,CCOL
          MULT      SEQ,CCOL     
          ADD       "40",CCOL
          DISPLAY   *ES,*HON,SP20,SP20,SP20,SP20:
                    *P1:1,PRGID,"/",VERSION,*PCCOL:1,CNAME:
                    *P66:1,"Date:",CDATE
          PACK      KEY60,PRGNAM,SP20
          CALL      DISPLIN2
          RETURN
.
. Display Line 2 of Standard Screen Heading KEY60=Heading to Display
.-------------------------------------------------------------------------
DISPLIN2  HLINE     *G37,2,1,80
          STRIP     KEY60
          MOVELPTR  KEY60,CCOL
          DIV       TWO,CCOL
          MULT      SEQ,CCOL
          ADD       "38",CCOL
          DISPLAY   *PCCOL:2,SP1,*+,KEY60,SP1
          RETURN
.-----------------------------------------------------------------------------
.  This Routine Clocks The Current Date & Time
.-----------------------------------------------------------------------------
IBACLOCK CLOCK     DAY,JWKDAY
         CLOCK     YEAR,JWKYER
         CLOCK     TIME,CTIMEIS
         CLOCK     CENTURY,JWKCC                 * clock century
.
         CALL      JULCON
.
         PACK      XDATE,CC,YY,MM,DD
         PACK      XCDATE,CCC,CYY,CMM,CDD
         MATCH     XDATE,XCDATE                  * same as current date?
         GOTO      IBCLK999 IF EQUAL             * don't write to controlf
.
         MOVE      DD,CDD
         MOVE      MM,CMM
         MOVE      YY,CYY
         MOVE      CC,CCC
         WRITAB    CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
.
         PACK      CDATE,CDD,SLASH,CMM,SLASH,CCC,CYY
.
         BRANCH    ERRA,IBCLK010
         GOTO      IBCLK020
.
IBCLK010 PACK      CDATE,CMM,SLASH,CDD,SLASH,CCC,CYY
.
IBCLK020 REP       " 0",CDATE
         MATCH     SP8,PGM           *** DONT DISPLAY IF IN ANSWER
         RETURN    IF EQUAL
         DISPLAY   *P71:1,*HON,CDATE
.
IBCLK999 RETURN
.---------------------------------------------------------------------------
.         This Routine allows entry of maintenance options 
.---------------------------------------------------------------------------
SELOPTN   
          HLINE     *G37,2,70,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete"
.
          KEYIN     *P1:9,*EL,"Select Option : _",*P17:9,*V2LON,COPTION
          COMPARE   ZERO TO COPTION
          GOTO      EXITOPTN IF EQUAL
.
          BRANCH    COPTION OF VALOPTN,VALOPTN,VALOPTN
          BEEP
          GOTO      SELOPTN
.
VALOPTN   LOAD      DISPOPT WITH COPTION OF ADDOPT,CHGOPT,DELOPT
          DISPLAY   *P70:2,*V2LON,SP1,DISPOPT,SP1
.
EXITOPTN
          MOVE      COPTION,OPTION
          RETURN
.------------------------------------------------------------------------
. This Routine Ok to Post (Y/N/C)
.------------------------------------------------------------------------
POSTQST   DISPLAY   *P1:24,*EL,"Ok to Post (":
                    *V2LON,ANSY,*HOFF,SLASH:
                    *V2LON,ANSN,*HOFF,SLASH:
                    *V2LON,ANSC,*HOFF,") ? ";
.
          MOVE      "22",CVERT
.
          GOTO      KEYANS
.
CONTQST   DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH:
                    *V2LON,ANSN,*HOFF,SLASH:
                    *V2LON,ANSC,*HOFF,") ? ";
.
          MOVE      "26",CVERT
.
          GOTO      KEYANS
.
DELQST    DISPLAY   *P1:24,*EL,"Ok to Delete (":
                    *V2LON,ANSY,*HOFF,SLASH:
                    *V2LON,ANSN,*HOFF,SLASH:
                    *V2LON,ANSC,*HOFF,") ? ";
.
          MOVE      "24",CVERT
.
KEYANS    MOVE      SP1,ANS
          KEYIN     *PCVERT:24,*DV,SP3,*PCVERT:24,*V2LON,ANS:
                    *PCVERT:24,*DV,ANS;
          TYPE      ANS
          GOTO      KEYYNCE IF EQUAL
.
.         Convert to number
.
          REP       "Y1N2C3y1n2c30a1a2a3a4a5a6a7a8a9a",ANS
.
.         Check for valid input
.
          MOVE      ANS,CEXIT
          TYPE      ANS
          RETURN    IF EQUAL
.
.         Invalid input
.
KEYYNCE
          BEEP
          GOTO      KEYANS   
+
.******************************************************************************
.         Subroutine to SELECT ITEM, POST or CANCEL
.------------------------------------------------------------------------------
.
MAINQST   MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MAINQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      MAINQST1 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      MAINQST2 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MAINQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MAINQSTE IF LESS
.
          RETURN
.
MAINQSTE  BEEP
          GOTO      MAINQST
.
MAINQST1  MOVE      "-1",CCITEM
          RETURN
.
MAINQST2  MOVE      "0",CCITEM
          RETURN
+
.******************************************************************************
.         Subroutine to SELECT ITEM, POST or CANCEL
.------------------------------------------------------------------------------
.
MAINQSTC  MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost ? ":
                    *V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MAINQSTC IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  P",CCITEMD
          GOTO      MAINQST3 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MAINQSTF IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MAINQSTF IF LESS
.
          RETURN
.
MAINQSTF  BEEP
          GOTO      MAINQSTC
.
MAINQST3  MOVE      "0",CCITEM
          RETURN
+
.***************************************************************************
.  This routine prints standard headings for both 80 & 132 column reports
.---------------------------------------------------------------------------
.
HEAD80    BRANCH   CNOUNDLN,HEAD80A          * No underlines
          COMPARE  ZERO,CPAGENO              * First page ?
          GOTO     HEAD80A IF EQUAL          * Yes. Don't print underlines
.
          CALL     UND80                     * Print the last line of prev page
.
HEAD80A   ADD      ONE,CPAGENO
.
          RESET    CPHDROPT                  * Do we have a header option ?
          GOTO     HEAD80B IF EOS            * No.
.
          MATCH    SP20,CPHDROPT             * Do we have a header option ?
          GOTO     HEAD80B IF EQUAL          * No.
.
          PRINT    *F,PRGID,*20,CNAME,*64,"DATE : ",CDATE:
                   *N,VERSION,*20,PRGNAM,SP1,*+,CPHDROPT:
                      *64,"TIME :   ",CTIMEIS:
                   *N,*64,"PAGE :        ",CPAGENO
          GOTO     HEAD80Z
.
HEAD80B   PRINT    *F,PRGID,*20,CNAME,*64,"DATE : ",CDATE:
                   *N,VERSION,*20,PRGNAM,*64,"TIME :   ",CTIMEIS:
                   *N,*64,"PAGE :        ",CPAGENO
.
HEAD80Z   CALL     IBACLOCK
          MOVE     THREE,CLNO
          RETURN
.
HEAD132   BRANCH   CNOUNDLN,HEAD132A         * No underlines
          COMPARE  ZERO,CPAGENO              * First page ?
          GOTO     HEAD132A IF EQUAL         * Yes. Don't print underlines
.
          CALL     UND132                    * Print the last line of prev page
.
HEAD132A  ADD      ONE,CPAGENO
.
          RESET    CPHDROP1                  * ADDED
          IF       !@EOS
            MATCH    SP70,CPHDROP1             * ADDED
            GOTO     HEAD132C IF NOT EQUAL     * ADDED
          ENDIF
.
          RESET    CPHDROPT                  * Do we have a header option ?
          GOTO     HEAD132B IF EOS           * No.
.
          MATCH    SP20,CPHDROPT             * Do we have a header option ?
          GOTO     HEAD132B IF EQUAL         * No.
.
          PRINT    *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                   *N,VERSION,*40,PRGNAM,SP1,*+,CPHDROPT:
                   *116,"TIME :   ",CTIMEIS:
                   *N,*116,"PAGE :        ",CPAGENO
          GOTO     HEAD132Z
.
HEAD132B  PRINT    *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                   *N,VERSION,*40,PRGNAM,*116,"TIME :   ",CTIMEIS:
                   *N,*116,"PAGE :        ",CPAGENO
          GOTO      HEAD132Z
.         
HEAD132C  RESET    CPHDROP2                  * ADDED
          PRINT    *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                   *N,VERSION,*40,CPHDROP1,*116,"TIME :   ",CTIMEIS:
                   *N,*40,CPHDROP2,*116,"PAGE :        ",CPAGENO
.
HEAD132Z  CALL     IBACLOCK
          MOVE     THREE,CLNO
          RETURN
.
UND80     PRINT    *1,"*---------------------------------------":
                      "---------------------------------------*"
          ADD      ONE,CLNO 
          RETURN 
.
UND132    PRINT    "*---------------------------------------":
                   "----------------------------------------":
                   "----------------------------------------":
                   "-----------*"
          ADD      ONE,CLNO
          RETURN
.---------------------------------------------------------------------------
.  This routine asks the standard "Hit <ENTER> to continue" message
.---------------------------------------------------------------------------
HITENTER  MATCH     "IBARSH",PGM
          RETURN    IF EQUAL
.
          SCAN      "WEB",PRGID
          RESET     PRGID
          RETURN    IF EQUAL
.
          MATCH     "HL7MDORU",PRGID
          RETURN    IF EQUAL
.
          MATCH     "HL7ORUMS",PRGID
          RETURN    IF EQUAL
.
          MATCH     "HL7RECVR",PRGID
          RETURN    IF EQUAL
.
          KEYIN     "Hit <",*V2LON,"ENTER",*HOFF,"> to continue ":
                    *EOFF,ANS,*P1:24,*EL;
          RETURN
.---------------------------------------------------------------------------
.  This routine asks the standard "Hit <ENTER> to exit" message
.---------------------------------------------------------------------------
HITEXIT   MATCH     "IBARSH",PGM
          RETURN    IF EQUAL
          KEYIN     "Hit <",*V2LON,"ENTER",*HOFF,"> to exit ":
                    *EOFF,ANS,*P1:24,*EL;
          RETURN
.---------------------------------------------------------------------------
.  Standard error message for locked program
.---------------------------------------------------------------------------
PRGLOCK  DISPLAY    *P1:24,*EL,*B,PRGNAM," In Progress. ";
         CALL       HITENTER
         RETURN
.---------------------------------------------------------------------------
.  Standard error message for locked record
.---------------------------------------------------------------------------
RECLOCK  DISPLAY    *P1:24,*EL,*B,"Record Locked. ";
         CALL       HITENTER
         RETURN
.---------------------------------------------------------------------------
.  Standard error message for locked record
.---------------------------------------------------------------------------
REXISTS  DISPLAY    *P1:24,*EL,*B,"Record Exists. ";
         CALL       HITENTER
         RETURN
.
+
.***************************************************************************
.  This routine formats a name into the requested format.
.     PACFRMT = 1 -> Title Given_names Surname
.               2 -> Surname, Title Given_names
.               3 -> Title Given_names Surname (PTMASNAM)
.---------------------------------------------------------------------------
.
PACNAME   PACK      PACFNAME,SP30,SP30       * Initialise to blanks.
          PACK      PACFNAMX,SP70,SP70
.
          BRANCH    PACFRMT,PACNA100,PACNA500,PACNA800
          GOTO      PACNA999
.
.         Format 1. Title Given_names Surname
.
PACNA100  CLEAR     PACFNAME
          APPEND    PACTITLE,PACFNAME        * Append title
.
.         Find the end of the title
.
PACNA150  CMATCH    SP1,PACFNAME
          GOTO      PACNA200 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNA150 IF NOT EOS
.
.         No title. Go directly to adding the given name
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNA210
.
.         Found the end of the title. Add the given name
.
PACNA200  APPEND    SP1,PACFNAME             * Add a blank after the title
PACNA210  APPEND    PACGNAME,PACFNAME        * Append the given names
.
.         Find the end of the given names
.
PACNA250  CMATCH    SP1,PACFNAME
          GOTO      PACNA300 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNA250 IF NOT EOS
.
.         No title or given name. Go directly to adding the surname
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNA310
.
.         Found the end of the given names. Add the surname
.
PACNA300  APPEND    SP1,PACFNAME             * Add a blank after the title
PACNA310  APPEND    PACSNAME,PACFNAME        * Append the surname
          APPEND    SP30,PACFNAME            * Pad out with spaces
          APPEND    SP30,PACFNAME            * Pad out with spaces
          RESET     PACFNAME
          GOTO      PACNA999
.
.         Format 2. Surname, Title Given_names
.
PACNA500  CLEAR     PACFNAME
          APPEND    PACSNAME,PACFNAME        * Append surname
.
.         Find the end of the surname
.
PACNA550  CMATCH    SP1,PACFNAME
          GOTO      PACNA600 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNA550 IF NOT EOS
.
.         No surname. Go directly to adding the title
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNA610
.
.         Found the end of the surname. Add the title
.
PACNA600  APPEND    ", ",PACFNAME            * Add a blank after the surname
PACNA610  APPEND    PACTITLE,PACFNAME        * Append the title
.
.         Find the end of the title
.
PACNA650  CMATCH    SP1,PACFNAME
          GOTO      PACNA700 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNA650 IF NOT EOS
.
.         No title or surname. Go directly to adding the given names
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNA710
.
.         Found the end of the title. Add the given names
.
PACNA700  APPEND    SP1,PACFNAME             * Add a blank after the title
PACNA710  APPEND    PACGNAME,PACFNAME        * Append the given names
          APPEND    SP30,PACFNAME            * Pad out with spaces
          APPEND    SP30,PACFNAME            * Pad out with spaces
          RESET     PACFNAME
          GOTO      PACNA999
.
.         Format 3. Title Given_names Surname (PTMASNAM)
.
PACNA800  CLEAR     PACFNAMX
          APPEND    PACTITLE,PACFNAMX        * Append title
.
.         Find the end of the title
.
PACNA850  CMATCH    SP1,PACFNAMX
          GOTO      PACNA860 IF NOT EQUAL
.
          BUMP      PACFNAMX,-1              * Try the previous character
          GOTO      PACNA850 IF NOT EOS
.
.         No title. Go directly to adding the given name
.
          CLEAR     PACFNAMX                 * Make sure the name is cleared
          GOTO      PACNA870
.
.         Found the end of the title. Add the given name
.
PACNA860  APPEND    SP1,PACFNAMX             * Add a blank after the title
PACNA870  APPEND    PACGNAME,PACFNAMX        * Append the given names
.
.         Find the end of the given names
.
PACNA880  CMATCH    SP1,PACFNAMX
          GOTO      PACNA890 IF NOT EQUAL
.
          BUMP      PACFNAMX,-1              * Try the previous character
          GOTO      PACNA880 IF NOT EOS
.
.         No title or given name. Go directly to adding the surname
.
          CLEAR     PACFNAMX                 * Make sure the name is cleared
          GOTO      PACNA892
.
.         Found the end of the given names. Add the surname
.
PACNA890  APPEND    SP1,PACFNAMX             * Add a blank after the title
PACNA892  APPEND    PACSNAMX,PACFNAMX        * Append the surname
          APPEND    SP30,PACFNAMX            * Pad out with spaces
          APPEND    SP30,PACFNAMX            * Pad out with spaces
          RESET     PACFNAMX
          PACK      PACFNAME,PACFNAMX,SP70
          GOTO      PACNA999
.
PACNA999  RETURN
.
.------------------------------------------------------------
. Routine     : GETSCR00
.
. Function    : Save Screen Display 
.
. Called by   : All Computer Generated Help Screens
.               Should be used in any Save Screen Situation
.
. Returns     : EXIT = 0 Save Ok
.               EXIT = 1 No Save Screens Left
.
. Requires    : Screen Positions HLEF,HTOP,HRIG,HBOT
.
.------------------------------------------------------------
GETSCR00  MOVE      ONE,CSSCRNUM
          MOVE      ZERO,EXIT
.
          WHILE     CSCRFREE[CSSCRNUM]
            IF        CSSCRNUM=25 
              GOTO      GETSCR10
            ENDIF
            ADD       ONE,CSSCRNUM
          DO                
.
          IF        HLEF=0
            MOVE    "1",HLEF
            MOVE    "1",HTOP
            MOVE    "80",HRIG
            MOVE    "24",HBOT
          ENDIF
          GETSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          MOVE      ONE,CSCRFREE[CSSCRNUM]
          GOTO      GETSCR99
.         
GETSCR10  MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,"Screen Save Limit Reached - ";
          CALL      HITENTER
          GOTO      GETSCR99
.
GETSCR99  RETURN
.
.------------------------------------------------------------
. Routine     : FRESCR00
.
. Function    : Free Screen Display 
.
. Called by   : All Computer Generated Help Screens
.               Should be used in any Save Screen Situation
.
.------------------------------------------------------------
.
FRESCR00  MOVE      ZERO,CSCRFREE[CSSCRNUM]
          SUB       ONE,CSSCRNUM
          RETURN
.------------------------------------------------------------
. Routine     : PUTSCR00
.
. Function    : Restore Screen Display & Free Screen Number
.
. Called by   : All Computer Generated Help Screens
.               Should be used in any Save Screen Situation
.
. Requires    : Screen Positions HLEF,HTOP,HRIG,HBOT
.
.------------------------------------------------------------
PUTSCR00 
          PUTSCR    CSSCRNUM
          CALL      FRESCR00
.
          RETURN
.
.------------------------------------------------------------
. Display Message on Line 24
.------------------------------------------------------------
MESSAGE1  GETVAR    LINE24,ATTR24,1,24
          RESET     DISPMSG
          STRIP     DISPMSG
          ENDSET    DISPMSG
          APPEND    SP1,DISPMSG
          RESET     DISPMSG
          DISPLAY   *P1:24,*EL,*+,DISPMSG;
          CALL      HITENTER
          PUTVAR    LINE24,ATTR24,1,24
          RETURN
.
.------------------------------------------------------------
. Display Message on Line 24 Continue (Y/N) EXIT 1=NO 2=YES
.------------------------------------------------------------
MESSAGE4  GETVAR    LINE24,ATTR24,1,24
          RESET     DISPMSG
          STRIP     DISPMSG
          ENDSET    DISPMSG
          APPEND    SP1,DISPMSG
          RESET     DISPMSG
          MOVE      ZERO,EXIT
.
          REPEAT
            DISPLAY   *P1:24,*EL,*+,DISPMSG;
            MOVE      "NY",KEY2
            MOVE      SP70,YESNO
            MOVE      DEFYESNO,ANS                 * get default
            KEYIN     " Continue (",*V2LON,"Y",*HOFF,"/":
                      *V2LON,"N",*HOFF,") ? ",*V2LON,*RV,ANS;
            REP       UPPLOW,ANS
            SCAN      ANS,KEY2
            IF        @EQUAL
              MOVEFPTR  KEY2,EXIT
            ELSE
              BEEP
            ENDIF
          UNTIL     EXIT>0
.
          PUTVAR    LINE24,ATTR24,1,24
          MOVE      SP70,DEFYESNO
          RETURN
.
.**********************************************************
.   This routine formats preferred names based on option
.   PRENAOPT = 1 -> [First_name]
.              2 -> [LAST_NAME]
.              3 -> [LAST_NAME, First_name]
.----------------------------------------------------------
PRNM0000  BRANCH    PRENAOPT,PRNM0100,PRNM0200,PRNM0300
.
PRNM0100  STRIP     PREGNAME
          MOVE      PREGNAME,PRNAMES
          PACK      PRFRNAME,LFTSQBRK,PRNAMES,RGTSQBRK
          GOTO      PRNM9999
.
PRNM0200  STRIP     PRESNAME
          REP       UPPLOW,PRESNAME
          MOVE      PRESNAME,PRNAMES
          PACK      PRFRNAME,LFTSQBRK,PRNAMES,RGTSQBRK
          GOTO      PRNM9999
.
PRNM0300  STRIP     PRESNAME
          STRIP     PREGNAME
          REP       UPPLOW,PRESNAME
          APPEND    PRESNAME,PRNAMES
          APPEND    ", ",PRNAMES
          APPEND    PREGNAME,PRNAMES
          RESET     PRNAMES
          PACK      PRFRNAME,LFTSQBRK,PRNAMES,RGTSQBRK
          GOTO      PRNM9999
.
PRNM9999  RETURN

