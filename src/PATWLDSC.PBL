.******************************************************************************
. Include Name    : PATWLDSC
. Description     : Option to put patient back on W/List after Discharge
. Author          : Glenn Berry
. Date            : 08/11/1993
. Comments        : This routine will give the option to put a patient back on
.                   the W/List after being discharged, if :
.                     The parameter is on;
.                     The patient did not die;
.                     The admission has a W/List record.
.******************************************************************************
. Parameters      : PURNO 
.                   AADMNO
.******************************************************************************
.         V12.00.01 22/05/2025  J.Tan          TSK 0955096
.                   Added Alphanumeric visit number changes
.****************************************************************************** 
PATWLDSC  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY7;*200,PTCNWLDC
          BRANCH    PTCNWLDC,PTWLD000        * parameter on?
          GOTO      PTWLD999               
.
PTWLD000  
.------   Check if discharge is a death
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1
          BRANCH    OVRCD,PTWLD910           * No discharge record
.
          MOVE      SP1,TCINDC1
          PACK      KEY5,ANSD,SP1,DSTAT
          CALL      RDCODE1
.
          MATCH     ANSD,TCINDC1             * Death?
          GOTO      PTWLD999 IF EQUAL        * yes, forget it.
.
.------   Check if patient is from Waiting List
.
          OPEN      WATTR1A1,"wattr1af"                 * Open file
          PACK      KEY19,PURNO,SP20         * position in wattr1af
          CALL      RDSTREA1
.
PTWLD010  CALL      RDKTREA1
          BRANCH    OVRCD,PTWLD999           * eof?
          MATCH     WMURNO,PURNO             * same patient?
          GOTO      PTWLD999 IF NOT EQUAL    
.
          MATCH     WMBOOK,AADMNO            * correct admission?
          GOTO      PTWLD010 IF NOT EQUAL
.
.------   Ask question
.
PTWLD020  KEYIN     *P1:24,*EL,"Do you want to put patient back on ":
                               "Waiting List? (Y/N) ",*V2LON,ANS
          REP       "yYnN",ANS               * upcase
          MATCH     "Y",ANS
          GOTO      PTWLD030 IF EQUAL
          MATCH     "N",ANS
          GOTO      PTWLD999 IF EQUAL
.
          BEEP
          GOTO      PTWLD020
.
.------   Keyin Reason
.
PTWLD030  DISPLAY   *P1:24,*EL,"Enter Reason for putting patient back on W/L :"
.
          MOVE      "24",EVERT
          MOVE      "48",ECOL
          MOVE      "RW",CODE
          MOVE      SP3,CKYIDEF3             * no default
          CALL      PATCODKY                 * keyin code
          BRANCH    EXIT,PTWLD030,PTWLD020
.
.------   Update Discharge file 
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                  * read variables
          BRANCH    OVRCD,PTWLD910           * missing discharge record
          MOVE      ACODE,DWLREASN          
          CALL      UPDSCH1
          MOVE      SP3,DWLREASN             * initialize field
.
.------   Update W/L
.
          CALL      SAVHIS00                     * save history
          MOVE      WMDATE3,SAVESCHA
          MOVE      ONE,WMSTAT               * Unscheduled
          MOVE      SP20,WMDATE3             * no schedule admission date
          CALL      GENBOKNO           * generate booking number
.
          CALL      UPTREA1                  * update wattr1af
          CALL      GETRSC00                     * get reason  
          CALL      WRTHIS00                     * write to history
.
.
          DISPLAY   *P1:24,*EL,"Waiting List Updated.  ";
          CALL      HITENTER
          GOTO      PTWLD999
.
.------   Error Messages
.
PTWLD910  DISPLAY   *P1:24,*EL,*B,"ERROR : Missing Discharge Record. ";
          CALL      HITENTER
          GOTO      PTWLD999
.
PTWLD999  RETURN
+
.
