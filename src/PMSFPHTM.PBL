.----------------------------------------------------------------------
. Modifications
.
. V10.11.01  17/07/2017  Peter Vela    TSK 0837619
.            Created include
.
.------------------------------------------------------------------------
.   Clear Parameters for Health Fund Payee Table
.------------------------------------------------------------------------
CLRFPH00  MOVE      SP70,PMFPH001
          MOVE      SP70,PMFPH002
          MOVE      SP70,PMFPH003
.
CLRFPH99  RETURN
+
.------------------------------------------------------------
. Output Health Fund Payee Fields
.------------------------------------------------------------
PMFH0000  BRANCH    FLDITMNO,PMFH0100,PMFH0200,PMFH0300,PMFH0400,PMFH0500:
                             PMFH0600
.
          GOTO      PMFH9999
.
PMFH0100  WRITE     HTMLFILE;PMFPHCPC;
          GOTO      PMFH9999
.
PMFH0200  MATCH     SP70,PMFPHCPC
          GOTO      PMFH9999 IF EQUAL
          GOTO      PMFH9999 IF EOS
.
          PACK      KEY10,PMFPHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PMFH9999
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      PMFH9999
.
PMFH0300  WRITE     HTMLFILE;PMFPHFND;
          GOTO      PMFH9999
.
PMFH0400  MATCH     SP70,PMFPHFND
          GOTO      PMFH9999 IF EQUAL
          GOTO      PMFH9999 IF EOS
.
          PACK      KEY14,PMFPHFND,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,PMFH9999
.
          WRITE     HTMLFILE;HFNAME;
          GOTO      PMFH9999
.
PMFH0500  WRITE     HTMLFILE;PMFPFPID;
          GOTO      PMFH9999
.
PMFH0600  CALL      FPTAB000
          GOTO      PMFH9999
.
PMFH9999  RETURN
+
.------------------------------------------------------------
.   Set Parameters for Health Fund Payee table
.------------------------------------------------------------
SPMFPH00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPMFPH01,SPMFPH02,SPMFPH03
.
          MOVE      ONE,EXIT
          GOTO      SPMFPH99
.
SPMFPH01  MOVE      QRYDATA,PMFPH001
          GOTO      SPMFPH99
.
SPMFPH02  MOVE      QRYDATA,PMFPH002
          GOTO      SPMFPH99
.
SPMFPH03  MOVE      QRYDATA,PMFPH003
          GOTO      SPMFPH99
.
SPMFPH99  RETURN
+
.------------------------------------------------------------------------
.         Move work variables to Health Fund Payee file variables
.------------------------------------------------------------------------
MVPMSFPH  PACK      PMFPHCPC,PMFPH001,SP70
          PACK      PMFPHFND,PMFPH002,SP70
          PACK      PMFPFPID,PMFPH003,SP70
          RETURN
+
.------------------------------------------------------------------------
.         Fund Payee Id List
.------------------------------------------------------------------------
FPTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY16,PMHCHCPC,SP70     * startkey for Goto Button
          CALL      RSPMFPH1                * positional read
FPTAB100  CALL      RKPMFPH1                * read a record
          BRANCH    OVRCD,FPTAB999          * end of file
.
          MATCH     PMHCHCPC,PMFPHCPC
          GOTO      FPTAB999 IF NOT EQUAL
.
          CALL      FPROW000                * Display Row
          GOTO      FPTAB100
.
FPTAB999  RETURN
.-----------------------------------------------------------------
. Fund Payee Id (TABLE ROW)
.------------------------------------------------------------
FPROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowFundPayee":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pmfph001=",PMHCHCPC:
                             "&pmfph002=",PMFPHFND,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             PMFPHFND,"</td>":
                             "<td align=left>&nbsp",PMFPFPID,"</td>":
                             "</tr>"
.
FPROW999 RETURN
