.****************************************************************************** 
.*                                                                            *
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     Program Name  :   IBAAAE15                                             *
.*                                                                            *
.*     Function      :   A&E Completion Listing                               *
.*                                                                            *
.*     Author        :   Karin Peak                                           *
.*                                                                            *
.*     Modifications :                                                        *
.*       V10.04.02  12/06/2014  Steve Armstrong    CAR 301639                 *
.*                  Mods to call KILLIDX prior to exiting the program         *
.*       V10.04.01  07/03/2014  Ania P             CAR 261630                 *
.*                  Remove the use of PORT for temp file names                *
.****************************************************************************** 
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  11/01/1999  Jill Habasque                                 *
.*                  Fixed header date display                                 *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*        V5.03.01  16/04/1996  Howellsy   5.04                               *
.*                  Changed Accident & Emergency to Emergency.                *
.******************************************************************************
.*        V5.01.01  16/05/89 K.Peak                                           *
.*                  New Zealand Changes                                       *
.*        V5.01.02  06/06/90 D.Di.Paola                                       *
.*                  Recompiled for changes to AAECONFD                        *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 25/06/93 J.TanSRF122173                                   *
.*                  Mods to check overcondition in master file                *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAECONFD/INC
.
          INC       PATMA1FD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
.
PSTROL    FILE      ASCII, FIXED=256
SORT1     INIT      "u1-20,53-77,29-36,21-28"
.
TEMPFILE  IFILE  SQL, FIXED=82, KEY=SORT1
.
. --------- Tempfile consists of following vars ---------
.
.Name          Type        Length     Physical      Start
.----          ----        ------     --------      -----
.PSNAME        DIM            20           20           1
DUNIQUE        DIM             8            8          21
.ADANUMB       DIM             8            8          29
.ADAURNO       DIM             8            8          37
.ADADATE       DIM             8            8          45
.PGNAME        DIM            25           25          53
.PTITL         DIM             4            4          78
.
.--------EOR 82 --------
.
FNAMET    DIM       8
UNIQUE    FORM      8
CMDLINE   DIM       50
.
ACTION    DIM       1
.
BCENT     DIM       2
BYEAR     DIM       2
BMON      DIM       2
.
CHG       FORM      1
CODE      DIM       2 
CNUMB     DIM       3
CURDATE   DIM       8
.
DIM3      DIM       3
.
FIELD     FORM      1
FLAG      DIM       1
FROMDATE  DIM       8
.
.
MODE      DIM       1
MODE1     DIM       1
.
PNAME     DIM       46
.
SP40      INIT      "                                        "
.
TODATE    DIM       8
TOTPATS   FORM      8
.
XCENT1    DIM       2
XYEAR1    DIM       2
XMON1     DIM       2
XDAY1     DIM       2
.
VAR       FORM      1
.
PRGID     INIT      "IBAAAE15"
PRGNAM    INIT      "Emergency Completion Listing"
VERSION   INIT      "V12.00.00"
+
******************************************************************************
START     CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening aaede1af";
          OPEN      AAEDE1A3,"aaede1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM
          PACK      FNAMET,TFILNAME,SP70
.
          READ      CONTROLF,THIRTY;*168,HNUMDES
.
          RESET     HNUMDES,9
NEXT60    CMATCH    SP1,HNUMDES
          GOTO      NEXT61 IF NOT EQUAL
          BUMP      HNUMDES BY -1
          GOTO      NEXT61 IF EOS
          GOTO      NEXT60
NEXT61    MOVEFPTR  HNUMDES,VAR
          RESET     HNUMDES,VAR
          LENSET    HNUMDES
          RESET     HNUMDES
.
.         Start of program
.
START0    MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          HLINE     *G37,2,54,80
          DISPLAY   *P39:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date Sequence"
.
          KEYIN     *P1:7,"Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
START1    DISPLAY   *P1:12,*EF,"From Date : ":
                    *P1:14,"To   Date : "
.
          UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
KIBA25    MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
FIXMON    CALL      KEYDATE
          BRANCH    OVRCD,START0
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
KIBA26    UNPACK    SP6 INTO CYEAR,CMON,CDAY
          MOVE      TEN5,CCOL
          MOVE      TEN4,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
FIXMON2   CALL      KEYDATE
          BRANCH    OVRCD,KIBA25
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      ERR1 IF LESS
          GOTO      REKEY
.
ERR1      DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** FROM Date Greater than TO Date ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      START1
.
.         Ok to Continue
.
REKEY     KEYIN     *P1:24,*EF,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,*DV,SLASH:
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
          CMATCH    "Y",ANS
          GOTO      OKCONT IF EQUAL
.
          CMATCH    "N",ANS
          GOTO      START0 IF EQUAL
.
          BEEP 
          GOTO      REKEY 
.
OKCONT    CALL      KILLIDX
          CALL      CREADIX
. 
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE,UNIQUE
. 
          MOVE      "60",CLNO
. 
          DISPLAY   *P1:24,*EL,*P15:24,HNUMDES," : "
. 
          PACK      KEY21 WITH FROMDATE,SP20
          CALL      RDSDETA3
NEXTD     CALL      RDKDETA3
          BRANCH    OVRCD,SORTD
.
          DISPLAY   *P29:24,*V2LON,ADANUMB
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NOMAST
.
          MATCH     ADADATE,TODATE
          GOTO      SORTD IF LESS
.
          MATCH     SP3,ADAWAIT
          GOTO      NEXTD IF NOT EQUAL
.
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY61 WITH PSNAME,PGNAME,ADANUMB,DUNIQUE
          WRITE     TEMPFILE,KEY61;PSNAME,DUNIQUE,ADANUMB,ADAURNO,ADADATE:
                                   PGNAME,PTITL
          ADD       ONE,UNIQUE
          GOTO      NEXTD
.
NOMAST    DISPLAY   *P1:24,*B,*EL,"** Missing Master Record for U/R No.: ":
                    *V2LON,ADAURNO;
          CALL      HITENTER
          GOTO      NEXTD
.
NOOPEN    DISPLAY   *P1:24,*B,*EL,"** TEMPORARY STATISTICS FILE ":
                                 FNAMET," NOT FOUND ** ";
          CALL      HITENTER
.
SORTD     MOVE      UNIQUE,TOTPATS
          SUB       ONE,TOTPATS
.
          DISPLAY   *P1:24,*EL,*P20:24,"** Processing Statistics **",*W;
.
          CLOSE     TEMPFILE
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          PACK      KEY61,SP40,SP40
          READ      TEMPFILE,KEY61;;
.
          DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :"
.
READLOP   READKS    TEMPFILE;PSNAME,DUNIQUE,ADANUMB,ADAURNO:
                             ADADATE,PGNAME,PTITL
          GOTO      ENDP IF OVER
.
          MOVE      DUNIQUE,UNIQUE
.
          DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
          CALL      DISPDA
          GOTO      READLOP
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          ENDSET    PSNAME
NEXT70    CMATCH    SP1,PSNAME
          GOTO      NEXT71 IF NOT EQUAL
          BUMP      PSNAME BY -1
          GOTO      NEXT71 IF EOS
          GOTO      NEXT70
NEXT71    LENSET    PSNAME
          RESET     PSNAME
.
          ENDSET    PGNAME
NEXT72    CMATCH    SP1,PGNAME
          GOTO      NEXT73 IF NOT EQUAL
          BUMP      PGNAME BY -1
          GOTO      NEXT73 IF EOS
          GOTO      NEXT72
NEXT73    LENSET    PGNAME
          RESET     PGNAME
.
          ENDSET    PTITL
NEXT74    CMATCH    SP1,PTITL
          GOTO      NEXT75 IF NOT EQUAL
          BUMP      PTITL BY -1
          GOTO      NEXT75 IF EOS
          GOTO      NEXT74
NEXT75    LENSET    PTITL
          RESET     PTITL
.
          CLEAR     PNAME
          APPEND    PSNAME,PNAME
          APPEND    COMMA,PNAME
          APPEND    SP1,PNAME
          APPEND    PGNAME,PNAME
          APPEND    SP1,PNAME
          APPEND    PTITL,PNAME
          RESET     PNAME
.
          UNPACK    ADADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,"! ",ADANUMB,*13,"! ",ADAURNO,*25,"! ",PNAME,*74,"! ":
                    CPDATE,*85,"! "
.
          ADD       ONE,CLNO
          RETURN
.
.*****************************************************************************
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          PACK      CURDATE,CDD,SLASH,CMM,SLASH,CYY
          REP       " 0",CURDATE
.
          PRINT     *F,*1,PRGID,*45,CNAME,*118,"DATE - ":
                    CURDATE:
                    *N,*1,VERSION:
                    *45,"*** EMERGENCY COMPLETION LISTING ***":
                    *118,"TIME - ",CTIMEIS,*N:
                    *118,"PAGE - ",*130,CPAGENO
.
          UNPACK    FROMDATE,XCENT,XYEAR,XMON,XDAY
          UNPACK    TODATE,XCENT1,XYEAR1,XMON1,XDAY1
.
          PRINT     *N,*45,"From Date : ",XDAY,"/",XMON,"/",XCENT,XYEAR:
                    *N,*45,"To   Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1,*N
.
          CALL      PAGEND
          PRINT     *1,"! ",HNUMDES,*13,"! U/R No.",*25,"! Patient Name":
                    *74,"! Vis. Dte",*85,"!" 
.
          CALL      PAGEND
.
          MOVE      "9",CLNO
          RETURN
.
.*****************************************************************************
PAGEND    PRINT     "*-------------------------------------------------------":
                    "----------------------------*"
          ADD       ONE,CLNO
          RETURN
.
.*****************************************************************************
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,"Number of Patients : ",TOTPATS:
                    *N,*N,"** END OF REPORT **"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P25:24:
                    "** Report Generation Completed **",*W;
          CALL      KILLIDX
          GOTO      START0
.
NODATA    DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** No Data Available **",*W:
                    *P1:24,*EL
          GOTO      START0
.
KILLIDX   CLOSE     TEMPFILE,DELETE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.*****************************************************************************
CREADIX   CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 82 ",CMDLINE
          APPEND    SORT1,CMDLINE
          RESET     CMDLINE
          DISPLAY   *P1:11,*EF;
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:11,*EF;
          RETURN
.
.*****************************************************************************
ENDIT     CALL      KILLIDX
          CHAIN     PGM
          STOP
.
          INC       STD001IO
          INC       TFILENAM
.
          INC       AAEDE1IO/INC
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       PATMA1IO/INC
          INC       PATCODIO/INC
.
