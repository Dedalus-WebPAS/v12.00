.******************************************************************************
.*  Include Name  : PATDOCKY.PBL                                              *
.*  Description   : Keyin of a Doctor file entry                              *
.*  Author        : Barney Rubble                                             *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYINACT - check doctor status                            *
.*                  CKYINACT VALUE                         VALID DRSTAT       *
.*                  ===================================================       *
.*                     0   No validation                        ALL           *
.*                     1   Validate all doctor types            0 or 2        *
.*                     2   Validate Attending doctors only      0             *
.*                     3   Validate Referring doctors only      0 or 2        *
.*                                                                            *
.*  Returns       : DCODE  - Keyed in code                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.09.B01 12/01/2001    Ebon Clements 509 Doctors Mods              *
.*                  Added validation for attending and referring doctors      *
.*        V5.07.00  16/11/2000    Ebon Clements 509 Mods                      *
.*                  Validate doctor codes returned from ? routine with        *
.*                  flag CKYINACT                                             *
.*        V5.06.02  25/09/1998    Katie Nelson                                *
.*                  Added procedure HPTDKA00 to key word search               *
.*        V5.06.01  10/06/1998    Glenn Berry       SRF 644657                *
.*                  Added CKYINACT - check if variable is active              *
.******************************************************************************
.*        V5.05.01  06/01/1998    Steve Armstrong   SRF 643664                *
.*                  Mods to validate active doctor code                       *
.*        V5.03.01  30/05/1995    Justin Coates  SRF 135603                   *
.*                  if nothing entered, clear DOCCODE                         *
.******************************************************************************
PATDOCKY  
PDCKY010  MATCH     SP6,CKYIDEF6                  * using a default ?
          GOTO      PDCKY050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:      * no default used
                    *PECOL:EVERT,*V2LON,DCODE:
                    *PECOL:EVERT,*DV,DCODE
          GOTO      PDCKY080
.
PDCKY050  MOVE      CKYIDEF6,DCODE
          KEYIN     *PECOL:EVERT,*DV,DCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,DCODE:
                    *PECOL:EVERT,*DV,DCODE
.
.         check what was entered
.
PDCKY080  CALL      DOCCK000
          BRANCH    EXIT,PDCKY100,PDCKY705,PDCKY805,PDCKY905
.                         "?"      valid    nothing exitchar
          GOTO      PDCKY010
.
.         a "?" was input
.
PDCKY100  MOVE      DCODE,KEY6
          PROC      HPTDKA00
          BRANCH    OVRCD,PDCKY010
          IF        CKYIMODE=1
            CALL      REXISTS
            GOTO      PDCKY010
          ENDIF
          GOTO      PDCKY080
.
. ------- valid doctor entered ------------------------
.
PDCKY705  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,DCODE
          MOVE      DCODE,DOCCODE
          GOTO      PDCKY999 
.
.         nothing was input
.
PDCKY805  MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      PDCKY999 
.
.         EXITCHAR input
.
PDCKY905  MOVE      TWO,EXIT                      * EXITCHAR
.
PDCKY999  RETURN
+
.***************************************************************************
.*                              DOCCK000             Called by: PATDOCKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
DOCCK000  ENDSET    DCODE
          APPEND    SP6,DCODE
          RESET     DCODE
.
          MATCH     EXITCHAR,DCODE
          GOTO      DOCCK600 IF EQUAL
.
          MATCH     SP6,DCODE
          GOTO      DOCCK700 IF EQUAL
.
.         check for a "?"
.
          MATCH     QUEST,DCODE
          GOTO      DOCCK800 IF EQUAL
.
. a code was entered so validate
. 
DOCCK500  PACK      KEY6,DCODE,SP10
          CALL      RDDOCT1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,DOCCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      DOCCK900 IF EQUAL
          ENDIF 
.
.         Make sure dr. is active   
.
          IF        DRSTAT=1 & CKYINACT=1
            DISPLAY   *P1:24,*EL,*B,"Inactive Doctor Code.  ";
            CALL      HITENTER
            GOTO      DOCCK910
          ENDIF
.
          IF        ( DRSTAT=1 | DRSTAT=2 ) & CKYINACT=2
            DISPLAY   *P1:24,*EL,*B,"Inactive Attending Doctor.  ";
            CALL      HITENTER
            GOTO      DOCCK910
          ENDIF
.
          IF        DRSTAT=1 & CKYINACT=3
            DISPLAY   *P1:24,*EL,*B,"Inactive Referring Doctor.  ";
            CALL      HITENTER
            GOTO      DOCCK910
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      DOCCK999
.
. exitchar entered
.
DOCCK600  MOVE      FOUR,EXIT
          UNPACK    SP30,DSNAME,DTITL
          MOVE      SP30,DGNAME
          MOVE      SP6,DCODE                   * clear code
          MOVE      SP6,DOCCODE                 * clear code
          GOTO      DOCCK999
.
. nothing entered
.
DOCCK700  BRANCH    CKYIMAND,DOCCK950             * mandatory
.
          UNPACK    SP30,DSNAME,DTITL
          MOVE      SP30,DGNAME
          MOVE      SP6,DOCCODE
          MOVE      THREE,EXIT
          GOTO      DOCCK999
.
. "?" entered
.
DOCCK800  MOVE      ONE,EXIT
          GOTO      DOCCK999
.
. invalid code entered
.
DOCCK900  IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
DOCCK910  MOVE      ZERO,EXIT
          GOTO      DOCCK999
.
DOCCK950  BEEP
          MOVE      ZERO,EXIT
.
DOCCK999  RETURN
...............................................................................
.
.          INCLUDE   PATDOCDS
          INC       PATDKIKY
.
