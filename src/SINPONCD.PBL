.----------------------------------------------------------------------
. Include to allocate Next Purchase Order Number
. Input 
. SIWAWAR - Warehouse Code
.
. Output
. SIPAPON - Purchase Order Number
.----------------------------------------------------------------------
SINORDNO
          MOVE     ZERO,COUNT
          MOVE     SIWAWAR,KEY5
.
SINORD05  CALL     RLSIWA1
          BRANCH   OVRCD,SINORD99,SINORD10
          MOVE     SIWANPO,ORDERNO
          SUB      ONE,ORDERNO
.
          REPEAT
            ADD      ONE,ORDERNO
            MOVE     ORDERNO,KEY7
            CALL     RASIPA1
          UNTIL    (OVRCD=1)
.
          MOVE     ORDERNO,SIPAPON
          ADD      ONE,ORDERNO
          MOVE     ORDERNO,SIWANPO
          CALL     UPSIWA1
          CALL     UUSIWA1
          MOVE     ZERO,OVRCD
          GOTO     SINORD99
.
SINORD10  ADD      ONE,COUNT
          DISPLAY  *P1:24,*W;
          COMPARE  FIVE,COUNT
          GOTO     SINORD05 IF NOT EQUAL
          MOVE     ZERO,COUNT
.
SINORD15  MOVE     SP70,DISPMSG
          CALL     MESSAGE2
          KEYIN     *P1:24,*EL,"Warehouse Record Locked - (":
                    *V2LON,"R",*HOFF,")etry or e(":
                    *V2LON,"X",*HOFF,")it ? ",ANS
          CALL      MESSAGE3
          REP       UPPLOW,ANS
          MATCH     ANSR,ANS
          GOTO      SINORD10 IF EQUAL
          MATCH     ANSX,ANS
          GOTO      SINORD20 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      SINORD15 IF NOT EQUAL
.
SINORD20  MOVE      ONE,OVRCD
.
SINORD99  RETURN

