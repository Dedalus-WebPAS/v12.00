.------------------------------------------------------------
.
. Function      : Procedure for Keyword Seach of 
.
. Parameters    : CKEYSUSP    1=Show Suspended 
.               : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.
. Modifications : 
.       V5.08.04  16/11/2000 Ebon Clements   509 Mods
.                 Changed to allow inactive doctor codes to be listed in ?
.                 routine.Changed PATDOCKY to validate with flag CKYINACT.
.       V5.08.03  22/09/2000  Jill Habasque  509 Mods
.                 Changed to display Surname, Title Given name          
.       V5.08.02  06/04/2000  Steve Armstrong   
.                 Fixed display of records for alphabetic search        
.                 (fields were running into each other)
.       V5.08.01  16/03/2000  Tonii SRF 646506
.                 Mods so inactive doctor codes can no longer be viewed
.                 Mods to make search Alphabetical by default
..............................................................................
.       V5.07.02  20/10/1999  Steve Downing
.                 Added PTCNDCQS - Single column for doctor code search. This
.                 was being reset when procedure was called
.       V5.07.01  23/08/1999  Jill Habasque
.                 Mods to stop speciality from overlapping
.         V5.07   16/08/1999  Jill Habasque SRF 633966
.                 Mods to display address if PTCNDCQS =1
.                 14/05/1999  Steve Armstrong  srf 631352
.                 Mods to redisplay flags after changing
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach
.  a Key Word Index Table and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PTDKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PTDKKWD
.  Index 2   PTDKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change sss to the System ID eg :%s/sss/PAT/g
.    Global Change fff to the File   ID eg :%s/fff/DKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Global Change KEYxx to the Key for the Key Word Table eg KEY21
.    Global Change KEYyy to the Key for the Code Field(s) Length eg KEY6
.    Change PTDKxxxx and PTDKyyyy to the Code Field(s) Segments
.    Modify List Display to Suit
.
.------------------------------------------------------------
           DEFPROC   HPTDKA00
.
           INC       STDHLPDF
           INC       STDKWSDF
           INC       PATDKIFD/INC
           INC       PATCONFD/INC
.
CATEGORY  DIM       2
DOCFNAME  DIM       50
DISPNAME  DIM       50
NAME35    DIM       35
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
SAVKEY21  DIM       21
SAVKEY6   DIM       6
.------------------------------------------------------------
. Search for a Doctor Code
.------------------------------------------------------------
HPTDKA00  OPEN      CONTROLF,"controlf"             *Included here because this
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS  *parameter was sometimes
.                                                   *reset when proc aws called
          MOVE      ONE,CKEYORDR         * Alpabetical search. SRF 646506
          MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDKIA1,"patdkiaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Keyword Search Table Missing (patdkiaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTDKAZZ
          ENDIF
.
          OPEN      PATDKIA2,"patdkiaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPTDKA10  CALL      KWINT000
          BRANCH    EXIT,HPTDKA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Code"            
          MOVE      STDCOL01,STDCOL02
          ADD       "7",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Doctor",*HOFF
          MOVE      STDCOL02,STDCOL03
          ADD       "21",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,SP1,*V2LON,*ULON,"Speciality",*HOFF
          MOVE      STDCOL03,STDCOL04              
          ADD       "12",STDCOL04 
          IF        PTCNDCQS=1
            DISPLAY   *PSTDCOL04:STDROW,SP1,*V2LON,*ULON,"Address",*HOFF
          ENDIF
.
          CALL      SHWFLG00
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPTDKA20  IF        SEARCHTY=1
            CALL      ALPSRH00
          ELSE
            CALL      KEYSRH00
          ENDIF
          BRANCH    DOAGAIN,HPTDKA10,HPTDKA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPTDKA99  CLOSE     PATDKIA1
          CLOSE     PATDKIA2
          GOTO      HPTDKAZZ           * End Procedure
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PTDKDOC 
.
          STRIP     KEYWRD01
          PACK      KEY21,KEYWRD01,SP70
          CALL      RSPTDK2 
KEYSRH10  CALL      RKPTDK2 
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY21,PTDKKWD,PTDKDOC
          STORE     KEY21,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTDKDOC
.
          CALL      DOCNAM00
          DISPLAY   *PSTDCOL02:STDROW,DOCFNAME
. 
          MOVE      "DT",CATEGORY
          MOVE      DRTYPE,KEY3 
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,SP1,TDESC
.
          IF        PTCNDCQS=1
            STRIP     DSADD1
            STRIP     DSADD2
            CLEAR     KEY32
            APPEND    DSADD1,KEY32
            APPEND    ", ",KEY32
            APPEND    DSADD2,KEY32
            RESET     KEY32
            DISPLAY   *PSTDCOL04:STDROW,SP1,KEY32  
          ELSE
            MOVE      DRTYPE2,KEY3
            CALL      GETCOD00
            DISPLAY   *PSTDCOL04:STDROW,SP1,TDESC
          ENDIF
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
KEYSRH20  CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH98:
                                  KEYSRH91,KEYSRH00,KEYSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKPTDK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY21,PTDKKWD,PTDKDOC,SP70                                  
          CALL      RPPTDK2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY21
          CALL      RDPTDK2 
.
KEYSRH36  CALL      RPPTDK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY21,PTDKKWD,PTDKDOC,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPPTDK2 
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY21
          CALL      RDPTDK2 
KEYSRH41  CALL      RPPTDK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKPTDK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY21,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTDK2 
.
KEYSRH46  CALL      RPPTDK2 
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH85  LOAD      KEY21,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY21,PTDKKWD,PTDKDOC                                       
          CALL      RDPTDK2 
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP70,PTDKSPA 
          MOVE      SP70,KEY21
          MOVE      SP70,PTDKDOC 
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP70,KEY21
          MOVE      SP70,PTDKDOC 
          LOAD      KEY21,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY21,PTDKKWD,PTDKDOC                                       
          MOVE      PTDKDOC,KEY6
          CALL      RDDOCT1
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS110  MOVE      "N",ANS
.
.----- Change statement from KEYIN to DISPLAY, user is no longer able 
.----- to select or view an inactive doctor code via the ? option. SRF 646506
.----- Changed to display inactive code but also validate to flag CKYINACT

          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAGS190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAGS190  ASSIGN    MINITEM+3,F2
FLAGS210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAGS290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAGS290  CALL      SHWFLG00
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFLG00  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search",FLAGDESC
          ELSE
            APPEND    "Key Word Search",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  BRANCH    SEARCHTY,CHREC100
          MATCH     KEYWRD01,PTDKKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY21,PTDKKWD,PTDKDOC
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY21,KEY21
            CALL      RDPTDK2
          ENDIF
.
          MOVE      PTDKDOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC800
.
CHREC100  MATCH     KEYWRD00,DSNAME
          GOTO      CHREC920 IF NOT EQUAL                                       
.
CHREC800  MOVE      DRSTAT,CHECKSUS
          BRANCH    SHOWSUSP,CHREC900
          BRANCH    DRSTAT,CHREC910
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY6,PTDKDOC
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY21,SAVKEY6,KEYWRDXX
          CALL      RDPTDK1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKPTDK1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY6,PTDKDOC,SP70
          MATCH     SAVKEY6,KEY6 
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PTDKKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
.------------------------------------------------------------
. Format Doctor Name in Title Text
.------------------------------------------------------------
DOCNAM00  CLEAR     DISPNAME
          MOVE      SP70,DOCFNAME
          STRIP     DSNAME
          PACK      DSNAME,DSNAME,COMMA
          PACK      NAME35,DSNAME,SP70
          CALL      NAMSTR00
          PACK      NAME35,DTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,DGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.-------------------------------------------------
. Format Name
.-------------------------------------------------
NAMSTR00  CMATCH    SP1,NAME35               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME35
            GOTO      NAMSTR00 IF NOT EOS
            GOTO      NAMSTR99               * entire name if blank
          ENDIF
          PACK      KEY35,NAME35,SP30        * reset form pointer
          MOVE      KEY35,NAME35
.
          SCAN      SP1,NAME35             * Locate the blank
          GOTO      NAMSTR20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
.         Check for any more names
.
          SETLPTR   NAME35,35              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME35,CCITEM          * reset to this point
          PACK      KEY35,NAME35,SP70      * reset form pointer
          MOVE      KEY35,NAME35
          GOTO      NAMSTR00
.
.         Rest of the string is one name
.
NAMSTR20  UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
NAMSTR99  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
GETCOD00  MOVE      SP70,TDESC
          MATCH     SP70,KEY3
          GOTO      GETCOD99 IF EQUAL
          PACK      KEY5,CATEGORY,KEY3
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
GETCOD99  RETURN
.
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
.
ALPSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,DCODE 
.
          STRIP     KEYWRD00
          PACK      KEY26,KEYWRD00,SP70
          CALL      RDSDOCT2 
ALPSRH10  CALL      RDKDOCT2 
          BRANCH    OVRCD,ALPSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH10,ALPSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY26,DSNAME,DCODE
          STORE     KEY26,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,DCODE
.
          CALL      DOCNAM00
          DISPLAY   *PSTDCOL02:STDROW,DOCFNAME
. 
          MOVE      "DT",CATEGORY
          MOVE      DRTYPE,KEY3 
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,SP1,TDESC
.
          IF        PTCNDCQS=1
            STRIP     DSADD1
            STRIP     DSADD2
            CLEAR     KEY32
            APPEND    DSADD1,KEY32
            APPEND    ", ",KEY32
            APPEND    DSADD2,KEY32
            RESET     KEY32
            DISPLAY   *PSTDCOL04:STDROW,SP1,KEY32
          ELSE
            MOVE      DRTYPE2,KEY3 
            CALL      GETCOD00 
            DISPLAY   *PSTDCOL04:STDROW,SP1,TDESC  
          ENDIF
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSRH10 IF NOT EQUAL
.
ALPSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
ALPSRH20  CALL      KWSEL000
          BRANCH    EXIT,ALPSRH30,ALPSRH35,ALPSRH40,ALPSRH45,ALPSRH98:
                                  ALPSRH91,ALPSRH00,ALPSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSRH92
.
ALPSRH30  CALL      RDKDOCT2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH30,ALPSRH85
.
          PACK      KEY26,DSNAME,DCODE,SP70                                  
          CALL      RDPDOCT2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH10
.
ALPSRH35  MOVE      ST1,KEY26
          CALL      RDDOCT2 
.
ALPSRH36  CALL      RDPDOCT2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH36,ALPSRH85
          PACK      KEY26,DSNAME,DCODE,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSRH37  CALL      RDPDOCT2 
          BRANCH    OVRCD,ALPSRH38
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH37,ALPSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH37 IF NOT EQUAL
.
ALPSRH38  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH40  MOVE      ST1,KEY26
          CALL      RDDOCT2 
ALPSRH41  CALL      RDPDOCT2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH41,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH09
.
ALPSRH45  CALL      RDKDOCT2 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH45,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY26,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RDSDOCT2 
.
ALPSRH46  CALL      RDPDOCT2 
          BRANCH    OVRCD,ALPSRH47
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH46,ALPSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH46 IF NOT EQUAL
.
ALPSRH47  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH85  LOAD      KEY26,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY26,DSNAME,DCODE                                       
          CALL      RDDOCT2 
.
ALPSRH90  BEEP
          GOTO      ALPSRH20
.
ALPSRH91  MOVE      SP70,DCODE 
          MOVE      SP70,KEY26
          MOVE      SP70,DSNAME 
          MOVE      ONE,OVRCD
          GOTO      ALPSRH96
.
ALPSRH92  MOVE      SP70,KEY26
          MOVE      SP70,DCODE 
          LOAD      KEY26,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY26,DSNAME,DCODE                                       
          MOVE      DCODE,KEY6
          CALL      RDDOCT1
.
ALPSRH96  MOVE      ZERO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH98  MOVE      ONE,DOAGAIN
.
ALPSRH99  RETURN
.
          INC       PATDKIIO/INC
          INC       IBAOVRIO/INC
          INC       STDHLPCD
          INC       STDKWSCD
.
HPTDKAZZ  ENDPROC   
