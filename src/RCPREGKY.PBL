.******************************************************************************
.*  Include Name  : RCPREGKY.PBL                                              *
.*  Description   : Keyin of Register codes                                   *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : REGCODE    - Keyed in code                                *
.*                  REGDESC    - Description                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
RCPREGKY
RPRGKY00  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      RPRGKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,REGCODE:
                    *PECOL:EVERT,*DV,REGCODE
          GOTO      RPRGKY20
.
RPRGKY10  MOVE      CKYIDEF3,REGCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,REGCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,REGCODE:
                    *PECOL:EVERT,*DV,REGCODE
.
.         check what was entered
.
RPRGKY20  CALL      REGCK000
          BRANCH    EXIT,RPRGKY25,RPRGKY31,RPRGKY41,RPRGKY95
.                        "?"      valid    nothing  exitchar
          GOTO      RPRGKY00
.
.         a "?" was input
.
RPRGKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
RPRGKY26  CALL      RCPREGDS
          BRANCH    CNEWDS,RPRGKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      RPRGKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
RPRGKY27  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      RPRGKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Register Code : ":
                    *P16:24,*DV,UNDLN3:      * no default used
                    *P16:24,*V2LON,REGCODE:
                    *P16:24,*DV,REGCODE
          GOTO      RPRGKY29
.
RPRGKY28  MOVE      CKYIDEF3,REGCODE
          KEYIN     *P1:24,*EL,*DV,"Register Code : ":
                    *P16:24,*DV,REGCODE:     * default used
                    *P16:24,*V2LON,*RV,REGCODE:
                    *P16:24,*DV,REGCODE
.
.         repeat the validation of the code
.         check what was entered
.
RPRGKY29  CALL      REGCK000
          BRANCH    EXIT,RPRGKY26,RPRGKY30,RPRGKY40,RPRGKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      RPRGKY27
.
.         a valid code was entered 
. 
RPRGKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,REGCODE
.
RPRGKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      RPRGKY99 
.
.         nothing was input
.
RPRGKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,REGCODE
.
RPRGKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      RPRGKY99 
.
.         EXITCHAR input
.
RPRGKY90  CALL      FRESCR00                * free the screen saved
.
RPRGKY95  MOVE      TWO,EXIT                * EXITCHAR
.
RPRGKY99  RETURN
+
.*********************************************************************
.*                  REGCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
REGCK000  ENDSET    REGCODE
          APPEND    SP3,REGCODE
          RESET     REGCODE
.
          MATCH     EXITCHAR,REGCODE
          GOTO      REGCK600 IF EQUAL       * exit char
.
          MATCH     SP3,REGCODE
          GOTO      REGCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,REGCODE
          GOTO      REGCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
REGCK500  PACK      KEY3,REGCODE,SP3
          CALL      RDREGA1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,REGCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      REGCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      REGCK999
.
.         exitchar entered
.
REGCK600  MOVE      FOUR,EXIT
          MOVE      SP3,REGCODE
          PACK      REGDESC,SP20,SP20      * clear desc field
          GOTO      REGCK999
.
.         nothing entered
.
REGCK700  BRANCH    CKYIMAND,REGCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,REGCODE
          PACK      REGDESC,SP20,SP20       * clear desc field
          GOTO      REGCK999
.
.         "?" entered
.
REGCK800  MOVE      ONE,EXIT
          GOTO      REGCK999
.
.         invalid code entered
.
REGCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Register Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      REGCK999
.
.         field is mandatory
.
REGCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
REGCK999  RETURN
.
         INCLUDE    RCPREGDS
+
